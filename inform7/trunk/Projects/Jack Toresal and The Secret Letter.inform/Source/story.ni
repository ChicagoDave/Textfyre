"Jack Toresal and The Secret Letter" by "Textfyre, Inc"

[  Change Log
When		Who		What
18-Mar-2010	R. Newcomb	Email, me to Dave, just after 8pm
13-Mar-2010	D. Cornelson	Changes to princess dialogue and some of the fight scene. Needs work.
14-Feb-2010	R. Newcomb	DD2.0 - various synonyms, fixed regionless new rooms, other misc.
19-Nov-2009	G. Jefferis	Eric's Transcript
17-Nov-2009	G. Jefferis	Eric's Transcript
29-Oct-2009	G. Jefferis	DD2.0 - War Room
22-Oct-2009	G. Jefferis	DD2.0 - War Room
19-Oct-2009	G. Jefferis	DD2.0 - Red Gate Estate
16-Oct-2009	G. Jefferis	DD2.0 - Red Gate Estate
14-Oct-2009	G. Jefferis	DD2.0 - Black Gate Estate
9-Oct-2009	G. Jefferis	DD2.0 - Jail scene
08-Jul-2009	G. Jefferis	Automatic exit checks
07-Jul-2009	G. Jefferis	De Smet bug fixes
06-Jul-2009	D. Cornelson	Fixed Chapter 10 title change.
02-Jul-2009	D. Cornelson	Added 'gap' and lowered 1 in 5 to 1 in 3 for Pieter's Mutterings.
02-Jul-2009	G. Jefferis	Distant descriptions fix.
01-Jul-2009	G. Jefferis	Pieter's mutterings, Amhyron fix.
29-Jun-2009	D. Cornelson	Jesse fixed FyreVM Support.
27-Jun-2009	D. Cornelson	Image changes.
26-Jun-2009	D. Cornelson	Final edit before release.
25-Jun-2009	G. Jefferis	Various location and scenario images
22-Jun-2009	G. Jefferis	UNDO hack moved to Restore After Undo extension
19-Jun-2009	G. Jefferis	Sleep in Maiden House
19-Jun-2009	D. Cornelson	Small fixes here and there.	
18-Jun-2009	G. Jefferis	UNDO hack
18-Jun-2009	D. Cornelson	Fixed fountain open bug.
07-Jun-2009	D. Cornelson	Fixed hint ordering, caused crash in Silverlight.
22-May-2009	G. Jefferis	Hints
21-May-2009	G. Jefferis	Hints
13-May-2009	G. Jefferis	complete changes from BermanTranscriptResponse
11-May-2009	G. Jefferis	changes from BermanTranscriptResponse
8-May-2009	G. Jefferis	changes from BermanTranscriptResponse
7-May-2009	G. Jefferis	further bug fixes
6-May-2009	G. Jefferis	changes from JLottTranscriptResponse
4-May-2009	G. Jefferis	changes from JLottTranscriptResponse
23-Apr-2009	G. Jefferis	Updated for 5Z71
18-Mar-2009	D. Cornelson	Fixed hints bug.
02-Mar-2009	G. Jefferis	Assorted bug finding and fixes
26-Feb-2009	G. Jefferis	Implemented changes from Paul_6_response
25-Feb-2009	G. Jefferis	Implemented changes from Paul_5_response
24-Feb-2009	G. Jefferis	Implemented changes from Paul_5_response
23-Feb-2009	G. Jefferis	Implemented changes from Paul_5_response
22-Feb-2009	G. Jefferis	Implemented changes from Paul_4_response
20-Feb-2009	G. Jefferis	Implemented changes from Paul_1-2-3_response
19-Feb-2009	G. Jefferis	Implemented changes from Paul_1-2-3_response
18-Feb-2009	G. Jefferis	Implemented changes from mcp01_response
16-Feb-2009	G. Jefferis	Implemented changes from mcp01_response
12-Feb-2009	G. Jefferis	Implemented changes from EE02_response
09-Feb-2009	G. Jefferis	Implemented changes from EE02_response
04-Feb-2009	G. Jefferis	Implemented changes from EE01_response
03-Feb-2009	D. Cornelson	Implementing changes from EE_notes_01.
03-Feb-2009	G. Jefferis	Implemented changes from EE01_response
02-Feb-2009	G. Jefferis	Implemented changes from EE01_response
29-Jan-2009	G. Jefferis	Implemented changes from EE01_response
21-Jan-2009	G. Jefferis	Implemented changes from JA_notes_02_response
20-Jan-2009	G. Jefferis	Implemented changes from JA_notes_02_response
19-Jan-2009	G. Jefferis	Implemented changes from JA_notes_01_response
16-Jan-2009	G. Jefferis	Fixing bugs from Transcript EE01
15-Jan-2009	G. Jefferis	Fixing bugs from Transcript EE01
14-Jan-2009	G. Jefferis	Fixing bugs from Transcript EE01
06-Dec-2008	G. Jefferis	Fixing bugs from Transcript Dec 3 08 (J.A.L.)
05-Dec-2008	G. Jefferis	Fixing bugs from Transcript Dec 3 08 (J.A.L.)
03-Dec-2008	D. Cornelson	Fixed hints XML.
29-Nov-2008	D. Cornelson	Added hints for grubbers.
20-Nov-2008	D. Cornelson	Added death channel.
06-Nov-2008	D. Cornelson	Fixed channel stuff and added chapter channel.
31-Oct-2008	M. Gentry	Fixed bugs from Jacqueline's R54 transcript.
28-Oct-2008	D. Cornelson	Updated channels to include prologue, title, and credits.
27 Sep 2008	G. Jefferis	Fixes for the walkthrough
15 Sep 2008	G. Jefferis	Changes to compile under 5U92
23-Aug-2008	D Cornelson	Fixed 5T bugs and added FyreVM Support.
03 Aug 2008	M. Gentry	Implements Test Session - R50 - Grubbers - Ashwell (2h)
27 Jul 2008	M. Gentry	Jacqueline's notes from Test Session - 2008-06-10 implemented (8h)
08 Jul 2008	G. Jefferis	DD #21 complete
07 Jul 2008	G. Jefferis	Adjacent rooms code fixing
06 Jul 2008	G. Jefferis	DD #21: Descriptions from afar / inspecting activity
				Moved pronouns to a module
29 Jun 2008	G. Jefferis	DD #21: Grammar for actions on adjacent rooms
28 Jun 2008	G. Jefferis	DD #21: Grammar for actions on current rooms
19 Jun 2008	G. Jefferis	DD #21: Begin adjacent rooms implementation
17 Jun 2008	J. Ashwell	Began adding change requests as comments in Book JAL - Search for JACQNOTE to find.  Tested Commerce Street and its three shops.
10 Jun 2008	J. Ashwell	Reduced test code to address current state of Grubbers
10 Jun 2008	G. Jefferis	Scenery implementation from DD #21
22 May 2008	G. Jefferis	DD #20 complete - Synonyms etc.
20 May 2008	G. Jefferis	DD #20 - Various backdrops
16 May 2008	G. Jefferis	DD #20 - Changes from Chapter 0
04 May 2008	J. Ashwell	Added tests for nouns, scenery taking, and smelling/listening.  Replaced names to credits which were lost during last change. 
04 May 2008	G. Jefferis	Revert updates to compile under 5J39
03 May 2008	G. Jefferis	Changes to Grubber's Market from DD #19
		G. Jefferis	Updates to compile under 5T18
11 Apr 2008	G. Jefferis	Last bits from DD #18.
09 Apr 2008	G. Jefferis	Progress on DD#18. Tidying up along the way.
06 Apr 2008	G. Jefferis	Prince Gravesal and War Room from DD#17
04 Apr 2008	G. Jefferis	DD#17 conversations: Inhyron, Amhyron, Bresa.
				Rewrite of Quips extension
03 Apr 2008	G. Jefferis	Started DD#17: Ball Room, Jacobs, and Princess.
				XML output toggling
02 Apr 2008	G. Jefferis	Finished DD#16: Clothier, Chorus Brothers,
				Journey with Pieter.
31 Mar 2008	G. Jefferis	Continue implementation of DD #16.
30 Mar 2008	G. Jefferis	Continue implementation of DD #16.
				Lost a lot of time fixing dynamic memory limit.
28 Mar 2008	G. Jefferis	Begin implementation of DD #16.
				Various bug fixes, some major.
27 Mar 2008	G. Jefferis	DD #15: Complete.
24 Mar 2008	G. Jefferis	Character lists extension.
22 Mar 2008	G. Jefferis	XML tag wrestling; topic lists.
21 Mar 2008	G. Jefferis	DD #15: Implementation to Maiden House
18 Mar 2008	G. Jefferis	Bug fixes.
17 Mar 2008	G. Jefferis	DD #14: Conversation and implementation
10 Mar 2008	G. Jefferis	Bug fixes and general tidying
03 Mar 2008	G. Jefferis	DD #13: Sewer puzzle rewrite
28 Feb 2008	G. Jefferis	DD #13: Jailhouse escape, fixed Bobby
27 Feb 2008	G. Jefferis	DD #13: Jailhouse; Bobby and Jacobs
25 Feb 2008	G. Jefferis	DD #12: Widow Shannon
23 Feb 2008	G. Jefferis	Bug fixing
15 Feb 2008	G. Jefferis	Conversation from 12B.
13 Feb 2008	G. Jefferis	Conversation document 12A complete.
12 Feb 2008	G. Jefferis	Conversation document 12A up to Olgan Minor.
07 Feb 2008	G. Jefferis	Fixed up Teisha's Tent and completed final chase.
				Removed old conversations throughout.
04 Feb 2008	G. Jefferis	Continued final chase scene
02 Feb 2008	G. Jefferis	Continued final chase scene
				More replacement stalls
31 Jan 2008	G. Jefferis	Amendments to stalls, stallkeepers
				Fixed map connections
				Beginning of final chase scene
30 Jan 2008	G. Jefferis	Amendments to stalls and merchandise
29 Jan 2008	G. Jefferis	New conversation system added
26 Jan 2008	G. Jefferis	Updates from DD#11:
				Trade component mostly removed
				Introduction amended
				Mercenary behavior updated
				Market Map updated
30 Dec 2007	G. Jefferis	Bug finding following test transcripts
21 Dec 2007	G. Jefferis	Updates from DD #10.
				Check correct compilation under 5J39
17 Nov 2007	G. Jefferis	Changes to run correctly under 5G67
15 Nov 2007	G. Jefferis	Changes to compile under 5G67
06 Nov 2007	G. Jefferis	Final changes from DD #9, and bug fixing.
05 Nov 2007	G. Jefferis	Changes from DD #9
04 Nov 2007	G. Jefferis	Further changes from DD#9
30 Oct 2007	G. Jefferis	Additional conversation
29 Oct 2007	G. Jefferis	Changes from DD#9, installment 1.
24 Oct 2007	G. Jefferis	General tidying up, of crufty trade code especially
				Converted random choices to use 4X60 syntax
				([one of]...[at random]) throughout.
20 Sep 2007	G. Jefferis	Updates from DD #8: secret doors at fountain.
19 Sep 2007	G. Jefferis	Updates from design documents #5, #6, & #7
18 Sep 2007	G. Jefferis	Updates from design document #5
31 Aug 2007	G. Jefferis	Updates from design document #5
				Simplified mercenaries:
				 * proper oblivious / approaching / grabbing status
				 * proper countdown before they grab us
				Assorted bug fixes in Grubber's
17 Aug 2007	G. Jefferis	Updates from revised design documents
16 Aug 2007	G. Jefferis	Oversights and corrections (don't bill)
				(e.g. couldn't cut the ropes to free Pieter.)
14 Aug 2007	G. Jefferis	Complete to alpha
11 Aug 2007	G. Jefferis	Pieter, and journey to the ball
				Sanity checks: day / night time simplified
				Sanity checks: HTMLese [i] [b] and [r] tags
				Commerce Street revisited (after Red Gate)
				I7 scenes bug to work around
				(can't reliably test "if <scene> is not happening")
9 Aug 2007	G. Jefferis	Red Gate Estate
8 Aug 2007	G. Jefferis	Raid on Maiden House
				Bobby's Execution scene
				Black Gate Estate
6 Aug 2007	G. Jefferis	Sewer to "pre-Alpha"
				Jail scenes to "pre-Alpha"
				Better definitions for vendors
5 Aug 2007	G. Jefferis	Preliminary Jail scenes
				Capture by Fossville's men
				Return journey with Bobby
4 Aug 2007	G. Jefferis	Journey to Lord's Keep
				Path to fountain, given scenery
				Bobby following the player
3 Aug 2007	G. Jefferis	Path to the fountain
				Night-time descriptions
				Remaining rooms of Maiden House
				Remaining rooms of Commerce Street
				Grubber's Market to "pre-Alpha."
2 Aug 2007	G. Jefferis	About a third of Commerce Street
				Beginning of Maiden House
2 Aug 2007	G. Jefferis	Most of Grubber's Market
]

Include FyreVM Support by Textfyre.
Include Basic Screen Effects by Emily Short.
Include Punctuation Removal by Emily Short.
Include Textfyre Standard Rules by Textfyre.
Include Test Suite by Textfyre.

Include Pronouns by Textfyre.
Include Adjacent Rooms by Textfyre.
Include Quips by Textfyre.
Include Counters by Textfyre.
Include Scripted Events by Textfyre.
Include Character Listing by Textfyre.
Include Dramatic Priority by Textfyre.
[ Include Conversation Topics by Textfyre. ]

Include Restoring After Undo by Textfyre.
Include Image Output by Textfyre.

Include Secret Letter Hints by Textfyre.
Include Xml Output Toggling by Textfyre.

Include Basic Help by David Cornelson.

Use no scoring, American dialect and full-length room descriptions.
Use authorial modesty.
The story creation year is 2009.

Rule for printing the banner text:
	select the title channel;
	say "[b][story title] - v2.0.20100313[r][line break]";
	select the credits channel;
	say "[story title] by [story author][line break]";
	say "Copyright @ [story creation year] by [story author][line break]";
	say "Story by David Cornelson[line break]";
	say "Written by Michael Gentry[line break]";
	say "Game Engine (FyreVM) by Jesse McGrew[line break]";
	say "Programming by Graeme Jefferis and Ron Newcomb[line break]";
	say "Testing by Jacqueline Ashwell, Peter Berman,[line break]Eric Eve, Jim Aikin, and Paul O'Brian[line break]";
	say "Deluxe Edition UI by Thomas Lynge[line break]";
	say "Sketches by Erika Swanson (www.simplyerika.com).[line break]";
	say "Special thanks to Graham Nelson and Emily Short[line break]";
	say "for Inform 7 (www.inform7.com)[line break]";
	say "All rights reserved[line break]";
	select the main channel;

Section - Randomizer - Not For Release

When play begins, seed the random-number generator with 1234.

After reading a command: 
	let T be indexed text; 
	let T be the player's command;
	if T matches the regular expression "^\*|\[bracket]":
		say "[bracket][one of]Your comment has been noted[or]Noted[stopping].[close bracket][paragraph break]";
		reject the player's command; 
	otherwise:
		continue the action. 

Book 0 - Worldly affairs

Part 1 - Beating memory constraints

Use dynamic memory allocation of at least 65536.

Use MAX_DICT_ENTRIES of 3000.
Use MAX_STATIC_DATA of 900000.
Use MAX_PROP_TABLE_SIZE of 500000.
Use MAX_OBJECTS of 1280.
Use MAX_EXPRESSION_NODES of 300.
Use MAX_SYMBOLS of 30000.

When play begins:
	change the time of day to 1:00 AM;
	change menu option number display to true;

Part 2 - Index Map Tweaking

[This is a lot of code for "tweaking", but I find having an accurate index map so valuable that it's really worth it.]

Index map with Grubbers Market Herb Stall mapped west of Grubbers Market Rope Dealer.
Index map with Grubbers Market Northwest Junction mapped north of Grubbers Market Herb Stall.
Index map with Grubbers Market Eastern Junction mapped east of Grubbers Market Pottery Stall.
Index map with Bakers Shop mapped east of Grubbers Market Fruit Stall.
Index map with Grubbers Market Exotic Gems Stall mapped south of Grubbers Market Herb Stall.
Index map with Grubbers Market Hat Stall mapped south of Grubbers Market Eastern Junction.

Index map with Alley mapped northwest of Grubbers Market Northwest Junction.
Index map with Outer Market Roof mapped north of Alley.

Index map with Grubbers Market Teishas Tent mapped north of Grubbers Market Outside Silk Stall.
Index map with Grubbers Market Center Post mapped east of Grubbers Market Grocery Stall.
Index map with Grubbers Market Top of Center Post mapped north of Grubbers Market Center Post.

Index map with Jail-Cell-2 mapped south of Grubbers Market Weapons Stall.
Index map with Jailhouse mapped south of Jail-Cell-2.
Index map with Drain Room mapped southwest of Jailhouse.
Index map with Way-Out mapped east of Jailhouse.
Index map with Sewer mapped south of Drain Room.
Index map with Access Tunnel mapped east of Sewer.

Index map with Butchers Shop mapped east of Bakers Shop.
Index map with Armory Shop mapped east of Butchers Shop.
Index map with Commerce Street mapped south of Butchers Shop.

Index map with Thin Alley mapped north of Butchers Shop.
Index map with Main Rooftop mapped north of Thin Alley.
Index map with Rooftop Edge mapped east of Main Rooftop.
Index map with Roof of Black Gate Estate mapped east of Rooftop Edge.
Index map with Rooftop Garden mapped northeast of Roof of Black Gate Estate.
Index map with Patio mapped southeast of Roof of Black Gate Estate.
Index map with Third Floor Landing mapped east of Rooftop Garden.
Index map with Music Room mapped south of Third Floor Landing.
Index map with Audience Area mapped south of Music Room. 
[Index map with Third Floor Balcony mapped south of Audience Area.]
Index map with Second Floor Landing mapped east of Third Floor Landing.
Index map with Living Room mapped south of Second Floor Landing.
Index map with Master Bedroom mapped south of Living Room.
Index map with Library mapped south of Master Bedroom.

Index map with East Commerce Street mapped southeast of Library.
Index map with Foyer mapped north of East Commerce Street.
Index map with Great Hall mapped north of Foyer.
Index map with Office mapped east of Great Hall.
Index map with Red Gate Cellar mapped south of Office.
Index map with Red Gate Estate Second Floor Landing mapped north of Office.
Index map with Bathroom mapped east of Office.
Index map with Red Gate Master Bedroom mapped north of Bathroom.
Index map with Red Gate Estate Third Floor Landing mapped north of Red Gate Master Bedroom.

Index map with Lords Market mapped east of East Commerce Street.
Index map with Jewelers Shop mapped east of Lords Market.
Index map with Clothiers Shop mapped northeast of Lords Market.
Index map with Clothiers Shop mapped north of Jewelers Shop.
Index map with Moneylenders Shop mapped southeast of Lords Market.

Index map with Market Square mapped north of Grubbers Market Northwest Junction.
Index map with Lords Road mapped northeast of Outer Market Roof.
Index map with Southern Gate mapped north of Lords Road.

Index map with Clearing mapped west of Woods.
Index map with Underneath the Fountain mapped north of Clearing.
Index map with Secret Passage mapped north of Underneath the Fountain.
Index map with Chapel mapped north of Secret Passage.
Index map with Lower Bailey mapped west of Secret Passage.
Index map with Entrance Hall mapped north of Lower Bailey.
Index map with Ballroom mapped north of Entrance Hall.
Index map with War Room mapped west of Entrance hall.
Index map with Upper Bailey mapped north of Chapel.
Index map with Guardhouse mapped north of Upper Bailey.

Index map with Back Alley mapped south of Lords Market.
Index map with Entrance to Maiden House mapped southwest of Back Alley.
Index map with Dormitory mapped west of Entrance to Maiden House.
Index map with Kitchen mapped northeast of Hallway.
Index map with Hallway mapped south of Entrance to Maiden House.
Index map with Laundry mapped southeast of Hallway.
Index map with Secret Closet mapped east of Hallway.
Index map with Secret Closet mapped north of Laundry.
Index map with Privy mapped southwest of Hallway.
Index map with Behind Maiden House mapped west of Privy.
Index map with Behind Maiden House mapped southwest of Entrance To Maiden House.

[Can't seem to get the map to draw Maiden House correctly, no matter how I arrange it. Oh well.]

Part 4 - Properties of Objects

Chapter 1 - Proximity

Definition: a thing is nearby:
	if it is in the location then decide yes;
	decide no.

Chapter 2 - Landings

A Landing is a kind of room.
Understand "main", "hall", "hallway" as a Landing.
Understand "main", "hall", "hallway", "stairs", "steps", "step", "stairway", "staircase" as a Landing when the location is not the item described.

Part 5 - Loitering and waiting around

[ This tracks the number of turns a player has remained in the same location ]

The last room is a room which varies.
The waiting count is a number variable.

The waiting around rule is listed first in the every turn rules.

Every turn (this is the waiting around rule):
	if the location is the same location as last time begin;
		stay put;
	otherwise;
		change the waiting count to zero;
	end if;

The last every turn rule:
	change the last room to the location;

Definition: a room is the same location as last time if it is the last room.
Definition: a person is on the move if the location is not the same location as last time.

To stay put:
	increment the waiting count by 1;

Part 6 - Conversation

Chapter 1 - Communicating and other-than-communicating

Telling someone about something is communicating. Asking someone about something is communicating. Asking someone for something is communicating. Answering someone that something is communicating. Talking to someone is communicating.

[ The following big definition exists only because we can't do "Instead of doing something other than communicating," and it really seems to be the easiest way to achieve the same effect: see Example 174, "Under Contract", in section 12.11 of the manual.]

Taking is other-than-communicating. Removing something from is other-than-communicating. Dropping is other-than-communicating. Putting something on is other-than-communicating. Inserting something into is other-than-communicating. Eating is other-than-communicating. Going is other-than-communicating. Entering is other-than-communicating. Exiting is other-than-communicating. Getting off is other-than-communicating. Looking under is other-than-communicating. Searching is other-than-communicating. Locking something with is other-than-communicating. Unlocking something with is other-than-communicating. Switching on is other-than-communicating. Switching off is other-than-communicating. Opening is other-than-communicating. Closing is other-than-communicating. Wearing is other-than-communicating. Taking off is other-than-communicating. Touching is other-than-communicating. Waving is other-than-communicating. Pulling is other-than-communicating. Pushing is other-than-communicating. Turning is other-than-communicating. Pushing something to is other-than-communicating. Burning is other-than-communicating. Tasting is other-than-communicating. Cutting is other-than-communicating. Jumping is other-than-communicating. Tying something to is other-than-communicating. Swinging is other-than-communicating. Rubbing is other-than-communicating. Buying is other-than-communicating. Climbing is other-than-communicating. 

Chapter 2 - Listening while having a conversation

[This allows you to e.g. "LISTEN TO BOBBY" as a way to reiterate the conversation menu.]

Instead of listening to a person while the noun is the current conversationalist:
	display the menu;

Chapter 3 - Error Message for Ask/Tell

[Since all dialogue is handled by TALK TO PERSON and then choosing menu options, we need to head off attempts to ASK/TELL PERSON ABOUT and other standard communication actions, so that the player doesn't get lame error messages.]

Understand the command "answer", "say", "shout", "speak" as something new. Understand the command "tell" as something new. Understand the command "ask" as something new.

Understand "answer [text]" as a mistake ("[just talk].").
Understand "answer [text] to/at [someone]" as a mistake ("[just talk]."). Understand the commands "say", "shout", "speak" as something new.
Understand "ask [someone] about/for [text]" as a mistake ("[just talk].").
Understand "tell [someone] about [text]" as a mistake ("[just talk].").

First before asking someone to try doing something:
	instead say "[just talk].";

First before answering someone that:
	instead say "[just talk].";

To say just talk:
	say "To talk to a person, simply type TALK TO (whomever)".

Chapter 4 - No unnecessary interruptions

[Although other things might well be going on in the Market whilst we're talking to people, it's likely that they unnecessarily distract one from the content of the conversation. However, Jack would still have an eye out to catch important events: such as the arrival of mercenaries.]

Before choosing a dramatic possibility when the current conversationalist is not the player:
	ratchet the current tension to interesting;

Before choosing a dramatic possibility when we are talking to someone:
	ratchet the current tension to important;

Before choosing a dramatic possibility when we are selecting menu option:
	ratchet the current tension to important;

[Although it may seems that only the first of these is necessary - and in fact it is by far the most important - still the second and third rules serve an important purpose as well. They each cover cases in which conversation can be ongoing during the player's turn, but be finished by the end of it. In neither of these cases is unnecessary interruption desirable.]

Part 9 - Spooling through dialogue

[ A generalisation of scripted dialogue. ]

To spool through dialogue in (t - a table-name):
	repeat through t begin;
		say "[text entry][no line break]";
		blank out the whole row;
		rule succeeds;
	end repeat.

Part 10 - Looking through

[Look through usually generates the "search" action, as in "look through pile of books." But mostly in this game we want to "look through the arrow slit" or "look through the viewing window"... ]

Looking through is an action applying to one visible thing.
Understand "look through [door]" as looking through.
Understand "look through [container]" as looking through.
[ ...And anything else is "search" as normal. ]

Carry out looking through:
	try examining the noun instead.

Part 12 - One shot texts

[Adapted from the manual. Say [first time]some text[subsequently]some other, optional text[only]. ]

To say first time -- beginning say_first_time: 
    (- {-allocate-storage:say_first_time}if (I7_ST_say_first_time-->{-counter:say_first_time} == false) {-open-brace} I7_ST_say_first_time-->{-advance-counter:say_first_time} = true; -). 
To say subsequently -- continuing say_first_time:
    (- {-close-brace} else {-open-brace} -).
To say only -- ending say_first_time: 
    (- {-close-brace} -). 

Part 13 - HTMLesque Informese

[105 "[italic type]...[roman type]"s later, I finally cracked.

Even these are not perfectly straightforward to use. If printing a dot immediately before a text substitution, Inform sees fit to add a line break. So there are quite a few "...[no line break][i]some text[r]" constructions. ]

To say i:
	say italic type;

To say r:
	say roman type;

To say b:
	say bold type;

Part 14 - The time of day

[It's becoming apparent that scenes are awkward to work with beyond the most simple applications. Keeping track of whether it's day or night according to which scenes are currently running is doomed to fail. Switching a flag when certain scenes start and end, on the other hand, is dead simple... ]

Hour is a kind of value. The hours are daylight and nighttime.
The circadia is an hour that varies. The circadia is daylight.

To decide if it is day:
	if the circadia is daylight, yes;
	otherwise no.

To decide if it is night:
	if it is day, no;
	otherwise yes.

Part 15 - Better senses of hearing and smell

['Better' only in the sense that they succeed by default: something I've always felt should be the case.]

Chapter 1 - Smell

The block smelling rule is not listed in any rulebook.
Report smelling: say "You smell nothing unexpected.";

Chapter 2 - Hearing

The block listening rule is not listed in any rulebook.

Report listening to the location:
	say "You keep your ears pricked." instead;

Report listening [to something]: say "You hear nothing unexpected.";

Part 16 - Attacking

Understand "attack" as attacking.

Definition: a thing is hostile mercenaries:
	if it is the chasing mercenaries, yes;
	if it is the group of mercenaries, yes;
	if it is the several mercenaries, yes;
	if it is the mercenary captain, yes;
	if it is the mercenary mob, yes;
	if it is the wandering mercenaries, yes;
	if it is the Lords Guards checking invites, yes;
	if it is the patrolling Lords Guards, yes;
	no;

Rule for supplying a missing noun when attacking or kicking:
	if the player can see Baron Fossville:
		change the noun to Baron Fossville;
	otherwise if the player can see a hostile mercenaries:
		change the noun to a random nearby hostile mercenaries person;
	otherwise if the player can see another person:
		change the noun to a random nearby other person;

Chapter 1 - Kicking

Kicking is an action applying to one thing.
Understand "kick [something]" as kicking.
Understand "kick" as kicking.

Check kicking the actor when the actor is the player (this is the kicking yourself rule):
	say "Not so hard really, was it?" instead;

Carry out kicking something:
	try attacking the noun;

Chapter 2 - Biting

Understand the command "bite" as something new.

Understand "bite [person]" as attacking.
Understand "bite [something preferably held]" as eating.

Chapter 3 - Attacking it with

Attacking it with is an action applying to two things.

Understand "attack [something] with [something preferably held]" as attacking it with.
Understand "stab [something] with [something preferably held]" as attacking it with.
Understand "knife [something] with [something preferably held]" as attacking it with.

Understand "stab [something]" as attacking it with when the player has a sensible weapon.
Understand "knife [something]" as attacking it with when the player has a sensible weapon.

Rule for supplying a missing second noun when attacking someone with:
	if the player has a sensible weapon:
		change the second noun to a random sensible weapon had by the player;

Check attacking something with something (called the proposed weapon):
	unless the proposed weapon is a sensible weapon:
		say "[The proposed weapon] [is or are for the proposed weapon] not a suitable weapon!" instead;

Check attacking something with:
	try attacking the noun instead;

Definition: a thing is a sensible weapon:
	if it is the plain dagger, yes;
	if it is the knife, yes;
	no;

Part 19 - Raising and lowering

[These are mainly here to deal with technicalities involving hoods, as on cloaks.]

Raising is an action applying to one thing.
Lowering is an action applying to one thing.

Understand "raise up [something]" as raising.
Understand "raise [something]" as raising.
Understand "raise [something] up" as raising.

Understand "put up [something]" as raising.
Understand "put [something] up" as raising.

Understand the command "lift" as "raise".

Understand "lower [something]" as lowering.

Check raising:
	say "That would not accomplish very much." instead;

Check lowering:
	say "That would not accomplish very much." instead;

Part 20 - Picking locks

Lockpicking is an action applying to one thing.

Understand "pick [something]" as lockpicking.
Understand "pick [something] with [something]" as unlocking it with.

Check lockpicking (this is the block lockpicking without the lockpick rule):
	if the player does not hold the piece of wire:
		if the piece of wire is visible:
			say "(first taking the lockpick)";
			try silently taking the piece of wire;
			if the player does not hold the piece of wire, stop;
			[ if the player is caught, stop; ]
		otherwise:
			say "You would need a lockpick for that." instead;

Check lockpicking up:
	try taking nothing instead;

Carry out lockpicking:
	try unlocking the noun with the piece of wire instead;

Part 21 - Sewer puzzle symbols

[ Get these defined early so as to avoid namespace clashes - with the Market *Square*, for example. ]

Sewer-symbol is a kind of value. The sewer-symbols are square, diamond, circle, triangle, crescent, star, cross, and arrow.

Symbol color is a kind of value. The symbol colors are red, blue, green, and yellow.

Part 23 - Boilerplate 'repeat' phrase

To say repeat phrase:
	say "'[one of]I already told you[or]I told you already[or]Like I said[or]As I said[or]I told you[at random],' says [the current conversationalist],"

To say rp:
	say repeat phrase;

Part 24 - Quip clustering

Clustering relates quips to each other in groups. The verb to be clustered with implies the clustering relation.

Part 25 - Holding out, teasing and taunting

Understand the command "hold" as something new.

Understand "hold [things]" as taking.
Understand "hold out [something preferably held]" as waving. 
Understand "hold out [something preferably held] to/for [someone]" as giving it to. 

Understand "tease [animal] with [something]" as showing it to (with nouns reversed). 
Understand "taunt [animal] with [something]" as showing it to (with nouns reversed). 

Part 26 - Extinguishing

Extinguishing is an action applying to one thing.
Understand "extinguish [something]" as extinguishing.
Understand "put out [something]" as extinguishing.

Check extinguishing something:
	say "[The noun] is not alight.";

Part 27 - Buying

The block buying rule is not listed in any rulebook.

Report buying:
	say "Nothing is on sale.";

Does the player mean buying something when the player has the noun:
	it is very unlikely;

[Part 28 - Fancy examining

[ Examining as an activity. Ganked verbatim from Example 296, "Crusoe." ]

The examining activity rule is listed instead of the standard examining rule in the carry out examining rules.

Printing the description of something is an activity. 

This is the examining activity rule: 
	carry out the printing the description activity with the noun;

The examine undescribed containers rule is not listed in any rulebook.
The examine undescribed devices rule is not listed in any rulebook.
The examine undescribed things rule is not listed in any rulebook.

Rule for printing the description of something (called item):
	unless the item goes undescribed by source text:
		say "[description of item] [run paragraph on]";
	otherwise:
		abide by the examine undescribed containers rule;
		abide by the examine undescribed devices rule;
		abide by the examine undescribed things rule;

Last after printing the description of something (called the item): 
	unless the item goes undescribed by source text:
		say paragraph break; ]

Part 29 - Cutting things with tools

A thing can be sharp or blunt. A thing is usually blunt.

Cutting it with is an action with past participle cut, applying to one visible thing and one carried thing.

Understand "cut [something] with [something]" as cutting it with.
Understand "cut through [something]" as cutting.
Understand "cut through [something] with [something]" as cutting it with.
Understand the command "saw" as "cut".

Understand "sever [something]" as cutting.
Understand "sever [something] with [something]" as cutting it with.

Check cutting it with:
	if the second noun is blunt,
		say "[That or those for the second noun] won't cut through [noun]." instead;

Check cutting it with:
	say "Cutting [the noun] up would achieve little." instead;

Part 30 - Running

Understand the command "run" as something new.

Fleeing vaguely is an action applying to nothing.

Understand "run" as fleeing vaguely.
Understand "run away" as fleeing vaguely.
Understand "run [planar direction]" as going.
Understand "flee" as fleeing vaguely.

The fleeing vaguely action has a direction called the chosen-direction.

Rule for setting action variables for fleeing vaguely:
	now the chosen-direction is nothing;
	let the possibilities be {north, northeast, east, southeast, south, southwest, west, northwest};
	sort the possibilities in random order;
	add {outside, inside} to the possibilities; [to be considered once all else fails]
	repeat with yonder running through the possibilities:
		let the proposed destination be the room-or-door yonder from the location;
		if the proposed destination is a room or the proposed destination is an open door:
			say "(to the [yonder])[command clarification break]";
			now the chosen-direction is yonder;
			continue the action;
	[at this point making no decision, and the action will be terminated.]

Check fleeing vaguely:
	if the chosen-direction is nothing,
		say "There is nowhere to run from here!" instead;

Carry out fleeing vaguely:
	try going the chosen-direction;

Part 31 - Hiding

Hiding is an action applying to nothing.
Hiding behind is an action applying to one thing.

Understand "hide" as hiding.

Understand "hide in/inside/on [something]" as entering.

Understand "hide [other things] in/inside/down/under/into/behind [something]" as inserting it into.

Understand "hide behind/under/beneath/underneath [something]" as hiding behind.
Understand "go behind [something]" as hiding behind.
Understand "get behind/under/beneath/underneath [something]" as hiding behind.
Understand "move behind [something]" as hiding behind.

Report hiding:
	say "There's nowhere for you to hide here.";

Report hiding behind something:
	say "There's not enough room for you to hide there.";

Part 32 - Catch / trap

Understand "catch [something]" as taking.
Understand "trap [something]" as taking.

Part 33 - Trade

Trading it with is an action applying to one carried thing and one thing.
Trading it for is an action applying to one carried thing and one visible thing.

Understand "trade [something]" as trading it with.
Understand "trade [something] with [someone]" as trading it with.
Understand "trade [something] to [someone]" as trading it with.
[ Understand "trade with [someone]" as trading it with (with nouns reversed). ]

Report trading it with:
	say "This isn't the time for haggling." instead;

Understand "trade [something] for [something]" as trading it for.
Understand "buy [something] with [something]" as trading it for (with nouns reversed).

Report trading it for:
	say "This isn't the time for haggling." instead;

Part 34 - Limited powers of "Get All"

Rule for deciding whether all includes backdrops: it does not. 
Rule for deciding whether all includes scenery: it does not. 
Rule for deciding whether all includes people: it does not. 
Rule for deciding whether all includes something enclosed by the player when we are taking or we are pseudotaking: it does not. 
Rule for deciding whether all includes something enclosed by the satchel when we are taking or we are pseudotaking: it does not. 
Rule for deciding whether all includes something that is part of something: it does not.

Part 35 - Limbo for Incorporation Relation

[ As of 5T18, we can't say "now the secret door is part of nothing". So instead we use "now the secret door is part of Limbo", etc. ]

[ The right way to do this is to simply remove the secret door (or whatever) from play. ]

Limbo is a thing.

Part 36 - No Dropping

Chapter 1 - No Dropping

The can't drop clothes being worn rule is not listed in any rulebook.

Last check dropping rule (this is the block dropping rule):
	say "You'd better hold onto [that or those for the noun]; you never know when you might need [it or them for the noun]." instead; 

Chapter 2 - No Inserting

Last check inserting it into (this is the block inserting it into rule):
	if the second noun is not held by the player,
		say "You'd better hold onto [that or those for the noun]; you never know when you might need [it or them for the noun]." instead; 

Chapter 3 - No Putting On Top Of

Last check putting it on (this is the block putting it on rule):
	say "You'd better hold onto [that or those for the noun]; you never know when you might need [it or them for the noun]." instead; 

Chapter 4 - No Giving Away

This is the new block giving rule:
	say "You'd better hold onto [that or those for the noun]; you never know when you might need [it or them for the noun]." instead; 

The new block giving rule is listed instead of the block giving rule in the check giving it to rulebook.

Part 37 - Squeezing and crawling

Understand the command "squeeze" as something new.
Understand the command "crawl" as "go".

Understand "squeeze [direction]" as going.
Understand "squeeze [something]" as squeezing.
Understand "squeeze through [something]" as going.

Part 38 - Examining Everything

Understand "examine everything/all" as a mistake ("You will need to be more specific about what you want to look at.").

Part 39 - Cleaning Up Punctuation

[ Cleaning up apostrophes and hyphens saves lots of work when declaring synonyms. It treats "Grubber's" the same as "Grubbers", for example, which makes the parser more forgiving of typos. It also means if you have a "reddish-green" apple, you can declare the synonyms "red", "reddish", and "green", and "reddish-green" will be automatically taken care of.

However, it does mean that possessive words must always be written WITHOUT an apostrophe when used as an object name in code (such as Grubber's), so that the name can be recognized by the parser. Even though it's always WITH an apostrophe in printed text. This is a small pain but, I think, a reasonable payoff.

GJ: Annoyingly, pieces of text separated by apostrophes aren't glued back together after being removed: so "examine grubber's market" translates to "examine grubber s market", which is bound to fail. The Punctuation Removal extension does this work by rewriting bytes in the input buffer, which is obviously fast, but we can't do it the same way if we want to take out the apostrophe altogether. Hence, a regular expression to do this job.
]

After reading a command:
	remove stray punctuation;
	[ remove apostrophes; ]
	let T be indexed text;
	let T be the player's command;
	replace the regular expression "\[']" in T with "";
	[ this next bit cleans up hyphens, which isn't covered by Emily's extension, although it should be ]
	replace the regular expression "\-" in T with " ";
	change the text of the player's command to T;

Part 40 - Fixing Response For Examining Objects in Adjacent Rooms

[ Objects in adjacent rooms should not be in scope except in very specific circumstances. However, somewhere along the way we created a bug where objects in other rooms are in scope when the player EXAMINES. However, examining something in another room generates an error message -- "You cannot reach into [that room]," which is just plain awful.

I've not been able to figure out exactly what causes this behavior yet, so until I do, here is a patch.

First we disable the basic accessibility rule, because that triggers the error message before any action-specific hooks are available. That leaves examining something in another room totally "blank," with no response at all, allowing us to fill in our own with an instead rule.]

A procedural rule:
	if we are inspecting something then ignore the basic accessibility rule.

Instead of inspecting something when the noun is not a room and the noun is not in the location:
	say "You can't see any such thing."

Part 41 - Multiple Removing (clothes)

[This doesn't really add multiple object grammar to the taking off action. When the player types something like REMOVE HAT AND CLOAK, the game will ask "what do you want to remove those things from?" Of course the obvious answer is MYSELF. This rule ensures that the game will interpret this as taking off the clothes. Tricky, if I may say so.]

Instead of removing something from yourself, try taking off the noun.

Part 42 - Get Down

Understand "get [down]" as going.

Part 43 - Can't Insert Something When It's Already In There Rule

[A check rule for inserting to generate more intuitive responses.]

Check an actor inserting something into (this is the can't insert something when it's already in there rule):
	if the noun is inside the second noun:
		say "[The noun] is already inside [the second noun]!" instead;

The can't insert something when it's already in there rule is listed before the can't insert what's not held rule in the check inserting it into rulebook.

Part 44 - Following

[This is implemented so the player can follow Bobby around during Bobby's adventure.]

Following is an action applying to one visible thing.

Understand "follow" as following. [Otherwise we get nonsensical defaults, usually "the group of mercenaries" or somesuch]
Understand "follow [any leading person]" as following.

Rule for supplying a missing noun while following:
	say "No-one is leading the way.";

Rule for supplying a missing noun while following during Bobby's Adventure:
	change the noun to Bobby;

Check following (this is the can't follow someone who's not here rule):
	if the noun is not in the location, say "You don't see [the noun] here." instead.

Check following (this is the can't follow someone who's not going anywhere rule):
	say "[The noun] isn't going anywhere." instead.

Definition: a person is leading:
	if it is a leader, yes;
	no;

To decide whether (p - a person) is a leader:
	consider the leading rules for p;
	if the rule succeeded, yes;
	no;

The leading rules are an object-based rulebook.

A leading rule for someone (called x):
	if the player can see x,
		rule succeeds; [But 'x' isn't going anywhere.]

Part 45 - Praying

Praying is an action applying to one topic.

Understand "pray [text]" as praying.
Understand "pray" as praying.

Carry out praying:
	say "You mutter a quick prayer under your breath. As usual, there is no reply."

Part 47 - Drinking, Tasting

The new block tasting rule is listed instead of the block tasting rule in the Check Tasting rulebook.

This is the new block tasting rule:
	say "You shouldn't go around putting your tongue on strange objects." instead;

The new block drinking rule is listed instead of the block drinking rule in the Check Drinking rulebook.

This is the new block drinking rule:
	say "You can't drink that!" instead;

Part 48 - Shaking

Shaking is an action with past participle shaken, applying to one thing.

Understand "shake [something]" as shaking.

Check shaking a person:
	say "That would be less than courteous." instead;

Check shaking something fixed in place:
	say "[That or those for the noun] [if the noun is not plural-named]is[otherwise]are[end if] fixed in place." in sentence case instead;

Check shaking scenery:
	say "[That or those for the noun] [if the noun is not plural-named]is[otherwise]are[end if] fixed in place." in sentence case instead;

Report shaking:
	say "Nothing obvious happens.";

Part 49 - Interfering with

Shaking something is interfering. Pulling something is interfering. Pushing something is interfering. Turning something is interfering. 

Part 50 - Thinking

First check thinking:
	say "You rack your brains, but the brilliant ideas fail to materialize." instead;

Part 51 - Jumping Over

Understand "jump over/across [something]" as a mistake ("[jumping mistake].").
Understand "leap over/across [something]" as a mistake ("[jumping mistake].").

Understand "jump off [supporter]" as getting off.
Understand "jump off [text]" as a mistake ("[jumping mistake]. If you want to get off of something, just type GET OFF (whatever).").

To say jumping mistake:
	say "If you want to jump, just type JUMP";

Part 52 - Going nowhere

Printing a refusal to go something is an activity.
The printing a refusal to go activity has a text called the reasoning.

Before printing a refusal to go a direction (called thataway):
	now the reasoning is "";
	if thataway is a planar direction:
		now the reasoning is the dead-end description of the location;

Rule for printing a refusal to go a direction (called thataway):
	if the reasoning is not "":
		say the reasoning, run paragraph on;
	otherwise if thataway is up:
		say "Flight eludes you. [run paragraph on]";
	otherwise:
		say "You can't go that way. [run paragraph on]";

Last after printing a refusal to go:
	say paragraph break;

Check going nowhere:
	carry out the printing a refusal to go activity with the noun instead;

Part 53 - Singing

First check singing:
	say "No way. Your singing is enough to make your friend Holstenoffer wince, and he can stomach anything." instead;

Part 54 - Winding, unwinding

Understand the command "wind" as "turn".
Understand the command "unwind" as "turn".

Part 54 - Handling Hints

Grubbers Hints is a scene. Grubbers Hints begins when Trouble in Grubbers begins. Grubbers Hints ends when Trouble in Grubbers ends.

Commerce Street Hints is a scene. Commerce Street Hints begins when Grubbers Hints ends. Commerce Street Hints ends when Meeting Bobby begins.

Mulling Hints is a scene. Mulling Hints begins when Commerce Street Hints ends. Mulling Hints ends when the location is in Maiden House [region].

Maiden House Hints is a scene. Maiden House Hints begins when Mulling Hints ends. Maiden House Hints ends when the location is not in Maiden House [the region].

Nighttime Hints is a scene. Nighttime Hints begins when Maiden House Hints ends. Nighttime Hints ends when the location is the Clearing.

Lord's Keep Hints is a scene. Lord's Keep Hints begins when Nighttime Hints ends. Lord's Keep Hints ends when Escaping Jail begins.

Escaping Jail Hints is a scene. Escaping Jail Hints begins when Lord's Keep Hints ends. Escaping Jail Hints ends when Escaping Jail ends.

Escaping Sewer Hints is a scene. Escaping Sewer Hints begins when Escaping Jail Hints ends. Escaping Sewer Hints ends when the location is Commerce Street.

Rooftop Hints is a scene. Rooftop Hints begins when Escaping Sewer Hints ends. Rooftop Hints ends when the Search begins.

Search Hints is a scene. Search Hints begins when Rooftop Hints ends. Search Hints ends when the location is East Commerce Street.

Hanging Hints is a scene. Hanging Hints begins when Search Hints ends. Hanging Hints ends when Bobby's Hanging ends.

Raid Hints is a scene. Raid Hints begins when Hanging Hints ends. Raid Hints ends when Shannon's Company begins.

Red Gate Hints is a scene. Red Gate Hints begins when Raid Hints ends. Red Gate Hints ends when DS31 is fired.

Preparing Hints is a scene. Preparing Hints begins when Red Gate Hints ends. Preparing Hints ends when Journey To The Ball Ends.

Dancing Hints is a scene. Dancing Hints begins when Preparing Hints ends. Dancing Hints ends when High Society ends.

Confrontation Hints is a scene. Confrontation Hints begins when Dancing Hints ends. Confrontation Hints ends when the Skirmish begins.

Skirmish Hints is a scene. Skirmish Hints begins when Confrontation Hints ends. The Skirmish ends when the Entire Game ends.

When Commerce Street Hints begins:
	carry out the sending hints activity;

When Mulling Hints begins:
	carry out the sending hints activity;

When Maiden House Hints begins:
	carry out the sending hints activity;

When Nighttime Hints begins:
	carry out the sending hints activity;

When Lord's Keep Hints begins:
	carry out the sending hints activity;

When Escaping Jail Hints begins:
	carry out the sending hints activity;

When Escaping Sewer Hints begins:
	carry out the sending hints activity;

When Rooftop Hints begins:
	carry out the sending hints activity;

When Search Hints begins:
	carry out the sending hints activity;

When Hanging Hints begins:
	carry out the sending hints activity;

When Raid Hints begins:
	carry out the sending hints activity;

When Red Gate Hints begins:
	carry out the sending hints activity;

When Preparing Hints begins:
	carry out the sending hints activity;

When Dancing Hints begins:
	carry out the sending hints activity;

When Confrontation Hints begins:
	carry out the sending hints activity;

When Skirmish Hints begins:
	carry out the sending hints activity;

Sending hints is an activity.

Rule for sending hints during Grubbers Hints:
	Send the Trouble-In-Grubbers-Hints;

Rule for sending hints during Commerce Street Hints:
	Send the Investigating-Hints;

Rule for sending hints during Mulling Hints:
	Send the Mulling-What-To-Do-Hints;

Rule for sending hints during Maiden House Hints:
	Send the Sneaking-One-Hints;

Rule for sending hints during Nighttime Hints:
	Send the Bobby-Adventure-One-Hints

Rule for sending hints during Lord's Keep Hints:
	Send the Bobby-Adventure-Two-Hints;

Rule for sending hints during Escaping Jail Hints:
	Send the Escaping-Jail-Hints;

Rule for sending hints during Escaping Sewer Hints:
	Send the Escaping-Sewer-Hints;

Rule for sending hints during Rooftop Hints:
	Send the Rooftops-Hints;

Rule for sending hints during Search Hints:
	Send the Secret-Letter-Hints

Rule for sending hints during Hanging Hints:
	Send the Gallows-Hints

Rule for sending hints during Raid Hints:
	Send the Sneaking-Two-Hints

Rule for sending hints during Red Gate Hints:
	Send the Red-Gate-Hints;

Rule for sending hints during Preparing Hints:
	Send the Preparations-Hints;

Rule for sending hints during Dancing Hints:
	Send the Sneaking-Three-Hints;

Rule for sending hints during Confrontation Hints:
	Send the Ballroom-Hints;

Rule for sending hints during Skirmish Hints:
	Send the War-Room-Hints;

Part 55 - Restoring After Undo

After restoring after undo:
	carry out the assembling activity;
	carry out the character-listing activity;
	carry out the displaying the menu activity;

After restoring after undo:
	carry out the sending hints activity;

Part 56 - Image updates

When play begins:
	update the current image;

Last every turn rule:
	update the current image;

After restoring after undo:
	update the current image;

An image-changing rule during Trouble In Grubbers:
	change the image currently being displayed to "GrubbersChase";

An image-changing rule when the location is Entrance to Maiden House or the location is Behind Maiden House:
	change the image currently being displayed to "MaidenHouse";

An image-changing rule when the location is in the Maiden House:
	change the image currently being displayed to "MaidenHouse";

An image-changing rule when the location is the Back Alley and Entrance to Maiden House is unvisited:
	change the image currently being displayed to "MeetingBobby";

An image-changing rule when the location is Market Square and the player can see Bobby:
	change the image currently being displayed to "EmptyGrubbers,JackInDisguise";

An image-changing rule when the location is Market Square and the player can see Pieter:
	change the image currently being displayed to "EmptyGrubbers,JackInBallGown";

An image-changing rule when the location is the Crossing during Bobby's Adventure:
	change the image currently being displayed to "StreamWithBobby,JackInDisguise";

An image-changing rule when the location is the Crossing during the Bodyguard Scene:
	change the image currently being displayed to "StreamWithPieter,JackInBallGown";

An image-changing rule when the location is the Clearing during Bobby's Adventure:
	change the image currently being displayed to "FountainWithBobby,JackInDisguise";

An image-changing rule when the location is the Clearing during the Bodyguard Scene:
	change the image currently being displayed to "FountainWithPieter,JackInBallGown";

An image-changing rule when the location is in the Tunnels and the player can see Pieter:
	change the image currently being displayed to "Tunnel,JackInBallGown";

An image-changing rule when the location is in the Tunnels:
	change the image currently being displayed to "Tunnel,JackInDisguise";

An image-changing rule when the location is the Chapel and the player can see Bobby:
	change the image currently being displayed to "Chapel,JackInDisguise";

An image-changing rule when the location is the Chapel and the player can see Bobby:
	change the image currently being displayed to "Chapel,JackInBallGown";

An image-changing rule when the location is the Guardhouse:
	change the image currently being displayed to "Guardhouse,JackInDisguise";

An image-changing rule during Bobby's Adventure:
	change the image currently being displayed to "JackInDisguise";

An image-changing rule when the location is in the Prison Region:
	change the image currently being displayed to "InJail";

An image-changing rule when the location is in the Sewers Region:
	change the image currently being displayed to "Sewer,JackInDisguise";

An image-changing rule when the location is in the Rooftops Region:
	change the image currently being displayed to "Rooftops,JackInDisguise";

An image-changing rule when the location is the Library and the player has the Secret Letter:
	change the image currently being displayed to "SecretLetter,JackInDisguise";

An image-changing rule when the location is in the Black Gate Estate Region:
	change the image currently being displayed to "BlackGateEstate,JackInDisguise";

An image-changing rule when the location is Lords Market during Bobby's Hanging:
	change the image currently being displayed to "Gallows";

An image-changing rule when the location is in the Red Gate Estate Region and the player is not wearing the daydress:
	change the image currently being displayed to "RedGateEstate,JackInDisguise";

An image-changing rule when the location is the Ballroom:
	change the image currently being displayed to "TheBall,JackInBallGown";

An image-changing rule when the location is the War Room:
	change the image currently being displayed to "WarRoom,JackInBallGown";

[Finally, the drop-through defaults:]

An image-changing rule when the player wears the daydress:
	change the image currently being displayed to "JackInSimpleDress";

An image-changing rule when the player wears the ball gown:
	change the image currently being displayed to "JackInBallGown";

An image-changing rule:
	change the image currently being displayed to "JackInDisguise";

Book 1 - Prologue

Part 1 - Jack Toresal

Chapter 1 - Jack herself

Section - Appearance

Instead of examining the player, say "Most people know you as 'Jack,' a fourteen-year-old street urchin of uncertain means. You are slight of build for a boy your age, though your baggy clothes hide much of your physique. Your shoulder-length hair is ragged, a victim of many self-inflicted trimmings. Though usually in need of a good scrubbing, you are an attractive youth, with a softness to your face that makes you seem younger than your years.[paragraph break]Fortunately, it also leads people to underestimate your wiles and resourcefulness, which have caused more than one stallkeeper to call you by your second most common sobriquet, 'Lousy thief!'[paragraph break]Only a few people know you by your real name, and you are pleased to keep it that way."
Understand "Jack", "Jacqueline" as yourself.
Understand "Jack Toresal", "Jacqueline Toresal" as yourself.

Instead of examining the player when Trouble in Grubbers has ended and Escaping Jail has not happened:
	say "You are Jack, a fourteen-year-old street urchin with a reputation for thievery and a talent for getting out of tough scrapes. Only a few people are aware that your real name is Jacqueline, and that your ever-present cap is not just a fashion statement, but part of your disguise.[paragraph break]You're still a bit rattled after your escape from Grubber's Market. Best not try anything too tricky for a while."

Instead of examining the player when Escaping Jail has happened and Escape Through The Sewer has not happened:
	say "You've definitely been in better shape. And it looks like your disguise is shot, too. Well, it was fun while it lasted.";

Instead of examining the player when Escape Through the Sewer has happened and the player is not clean:
	say "You are utterly filthy.";

Instead of examining the player when the player is clean:
	if the player is wearing the ball gown, say "It's kind of embarassing, but you really do look like a princess.";
	otherwise say "With all the dirt and grime scrubbed away, you hardly look anything like the urchin you once were. You're not Jack anymore; you are Jacqueline Toresal, daughter of the Duke."

Instead of smelling the player, say "You smell like you haven't had a bath in a long, long time. Which, to be honest, you haven't."

Instead of smelling the player when Escape Through the Sewer has happened and the player is not clean:
	say "You already needed a bath before wading through the sewers... now you would [i]kill[r] for one."

Instead of smelling the player when the player is clean:
	say "You smell clean for the first time in ages."

Chapter 2 - An apple

[ See "Grubbers Market Fruit Stall" ]

Chapter 3 - Clothing

Section 1 - Some baggy old clothes

The baggy threadbare clothes are a thing. The player wears the baggy threadbare clothes. The description of the baggy threadbare clothes is "Your clothing is baggy and threadbare." The printed name of the baggy threadbare clothes is "clothes". The baggy threadbare clothes are plural-named.

Understand "baggy", "patchy", "threadbare", "clothing", "pants", "shirt" as the baggy clothes.

Instead of taking off the baggy clothes:
	say "This isn't the time or place for getting undressed!";

Include (-
	with articles "Your" "your" "your",
-) when defining the baggy threadbare clothes.

Section 2 - A gray cloth cloak

The player wears a gray cloak. [The gray cloak is proper-named.] The description of the cloak is "Your cloak is made of undyed wool, stained and patched in several places. You wear it in the masculine style, fastened on the side and thrown back over your right shoulder." Understand "grey", "gray", "old", "masculine", "wool/woolen/woollen", "undyed", "stained", "patched", "patchy", "patch", "patches", "stain", "stains", "my" as the cloak. The printed name of the cloak is "old gray cloak". 

Include (-
	with articles "Your" "your" "your",
-) when defining the gray cloak.

Chapter 4 - A cloth satchel

The player carries a cloth satchel. The satchel is a player's holdall. The description is "Your trusty cloth satchel: big enough to hold the things you nick, small enough to not hamper your getaway." The printed name is "satchel". Understand "bag", "sack", "purse" as the cloth satchel.

Include (-
	with articles "Your" "your" "your",
-) when defining the cloth satchel.

The satchel is open, openable, transparent. The satchel is not wearable.

Last before inserting something into the closed satchel:
	say "(opening the satchel first)[command clarification break]";
	try silently opening the satchel;

Last before removing something from the closed satchel:
	say "(opening the satchel first)[command clarification break]";
	try silently opening the satchel;

Before searching the closed satchel:
	say "(opening it first)[command clarification break]";
	try silently opening the satchel;

After inserting something into the satchel: say "You stuff [the noun] into your satchel.";

Chapter 5 - A woolen hat

The player wears a thing called your woolen hat. The description of the woolen hat is "Your woolen cap is patchy and stained, like the rest of you. You usually keep your hair stuffed up under it." Understand "woolen/woollen", "wool", "cap", "patched", "patchy", "patch", "patches", "stained", "stain", "stains", "my" as the hat. 

Include (-
	with articles "Your" "your" "your",
-) when defining the woolen hat.

The printed name of the woolen hat is "your hat".

After wearing the woolen hat:
	say "You pull the cap down over your head and stuff your hair up into it."

Instead of taking off the woolen hat in the presence of Bobby:
	say "No [i]way[r] are you going to take your cap off around Bobby!";

Instead of taking off the woolen hat when Trouble in Grubbers has ended and Escaping Jail has not happened and the location is not in Maiden House:
	say "Going around without your cap on makes you nervous; it's a lot harder to pretend to be a boy with your hair hanging down around your ears."

[ Instead of taking off something that is not worn:
	say "You're not wearing that." instead; [Library message duplication ~ taking off the hat in the presence of Bobby *while not wearing the hat* triggers the previous rule.] ]

After taking off the woolen hat:
	say "You take the cap off and shake your hair out.";

[ Instead of taking off the hat when we are nervous about our disguise:
	say "Going around without your cap on makes you nervous; it's a lot harder to pretend to be a boy with your hair hanging down around your ears." ]

[ To decide whether we are nervous about our disguise:
	if the mercenary captain is distracted, no;
	if the location is Grubbers Market Teishas Tent or the location is Grubbers Market Center Post or the location is Grubbers Market Top of Center Post, no;
	if the location is Maiden House, no;
	[otherwise] yes. ]

Does the player mean wearing or taking off the woolen hat:
	it is very likely;

Does the player mean doing something with the woolen hat:
	it is unlikely;

Does the player mean taking the woolen hat:
	it is very unlikely;

Chapter 6 - Bodily parts

Your body is part of the player. [Your body is an inform library animate object.] Understand "body", "build", "physique", "head", "face", "hair", "arms", "arm", "shoulders", "shoulder", "elbows", "elbow", "wrists", "wrist", "hands", "hand", "fingers", "finger", "fingernails", "fingernail", "chest", "stomach", "belly", "torso", "back", "waist", "hips", "hip", "legs", "leg", "knees", "knee", "thighs", "thigh", "shins", "shin", "ankles", "ankle", "feet", "foot", "toes", "toe", "toenails", "toenail", "mouth", "nose", "eye", "eyes", "ear", "ears", "face", "left", "right" as the body.

Include (-
	with articles "Your" "your" "your",
-) when defining your body.

The indefinite article of your body is "your".

The description of your body is "[first time]All your parts are where they should be. [only]If you want to know what you look like, just examine yourself or look in a mirror."

Before doing something other than examining when the noun is your body and TRIG_BODY is unfired:
	fire TRIG_BODY instead;

Before doing something other than examining when the second noun is your body and TRIG_BODY is unfired:
	fire TRIG_BODY instead;

TRIG_BODY is a quip. The display text is "If you want to do something to yourself, just refer to [']me['] or [']myself['].".

First before doing something other than examining when the noun is your body and TRIG_BODY is fired:
	change the noun to the player;

First before doing something other than examining when the second noun is your body and TRIG_BODY is fired:
	change the second noun to the player;

Instead of waving the player:
	try waving hands;

Part 2 - Alleyway Introduction

When play begins:
	Send the Trouble-In-Grubbers-Hints;
	select theme "GrubbersMarket";
	select the prologue channel;
	say "Mid-morning always sees the Grubber's Market at its most crowded. Servants from the aristocratic districts and villagers from outside Toresal crush themselves into the maze of stalls, hoping to beat the noontime heat and get the farmers[apostrophe] produce while it's still fresh. The press of bodies, the din of haggling and bargains struck — it all makes getting around unnoticed easier, although not necessarily more pleasant. No fewer than half a dozen stall-gazing bumpkins have trod on your feet already this morning. You've kept your mouth shut, though. People remember an urchin who gives lip.[paragraph break]'See anything you like?' asks the silk seller, with a teasing smile. You smile back. You couldn’t afford one of her cloaks without nicking a fat purse first, and Teisha doesn’t approve of you stealing from people, even when they’re not her customers.[paragraph break]'Oh, these are [i]much[r] too plain,' you scoff, even as you gaze wistfully at the bright colors, the beautiful embroidery. Time for a bit of breakfast, you decide, and anyway if you linger too long, people will wonder what a boy is doing shopping for women's cloaks. 'Perhaps I'll come back this afternoon, when you've put out your better wares.'[paragraph break]Teisha laughs. 'Don't be a stranger, Jack.'[paragraph break]You make your way through the market, nicking an apple from the fruit stall while its owner argues with a fat Easterner over local politics. Everyone seems to be talking politics these days, you've noticed. Everywhere people are heatedly discussing Baron Fossville's tax policies or something called the 'Ascension' — whatever that is. Boring stuff, really. Finally you reach a quiet alley where you can catch a breath from the crowds and enjoy your apple in peace.[paragraph break]You settle yourself on a crate and take a bite of your apple. Suddenly, from the alley's entrance, you hear voices.";
	output chapter heading "Chapter 1 - Grubber's Market";

Chapter 1 - The Alley

The Alley is a room. "This narrow alley is tucked away between a storehouse on one side and the canvas wall of a stallkeeper's tent on the other. It serves primarily as somewhere out-of-the-way for stallkeepers to sweep their garbage, so it's not exactly the most pleasant-smelling place in all of Miradania, but at least no one is likely to bother you here. You can leave to the southeast." The dead-end description is "This alley is a dead end. The only way out is to the southeast."

Instead of facing outside in the Alley:
	try facing southeast instead;

Instead of exiting in the Alley when nothing supports the player:
	try going southeast instead;

Instead of going outside in the Alley:
	try going southeast instead;

After looking in the Alley for the first time, say "A group of people have gathered just outside the alley. You can't quite hear what they're saying from here, but there is a grim, ugly tone to them that speaks of dangerous doings.";

Rule for printing the distant description of the Alley when in Grubbers Market Northwest Junction:
	say "The entrance to a narrow alley lies northwest. ";

Rule for printing the distant description of the Alley when in Outer Market Roof:
	say "You can climb back down the crates to the alley below. ";

Understand "narrow" and "alley" as the Alley.
Understand "entrance" as the Alley when the location is Grubbers Market Northwest Junction.

Section 1.2 - Contents (Crates)

Some wooden crates are an enterable supporter, in the Alley. The printed name is "crates". "Several crates are stacked up against the back wall of the alley." The description of the crates is "Just ordinary wooden crates, used by merchants throughout Miradania to store and ship goods. There are scores more just like these to be found all over the market." Understand "crate", "box", "boxes", "wood", "stack", "stack of", "closed", "nailed", "shut", "ordinary", "shipping" as the crates.

The crates can be openable. The crates can be open. The crates are openable, not open.

Instead of raising or taking or pushing the crates, say "Oof! What's in these things, gold bricks? At any rate, the crates are far too heavy for you to move."

Instead of looking under the crates:
	say "[first time](lifting them up first, in order to see)[command clarification break][only]";
	try raising the crates;

Instead of opening or searching the crates, say "The crates are all nailed shut."

Rule for supplying a missing noun when climbing in the alley:
	change the noun to the crates;
	say "(up the crates)[command clarification break]";

Instead of climbing the crates:
	if the location is the Alley begin;
		try going up instead;
	otherwise;
		try going down instead;
	end if;

Instead of standing on the crates: 
	try climbing the crates; 

Instead of entering the crates:
	try opening the crates;

Some nails are part of the crates. The nails are plural-named.

Section 3 - Scenery (Walls)

The storehouse wall is scenery, in the Alley. The printed name is "storehouse". The description is "The wall of the storehouse is too high and too smooth to climb."

Understand "high", "warehouse", "store", "house", "back" as the storehouse wall.

Instead of climbing the wall, try examining the wall.

The canvas tent is a backdrop. It is in the Alley and in Grubbers Market Center Post. The description is "The canvas wall of the tent is tough and stretched tight." 

Understand "stall", "stallkeeper's", "stallkeepers", "stallkeeper", "keeper's", "keepers", "smooth", "stretched", "tough", "tight", "wall" as the canvas tent.

Instead of climbing the canvas tent:
	say "You can't climb up the smooth, tight canvas." 

Section 4 - Scenery - Garbage

Some piles of garbage are scenery, in the Alley. "Little piles of garbage litter the alley. Some of it has been sitting back here for weeks."

Understand "trash", "litter", "refuse", "rubbish", "pile", "little" as the piles of garbage

Instead of smelling the garbage:
	say "The garbage smells pretty ripe.";

Instead of smelling the location in the presence of garbage:
	say "It smells pretty ripe back here.";

Instead of doing something other than examining or smelling the garbage:
	say "You don't feel much like digging through garbage.";

Section 5 - Exits

Up from the Alley is the Outer Market Roof.

Instead of going to the Outer Market Roof from the Alley when the wandering mercenaries are nearby:
	now the wandering mercenaries are grabbing;
	the leader arrives in two turns from now;
	say "You try to scramble up the crates, but one of the mercenaries grabs your ankle and drags you back down!";

Before going to the Outer Market Roof from the Alley:
	if the player is on the crates begin;
		try silently exiting;
	end if;

After going to the Outer Market Roof from the Alley:
	say "You nimbly hop up on top of the crates; from there, it's only a short scramble to the roof of the market.";
	continue the action;

Instead of going southeast in the Alley when the mercenary mob is on-stage:
	say "Those men are still talking right outside the alley. [one of]Something — an intuition? — tells you it would be a good idea to stay out of sight for the moment.[or]Better stay out of sight until they leave.[stopping]";

Chapter 2 - The Rooftop

Section 1 - Description

The Outer Market Roof is a privately-named room. "You're perched atop the red tile roof of the storehouse. From here you have a good view of the market's northwest junction and Lord's Road leading out of town to the north." [Outside from the Outer Market Roof is the Alley. ] The distant description of the Outer Market Roof is "You cannot see up to the roof from down here." The dead-end description of the Outer Market Roof is "Around you there are more tiled rooftops, too steep for you to climb."

Understand "roof", "outer market", "market roof/rooftop", "market roof top", "rooftop", "roof top" as the Outer Market Roof.

Instead of examining the backdrop-floor in the Outer Market Roof, try inspecting the Alley.

Outer Market Roof overlooks Grubbers Market Northwest Junction.
Outer Market Roof overlooks Lords Road.

Instead of facing southeast in the Outer Market Roof,
	try inspecting Grubbers Market Northwest Junction;

Instead of facing north in the Outer Market Roof,
	say "Off to the north you can see Lord's Road leading away from the market.";

Instead of going outside in the Outer Market Roof:
	try going down instead;

Section 2 - Contents - Group of Mercenaries

A group of mercenaries are people, male, in the Outer Market Roof. "A group of tough-looking mercenaries has gathered at the Northwest Junction. One of them is barking orders at the others in an angry voice.". The description is "They're not Lord's Guards, that's for sure. They wear no insignia, for one thing. Their arms and armor are mismatched, ugly, and scarred — not an unfit description of the men themselves, either. Also, while the Guard would be concerned with keeping the peace in Grubber's Market, these bravos look ready to clobber anyone who gets in their way, and maybe burn down a few stalls while they're at it.[first time][paragraph break]So what are they doing here, you wonder? And come to think of it, where are the Lord's Guards, anyway?[only]".

To decide whether (x - the group of mercenaries) takes a plural pronoun:
	yes;

Understand "people", "person", "soldiers",  "men", "bravos", "guards", "mercs" as the group of mercenaries.

Rule for printing the name of the group of mercenaries when in the Alley:
	say "group of men";

Rule for clarifying the parser's choice of the group of mercenaries:
	do nothing;

[ Does the player mean doing something with the group of mercenaries: it is likely. ]

Before writing a paragraph about the group of mercenaries:
	if the location is the alley begin;
		now the group of mercenaries is scenery;
	otherwise;
		now the group of mercenaries is not scenery;
	end if;

Instead of listening to the group of mercenaries: try listening instead. [ We will hear their next line of dialogue anyway. ]

[The Mercenaries are in scope from the Alley.]
After deciding the scope of the player while in the Alley:
	if the group of mercenaries is on-stage,
		place the group of mercenaries in scope;
	if the mercenary captain is on-stage,
		place the mercenary captain in scope;

A rule for reaching inside the outer market roof when in the Alley:
	allow access.

Instead of doing something other than examining or listening to the group of mercenaries:
	if the location is the alley:
		say "You can't do anything to the men while they're out there and you're in this alley.";
	otherwise if the location is the Outer Market Roof:
		say "You can't do anything to the mercenaries while they're down on the ground and you're up on this roof.";
	otherwise:
		say "Somehow, you think it might be unwise to attract their attention.";

[Persuasion rule for asking the group of mercenaries to try doing something:
	say "Somehow, you think it might be unwise to attract their attention.";
	persuasion fails. ]

Instead of throwing something at the group of mercenaries:
	say "Somehow, you think it might be unwise to attract their attention.";

Instead of examining the group of mercenaries when in the Alley:
	say "You'd have to poke your head out of the alley to get a good look at the men. You only know they're there because you can hear them talking.";

After assembling when in the Outer Market Roof:
	[not a valid target of conversation]
	remove the group of mercenaries from the roster of available characters, if present;
	remove the mercenary captain from the roster of available characters, if present;

Section 3 - Contents - Mercenary Captain

The mercenary captain is a man, scenery, in the Outer Market Roof.

The mercenary captain can be wise to our tricks.
The mercenary captain is not [initially] wise to our tricks.

The description of the mercenary captain is "The leader of the mercenaries looks a bit older and a bit more grizzled than the rest. He also looks smarter. You'll have a hard time fooling him, if it comes to that."

Understand "man", "leader", "bravo", "person", "soldier", "mercenary", "merc", "captain", "guard", "old", "older", "grizzled", "smart", "smarter" as the mercenary captain.

The description of the mercenary captain is "The leader of the mercenaries looks a bit older and a bit more grizzled than the rest. He also looks smarter. You'll have a hard time fooling him, if it comes to that."

Instead of examining the mercenary captain when in the Alley:
	say "You can't get a good look at the mercenary leader from inside the alley.";

Before doing something other than examining to the mercenary captain:
	change the noun to the group of mercenaries;

Section 3.5 - Contents - Mercenary Arms and Armor

Some arms are part of the group of mercenaries.

The description of the arms is "The mercenaries are each wearing a mail shirt and carrying wicked-looking swords."

Instead of doing something other than examining the arms, say "They are too far away."

Understand "mail", "chainmail", "chain", "shirt", "shirts", "weapon", "weapons", "equipment", "sword", "swords", "wicked", "looking", "sharp", "blade", "blades" as the arms.

Section 4 - Scenery - Tiles

Some tiles are scenery, in the Outer Market Roof. The description is "The roof tiles are warm under the morning sun." Understand "red", "tile", "shingle", "shingles", "rooftop", "storehouse", "roof", "top of roof", "store", "house" as the tiles.

Instead of touching the tiles,
	try examining the tiles.

Section 5 - Scenery - View

The view of town is scenery, in the Outer Market Roof. The description is "You have a good view of the market's northwest junction and Lord's Road leading out of town to the north." The printed name is "view". Understand "vista", "landscape", "Miradan", "Miradania", "Toresal", "city" as the view of town.

Section 6 - Exits

Below the Outer Market Roof is the Alley.

Instead of going nowhere from the Outer Market Roof, say "The rooftop is steep and slick, and the tiles have been known to come loose and fall off. It's precarious enough just staying here in one spot. If you're to make your escape, you'll have to make it down on the ground."

Section 7 - Scenery - Market Gates - Out of scope

After deciding the scope of the player when in Outer Market Roof:
	place the market gates in scope;

Section 8 - Scenery - Crates out of scope

A rule for reaching inside the alley when in Outer Market Roof:
	allow access.

After deciding the scope of the player when in Outer Market Roof:
	place the crates in scope;

Instead of doing something other than examining or climbing or descending to the crates when in Outer Market Roof:
	say "You'll need to climb down before you can do anything to the crates.";

Section 9 - Event on exit

After going down from Outer Market Roof for the first time:
	select theme "GrubbersChase";
	say "As you climb back down the crates, one of them wobbles and your foot slips. You tumble loudly to the cobblestones. 'Stupid! Clumsy!' you mutter, scrambling to your feet.[paragraph break]From the street, you hear, 'What was that? Check the alley!'[paragraph break]Two mercenaries rush in and spot you immediately. They approach warily, their arms spread wide.[paragraph break]You're in trouble now...";
	move the wandering mercenaries to the location;
	now the wandering mercenaries are approaching;
	update the character list;

After going down from Outer Market Roof [subsequent times]:
	say "Carefully, you descend the stack of crates.";
	continue the action;

Chapter 3 - Eavesdropping on Soldiers

Section 1 - Beginning and ending

Trouble in Grubbers is a scene. Trouble in Grubbers begins when play begins. Trouble in Grubbers ends when Final Chase ends.

Eavesdropping on Soldiers is a scene. Eavesdropping on Soldiers begins when play begins. Eavesdropping on soldiers ends when the wandering mercenaries are in the alley or the table of overheard mercenary utterances is empty.

When Eavesdropping on Soldiers ends:
	remove the group of mercenaries from play;
	remove the mercenary mob from play;
	remove the mercenary captain from play;
	update the character list;
	now the market gates are closed; now the market gates are locked;

Avoiding Soldiers is a scene. Avoiding Soldiers begins when Eavesdropping on Soldiers ends. Avoiding Soldiers ends when Final Chase begins.

[At the time when the mercenaries check the alley:
	if the location is the alley and the wandering mercenaries are not in the alley begin;
		say "You are still wondering what you should do when a voice from the street calls out, 'Hey, you checked that alley yet? Let's give it a look.'[paragraph break]Two mercenaries enter the alley and spot you immediately. They approach warily, their arms spread wide.[paragraph break]You're in trouble now...";
		move the wandering mercenaries to the alley;
		now the wandering mercenaries are approaching;
	end if; ]

Section 3.2 - Conversations overheard

The conversation is a backdrop, in the Alley and Outer Market Roof.
Understand "voice", "voices", "word", "words", "talking" as the conversation.
Understand "barking", "growling", "barks", "growls", "orders", "order" as the conversation when the location is Outer Market Roof. 

Instead of listening to the conversation:
	try listening to the location instead;

Instead of examining the conversation:
	try listening to the location instead;

Every turn when the Alley encloses the player during Eavesdropping on Soldiers:
	let x be a random number between 1 and 4;
	if x is 1,
		say "The [one of]men[or]voices[at random] [one of]are still talking[or]continue talking[or]continue their conversation[at random] [one of]outside[or]just outside[or]at the entrance to[at random] the alley.";
	if x is 2,
		say "The [one of]men[or]voices[at random] [one of]outside[or]just outside[or]at the entrance to[at random] the alley [one of]are still talking[or]continue talking[or]continue their conversation[at random].";
	if x is 3,
		say "You can still hear the [one of]men[or]voices[at random] [one of]outside[or]just outside[or]at the entrance to[at random] the alley [one of]talking[or]talking[or]continue their conversation[at random].";
	if x is 4,
		say "You can still hear the [one of]men[or]voices[at random] [one of]talking[or]talking[or]continue their conversation[at random] [one of]outside[or]just outside[or]at the entrance to[at random] the alley.";

Every turn when the player is in the Outer Market Roof during Eavesdropping on Soldiers:
	if the table of overheard mercenary utterances is not empty begin;
		spool through dialogue in the table of overheard mercenary utterances;
		say "[paragraph break]";
		if the table of overheard mercenary utterances is [now] empty begin;
			remove the group of mercenaries from play; 
			remove the conversation from play;
			update the character list;
		end if;
	end if;

Instead of listening when the location is the Alley during Eavesdropping on Soldiers: say "You strain to hear what the men are saying, but can't make out the words. Maybe if you got some elevation, you could hear better."

Instead of listening when the location is the Outer Market Roof during Eavesdropping on Soldiers: say "You listen carefully to what the mercenaries are saying..."

Table of overheard mercenary utterances
text
"'...we know he skulks Grubber's most mornings, so spread out quick, cover all streets from the square,' growls one of them, evidently the group's leader."
[row marker]
"The leader spits on the dusty ground. 'Keep your eyes open and don't take guff. But for the love of Brigid, keep your tempers and your swords sheathed unless you really need [']em. Ain't bein['] paid to toss the locals.'"
[row marker]
"'And keep sharp!' the leader snaps. 'Remember this is a kid we're lookin['] for. Kids move fast, stay out of sight, slip through crowds easy. I find out later he got away under your nose, I'll have it off!'[paragraph break]Suddenly, that bit of apple you swallowed a minute ago turns into a cold, hard chunk in your belly."
[row marker]
"'Now, everyone memorize the description.' The leader pulls a dirty, folded bit of parchment from his belt and squints at it. 'Shoulder-length brown hair,' he reads, haltingly. 'Green eyes. Slight of build. Ten, maybe twelve spans high.' He holds his hand out flat, at what happens to be exactly your height. 'Usually wears a hat and a gray cloak.'[paragraph break]He refolds the parchment and jams it back into his belt. 'You dims got all that?'[paragraph break]The others nod."
[row marker]
"'Okay, go!' barks the leader, and the mercenaries set out in several directions, moving at a brisk walk. Two men go to the north gates and start shoving them closed; the others enter the crowded market and start shoving their way through the suddenly flabbergasted crowds.[paragraph break]Your heart sinks. This is [i]definitely[r] not good."

Book 2 - Grubber's Market

Part 2 - Commerce and barter in Grubber's Market

Chapter 1 - Definitions

Section 1 - Merchandise

Things can be merchandise.

Section 2 - Shopping places

A shop is a kind of room. 
A market stall is a kind of shop. A market stall can be blocked or unblocked. A market stall is usually unblocked.

After going to a blocked room:
	say "[command clarification break]";
	try looking;
	say "As soon as you get near, the stallkeeper spots you and starts yelling again. Before the idiot can bring the mercenaries right to you, you dart away through the crowd.";
	let x be a random adjacent unblocked room;
	if the number of adjacent unblocked rooms is greater than 1 begin;
		while x is the same location as last time
			repeatedly let x be a random adjacent unblocked room;
	end if;
	move the player to x;
	update the character list;

Chapter 2 - Generic Stallkeepers

Section 1 - Definition

A stallkeeper is a kind of person. A stallkeeper is usually scenery. The description of a stallkeeper is usually "The stallkeeper looks nervous and irritable. Armed thugs stomping around the Grubber's Market is not good for business." 

[Stallkeepers can be described by their wares -- e.g., a "fruit merchant" or a "weapon seller". However, we don't want that to clash with simply referring to "fruit" or "rope", so we'll use a disambiguation rule.]

Rule for printing the name of a stallkeeper:
	say "stallkeeper";

Does the player mean doing something with a stallkeeper: it is unlikely.

Understand "stallkeeper", "shopkeeper", "merchant", "seller", "stall/shop keeper", "merchant", "person", "vendor", "man", "salesman", "sales man" as a stallkeeper.

Instead of smelling a stallkeeper:
	say "The stallkeepers are faring slightly better than the shoppers in the body odor department, if only because they can sit behind their stalls and don't have to press up with the crowds quite so much.";

Instead of listening to a stallkeeper:
	say "The stallkeeper is alternating between hawking his wares, haggling with customers, and muttering about the general stinginess of the crowds.";

Instead of kissing a stallkeeper, say "You're pretty sure the stallkeeper wouldn't appreciate that."

Every turn when the player can see a stallkeeper and a random chance of 1 in 2 succeeds and Final Chase is not happening:
	add ATMOS_sales_pitch to the dramatic possibilities, if absent;

ATMOS_sales_pitch is a dramatic event.

For firing ATMOS_sales_pitch:
	if a plural-named stall display is in the location:
		let X be a random plural-named stall display in the location;
		say "[one of]'[sales pitch for X]!' calls the stallkeeper.[or]The stallkeeper haggles with a customer over the price of [X].[or]The stallkeeper carefully straightens his display of [X].[at random]";

To say sales pitch for (X - a stall display):
	say "[one of]I have [X] for sale[or]Get your [X] right here[or]Fine quality [X][or]Fine quality [X] for sale[or]You'll not find better [X] in all of Toresal[at random]";

Section 2 - Generic Stallkeeper Conversations

Instead of talking to a stallkeeper in the presence of oblivious wandering mercenaries:
	say "Better find somewhere else to chat; if you start making conversation, the mercenaries might take notice.";

Instead of talking to a stallkeeper in the presence of [approaching or grabbing] wandering mercenaries:
	say "No time to chat now; you've got to run!";

Rule for initiating conversation with a stallkeeper:
	change the chosen opening gambit to ST1;

ST1 is a quip. The display text is "'What do you want?' snaps the stallkeeper." 

ST2 is a transitional quip. The menu text is "'What do you have for sale?" The display text is "'So, what are you selling today?' you ask, trying to sound casual." The following quip is ST5.

ST3 is a quip. The menu text is "'Do you know anything about these mercenaries?'" The display text is "'You know anything about these mercenaries running around Grubber's?' you ask.[paragraph break]'Only that they're bad for business,' mutters the stallkeeper. 'I don't know who hired them, but the Lord's Guard ought to run them off.'" 

ST4 is a transitional quip. The menu text is "'I'd like to buy something, please.'" The display text is "'I'd like one of those, please,' you say, pointing." The following quip is ST5.

ST5 is a quip. The display text is "The merchant sneers at you. 'You have any coin?'" 

ST6 is a quip. The menu text is "'Yes.'" The display text is "'Yes,' you respond indignantly, 'and lots of it.'[paragraph break]The stallkeeper snorts derisively. 'If you've half a penny that isn't stolen from somewhere, I'll sell my stall to the monkey. Now go bother someone else before I call those mercenaries over here myself!'"

ST7 is a quip. The menu text is "'No.'" The display text is "'Um, no,' you say.[paragraph break]The stallkeeper rolls his eyes. 'Do I look like I have time to gab at you all day? Beat it, kid.'"

ST8 is a quip. The menu text is "'Never mind.'" The display text is "'Uh, never mind.'[paragraph break]'Whatever,' the stallkeeper mutters, and goes back to nervously scanning the crowd."

ST9 is a quip. The menu text is "'Good-bye.'" The display text is "'Yeah, fine, scram,' the stallkeeper mutters."

The response of ST1 is { ST2, ST3, ST4, ST9 }.

The response of ST3 is { ST2, ST4, ST9 }.

The response of ST5 is { ST6, ST7, ST8 }.

After populating ST1:
	if ST2 is fired:
		remove ST2 from the current conversation, if present;
	if ST3 is fired:
		remove ST3 from the current conversation, if present;
	if ST4 is fired:
		remove ST4 from the current conversation, if present;

After populating ST3:
	if ST2 is fired:
		remove ST2 from the current conversation, if present;
	if ST4 is fired:
		remove ST4 from the current conversation, if present;

Section 3 - Impatient stallkeepers

Stallkeeper patience count is a number that varies.

Every turn when the player is on the move in a shop:
	change the stallkeeper patience count to 0.

Before talking to a stallkeeper:
	increment the stallkeeper patience count by 1;
	continue the action;

After talking to a stallkeeper when the stallkeeper patience count is 2:
	say "The stallkeeper suddenly looks exasperated. 'Didn't I tell you to take it somewhere else?'";

After talking to a stallkeeper when the stallkeeper patience count > 2:
	say "'I said, [one of]beat it[or]scram[or]get out of here[at random]!' the stallkeeper yells."

Section 4 - Unshopperly behavior

Instead of doing something other than stealing when the noun is a stall display and a stallkeeper is nearby:
	if the action requires a touchable noun begin;
		say "'Hey!' the stallkeeper glares at you. 'Keep your sticky fingers away from the goods.'" instead;
	end if;
	continue the action;

Chapter 4 - Actions relating to commerce

Section 1 - Stealing

Stealing is an action applying to one visible thing.
Stealing it from is an action applying to one visible thing and one thing.

Understand "steal [something]" as stealing.
Understand "steal [something] from [something]" as stealing it from.

Understand the command "filch" as "steal".
Understand the command "grab" as "steal".
Understand the command "loot" as "steal".
Understand the command "nick" as "steal".
Understand the command "pilfer" as "steal".
Understand the command "shoplift" as "steal".
Understand the command "snatch" as "steal".
Understand the command "swipe" as "steal".
Understand the command "thieve" as "steal".

A person can be a successful thief or a mere urchin. The player is a mere urchin.

Check stealing:
	unless the noun is merchandise:
		try taking the noun instead;

Check stealing:
	if the noun is on-stage:
		try taking the noun instead;

Carry out stealing:
	if the player was a mere urchin:
		now the player is a successful thief;
	if the player holds the satchel:
		move the noun to the satchel;
	otherwise:
		move the noun to the player;
	now the location is blocked;

Check stealing it from:
	try stealing the noun instead;

After stealing something when the player was a mere urchin:
	ratchet the current tension to important;
	say "Ah, [i]this[r] is something you know how to do, at least. You sidle up casually to the stall, wait until the stallkeeper seems preoccupied, then reach out and pluck [the noun] from [its or their for the noun] shelf.[paragraph break]Suddenly the stallkeeper jabs his finger at you and screams, [i]'Stop, thief!'[no line break][r][paragraph break]Apparently, he wasn't quite as preoccupied as you thought.[paragraph break]You sprint through the crowd, stuffing [the noun] into your satchel. This stallkeeper will be on the lookout for you now; probably best to stay away from this part of Grubber's for a while.";
	move the player to a random adjacent unblocked room;
	update the character list;

After stealing something when the player was a successful thief:
	ratchet the current tension to important;
	say "With the stallkeepers all on edge about the mercenaries, it's not likely you'll be able to nick anything without someone noticing. Still, nothing ventured...[paragraph break][i]'Thief!'[no line break][paragraph break][r]Here you go again.[paragraph break]You sprint through the crowd, stuffing [the noun] into your satchel. This stallkeeper will be on the lookout for you now; probably best to stay away from this part of Grubber's for a while.";
	move the player to a random adjacent unblocked room;
	update the character list;

Section 2 - Wares

Wares are a kind of thing. Wares are usually scenery. Understand "merchandise", "wares", "goods", "fare" as wares. 

Does the player mean doing something with wares: it is unlikely.

Rule for printing the name of wares: 
	say "merchandise";

Section 3 - Stall displays

A stall display is a kind of thing. A stall display is usually scenery. A stall display has a thing called the corresponding item.

Does the player mean doing something with a stall display:
	it is likely;

Before taking a stall display:
	let the stolen item be the corresponding item of the noun;
	if the stolen item is off-stage:
		try stealing the noun instead;

Before stealing a stall display:
	let the stolen item be the corresponding item of the noun;
	if the stolen item is off-stage:
		try stealing the stolen item instead;

Part 2 - Mercenary Encounters

[ Stick around for too long in Grubber's Market, and the wandering mercenary will come to beat you up. Lots of timing issues here. ]

A person [ie the mercenary] can be oblivious, approaching, or grabbing. A person is usually oblivious.

Chapter 1 - A Pair of Angry Mercenaries

Section 1 - Description of Mercenaries

Some wandering mercenaries are a man. The printed name is "mercenaries".
Understand "man", "men", "soldier", "soldiers", "thug", "thugs", "bravo", "bravos", "mercenary", "mercenaries", "merc", "mercs" as the wandering mercenaries.

The description is "Rough and ugly, with dirty clothes and mean, scarred faces. They carry serious-looking swords on their hips."

A sword is carried by the wandering mercenaries. The description is "Not some decorated dandy's blades, these. Their steel is dull and tarnished, honed to a wicked edge. Nothing less than three-foot-long, practical tools for stabbing people dead." Understand "swords", "blade", "blades", "sharp", "serious", "looking", "dull", "tarnished", "steel", "metal", "wicked", "honed", "edge", "three/3 foot/feet", "long", "practical", "tool", "tools" as the sword.

Rule for printing the name of the sword when clarifying the parser's choice of something:
	say "mercenary's blade";

Before taking the sword when the sword is carried by the wandering mercenaries:
	say "Even if you did manage to nick one of those swords without getting yourself totally killed, you wouldn't know what to do with it once you had it." instead;

Section 2 - Initial appearance

The initial appearance of the mercenaries is "[initial mercenary appearance]";

To say initial mercenary appearance:
	if the mercenaries are oblivious begin;
		say "A couple of the mercenaries are nearby, scanning the crowds. ";
	else if the mercenaries are approaching;
		say "The mercenaries are heading straight for you, arms spread to block your escape. ";
	else; [mercenaries are grabbing]
		say "One of the mercenaries has his huge, hammy fist firmly clamped around your arm; the other one is shouting for the others to come. ";
	end if.

Section 3 - Actions and interactions

[ > prod mercenary with pointy stick ]
Instead of doing something other than examining to the oblivious wandering mercenaries:
	say "You resist the foolish impulse, reminding yourself that what you want is for them to [i]not[r] catch you.";

Instead of doing something other than examining to the sword: say "You resist the foolish impulse, reminding yourself that what you want is for them to [i]not[r] catch you."

Every turn when oblivious wandering mercenaries are nearby:
	if the player is not on the move and a random chance of 1 in 5 succeeds:
		add ATMOS_mercenaries to the dramatic possibilities, if absent;

ATMOS_mercenaries is a dramatic event. The priority is interesting.

Rule for firing ATMOS_mercenaries:
	say "[one of]The mercenaries peer around aimlessly. They still haven't spotted you, but it's only a matter of time.[or]The mercenaries look bored.[or]One of the mercenaries spits on the ground.[or]One of the mercenaries scratches his head and scowls.[or]The mercenaries glare at a passing shopper.[or]One of the mercenaries bumps into a passing shopper, and angrily shoves him out of his way.[or]One of the mercenaries mutters something to his companion; the other [uncouth mercenary response].[at random]";

To say uncouth mercenary response:
	say "[one of]laughs bawdily[or]scowls[or]nods[or]snarls something back[at random]";

Instead of kissing the wandering mercenaries, say "That just seems like an incredibly foolish thing to do."

[ When we run away, we lose the mercenaries. ]
Every turn when the player is on the move during Avoiding Soldiers:
	if the wandering mercenaries are on-stage and the mercenaries are not in the location begin;
		remove the wandering mercenaries from play;
		now the wandering mercenaries are oblivious;
	end if;
	change the timeout of the wandering mercenaries to a random number between 1 and 3;

Section 4 - Waiting for them to turn up

The wandering mercenaries have a number called the timeout.
When play begins:
	change the timeout of the wandering mercenaries to a random number between 1 and 3;

To decide if the mercenaries will not recognise us:
	if the player wears the woolen hat:
		no; [they will recognise us whenever we have the hat on]
	otherwise:
		if the mercenary captain is wise to our tricks:
			no; [no hat, but the word is out: they will recognise us]
	yes;

Every turn when in the Open-Air Market [region] during Avoiding Soldiers :
	if the waiting count is at least the timeout of the wandering mercenaries and the wandering mercenaries are not in the location:
		add EVENT_mercenary_arrival to the dramatic possibilities, if absent;
	otherwise if the oblivious wandering mercenaries have been in the location for three turns:
		add EVENT_mercenary_approach to the dramatic possibilities, if absent;
	otherwise if the oblivious wandering mercenaries have been in the location for two turns:
		add EVENT_mercenary_warning to the dramatic possibilities, if absent;

EVENT_mercenary_arrival is an dramatic event. The priority is essential.

For firing EVENT_mercenary_arrival:
	move the wandering mercenaries to the location;
	say "You notice a couple of the mercenaries pushing through the crowd, heading your way. [if the mercenaries will not recognise us]One of them looks right at you, but with your hat off and your hair down they don't recognize you[first time]. You're safe for the time being[only][otherwise]They haven't seen you yet, but they're getting close[end if].";

EVENT_mercenary_warning is a dramatic event. The priority is important.

For firing EVENT_mercenary_warning:
	if the mercenaries will not recognise us:
		rule fails;
	if the wandering mercenaries have been nearby for two turns:
		say "Those mercenaries are getting uncomfortably close. You'd better get going before they notice you!";

EVENT_mercenary_approach is a dramatic event. The priority is essential.

For firing EVENT_mercenary_approach:
	if the wandering mercenaries have been nearby for three turns:
		if the mercenaries will not recognise us:
			rule fails;
		now the wandering mercenaries are approaching;
		change the grabbing countdown to 1;
		say "Suddenly one of the mercenaries does a double-take and sees you. 'There he is!' he yells, pointing, and both of them start shoving through the crowd towards you.";

Chapter 2 - Captured

Section 1 - Only a matter of time 'til you're caught

The grabbing countdown is a number that varies. The grabbing countdown is [initially] 2 [* but it needs to be reset, each time the mercenaries notice us].

Every turn when approaching wandering mercenaries are nearby:
	if the grabbing countdown is at most 0:
		add EVENT_mercenary_grab to the dramatic possibilities, if absent;
	change the grabbing countdown to the grabbing countdown - 1.

EVENT_mercenary_grab is a dramatic event. The priority is essential.

For firing EVENT_mercenary_grab:
	if the grabbing countdown is at most 0:
		say "A rock-hard fist closes around your upper arm. 'Gotcha!' the mercenary yells. The other one starts hollering for his companions.";
		now the mercenaries are grabbing;
		the leader arrives in two turns from now;

Section 2 - Gotcha!

At the time when the leader arrives:
	[if the player has not been caught for two consecutive turns, then they must have escaped by some means. The leader does not arrive in this case; in fact, nothing happens.]
	if the wandering mercenaries have been grabbing for two [consecutive] turns begin;
		say "The captain soon arrives, followed by two more of his men. One of them grabs your other arm as their leader smiles. 'Good work, dims. Got ourselves a nice little commission with this one.' And before you can react, he takes a sack of black cloth from his belt and jerks it down over your head.[paragraph break]Everything goes dark. You can't breathe. You struggle, but you can no more break the iron grip on your arms than you could uproot a tree. The men holding you curse. Dimly, you hear shouts from the crowd — someone protesting this rough treatment of a child.[paragraph break]'Don't worry, m'lady,' laughs the mercenary leader. 'We'll treat the little rat nice an['] gentle. Like this, see?'[paragraph break]Something huge and heavy crashes into your head, and the rest of your senses are yanked away. Why they wanted you, you never awake to find out.";
		end the game in death;
	end if;

Instead of going when the wandering mercenaries are grabbing:
	say "You [one of]pull and struggle[or]beat on the mercenary's arms with your fists[or]claw the mercenary's arms skin with your fingernails[at random], but the man is just too strong."

Instead of talking to the wandering mercenaries when the wandering mercenaries are not oblivious:
	say "The mercenaries both ignore your pleas.";

After going to somewhere (called the destination) when the wandering mercenaries are approaching:
	ratchet the current tension to important;
	say "You dash away into the crowd, weaving between customers and pushing your way through the thickest knots of people. The mercenaries pound after you, yelling and shoving people out of their way, but they're too big and too slow, and you quickly manage to lose them.";
	continue the action;

Instead of pushing the wandering mercenaries:
	try attacking the wandering mercenaries;

Instead of kicking or attacking the wandering mercenaries when the wandering mercenaries are not oblivious:
	now the wandering mercenaries are approaching;
	change the grabbing countdown to 2;
	if we are kicking the wandering mercenaries:
		say "Thinking quickly, you kick the mercenary in the shin.[paragraph break]";
	otherwise:
		say "Thinking quickly, you [narrow escape].[paragraph break]";
	say "'Argh! Oh, you miserable runt—' [if the wandering mercenaries were grabbing]the mercenary's grip on your arm loosens, and you twist away. You're free![otherwise]He stumbles backwards into his companion. Now they're both distracted; now's your chance to run![end if]";

To say narrow escape:
	say "[one of]kick the mercenary in the shin[or]elbow the mercenary as hard as you can, aiming below the belt[or]spit at the mercenary's face, and a gob of spittle lands right in his eye[or][if the location is a market stall]scream your head off. People from two stalls away stop what they're doing to see what the commotion is, and when the mercenary clamps his hand over your mouth, you bite down hard[otherwise][another narrow escape][end if][at random]";

To say another narrow escape:
	[This is a recursion tactic; direct recursion is rightly disallowed, but this variant is ok. In fact, because '[one of]...[at random]' never selects the same option twice in succession, this is guaranteed to terminate after exactly one recursion. ]
	say narrow escape;

Before choosing a dramatic possibility in the presence of wandering mercenaries:
	[if they are approaching or grabbing, then we would be focussed on them:]
	unless the wandering mercenaries are oblivious:
		ratchet the current tension to important;

Part 3 - Northwest Junction

Chapter 1 - Description

Grubbers Market Northwest Junction is a room. The description is "This is the northwest corner of Grubber's Market. You can skirt around the edge of the market to the northeast or south, or head into the thick of it to the east or southeast. A wide, paved road leads north. You can also duck back into the alley to the northwest." The printed name is "Northwest Junction". The distant description of Grubbers Market Northwest Junction is "The junction is a relatively open space at the market's northwest corner." The dead-end description of Grubbers Market Northwest Junction is "There's nothing in that direction except the wall that surrounds the market square."

Northwest of Grubbers Market Northwest Junction is the Alley.
 
Instead of going or facing north in Grubbers Market Northwest Junction during Avoiding Soldiers:
	try entering the market gates instead;

Understand "relatively", "open", "space", "corner", "edge" as Grubbers Market Northwest Junction.
Understand "narrow", "slice" as Grubbers Market Northwest Junction when the location is the Alley.
Understand "crowded", "proper" as Grubbers Market Northwest Junction when the location is the Outer Market Roof.

Rule for printing the distant description of Grubbers Market Northwest Junction while in the Alley:
	say "You can't really see more than a narrow slice of the northwest junction from back here. ";

Rule for printing the distant description of Grubbers Market Northwest Junction while in Top of Center Post:
	say "It's too far to make out any details from here. ";

Grubbers Market Northwest Junction overlooks Lords Road.

A mercenary mob is here.

After assembling available exits of Grubbers Market Northwest Junction:
	add north at entry 1 in the viable directions, if absent;

Chapter 2 - Scenery

Section 1 - Market Gate

A gate is a kind of thing. A gate can be open. A gate can be lockable. A gate can be locked. A gate is usually not open. A gate is usually lockable. A gate is usually not locked.

Some market gates are a gate, [initially] open, lockable, in Grubbers Market Northwest Junction. "The north gates leading to Lord's Road are closed." The description is "The north gates are huge wooden doors, nearly ten feet wide each, that date all the way back to when Toresal was a fortified town. They're kept around for ceremony, mostly; they're certainly never closed. [if closed]Until now.[end if]". They are fixed in place.

Understand "north", "gate", "ceremonial" as the market gates.

Instead of doing something other than examining the market gates when in Outer Market Roof:
	say "You can't do anything to the gates from up here.";

Instead of entering or pushing the closed market gates:
	try opening the noun;

Instead of opening the market gates:
	say "The mercenaries have locked the gates. You're not getting out this way.";

A rule for reaching inside Grubbers Market Northwest Junction when in Outer Market Roof:
	allow access.

Part 4 - Grocery Stall

Chapter 1 - Description

Grubbers Market Grocery Stall is a market stall and northeast of Grubbers Market Northwest Junction. "This is a typical grocer's stall, with various kinds of food set out. There are baskets bristling with loaves of bread, yellow pyramids of cheese, and spiced jerky hanging in bundles overhead. You can continue along the market's outer ring to the southwest or southeast, or head in towards the center of the market to the south." The printed name is "Grocery Stall". 

The distant description of the Grocery Stall is "[one of]There's a grocery stall[or]There's groceries for sale[or]Looks like they're selling groceries[at random] to the [quick best route from the location to the Grocery Stall][if the Grocery Stall is blocked]. The stallkeeper looks like he's still keeping a sharp eye out for you[end if]." The dead-end description of the Grocery Stall is "There's nothing in that direction except the wall that surrounds the market square."

Understand "groceries", "food" as the Grubbers Market Grocery Stall. 

Chapter 2 - Contents (Grocer's Displays)

Some baskets are wares, in the Grocery Stall. "There are baskets bristling with loaves of bread, yellow pyramids of cheese, and spiced jerky hanging in bundles overhead." Understand "basket", "baskets", "display", "displays", "fare" as the baskets.

A food stall display is a kind of stall display. Understand "kinds", "kind", "of", "grocery", "groceries", "food" as a food stall display.

The display of loaves is a food stall display, plural-named, in the Grocery Stall. The description is "The loaves are dark and grainy and look rock-hard." The printed name of the display of loaves is "bread". The corresponding item of the display of loaves is the loaf. 

The pyramid display is a food stall display, plural-named, in the Grocery Stall. The description is "The cheese is a rich, milky-yellow color, with a thick, brown rind." The printed name of the pyramid display is "cheese". The corresponding item of the pyramid display is the cheese.

The strips display is a food stall display, plural-named, in the Grocery Stall. The description is "The jerky hangs in dried, shriveled strips, and looks just slightly more appetizing than eating your own belt." The printed name of the strips display is "jerky". The corresponding item of the strips display is the strip of beef jerky.

Understand "loaf", "bread", "breads", "dark", "grainy", "hard", "rock" as the display of loaves.
Understand "cheese", "cheeses", "pyramids", "of", "rich", "milky", "yellow", "thick", "brown", "rind" as the pyramid display.
Understand "spiced", "meat", "strip", "beef", "jerky", "salt", "dried", "shriveled", "shrivelled", "hanging", "bundle", "bundles", "bundle of", "bundles of" as the strips display.

Instead of smelling a food stall display:
	say "The food smells tasty!";

Instead of eating a food stall display:
	say "You don't have time for breakfast now!";

Chapter 3 - Definition of food

Food is a kind of thing. Food is always edible. Food is usually merchandise.

Instead of eating or tasting food during Avoiding Soldiers: say "No time for breakfast right now!";

Chapter 4 - Contents (Individual foodstuffs)

Delicatessenery is a kind of food. Understand "food" as delicatessenery.

The loaf is delicatessenery. The description is "The loaf is dark and grainy and looks rock-hard." The printed name is "bread". Understand "bread", "dark", "grainy", "hard", "loaf of", "rock" as the loaf.

The cheese is delicatessenery. The description is "The cheese is a rich, milky-yellow color, with a thick, brown rind." Understand "rich", "milky", "yellow", "thick", "brown", "rind" as the cheese.

The strip of beef jerky is delicatessenery. The description is "It's a twisted, shriveled strip of dried salt-beef. Yum." Understand "spiced", "meat", "beef", "dried", "twisted", "shrivelled", "shriveled", "salt", "dry" as the strip of beef jerky.

Instead of smelling delicatessenery:
	say "The food smells tasty!"

Instead of eating delicatessenery:
	say "You don't have time for breakfast now!";

After eating or tasting delicatessenery:
	remove the noun from play;
	say "You scarf [the noun] down. Dry, but tasty.";

Chapter 5 - Contents (Stallkeeper)

The grocer is a stallkeeper in the Grocery Stall. 

Part 5 - Fruit Stall

Chapter 1 - Fruit

Fruit is a kind of food. Understand "fruit", "bright", "brightly", "colored", "coloured", "fruits" as fruit.
Report eating a fruit: say "Delicious!" instead.
Report smelling a fruit: say "Smells delicious." instead.

After eating or tasting fruit:
	remove the noun from play;
	say "You scarf [the noun] down. Delicious!";

Chapter 2 - Description 

Grubbers Market Fruit Stall is a market stall and southeast of Grubbers Market Grocery Stall. "[if Grubbers Market Fruit Stall is orderly]Bins heaped high with brightly colored fruit lend a pleasant fragrance to this end of the market (though by afternoon it will have become a cloying reek that attracts more flies than customers). They have apples and pears from the city orchards, and brambleberries from the northern counties. Oranges, limes, kello fruit — even a bushel of bananas from the Kozar Delta. Aisles between the stalls lead northwest, west, southwest, and south.[otherwise]The fruit stall is still a complete mess, with most of the merchandise scattered all over the ground. The stallkeeper is practically tearing his beard out with frustration.[end if]". The printed name is "Fruit Stall". The distant description of the Fruit Stall is "[one of]There's a fruit stall[or]There's fruit for sale[or]Looks like they're selling fruit[at random] to the [quick best route from the location to the Fruit Stall][if the Fruit Stall is chaotic]. The stallkeeper looks like he's still busy cleaning up the mess from when the mercenary crashed into his merchandise[end if]." The dead-end description of the Fruit Stall is "There's nothing in that direction except the wall that surrounds the market square."

Grubbers Market Fruit Stall can be orderly or chaotic. Grubbers Market Fruit Stall is orderly.

Rule for printing the distant description of Grubbers Market Fruit Stall while in Grubbers Market Top of Center Post:
	say "It's too far to make out any details from here. ";

Instead of smelling the Grubbers Market Fruit Stall:
	say "It smells delicious here.";

Chapter 3 - Contents (Stocks of fruit)

Some bins are in the fruit stall. They are wares. "[if the Fruit Stall is orderly]They have apples and pears from the city orchards, and brambleberries from the northern counties. A few of the baskets contain more exotic fare: oranges, limes, kello fruit — even a bushel of bananas from the Kozar Delta.[otherwise]The fruit is scattered all over the place — apples, pears, brambleberries, oranges, limes, kello, and bananas, all rolling in the dust and being trampled to mush by passing shoppers.[end if]";

Understand "bin", "bins", "basket", "baskets", "display", "displays", "exotic", "heap", "heaps", "heap of", "heaps of" as the bins.

Instead of smelling the bins:
	say "Smells delicious.";

Instead of stealing the bins:
	if the location encloses an unplundered storage bin begin;
		let x be a random unplundered storage bin in the location;
	otherwise;
		let x be a random storage bin in the location;
	end if;
	try stealing x;

Does the player mean examining the bins:
	it is likely;

Definition: a storage bin (called the bin in question) is unplundered:
	if the corresponding item of the bin in question is off-stage, yes;
	no;

Definition: a storage bin is plundered:
	if it is unplundered, no;
	yes;

Chapter 4 - Fruit Storage Bins

A storage bin is a kind of stall display. Understand "fruit", "food", "bright", "brightly", "colored", "coloured" as a storage bin.

Instead of smelling a storage bin:
	try smelling the bins;

Instead of stealing a plundered storage bin:
	say "You already have enough [noun].";

[ Bad grammar follows because the plurality of the printed name differs from the singularity of the internal name. ]

Some display of kello-fruit is a storage bin, in the Fruit Stall. The description is "Normally you'd [i]kill[r] for a slice of kello, but these look a bit too green. Not quite in season." The printed name of the display of kello-fruit is "kello fruit". The corresponding item of the display of kello-fruit is the kello fruit.

Some display of apples is a storage bin, in the Fruit Stall. The description is "These don't look half bad. If you hadn't stumbled into so much trouble, you'd be eating one right now." The printed name of the display of apples is "apples". Understand "apple" as the display of apples. The corresponding item of the display of apples is the apple.

Some display of pears is a storage bin, in the Fruit Stall. The description is "These vary in color from yellowish-green to dusky orange-brown, with thick, twisty stems." The printed name of the display of pears is "pears". Understand "pear" as the display of pears. The corresponding item of the display of pears is the pear.

Some display of brambleberry clusters is a storage bin, in the Fruit Stall. The description is "The thumb-sized clusters are shiny and bluish-black. Your mouth waters at the thought of how tart they must be." The printed name of the display of brambleberry clusters is "brambleberries". Understand "cluster", "brambleberries" as the display of brambleberry clusters. The corresponding item of the display of brambleberry clusters is the brambleberries.

Some display of oranges is a storage bin, in the Fruit Stall. The description is "The oranges are pale and disappointingly small." The printed name of the display of oranges is "oranges". The corresponding item of the display of oranges is the orange.

Some display of limes is a storage bin, in the Fruit Stall. The description is "The dark green limes look refreshing." The printed name of the display of limes is "limes". The corresponding item of the display of limes is the lime.

Some display of bananas is a storage bin, in the Fruit Stall. It is plural-named. "You rarely see bananas this far north. These look just about perfect — plump, bright yellow with just a sprinkling of brown freckles." Understand "bushel" as the display of bananas. The printed name of the display of bananas is "bananas". The corresponding item of the display of bananas is the banana.

Understand "kello", "green", "unripe" as the display of kello-fruit.
Understand "apple", "round", "firm", "green", "red", "stem", "stems" as the display of apples.
Understand "pear", "yellow", "green", "orange", "brown", "yellowish", "green", "dusky", "thick", "twisty", "twisted", "stem", "stems" as the display of pears.
Understand "cluster", "brambleberries", "bramble", "thumb", "sized", "clusters", "cluster", "shiny", "blue", "black", "bluish", "tart", "berry", "berries", "from", "from the", "the north/northern", "counties", "north/northern counties" as the display of brambleberry clusters.
Understand "orange", "pale", "small", "disappointing", "disappointingly" as the display of oranges.
Understand "lime", "dark", "green", "refreshing" as the display of limes.
Understand "banana", "perfect", "plump", "bright", "yellow", "brown", "freckles", "freckle", "bushel", "bushels", "bushel of", "bushels of", "kozarian", "from", "from the", "kozar", "delta" as the display of bananas.

Rule for clarifying the parser's choice of a stall display (called sd):
	let x be the corresponding item of sd;
	if the player can see x,
		do nothing;

Rule for clarifying the parser's choice of food:
	if the player can see a stall display,
		do nothing;

Chapter 5 - Contents (Individual fruits)

A kello fruit is fruit. The description is "It's a little bit too green, but probably still tasty." Understand "green", "unripe" as the kello fruit.

A banana is fruit. The description of the banana is "The banana is plump, bright yellow with just a sprinkling of brown freckles." Understand "perfect", "plump", "bright", "yellow", "brown", "freckles", "freckle", "kozarian", "from", "from the", "kozar", "delta" as the banana.

The player carries an apple. The apple is fruit. The description is "The apple is round, firm, green at the bottom shading up to red near the stem[if Eavesdropping on Soldiers is happening or Avoiding Soldiers is happening]. There is a big bite taken out of one side[end if]." Understand "round", "firm", "green", "red", "stem" as the apple.

Before eating or tasting the apple when Avoiding Soldiers is happening or Eavesdropping on Soldiers is happening:
	say "No time for breakfast right now!" instead;

After eating the apple:
	say "Crisp and delicious!";

A pear is fruit. The description is "The pear is yellowish green with a bit of light brown on the bottom." Understand "yellow", "green", "yellowish", "greenish", "light", "brown", "thick", "twisty", "twisted", "stem" as the pear.

A cluster of brambleberries is fruit. The description is "The thumb-sized cluster is shiny and bluish-black." Understand "cluster", "berry", "bramble", "thumb", "sized", "shiny", "blue", "black", "bluish", "tart", "from", "from the", "the north/northern", "counties", "north/northern counties" as the brambleberries.

An orange is fruit. The description is "The orange is pale and disappointingly small." Understand "pale", "small", "disappointing", "disappointingly" as the orange.

A lime is fruit. The description of the lime is "The dark green lime looks refreshing." Understand "dark", "green", "refreshing" as the lime.

Chapter 6 - Contents (Stallkeeper)

The fruit stallkeeper is a stallkeeper in the Fruit Stall. The description of the fruit stallkeeper is "[if Grubbers Market Fruit Stall is orderly]The stallkeeper looks nervous and irritable. Armed thugs stomping around the Grubber's Market is not good for business.[otherwise]The stallkeeper is frantically trying to put his wares back into some semblance of order.[end if]"

Instead of doing something other than examining to the fruit stallkeeper when the Fruit Stall is chaotic:
	say "The stallkeeper is far too busy trying to clean up all the spilled fruit to deal with you.";

Chapter 7 - Fruit Theft

After stealing fruit in the orderly Grubbers Market Fruit Stall:
	say "[if the player was a mere urchin]Ah, [i]this[r] is something you know how to do, at least. You sidle up casually to the stall, wait until the stallkeeper seems preoccupied, then reach out and pluck [the noun] from the shelf.[paragraph break]Suddenly the stallkeeper jabs his finger at you and screams, [i]'Stop, thief!'[no line break][r][paragraph break][otherwise]With the stallkeepers all on edge about the mercenaries, it's not likely you'll be able to nick anything without someone noticing. Still, nothing ventured...[paragraph break][i]'Thief!'[no line break][r][paragraph break][end if]One of the mercenaries happens to be wandering by at that moment. He hears the stallkeeper's shouts and comes running straight for you![paragraph break]Then there is a loud screech, and something gray and hairy scampers out from under the mercenary's feet. He stumbles, trips, and pitches headlong into the fruit stall. Fruit scatters everywhere. The stallkeeper is wailing and berating the mercenary, who is covered with juice and slimy pulp.[paragraph break]In the general chaos, you slip away easily, stuffing [the noun] into your satchel. Fortunately, no one managed to get a very good look at you, and the stallkeeper will be too busy cleaning up his wares to keep an eye out for you. You shouldn't have any trouble coming back here if you need to.";
	now the location is unblocked;
	now the location is chaotic;
	move the player to a random adjacent unblocked room;
	update the character list;

After stealing fruit in the chaotic Grubbers Market Fruit Stall:
	say "With the stallkeeper still sorting out his spilled fruit, and most of the shoppers trying to avoid the mess, no one notices you picking up another [noun]. You slip the fruit into your satchel and move quickly to another part of the market.";
	now the location is unblocked;
	move the player to a random adjacent unblocked room;
	update the character list;

Chapter 8 - Peeling

Peeling is an action applying to one visible thing, and requiring light.
Understand "peel [something preferably held]" as peeling.

Check peeling (this is the block peeling rule):
	say "Strange idea!"

Instead of peeling a fruit:
	try eating the noun.

Before peeling the brambleberries:
	say "Brambleberries don't need peeling!" instead;

Before peeling something:
	if the noun is the apple or noun is the pear,
		say "[The noun] doesn't need peeling!" instead;

Part 6 - Eastern Junction

Chapter 1 - Description

Grubbers Market Eastern Junction is a market stall and south of Grubbers Market Fruit Stall. "You're near the eastern edge of Grubber's Market. Commerce Street lies to the east. You can travel along the outer ring of stalls to the north or south, or head towards the market's central hub to the west." The printed name is "Eastern Junction". The distant description of the Eastern Junction is "There is a relatively open space at the market's eastern edge." The dead-end description of the Eastern Junction is "There's nothing in that direction except the wall that surrounds the market square."

Understand "relatively", "open", "space", "edge" as Grubbers Market Eastern Junction.

Chapter 2 - Exits

Instead of going east from Grubbers Market Eastern Junction when the player wears the woolen hat and Market Escape is not happening for the first time:
	ratchet the current tension to important;
	say "Just as you reach the edge of the market square, you notice the mercenary captain standing watch at the entrance to Commerce Street. His eyes fall on you before you can duck out of the way, and the next moment you are sprinting back into the market with the captain hot on your heels. You manage to lose him in the crowd... barely.";
	move the player to a random adjacent unblocked market stall;
	update the character list;

Instead of going east from Grubbers Market Eastern Junction when the player wears the woolen hat and Market Escape is not happening:
	say "The mercenary captain is too sharp; you'll need a disguise if you're to have any chance of sneaking past him.";

Instead of going east from Grubbers Market Eastern Junction when the player does not wear the woolen hat and Market Escape is not happening:
	ratchet the current tension to important;
	if the mercenary captain is not wise to our tricks begin;
		say "As you reach the edge of the market square, you see the mercenary captain scanning the crowd. He looks at you, and his eyes narrow. He pulls a piece of parchment from his belt and stares at it, then stares hard at you...[paragraph break]And the next moment he is pounding after you, yelling for his men. [i]He knows what I look like[r], you realize, [i]and now he knows I'm not what I appear to be[r]. Just before you dive into the crowd, you glance back over your shoulder, and see the captain talking to his men, showing them the drawing.[paragraph break]Now the mercenaries know they're looking for a girl. You won't be able to fool them just by taking your hat off.";
		now the mercenary captain is wise to our tricks;
		move the player to a random adjacent unblocked market stall;
		update the character list;
	otherwise;
		say "The mercenary captain is wise to your tricks. You'll need a disguise better than just taking your hat off if you're to have any chance of sneaking past him.";
	end if;

Chapter 3 - Escaping from Grubber's Market

Definition: A person is disguised as a girl if they are not wearing the woolen hat.

Part 7 - Hat Stall

Chapter 1 - Description

Grubbers Market Hat Stall is a market stall and south of Grubbers Market Eastern Junction. "Hats of all styles and sizes hang from scores of wooden pegs, a forest of varied headgear. They have big, flop-brimmed traveling hats; small, leather workman's caps; wide sappans of woven straw from the Kozar delta; fancy, feathered hats for courtly balls; and even a few ordinary bonnets. More stalls lie to the north, northwest, west, and southwest." The printed name is "Hat Stall". The distant description of the Hat Stall is "[one of]There's a hat stall[or]There's hats for sale[or]Looks like they're selling hats[at random] to the [quick best route from the location to the Hat Stall]." The dead-end description of the Hat Stall is "There's nothing in that direction except the wall that surrounds the market square."

Understand "hats" as the Hat Stall.

Rule for printing the distant description of Grubbers Market Hat Stall while in Top of Center Post:
	say "It's too far to make out any details from here. ";

Chapter 2 - Contents (Pegs)

Some wooden pegs are a backdrop, in the Hat Stall and the Market Rope Dealer. "There's nothing special about the wooden pegs; they're just there to hold the [if the location is the Hat Stall]hats[otherwise]lengths of rope[end if] up." Instead of doing something other than examining to the wooden pegs, try examining the wooden pegs.

Understand "wood", "peg", "score of", "scores of", "score", "scores" as the wooden pegs. 

Chapter 2 - Contents (Hats)

A stock of hats is wares in the Hat Stall. It is scenery. The indefinite article is "some". "If you can wear it on your head, it's sold here. Big, flop-brimmed traveling hats; small, leather workman's caps; wide [i]sappans[r] of woven straw from the Kozar delta; fancy, feathered hats for courtly balls; and even a few ordinary bonnets." Understand "display", "displays", "fare", "forest", "of", "varied", "headgear" as the stock of hats.

Instead of stealing the stock of hats:
	say "You already have a perfectly serviceable hat. And anyway, you're going to need more than just a new hat to fool these mercenaries.";

A hatstand is a kind of stall display. Understand "sizes", "sizes of", "hat", "hats", "headgear", "style", "style of", "styles", "styles of" as a hatstand.

Instead of stealing a hatstand:
	try stealing the stock of hats;

Some traveling hats are a hatstand, scenery, in the Hat Stall. The description is "The traveling hats have big, floppy brims to keep the sun out of your eyes while on the road." Understand "travelling", "floppy", "big", "flop", "floppy", "brimmed", "brim", "brims" as the traveling hats.

Some workmans caps are a hatstand, scenery, in the Hat Stall. The description is "These leather caps are small and functional, more to protect your head from things falling on it than anything else." Understand "cap", "leather", "small", "workman", "functional" as the workmans caps.

Some sappans are a hatstand, scenery, in the Hat Stall. The description is "The [i]sappan[r] is a wide, round hat made of woven straw, pointy at the top. They're popular in the southern regions. You never see anyone wearing them in this part of the kingdom." Understand "sappan", "wide", "woven", "straw", "Kozar", "Kozar Delta", "straw hat/hats", "round", "pointy", "kozarian" as the sappans.

Some fancy hats are a hatstand, scenery, in the Hat Stall. The description is "At a closer look, the 'fancy' hats are actually sort of cheap and gaudy. Anyone who is attending a real courtly ball probably wouldn't be shopping for hats in Grubber's Market anyway." Understand "cheap", "gaudy", "feather", "feathers", "feathered", "courtly", "ball" as the fancy hats.

Some bonnets are a hatstand, scenery, in the Hat Stall. The description is "The bonnets are just ordinary peasant wear, little white caps that tie under the chin." Understand "bonnet", "white", "ordinary", "peasant", "wear", "tie", "ties" as the bonnets.

The hat stallkeeper is a stallkeeper, in the Hat Stall. Understand "milliner", "hatter" as hat stallkeeper.

Part 8 - Leather Stall

Chapter 1 - Description

Grubbers Market Leather Shop is a market stall and southwest of Grubbers Market Hat Stall. "[first time][if Final Chase is not happening]You pause here for a few deep breaths — you've always enjoyed the smell around the leather merchant's stall. [end if][only]This merchant mostly deals in riding equipment — reins, harnesses, stirrups, and the like. A full riding saddle sits on proud display near the back. There are a few scabbards and belts for sale, as well. More stalls lie to the west, northwest, and northeast." The printed name is "Leather Stall". The distant description of the Leather Shop is "[one of]There's a leather goods stall[or]There's leather goods for sale[or]Looks like they're selling leather goods[at random] to the [quick best route from the location to the Leather Shop][if the Leather Shop is blocked]. The stallkeeper looks like he's still keeping a sharp eye out for you[end if]." The dead-end description of the Leather Shop is "There's nothing in that direction except the wall that surrounds the market square."

Understand "goods" as the leather shop when the location is not the leather shop. 

Instead of facing north in the Leather Shop:
	say "The stalls are too close together in that direction; you can't squeeze through.";

Instead of smelling when the location is the Leather Shop, say "The smell of leather always makes you think of traveling in distant, dangerous lands. It's the smell of camping out under the stars, the smell of adventure."

Chapter 2 - Contents (Leather Goods)

Some leather-goods are wares, in the Leather Shop. The description is "This merchant mostly deals in riding equipment — reins, harnesses, stirrups, and the like. A full riding saddle sits on proud display near the back. There are a few scabbards and belts for sale, as well." Understand "display", "displays", "fare" as the leather-goods.

Instead of stealing the leather-goods:
	let x be a random nearby stall display;
	while x is the riding saddle repeatedly let x be a random nearby stall display; 
	try stealing x instead;

A riding saddle is scenery, a stall display, in the Leather Shop. The description is "The saddle is made of many layers of leather, shaped into sweeping curves and polished and lacquered until it gleams." The printed name is "saddle". Understand "leather", "saddles", "equipment", "polish", "polished", "lacquer", "lacquered", "gleaming", "sweeping", "curve", "curves", "curved", "layer", "layers", "layer of", "layers", "layered" as the riding saddle.

Instead of stealing or taking the saddle, say "The saddle is way too huge for you to nick. Anyway, horses make you nervous."

Chapter 2 - Contents (Leather Goods)

Section 1 - Collective goods

Leather stock is a kind of stall display. Understand "leather", "leathers" as leather stock.

Some collective reins are leather stock, in the leather shop. "They're ordinary riding reins, used for steering horses." The corresponding item of the collective reins is the riding reins. The printed name of the collective reins is "reins". Understand "equipment", "riding", "rein", "ordinary" as the collective reins.

Some harnesses are leather stock, in the leather shop. "The harnesses are complicated contraptions of tough leather straps and buckles, for hitching horses to wagons." The corresponding item of the harnesses is the harness. Understand "complicated", "contraption", "contraptions", "tough", "harness", "hitching", "strap", "straps", "buckle", "buckles", "riding", "equipment" as the harnesses.

Some stirrups are leather stock, in the leather shop. "They're ordinary riding stirrups, for getting up on horses." The corresponding item of the stirrups is the stirrup. Understand "ordinary", "riding", "equipment", "stirrup" as the stirrups.

Some scabbards are leather stock, in the Leather Shop. "The scabbards are mostly plain, unadorned leather, good for sliding a sword into and not much else." The printed name is "scabbards". The corresponding item of the scabbards is the plain scabbard. 
Understand "scabbard", "sheath", "sheaths", "plain", "unadorned" as the scabbards.

A rack of belts is leather stock, in the Leather Shop. "The belts on display here aren't anything fancy, but they're sturdy and well-crafted." The corresponding item of the rack of belts is the belt. Understand "belt", "sturdy", "well", "crafted" as the rack of belts.

Section 2 - Individual goods

Leatherware is a kind of thing. Leatherware is usually merchandise. Understand "leather" as leatherware.

Some riding reins are leatherware, plural-named. The description is "They're ordinary riding reins, used for steering horses. What use they are to you without a horse, you're not sure yet." The printed name is "reins". Understand "rein", "ordinary", "useless" as the reins.

A harness is leatherware. The description is "It's a complicated contraption of tough leather straps and buckles, for hitching horses to wagons." Understand "complicated", "contraption", "hitching", "strap", "straps", "buckle", "buckles", "complicated", "contraption", "tough" as the harness.

A stirrup is leatherware. The description is "It's an ordinary riding stirrup, for getting up on a horse. What use it is to you without a horse, you're not sure yet." Understand "ordinary", "riding" as the stirrup.

The plain scabbard is wearable, leatherware. The description is "It's a sheath of plain, unadorned leather. You've got no sword to put in it, nor any belt to hang it on, but for all that it's a perfectly good scabbard." The printed name is "scabbard". Understand "plain", "unadorned", "sheath", "perfectly", "good" as the scabbard.

Before inserting the knife into the scabbard:
	say "The knife isn't nearly long enough to fit properly in the scabbard." instead;

Before wearing the scabbard:
	say "You need a belt in order to wear this thing." instead;

A belt is wearable, leatherware. The description of the belt is "Now, a good belt is always useful. This one isn't anything fancy, but it's sturdy and well-crafted." Understand "sturdy", "supple", "strap", "well crafted", "crafted", "good" as the belt.

Chapter 5 - Contents (Stallkeeper)

The leather stallkeeper is a stallkeeper and in the Leather Shop. 

Part 9 - Weapons Stall

Chapter 1 - Description

Grubbers Market Weapons Stall is a market stall and west of Grubbers Market Leather Shop. "The weaponsmith's wares hang along the sides of his stall, a grim picket fence of down-hanging blades. You can find less intimidating stalls to the east, northeast, and northwest." The printed name is "Weaponsmith's Stall". The distant description of the Weapons Stall is "[one of]There's a weapon stall[or]There's weapons for sale[or]Looks like they're selling weapons[at random] to the [quick best route from the location to the Weapons Stall][if the Weapons Stall is blocked]. The stallkeeper looks like he's still keeping a sharp eye out for you[end if]." The dead-end description of the Weapons Stall is "There's nothing in that direction except the wall that surrounds the market square."

Understand "weapon", "weaponsmiths" as Grubbers Market Weapons Stall. 

Instead of facing north in Grubbers Market Weapons Stall:
	say "The stalls are too close together in that direction; you can't squeeze through.";

Chapter 2 - Contents (Weaponry)

Some weaponry is wares, in the Weapons Stall. "Swords and knives, mostly. Nothing fancy — just sharp, practical, and deadly." Understand "display", "displays", "of", "various", "fare", "sharp", "practical", "deadly" as the weaponry.

Rule for printing the name of the weaponry while asking which do you mean:
	say "display of various weaponry";

Before stealing the weaponry:
	try stealing the knives instead.

A weapon is a kind of thing. A weapon is usually merchandise. A weapon is usually sharp.

Some longswords are scenery, in the Weapons Stall. "The swords vary from three to four feet long. They look too heavy for you to even lift, much less swing at a rapidly approaching mercenary." The printed name is "swords". Understand "heavy", "sword", "swords", "weapon", "weapons", "blade", "blades", "picket", "fence" as the longswords.

Instead of stealing or taking the longswords:
	say "The swords are too big for you to nick.";

Some knives are a stall display, in the Weapons Stall. "The knives are just your speed: short and wicked sharp." Understand "dagger", "daggers", "weapon", "weapons", "blade", "blades", "knife", "wicked", "sharp" as the knives. The corresponding item of the knives is the knife.

The knife is a weapon. The description is "The knife is short and wicked sharp." Understand "dagger", "weapon", "blade", "wicked", "sharp" as the knife.

Chapter 3 - Contents (Stallkeeper)

The weaponsmith is a stallkeeper, in the Weapons Stall. Understand "smith", "armorer", "amourer" as the weaponsmith.

Part 10 - Exotic Gems Stall

Chapter 1 - Description

Grubbers Market Exotic Gems Stall is a market stall and northwest of Grubbers Market Weapons Stall. "Dozens of jewels in exotic colors and cuts are spread out on display here, glittering in the dusty sunlight. Some of them are loose stones, others are set in finely wrought rings and necklaces. Most of them were probably smuggled in from over the western mountains, which is why they're cheap enough to be affordable to anyone in Grubber's Market. Still, they're far, far out of your price range. Other stalls lie to the north, northeast, east, and southeast." The printed name is "Exotic Gems Stall". The distant description of the Gems Stall is "[one of]There's a gem stall[or]There's gems for sale[or]Looks like they're selling gems[at random] to the [quick best route from the location to the Exotic Gems Stall]." The dead-end description of the Gems Stall is "There's nothing in that direction except the wall that surrounds the market square."

Understand "gem", "jewel", "jewels", "jewelry", "jewelery" as the Grubbers Market Exotic Gems Stall. 

Rule for printing the distant description of Grubbers Market Exotic Gems Stall while in Top of Center Post:
	say "It's too far to make out any details from here. ";

Chapter 2 - Contents (Gems)

Some precious gems are wares, privately-named, in the Grubbers Market Exotic Gems stall. The description is "Some of them are loose stones; others are set in finely wrought rings and necklaces. Most of them were probably illegally smuggled from over the western mountains, which is why they're cheap enough to be affordable by anyone in Grubber's Market. Still, they're far, far out of your price range." Understand "illegal", "cheap", "smuggled", "gems", "wares", "merchandise", "display", "displays", "fare" as the precious gems.

Some loose stones are scenery, merchandise, in the Exotic Gems Stall. "Most of the loose stones are tiny, some no bigger than your little fingernail. They sure are shiny, though." Understand "stone", "gemstone", "gemstones", "gem stone", "gem stones", "glittering", "exotic", "colors", "color", "colours", "colour", "tiny", "shiny", "cut", "cuts" as the loose stones.

Some pretty jewelry is scenery, merchandise, in the Exotic Gems Stall. "The rings and necklaces are pretty, but they all look a bit [i]too[r] flashy. You have a sneaking suspicion that some of them might turn your skin green if you wore them too long." Understand "jewel", "jewels", "jewelery", "flashy", "ring", "rings", "necklace", "necklaces", "glittering", "exotic", "color", "colors", "colour", "colours", "flashy" as the pretty jewelry.

Does the player mean doing something with the loose stones:
	it is very likely;

Does the player mean doing something with the pretty jewelry:
	it is very likely;

Definition: a thing is a gem display:
	if it is the precious gems, yes;
	if it is the loose stones, yes;
	if it is the jewelry, yes;
	no.

Instead of stealing or taking a gem display:
	say "The angry stallkeeper is constantly glaring at you; there's no way you'd manage to nick something and get away with it."

Chapter 3 - Contents (Stallkeeper)

The gems stallkeeper is a stallkeeper, privately-named, in the Exotic Gems stall. Understand "gems stallkeeper", "gem stall keeper", "gems stall keeper", "gem stallkeeper", "jeweler", "stallkeeper", "hathid" as the gems stallkeeper.

Does the player mean doing something with the gems stallkeeper:
	it is unlikely;

[ Instead of buying a gem display, say "These may not be the most expensive gems in the city, but they're still too expensive for what you're offering." ]

Chapter 4 - Events ("Monkey Business")

Section 1 - Scene Begins

Monkey business is a scene. Monkey business begins when the player is in the Exotic Gems Stall for the first time.

Monkey business ends when the player has the necklace.

When Monkey business begins:
	add EVENT_monkey to the dramatic possibilities, if absent;

EVENT_monkey is a dramatic event. The priority is show-stopping.

For firing EVENT_monkey:
	say "Suddenly you hear a commotion from the back of the stall: the crash of a display case hitting the ground; shouts and curses from the stallkeeper; and a horrific, gibbering shriek.[paragraph break]'Come back here, you flea-bitten thief!' yells the stallkeeper, and for a panicked moment you think he means [i]you[r]. Then you see something small and gray dart up to the roof of the stall...it's just a forest monkey, probably someone's pet running loose. And it's clutching one of the stallkeeper's necklaces in its tiny black fist.[paragraph break]The monkey turns and screeches back at the stallkeeper, dodges a clumsily thrown dirt clod, and scampers away.";
	move the monkey to Grubbers Market Top of Center Post;

[ See Top Of Center Post for more Monkey business ]

Part 11 - Herb Stall

Chapter 1 - Description

Grubbers Market Herb Stall is a market stall and north of Grubbers Market Exotic Gems Stall and south of Grubbers Market Northwest Junction. "The herb stall is usually a quiet place. Customers here like to browse, poke their noses into the various jars, and judge the potency of a potential purchase at their leisure. Other stalls are to the south, and east, and the market opens out a little to the north." The printed name is "Herb Stall". The distant description of the Herb Stall is "[one of]There's a herb stall[or]There are herbs for sale[or]Looks like they're selling herbs[at random] to the [quick best route from the location to the Herb Stall][if the Herb Stall is blocked]. The stallkeeper looks like he's still keeping a sharp eye out for you[end if]." The dead-end description of the Herb Stall is "There's nothing in that direction except the wall that surrounds the market square."

Understand "herbs" as the Grubbers Market Herb Stall. 

Instead of facing northeast in Grubbers Market Herb Stall:
	say "The stalls are too close together in that direction; you can't squeeze through.";

Instead of facing southeast in Grubbers Market Herb Stall:
	say "The stalls are too close together in that direction; you can't squeeze through.";

Chapter 2 - Contents (Herbs)

Some herbwares are wares, in the Herb Stall. The description is "The dozens of tiny jars are filled with dried and crushed bits of leaves, roots, berries, petals, moss, bark — basically, if it's from a plant, and it smells bad, you can find some shriveled piece of it here." Understand "display", "displays", "fare", "jars", "herb jars/display/displays/wares", "of" as the herbwares.

Rule for printing the name of the herbwares:
	say "jars of herbs";

Instead of smelling the herbwares, say "Phew, strong stuff.";

Instead of taking or stealing the herbwares:
	try stealing the collective herbs;

Some collective herbs are scenery, a stall display, in the Herb Stall. The description is "All of this herb stuff looks pretty much the same to you." The printed name is "herbs". Understand "jar", "herb", "stuff", "crushed", "bits", "pieces", "piece", "leaves", "leaf", "roots", "root", "berries", "berry", "petals", "petal", "moss", "mosses", "bark", "barks", "shriveled", "shrivelled" as the collective herbs. The corresponding item of the collective herbs is the herb jar. 

Before smelling the collective herbs,
	try smelling the herbwares instead;

Section 1 - Herb Jar

The herb jar is a container, open, transparent, merchandise. The description is "It's a little glass jar containing a few crumbling, dried weeds." Understand "little", "glass" as the herb jar. 

Some dried weeds are inside the herb jar. The description is "The stuff is dark and pungent." Understand "crumbling", "weed", "dark", "pungent", "herbs" as the weeds.

Instead of smelling the herb jar:
	try smelling the dried weeds;

Instead of opening the herb jar, try searching the noun.

Instead of searching the herb jar, say "You check the jar: yep, the weeds are still in there."

Instead of closing the herb jar, say "The jar is closed; those weeds aren't going anywhere."

Instead of inserting something into the herb jar: say "You can't put [the noun] in the jar; you keep your weed in there."

Instead of taking the dried weeds,
	say "They'd just spill everywhere.";

Instead of smelling the weeds:
	say "Phew, strong stuff."

Chapter 3 - Contents (Stallkeeper)

The herbalist is a stallkeeper, in the Herb Stall. Understand "herb", "herbs" as the herbalist.

Part 12 - Rope Dealer

Chapter 1 - Description

Grubbers Market Rope Dealer is a market stall and southeast of Grubbers Market Northwest Junction, east of Grubbers Market Herb Stall, northeast of Grubbers Market Exotic Gems Stall. "One thing you can say about the rope stall: you'll never spend a moment wondering what's for sale. It's all rope, and lots of it: thick and thin, knotted and loose, in heaps and in coils, stacked on the ground and dangling in loops. More stalls are to the northeast, southeast, west, and southwest, and the market opens out a bit to the northwest." The printed name is "Rope Stall". The distant description of Grubbers Market Rope Dealer is "[one of]There's a rope stall[or]There's rope for sale[or]Looks like they're selling rope[at random] to the [quick best route from the location to the Grubbers Market Rope Dealer][if the Grubbers Market Rope Dealer is blocked]. The stallkeeper looks like he's still keeping a sharp eye out for you[end if]." The dead-end description of Grubbers Market Rope Dealer is "The stalls are too close together in that direction; you can't squeeze through."

Chapter 2 - Contents (Ropes)

Some rope wares are a stall display, scenery, in Grubbers Market Rope Dealer. "It's all rope, and lots of it: thick and thin, knotted and loose, in heaps and in coils, stacked on the floor and hanging from pegs." The printed name is "rope". Understand "wares", "goods", "merchandise", "rope", "ropes", "knot", "knots", "knotted", "loose", "thick", "thin", "heap", "heaps", "heaped", "coil", "coils", "coiled", "loop", "loops", "looped", "display", "displays", "stack", "stacks", "stacked", "of", "hanging", "hung", "dangling" as the rope wares.

The corresponding item of the rope wares is the length of rope.

A length of rope is merchandise. The description is "The piece of rope is about five feet long, stiff and scratchy." Understand "piece", "stiff", "scratchy", "five/5 feet/foot", "long", "short" as the length of rope.

Chapter 3 - Contents (Stallkeeper)

The rope dealer is a stallkeeper, in Grubbers Market Rope Dealer. 

Part 13 - Candle Stall

Chapter 1 - Description

Grubbers Market Candle Maker Stall is a market stall, privately-named, northeast of Grubbers Market Rope Dealer, east of Grubbers Market Northwest Junction, south of Grubbers Market Grocery Stall, west of Grubbers Market Fruit Stall. "The heat and fumes from the candlemaker's tallow pots make this stall an unpleasant place to loiter. Still, people need candles, and customers seem to be buying them as fast as the stallkeeper can dip them. Most of the candles are tallow, fast-burning and cheap, but there are also a number of premade wax candles in different colors. Other stalls are to the north, east, west, southeast, and southwest." The printed name is "Candlemaker's Stall". The distant description of the Candle Stall is "[one of]There's a candle stall[or]There's candles for sale[or]Looks like they're selling candles[at random] to the [quick best route from the location to the Candle Stall][if the Candle Stall is blocked]. The stallkeeper looks like he's still keeping a sharp eye out for you[end if]." The dead-end description of the Candle Stall is "The stalls are too close together in that direction; you can't squeeze through."

Understand "candles", "makers", "candlemakers", "stall", "market stall" as the Candle Maker Stall. 

Instead of smelling the Candle Maker Stall:
	say "The fumes from the boiling tallow make your eyes water.";

Chapter 2 - Concerning wax

Wax is a kind of value. The waxes are red wax, green wax, blue wax and white wax.

To say (w - a wax) wax:
	if w is red wax begin;
		say "red";
	otherwise if w is green wax;
		say "green";
	otherwise if w is blue wax;
		say "blue";
	otherwise if w is white wax;
		say "white";
	end if;

Understand "red" as red wax.
Understand "green" as green wax.
Understand "blue" as blue wax.
Understand "white" as white wax.

When play begins:
	change the wax color of the wax candle to a random wax.

Understand the wax color property as referring to the wax candle.

Chapter 3 - Contents (Candles)

Section 1 - Wares

A stock-of-candles is wares, in the Candle Maker Stall. The description is "The candles hang in pairs, connected to each other by a common wick. Most of them are tallow, fast-burning and cheap, but there are also a number of premade wax candles in different colors." Understand "display", "displays", "fare" as the stock-of-candles.

Instead of stealing or taking the stock-of-candles:
	try stealing a random nearby candle display;

A candle display is a kind of stall display. Understand "candle", "candles", "pair", "pairs", "pair of", "pairs of" as a candle display.

Section 2 - Tallow Candles

Some tallow candles are a candle display, in the Candle Maker Stall. "The tallow candles are yellow and slightly lumpy." The printed name is "tallow candles". The corresponding item of the tallow candles is the tallow candle. Understand "yellow", "lumpy", "cheap", "fast", "burning" as the tallow candles.

The tallow candle is merchandise. The description is "The tallow candle is yellow and slightly lumpy." The printed name is "candle". Understand "yellow", "lumpy", "cheap", "fast", "burning" as the tallow candle.

Section 3 - Wax candles

Some wax candles are a candle display, in the Candle Maker Stall. "The wax candles are smooth and straight, and come in several colors." The printed name is "wax candles". Understand "smooth", "straight", "colored", "coloured", "wax candle", "premade", "color", "colors", "colour", "colours" as the wax candles. The corresponding item of the wax candles is the wax candle. 

Before stealing or taking the wax candles:
	try stealing a random nearby waxy display instead;

The wax candle is merchandise. The description is "The wax candle is smooth and straight, and colored bright [wax color wax]." The printed name is "candle". The wax candle has a wax called the wax color. Understand "smooth", "straight", "colored", "coloured", "wax", "premade", "color", "colors", "colour", "colours" as the wax candle. 

Chapter 4 - Stealing particularly colored wax candles 

[Grotesquely inelegant, but typing "steal red candle" and ending up with a green one wasn't very satisfying.]

A waxy display is a kind of stall display. The description of a waxy display is usually "The wax candles are smooth and straight, and come in several colors, including [waxy color wax]."
A waxy display has a wax called the waxy color.

Rule for printing the name of a waxy display:
	say "[waxy color wax] wax candles";

The corresponding item of a waxy display is the wax candle.

Some red-wax-candles are a waxy display, in the Candle Maker Stall. The waxy color is red wax. Understand "red", "red wax", "red wax candle/candles", "red candle/candles" as red-wax-candles.

Some blue-wax-candles are a waxy display, in the Candle Maker Stall. The waxy color is blue wax. Understand "blue", "blue wax", "blue wax candle/candles", "blue candle/candles" as blue-wax-candles.

Some green-wax-candles are a waxy display, in the Candle Maker Stall. The waxy color is green wax. Understand "green", "green wax", "green wax candle/candles", "green candle/candles" as green-wax-candles.

Some white-wax-candles are a waxy display, in the Candle Maker Stall. The waxy color is white wax. Understand "white", "white wax", "white wax candle/candles", "white candle/candles" as white-wax-candles.

Before stealing a waxy display (called the waxy source):
	[ possibly my least favourite single line of Inform in the whole game: ]
	change the wax color of the wax candle to the waxy color of the waxy source;
	[ okay that's frikkin' funny -- MSG ]

Chapter 4 - Scenery (Tallow Pots)

Some tallow pots are a container, in the Candle Maker Stall. They are scenery. "The yellow, bubbling goop is kept at a constant boil." The printed name is "pots". Understand "pot", "cauldron", "cauldrons", "yellow", "boiling", "bubbling", "goop" as the tallow pots. Understand "tallow pot/pots/cauldron/cauldrons" as the tallow pots.

Before smelling the tallow pots:
	try smelling the location instead;

Instead of doing something other than examining to the tallow pots:
	say "'Hey,' snaps the stallkeeper, 'stay out of my tallow pots!'";

Chapter 4 - Scenery (Dowel rods)

The dowel rods are scenery, in the Candle Maker Stall. The description is "The long, wooden dowels run the width of the stall." Understand "dowels", "rod", "long", "wood", "wooden" as the dowel rods.

Chapter 4.5 - Scenery (Fumes)

Some fumes are scenery, in the Candle Maker Stall. The description is "Pungent fumes rise from the tallow pots."

Understand "fume", "steam", "vapor", "vapors" as the fumes.

Before smelling the fumes, try smelling the location.

Instead of doing something other than smelling the fumes, say "They're just insubstantial fumes."

Chapter 5 - Candle Stallkeeper

The candlemaker is a stallkeeper and in the Candle Maker Stall. The printed name of the candlemaker is "stallkeeper". 

Understand "candlemaker", "candle maker/merchant/seller/stallkeeper", "candle stall keeper" as the candlemaker. 

Part 14 - Pottery Stall

Chapter 1 - Description

Grubbers Market Pottery Stall is a market stall and southeast of Grubbers Market Candle Maker Stall, southwest of Grubbers Market Fruit Stall, west of Grubbers Market Eastern Junction, northwest of Grubbers Market Hat Stall. "The wares at the pottery stall are spread out on the ground, forcing browsers and passers-by to step carefully around them. The urns, jars, and bowls are each uniquely shaped, yet they all share a common, graceful curve. Most of them are made of fired clay from the Westlands, decorated with colorful glazes. There are more stalls to the northwest, northeast, southeast, and southwest, or you can head out to the market's edge to the east." The printed name is "Pottery Stall". The distant description of the Pottery Stall is "[one of]There's a pottery stall[or]There's pottery for sale[or]Looks like they're selling pottery[at random] to the [quick best route from the location to the Pottery Stall][if the Pottery Stall is blocked]. The stallkeeper looks like he's still keeping a sharp eye out for you[end if]." The dead-end description of the Pottery Stall is "The stalls are too close together in that direction; you can't squeeze through."

Understand "pots" as the Pottery Stall. 

Chapter 2 - Contents (Pottery)

Section 1 - General merchandise

Some assorted-pottery is wares, in the Pottery Stall. "From tall, narrow, precariously balanced urns to round, gourd-like jars to shallow bowls, each is uniquely shaped, yet they all share a common, graceful curve. Most of them are made of fired clay from the Westlands, decorated with colorful glazes." Understand "display", "displays", "fare" as the assorted-pottery.

Instead of stealing the assorted-pottery:
	try stealing a random nearby stall display;

Section 2 - Stall displays

A pottery display is a kind of stall display. Understand "clay", "fired", "graceful", "curve", "curves", "colorful", "color", "colors", "glaze", "glazes", "glazed", "pottery", "decorations", "decoration" as a pottery display.

Some clay pots are a pottery display, in the Pottery Stall. "The clay pots are simple but functional, and come in all sizes." The printed name is "clay pots". The corresponding item of the pots is the clay pot. Understand "pot", "simple", "functional" as the clay pots.

Some urns are a pottery display, in the Pottery Stall. "The urns are mostly decorative, for putting flowers or rushes in." The printed name is "urns". The corresponding item of the urns is the clay urn. Understand "urn", "tall", "narrow", "precarious", "precariously", "balanced", "decorative" as the urns.

Some clay jars are a pottery display, in the Pottery Stall. "The round jars are just the thing for keeping in a pantry." The printed name is "jars". The corresponding item of the jars is the clay jar. Understand "jar", "round", "gourd", "gourds", "like" as the clay jars.

Some bowls are a pottery display, in the Pottery Stall. "The shallow bowls are ideal for holding fruit or wine." The printed name is "bowls". The corresponding item of the bowls is the clay bowl. Understand "bowl", "shallow", "dish", "dishes" as the bowls.

Section 3 - Individual items

Ceramics are a kind of container. Ceramics are usually merchandise. Understand "clay", "fired", "glazed", "pottery" as Ceramics.

Instead of inserting something into Ceramics, say "It's really too small to conveniently hold things while you're carrying it."

The clay pot is Ceramics. The description is "It's a little clay pot, small enough to fit comfortably in your hand." The printed name is "pot". Understand "little", "small" as the clay pot.

The clay urn is Ceramics. The description is "You grabbed one of the smaller, thinner, urns." The printed name is "urn". Understand "small", "smaller", "thin", "thinner", "narrow", "decorative" as the clay urn.

The clay jar is Ceramics. The description is "It's a small, round jar, just the thing for keeping in a pantry." The printed name is "jar". Understand "small", "round", "like", "gourd" as the clay jar.

The clay bowl is Ceramics. The description is "It's a small, shallow bowl, ideal for holding fruit or wine." The printed name is "bowl". Understand "small", "shallow" as the clay bowl.

Chapter 3 - Pottery Stallkeeper

The pottery stallkeeper is a stallkeeper and in the Pottery Stall. The printed name is "stallkeeper". Understand "pot", "potter" as the pottery stallkeeper.

Part 15 - Some wooden posts

Chapter 1 - Tantalisingly inaccessible wooden post

To say tent or stall:
	if the location is Outside Silk Stall,
		say "tent";
	otherwise say "stall";

The inaccessible center post is a backdrop. It is not scenery. The printed name is "center post".

The description is "This is the central post that holds up the canvas awnings stretched over the market square. It rises from the midst of a cluster of stalls in the middle of the market, to a height of about fifty feet. Thick, taut support wires stretch from the top of the post in four directions: northwest, northeast, southwest, and southeast."

The initial appearance is "[one of]Rising up behind the [tent or stall] is a tall wooden post[or]You see a tall wooden post rising up behind the [tent or stall][or]A tall wooden post is visible behind the [tent or stall][or]Behind the [tent or stall] is a tall wooden post[at random]."

The inaccessible center post is in the Rope Dealer, the Pottery Stall, the Candle Maker and Grubbers Market Outside Silk Stall.

Instead of doing something other than examining to the inaccessible center post, say "You need to find a way behind the stalls to get to the post."

Understand "big", "central", "wooden", "pole", "support", "center" as the inaccessible center post.

Chapter 2 - Rather more distant wooden post

The distant center post is a backdrop. "The big central support post rises up from the middle of the market, holding up the canvas awnings that shade the square." It is in Outer Market Roof, Grubbers Market Northwest Junction, Grubbers Market Grocery Stall, Grubbers Market Fruit Stall, Grubbers Market Eastern Junction, Grubbers Market Hat Stall, Grubbers Market Leather Shop, Grubbers Market Weapons Stall, Grubbers Market Exotic Gems Stall, and Grubbers Market Herb Stall. 

Instead of doing something other than examining to the distant center post, say "You need to find a way behind the stalls to get to the post."

Understand "big", "wooden", "pole", "support", "center", "central", "tall" as the distant center post.

Part 16 - Outside Silk Tent

Chapter 1 - Description

Grubbers Market Outside Silk Stall is a market stall, southeast of Grubbers Market Rope Dealers, southwest of Grubbers Market Pottery Stall, west of Grubbers Market Hat Stall, northwest of Grubbers Market Leather Shop, northeast of Grubbers Market Weapons Stall, east of Grubbers Market Exotic Gems Stall. "Teisha has done well enough for herself that her 'stall' is actually a full-on tent. You can enter the tent to the north, or move on to one of the many other stalls to the east, west, northeast, northwest, southeast, or southwest." The printed name is "Outside the Silk Tent". The distant description of Outside Silk Stall is "Teisha's silk tent is [quick best route from the location to Outside Silk Stall] from here." The dead-end description of Outside Silk Stall is "The stalls are too close together in that direction; you can't squeeze through."

Understand "Tent" as Grubbers Market Outside Silk Stall. Understand "Teishas" as Grubbers Market Outside Silk Stall when the location is not Grubbers Market Teishas Tent. [This is to prevent the parser from getting confused when the player types "x Teisha's Tent" from inside the tent.]

Instead of going inside in Grubbers Market Outside Silk Stall:
	try going north;

Instead of facing inside in Grubbers Market Outside Silk Stall:
	try facing north;

Rule for printing the distant description of Grubbers Market Outside Silk Stall while in Grubbers Market Teishas Tent:
	say "You can't see much from inside the tent. ";

Chapter 2 - Scenery (Tent)

The backdrop-tent is a backdrop, in Grubbers Market Outside Silk Stall, Grubbers Market Teishas Tent, and Grubbers Market Center Post. Understand "silk", "bright", "blue", "faded", "dust", "dusty" as the backdrop-tent. "Teisha's tent is bright blue, though now it's somewhat faded and covered with market dust. You can enter to the [if the location is Grubbers Market Outside Silk Stall]north[otherwise]south[end if]."

Part 17 - Inside Silk Tent

Chapter 1 - Description

Grubbers Market Teishas Tent is a shop, north from Grubbers Market Outside Silk Stall. "The walls of the tent provide a bit of quiet and privacy from the crowds outside (in addition to keeping dust off the merchandise). Beautiful silk garments hang on either side. Gowns, robes, capes, cloaks, and shawls, the fabric rippling with the slightest movement of air, shimmering with that soft, liquid glow that only silk has. The main market lies to the south. A narrow gap to the north leads behind the tent." The printed name is "Inside the Silk Tent". The distant description of Teishas Tent is "Teisha's tent is bright blue, though now it's somewhat faded and covered with market dust. You can enter to the [if the location is Grubbers Market Outside Silk Stall]north[otherwise]south[end if]." The dead-end description of Teishas Tent is "The only exits from the tent are to the north and south."

Understand "silk", "bright", "blue", "faded", "dust", "dusty" as Grubbers Market Teishas Tent. 

Instead of facing south in Grubbers Market Teishas Tent:
	say "You can go back outside to the south."

Instead of facing north in Grubbers Market Teishas Tent:
	say "You can slip out the back way to the north.";

Instead of smelling Grubbers Market Teishas Tent, say "It smells of warm, dusty fabric in here."
Instead of listening to Grubbers Market Teishas Tent in Grubbers Market Teishas Tent, say "The sounds of the market are muffled in here."

Understand "Teishas", "tent", "walls", "canvas" as the backdrop-ceiling when the location is Grubbers Market Teishas Tent.

[ Rule for printing the description of the backdrop-ceiling when the location is Grubbers Market Teishas Tent:
	say "..."; ]

Chapter 2 - Teisha's Wares

Some silkgarments are wares, in Grubbers Market Teishas Tent. "Gowns, robes, capes, cloaks, and shawls — the fabric rippling with the slightest movement of air, shimmering with that soft, liquid glow that only silk has. You sigh. Dressing up like a boy is definitely [i]fun[r], but looking at Teisha's wares always makes you yearn for something a little bit more... elegant." 

Understand "display", "displays", "fare", "Teishas" as the silkgarments. 

Instead of stealing the silkgarments:
	say "You'd sooner go hungry than steal from Teisha. She's your friend.";

Instead of buying the silkgarments:
	say "You'd love to, but you haven't any money.";

Instead of buying the silkgarments when the player has the necklace or the necklace is in the satchel:
	try trading the necklace with Teisha.

Instead of trading the silkgarments for the necklace:
	try trading the necklace with Teisha.

Instead of buying silkware:
	try buying the silkgarments instead;

Silkware is a kind of thing. Silkware is scenery. Instead of stealing silkware, try stealing the silkgarments. Understand "beautiful", "silk", "silks", "garment", "garments", "clothes", "fabric", "fabrics", "rippling", "shimmering" as silkware.

Some collective gowns are silkware, in Grubbers Market Teishas Tent. "The gowns are simple designs; not like the over-decorated things all the rich women wear to high-fashion parties. Just elegant and beautiful." The printed name is "gowns". Understand "gown", "simple", "design", "designs", "elegant" as the collective gowns.

Some collective capes are silkware, in Grubbers Market Teishas Tent. "The capes are short, waist-length, more for decoration than to actually keep you warm. Most of them have different-colored inner linings that complement the outer fabric." The printed name is "capes". Understand "cape", "short", "waist", "length", "decorative", "inner", "lining", "linings", "color", "colors", "colored", "colour", "colours", "coloured", "outer" as the collective capes.

Some collective cloaks are silkware, in Grubbers Market Teishas Tent. "The cloaks are long and voluminous. Most of them would reach down to your ankles and wrap completely around your shoulders, and with the hood up your face would be draped in shadow. [first time][paragraph break]You realize suddenly that this is exactly what you need: the perfect disguise![subsequently]The perfect disguise, if only you could afford one.[only]". The printed name of the collective cloaks is "cloaks". Understand "cloak", "long", "voluminous", "hood", "hoods" as the collective cloaks.

Rule for clarifying the parser's choice of the collective cloaks:
	do nothing;

Some collective shawls are silkware, in Grubbers Market Teishas Tent. "The shawls are just simple wraps of brightly colored silk, to cover your arms and shoulders." Understand "shawl", "wrap", "wraps", "bright", "brightly", "color", "colors", "colored", "colour", "colours", "coloured" as the collective shawls. The printed name is "shawls". 

Some collective robes are silkware, in Grubbers Market Teishas Tent. "The robes are long-sleeved, with ties in the front." Understand "robe", "sleeves", "sleeve", "long", "sleeved", "tie", "ties" as the collective robes. The printed name is "robes".

Chapter 3 - The silk cloak

The green silk cloak is wearable, merchandise. The description is "It comes down to your ankles, a simple drape of soft, dusky green with silver trim and a voluminous hood[if the silk cloak is worn]. You're wearing it in the feminine style, forward over both shoulders and clasped under your chin, and the hood drawn up[end if]."
Understand "drape", "silver", "trim" as the green silk cloak.
Understand "new" as the green silk cloak when the green silk cloak is handled.

The hood is part of the green silk cloak. The description is "The hood is part of the cloak and has the same silver trim[if the green silk cloak is worn]. It covers your head completely, casting your face in shadow[end if]." Understand "my" as the hood.

Understand "wear [hood]" as raising.

Before raising the hood when the silk cloak is not worn:
	say "You need to be wearing the green cloak before you can raise the hood." instead;

Instead of raising the hood when the silk cloak is worn:
	say "The hood is already up.";

Before lowering the hood when the silk cloak is not worn:
	say "You need to be wearing the green cloak before you can lower the hood." instead;

Instead of lowering the hood when the silk cloak is worn:
	say "If you lower the hood, you run the risk of someone recognizing you when you try to leave the market.";

[ If there were more things to wear, we might simulate clothing more carefully, but this will do for now: ]

Before wearing the silk cloak when Trouble in Grubbers has ended:
	say "Now that you're not being chased by Mercenaries, you ought to stick to your [i]actual[r] disguise... the one where you're trying to [i]not[r] look like a girl." instead;

After wearing the silk cloak during Trouble in Grubbers:
	say "You slip the silk cloak over your shoulders and pull the hood up. The fabric feels smooth and cool on your skin, but you don't have time to admire it now — you've got to run!";

Before taking off the silk cloak during Final Chase:
	say "Leave the cloak on! Your only chance is to get out of Grubber's without being recognized." instead;

Instead of giving or showing the silk cloak to someone during Avoiding Soldiers:
	say "You need this cloak to get past the mercenaries. Better hold on to it for now.";

Instead of wearing the silk cloak when the player wears the gray cloak:
	say "You can't wear more than one cloak!" instead;

Instead of wearing the gray cloak when the player wears the silk cloak:
	say "You can't wear more than one cloak!" instead;

After wearing the silk cloak:
	say "You slip the cloak around your shoulders, fastening it just under your neck in the feminine style and drawing the edges together to hide your clothes. [if the player wears the woolen hat]If you took your cap off, you'd[otherwise]With your cap off, you[end if] look just like a young girl, albeit one with a very uneven haircut.[paragraph break]";
	if Teisha is nearby:
		say "Teisha leans over and adjusts the fit, plucking at the fabric where it hangs from your shoulders. 'I was right,' she says. 'It brings out your eyes. You look... almost [i]royal[r].'";

Chapter 4 - Teisha

Section 1 - Description

Teisha is a woman, in Grubbers Market Teishas Tent. The initial appearance of Teisha is "Teisha sits on a stool in back, smiling distractedly." Teisha is female. The description of Teisha is "Teisha is a short, busty woman with a smile as warm as her tumbling red curls. She likes you, and she knows that you'd never nick from her stall, so she lets you hang around the stall and look through the merchandise. She also knows your big secret (guessed it the very first time she caught you loitering at the tent flap), which is a little bit scary and a little bit of a relief — it's nice to be able to let your guard down and talk to someone outside Maiden House." Understand "woman", "merchant", "saleswoman", "seller", "vendor", "clothier", "short", "busty", "warm", "smile", "tumbling", "red curls", "curls", "red hair", "hair", "redhead", "red head", "Teishas" as Teisha.

A measuring cord is carried by Teisha. The description is "It's a five-foot-long length of cord, marked off in ten segments, used to take a customer's measurements." Understand "Teishas" as the measuring cord.

Instead of kissing Teisha, say "You throw your arms around Teisha's neck and give her a kiss on the cheek. She laughs and looks at you strangely. 'What's got into you, Jack?'"

Section 2 - Actions

Every turn when Teisha is nearby and the player is not on the move:
	if a random chance of 1 in 5 succeeds:
		add ATMOS_Teisha to the dramatic possibilities, if absent;

ATMOS_Teisha is a dramatic event.

For firing ATMOS_Teisha:
	say "[one of]Teisha hums quietly to herself[or]Teisha smiles at you warmly[or]Teisha brushes a loose thread from the sleeve of one of her gowns[or]Teisha toys with her measuring cord, spinning it idly[at random].";

Section 3 - Reactions

Chatting to Teisha is a recurring scene. Chatting to Teisha begins when the player is in Teishas Tent. Chatting to Teisha ends when the player is not in Teishas Tent.

When Chatting to Teisha begins:
	if the player is in Grubbers Market Teishas Tent for the first time:
		start conversation with Teisha on TE1;
	otherwise:
		if the last room was Outside Silk Stall:
			if TE2 is unfired and TE6 is unfired and TE5 is unfired:
				start conversation with Teisha on TE5;
			otherwise:
				start conversation with Teisha on TE9;

Rule for initiating conversation with Teisha:
	change the chosen opening gambit to TE10.

Instead of trading something for the collective cloaks:
	try trading the noun with Teisha; 

Does the player mean trading something for the collective cloaks: it is likely.
Does the player mean buying the collective cloaks: it is likely. 

After trading something with Teisha:
	if the noun is not the necklace,
		start conversation with Teisha on TE16;

After trading the necklace with Teisha:
	try giving the necklace to Teisha instead;	

Instead of giving or showing the necklace to Teisha:
	start conversation with Teisha on TE20.

After going through the narrow gap from Grubbers Market Teishas Tent:
	if Tent Escape is happening begin;
		say "Without pausing to think, you dive through the back flap.";
	otherwise;
		say "[first time]Teisha holds the back flap back as you duck through. 'Be careful now...' she says, '...[run paragraph on][i]Jacqueline[r].'[subsequently]'Careful out there,' calls Teisha as you duck through the back again.[only]";
	end if;
	continue the action;

After going south from Grubbers Market Teishas Tent:
	say "[first time]'Good-bye, Teisha,' you call as you head back out into the market.[subsequently]'[']Bye, Teisha,' you say.[paragraph break]'Take care, Jack,' calls Teisha.[only]";
	continue the action;

Section 4 - Conversation

[Tried defining these in a table, a la 15.16 "Defining things with tables" but ran into difficulties and I7 compiler crashes. Probably to do with the response lists referring to other items defined in the same table. ]

TE1 is a quip. The display text is "'Hello, Jack,' says Teisha. 'Change your mind about that cloak?' Then she gets a look at your face and frowns. 'Something the matter, sweetie? You look spooked.'"

TE2 is a quip. The menu text is "'There are big guys with swords chasing me.'" The display text is "'Now that you mention it, I am a bit spooked,' you say, trying to catch your breath. 'There's a bunch of big guys with swords out there, chasing me all over the market!'[paragraph break]Teisha frowns. 'Big guys with — you mean [i]mercenaries[r]? In Grubber's?' She peeks out through the front flap. 'Brigid help us,' she mutters, glancing back at you. 'Don't tell me what they're after you for — I don't want to know.'" 

TE3 is a quip. The menu text is "'Actually, I'd like to look at that cloak again.'" The display text is "'Um, actually,' you mutter, glancing nervously at the tent flap, 'I'd like to look at that cloak again. If you don't mind.'[paragraph break]Teisha gives you a suspicious look. 'Well, all right, Jack,' she says finally. 'Browse all you like.'" 

TE4 is a quip. The menu text is "'No, everything's fine.'" The display text is "'No, no,' you say quickly. 'Everything's just fine. Couldn't be better.'[paragraph break]Teisha raises one eyebrow at you, but doesn't push it." 

TE5 is a quip. The display text is "'Back so soon, Jack?' Teisha asks. She eyes you closely. 'Are you [i]sure[r] there's nothing the matter?'" 

TE6 is a quip. The menu text is "'No, really, everything's just tricky.'" The display text is "'No, really,' you say, in a not-quite-steady voice. 'Everything's just tricky.'[paragraph break]Teisha smirks. 'Uh-huh. You know I don't just sit by myself in this silk tent all day, don't you, Jack? I do have some idea of what's going on in the world outside, you know. I know there are mercenaries searching Grubber's, and I'm guessing they're probably after you.' Her voice softens, and she puts her hand on your shoulder. 'Don't worry, Jack. I don't need to know what you did to get them after you. I won't turn you in.'"

TE7 is a quip. The menu text is "'I need somewhere to hide.'" The display text is "'I need help, Teisha,' you tell her plaintively. 'I need somewhere to hide.'[paragraph break]Teisha sighs. Although you know she cares about you, you also know that Teisha is a smart, practical woman, and first and foremost she is worried about losing her customers. 'All right,' she says, 'you can stay in here as long as you like, but if the mercenaries decide to search the place I can't stop them. Probably your best bet is to slip out that way—' she points to the narrow gap in the back wall of the tent. 'I'll pretend I never saw you.'"

TE8 is a quip. The menu text is "'I need a disguise.'" The display text is "Suddenly your eyes fall on the many cloaks hanging among Teisha's silks. 'I could use one of your cloaks as a disguise,' you say. 'The mercenaries expect me to be wearing my usual ratty stuff.'[paragraph break]Teisha sighs. 'Jack... I'm sorry. These silks are expensive, and I have to pay my suppliers and make a living for myself. If you have coin, or something worth trading, maybe I can cut you a deal. Otherwise...' she shakes her head."

TE9 is a quip. The display text is "Teisha's face falls as you stumble back into the tent. 'You're still here?' she asks. 'I'd hoped you'd gotten away by now.'" 

TE10 is a quip. The menu text is "'Can I ask you a question?'" The display text is "'Teisha, can I ask you a question?'[paragraph break]'Of course, Jack,' she says." 

TE11 is a quip. The menu text is "'What can you tell me about Grubber's Market?'"
The display text is "[first time]'What can you tell me about Grubber's Market?' you ask. 'Are there any other ways out of here?'[paragraph break]Teisha shrugs. 'I figured you'd know more about the place than I do, the way you sneak about,' she says. 'From the northwest junction you can leave the city. East will take you to Commerce Street. Those are the only two ways out of the square, as far as I know.'[subsequently][rp] 'north from the northwest junction, or east from the east junction. That's all I know.'[only]"

TE12 is a quip. The menu text is "'Who's selling what today?'" The display text is "[first time]'I know that different merchants set up their stalls on different days of the week,' you say. 'So who's selling what today?'[paragraph break]'Well, there's the fruit stall on the northeast corner, but that's there every day,' says Teisha. 'To the north is a grocer's stall; a hat seller to the southeast; to the west is an herb seller and the gem merchant; and the leatherworker and weaponsmith have set up shop on the south end. The inner ring is me, a rope merchant, a pottery merchant, and that awful candlemaker — the stench has been driving off my customers all morning.'[subsequently][rp] 'there's the fruit stall to the northeast; the grocer to the north; the hat seller to the southeast; the herb seller and the gem merchant to the west; the leatherworker and weaponsmith to the south; and me, the rope merchant, the potter merchant, and the candlemaker in the inner ring.'[only]"

TE13 is a quip. The menu text is "'What's behind your tent?'" The display text is "[first time]'What's that behind your tent?' you ask, nodding towards the back exit.[paragraph break]'There's nothing back there except the central pole holding up the big awning over the market,' Teisha says. 'That's why all the merchants want this spot,' she adds, '...it's got the best shade.'[subsequently][rp] 'it's just a big pole.'[only]"

TE14 is a quip. The menu text is "'Do you make these clothes yourself?'" The display text is "[first time]'These clothes are all so beautiful,' you say. 'Do you make them yourself?'[paragraph break]Teisha smiles. 'Some of them,' she says. 'Most of them I sell on consignment. One of these days, I'm going to clear enough profit to rent a shop on Commerce Street and move out of this hot, smelly tent.'[subsequently][rp] 'some of them I make myself, some I sell for others.'[only]".

TE15 is a quip. The menu text is "'What's the deal with that monkey?'" The display text is "[first time]'The gem merchant sounds like he's got a real vendetta against that monkey,' you remark. 'What's the story with them?'[paragraph break]Teisha rolls her eyes. 'That pesky thing came in on the fruit vendor's wagon yesterday and just made itself at home. Now it's all over the place, bothering everybody. Fortunately, it likes shiny things more than it likes women's clothes. Oh, and bananas. The little rodent loves bananas. Constantly drops the peels on the roof of my tent.'[subsequently][rp] 'it just runs around, eats bananas, and steals anything shiny that isn't nailed down.'[only]".

TE16 is a quip. The menu text is "'What will you trade for a cloak?'" The display text is "[first time]'I [i]need[r] one of those cloaks, Teisha,' you say. 'Just tell me what you want for it, and I'll get it, somehow.'[paragraph break]Teisha thinks for a moment. 'Most of the things you'd find in Grubber's are staple goods, not much use to me. I'd need something with [i]resale[r] value. Jewelry, or the like. But don't you go stealing anything, Jack,' she adds hastily. 'I don't want you getting into even more trouble than you're already in!'[subsequently][rp] 'a piece of jewelry would do nicely.'[only]".

TE17 is a quip. The menu text is "'One more question.'" The display text is "'[one of]Can I ask you another question[or]One more question[or]Can I ask you something else[or]Do you mind if I ask you one more question[at random]?' you ask.[paragraph break]'[one of]Of course, Jack[or]Ask away[or]No problem[or]Sure[at random],' says Teisha." 

TE18 is a quip. The menu text is "'Never mind.'" The display text is "'Um, never mind,' you say.[paragraph break]'All right,' says Teisha. 'Just let me know if you need anything.'"

TE19 is a quip. The menu text is "'Thanks.'" The display text is "'Thanks, Teisha.'[paragraph break]'No problem, Jack.'"

TE20 is a quip. The menu text is "'Will you trade me a cloak for this necklace?'" The display text is "You hold out the necklace, dangling it by its chain. 'Have a look at this, Teisha,' you say. 'You think this is worth one of your cloaks?'[paragraph break]Teisha's eyebrow shoots up, but she manages to hide her excitement with the coolness of a professional haggler. 'That's not bad,' she mutters. 'Most of Hathid's wares are just pretty glass, but this...' She gives you a look. 'Now, tell me the truth, Jack, you didn't just steal this from somewhere, did you?'[paragraph break]You shake your head proudly. It's not [i]exactly[r] a lie.[paragraph break]'Well, not that it matters.' The necklace disappears into one of Teisha's pockets. 'Let's see...' she starts searching through the clothes, looking for something in your size. 'This should fit. I think it suits you, too.' She holds out a light green cloak. 'What do you think?'[paragraph break]It takes your breath away, but you try to hide your eagerness just as Teisha hid hers. 'It's tricky,' you say, nodding casually.[paragraph break]Teisha laughs. 'Then it's yours.'"

TE21 is a quip. The menu text is "'What do you think about Baron Fossville?'". The display text is "[first time]'It seems like everyone has an opinion about Baron Fossville, lately,' you remark. 'What's yours?'[paragraph break]Teisha laughs. 'So you've noticed it too, huh? I don't usually like to talk politics, which means I haven't had much in the way of conversation in Grubber's Market lately.' She shrugs. 'I don't like his high taxes, but who does? Fossville doesn't strike me as a very nice person, but I guess he doesn't have to be.'[subsequently][rp] 'I don't like him very much, but there's not much use complaining.'[only]".

TE22 is a quip. The menu text is "'What's this Ascension thing?'". The display text is "[first time]'What's this Ascension thing I keep hearing about?' you ask. 'Everyone in the market seems to be talking about it.'[paragraph break]'Ascension is when they name the heir to the throne of all Miradania,' Teisha says. 'Everyone's talking about it because the King didn't explicitly name his daughter heir before he died, so now there's talk the new ruler might be chosen by a council of noblemen. I don't see why it's so interesting, frankly. What the King and Queen do miles away in the capital doesn't have much to do with how many cloaks I sell today.'[subsequently][rp] 'it's the naming of the new heir to the throne. That's really all I know; I don't pay much attention to Royal politics.'[only]".

The response of TE1 is {TE2, TE3, TE4, TE20}.

The response of TE2 is {TE7, TE8, TE20}.

The response of TE3 is {TE10, TE20}.

The response of TE4 is {TE10, TE20}.

The response of TE5 is {TE2, TE6, TE20}.

The response of TE6 is {TE7, TE8, TE20}.

The response of TE7 is { TE8, TE10, TE19, TE20 }. 

The response of TE8 is { TE7, TE10, TE20 }.

The response of TE9 is {TE7, TE8, TE10, TE20}.

The response of TE10 is { TE11, TE12, TE13, TE14, TE15, TE16, TE21, TE22, TE20, TE18 }.

The response of TE11 is {TE17, TE19}.

The response of TE12 is {TE17, TE19}.

The response of TE13 is {TE17, TE19}.

The response of TE14 is {TE17, TE19}.

The response of TE15 is {TE17, TE19}.

The response of TE16 is {TE17, TE19, TE20}.

The response of TE17 is { TE11, TE12, TE13, TE14, TE15, TE16, TE21, TE22, TE20, TE18 }.

The response of TE21 is {TE17, TE19}.

The response of TE22 is {TE17, TE19}.

After populating TE7:
	if TE8 is fired begin;
		remove TE8 from the current conversation, if present;
	end if;

After populating TE8:
	if TE7 is fired begin;
		remove TE7 from the current conversation, if present;
	end if;

After populating TE9:
	if TE7 is fired begin;
		remove TE7 from the current conversation, if present;
	end if;
	if TE8 is fired begin;
		remove TE8 from the current conversation, if present;
	end if;

To maybe prepend TE7 and TE8:
	if TE6 has been fired or TE2 has been fired begin;
		if TE8 has not been fired begin;
			add TE8 at entry 1 in the current conversation;
		end if;
		if TE7 has not been fired begin;
			add TE7 at entry 1 in the current conversation;
		end if;
	end if;

After populating TE10:
	maybe prepend TE7 and TE8;
	if Grubbers Market Exotic Gems is unvisited or the player carries the necklace begin;
		remove TE15 from the current conversation, if present;
	end if;
	if TE8 is unfired begin;
		remove TE16 from the current conversation, if present;
	end if;		

After populating TE17:
	maybe prepend TE7 and TE8;
	if Grubbers Market Exotic Gems is unvisited or the player carries the necklace begin;
		remove TE15 from the current conversation, if present;
	end if;
	if TE8 is unfired begin;
		remove TE16 from the current conversation, if present;
	end if;

After populating when Teisha is the current conversationalist and the necklace is not held by the player:
	remove TE20 from the current conversation, if present;

After firing TE20:
	remove the necklace from play;
	now the player carries the green silk cloak;

Chapter 5 - Back Flap

The narrow gap is an open door, not openable, scenery. It is north of Grubbers Market Teishas Tent. It is south of Grubbers Market Center Post. The description is "A narrow gap in the back wall of the tent leads [if the location is Grubbers Market Teishas Tent]outside[otherwise]inside[end if]."

Understand "flap", "back", "wall", "in", "the back", "of the tent", "of tent" as the narrow gap.

Instead of exiting when in Grubbers Market Teishas Tent and Tent Escape is not happening:
	if TE7 is fired or Grubbers Market Center Post is visited:
 		say "There are two ways out of this tent. Did you mean to go north or south?";
	otherwise:
		try going south;

Part 19 - Market Region and Backdrops

Chapter 1 - The open-air market region

The Open-Air Market is a region.

Grubbers Market Candle Stall, Grubbers Market Pottery Stall, Grubbers Market Outside Silk Stall, Grubbers Market Rope Dealer are in the Open-Air Market. [And later we place the perimeter locations in this region as well. See Chapter 3, 'The Market Perimeter Region'.]

[ Instead of going nowhere from the Open-Air Market:
	if the noun is a planar direction:
		try facing the noun instead; ]

Chapter 2 - Shoppers Backdrop

Section 1 - Description

Some shoppers are a backdrop, in the Open-Air Market, Alley, Outer Market Roof, Grubbers Market Teishas Tent, Grubbers Market Center Post, Grubbers Market Top of Center Post.

Rule for printing the name of the shoppers when asking which do you mean:
	say "crowd of people";

Does the player mean doing something with the shoppers when the player can see the group of mercenaries:
	it is very unlikely;

Rule for printing the description of the shoppers when in Outer Market Roof:
	say "The main crowd is in the market proper, shopping amidst the stalls. Nobody ever climbs up here (except you). ";

Rule for printing the description of the shoppers when the location is Alley or the location is Grubbers Market Center Post:
	say "The main crowd is in the market proper, shopping amidst the stalls. Nobody comes back here. ";

Rule for printing the description of the shoppers when in Grubbers Market Top of Center Post:
	say "The crowd is far below you. ";

Rule for printing the description of the shoppers when in Grubbers Market Teishas Tent:
	say "The main crowd is outside the tent. Right now it's just you and Teisha in here. ";

The description of the shoppers is "The crowd is thick with shoppers and merchants alike, all shouting at each other, shuffling and squeezing against each other to get a better look at the merchandise. [if the Wandering Mercenaries are in the location]Some of them try to give the mercenaries a slightly wider berth, but elsewhere it's shoulder to shoulder.[end if] Staying out of sight should be easy, as long as you keep moving."

Understand "main crowd", "market folk/goers", "town/towns folk/people", "townsfolk", "townfolk", "crowd", "crowds", "people", "marketfolk", "passers by", "folk", "merchants", "buyers", "knots/knot", "of", "fat", "gangly", "pock marked", "piebald", "sweaty", "red", "faced", "ugly", "gawking", "sour", "flabbergasted", "customer/customers", "others", "hagglers", "browsers" as the shoppers.

Understand "marketgoers" as the shoppers when the conspicuous shopper is not in the location and the peasant variety of the conspicuous shopper is not marketgoer.

Section 2 - Interactions

Before doing something when the shoppers are physically involved:
	if the location is Alley or the location is Grubbers Market Center Post or the location is Grubbers Market Top of Center Post or the location is Grubbers Market Teishas Tent or the location is Outer Market Roof:
		carry out the printing the description activity with the shoppers;
		stop;

Instead of talking to the shoppers:
	say "[first time]Some of the marketfolk glance at you; a few put their hands protectively around their moneypurses. But no one seems interested in having a conversation with a common street urchin.[subsequently]It's no use; these people just want to get on with their business and be left alone.[only]" instead;

Instead of doing something other than talking to or examining to the shoppers: 
	say "[one of]'Here, you! Scamper off, why don't you?'[or]The shopper roughly shoulders you aside.[or]The shopper waves you off, irritated.[or]'Take it somewhere else, kid, before I call the guard.'[at random]" instead.

Instead of smelling the shoppers:
	say "This many bodies, pressed this close together on a morning this hot, is not exactly the most pleasant thing your nose has encountered.";

Instead of listening to the shoppers:
	say "With dozens of people around you constantly chattering, muttering, arguing and haggling, it is difficult to make out anything specific.";

Section 3 - Random appearances

Peasant adjective is a kind of value. The peasant adjectives are fat, gangly, pock-marked, piebald, sweaty, red-faced, ugly, gawking and sour-faced.

Peasant variety is a kind of value. The peasant varieties are bumpkin, farmer, shopper, marketgoer, matron, servant and merchant.

The conspicuous shopper is a person, scenery.
The conspicuous shopper has a peasant adjective.
The conspicuous shopper has a peasant variety.
Understand the peasant adjective property as referring to the conspicuous shopper.
Understand the peasant variety property as describing the conspicuous shopper.
Understand "person" as the conspicuous shopper. Understand "man" as the conspicuous shopper when the peasant variety of the conspicuous shopper is not matron. Understand "woman" as the conspicuous shopper when the peasant variety of the conspicuous shopper is matron.

The printed name of the conspicuous shopper is "[peasant adjective] [peasant variety]"

Every turn when the conspicuous shopper is on-stage (this is the conspicuous shopper disappears rule):
	remove the conspicuous shopper from play.

Every turn in the Open-Air Market when the shoppers are nearby and the wandering mercenaries are oblivious and a random chance of 1 in 10 succeeds (this is the conspicuous shopper appears rule):
	Add ATMOS_shopper to the dramatic possibilities, if absent;

ATMOS_Shopper is a dramatic event.

For firing ATMOS_Shopper:
	move the conspicuous shopper to the location;
	change the peasant adjective of the conspicuous shopper to a random peasant adjective;
	change the peasant variety of the conspicuous shopper to a random peasant variety;
	say "[A conspicuous shopper] [if a random chance of 1 in 4 succeeds]with [peasant details] [end if][peasant action].";

The conspicuous shopper appears rule is listed after the conspicuous shopper disappears rule in the every turn rulebook. [Otherwise, the shopper appears and then vanishes (i.e., the player cannot reference him) in the same instant.]

To say peasant details:
	say "[one of]an overstuffed basket over one arm[or]a scraggly moustache[or]a wen the size of a robin's egg on [his-or-her] nose[or]a look of grim determination on [his-or-her] face[or]crooked teeth[or]lice jumping out of [his-or-her] clothes[or]near-paralyzing halitosis[at random]";

To say peasant action:
	[remember, no stallkeepers in some market locations --]
	if a stallkeeper is nearby begin;
		say "[one of]shoves you out of the way, muttering something about 'street trash'[or]bumps into you hard enough to send you reeling against a nearby stall[or]steps on your foot[or]squeezes past you[or]argues loudly with a nearby stallkeeper[or]pauses to inspect the merchandise, then moves on[or]hurries through the crowd, muttering something about 'bloodthirsty hooligans' invading the market[or]stalks by, muttering something about 'penny-grubbing trinket-sellers'[at random]";
	otherwise;
		say "[one of]shoves you out of the way, muttering something about 'street trash'[or]bumps into you hard enough to send you reeling against a nearby stall[or]steps on your foot[or]squeezes past you[or]pauses to inspect the merchandise, then moves on[or]hurries through the crowd, muttering something about 'bloodthirsty hooligans' invading the market[or]stalks by, muttering something about 'penny-grubbing trinket-sellers'[at random]";
	end if;

To say his-or-her:
	if the peasant variety of the conspicuous shopper is matron:
		say "her";
	otherwise:
		say "[one of]his[or]her[purely at random]";

Instead of doing something to the conspicuous shopper:
	say "[The noun] has already melted back into the crowd.";

Chapter 3 - The market perimeter region

The Market Perimeter is a region. The Market Perimeter is in the Open-Air Market.

Grubbers Market Fruit Stall, Grubbers Market Northwest Junction, Grubbers Market Grocery Stall, Grubbers Market Eastern Junction, Grubbers Market Hat Stall, Grubbers Market Exotic Gems Stall, Grubbers Market Weapons Stall, Grubbers Market Leather Shop and Grubbers Market Herb Stall are in the Market Perimeter.

Chapter 4 - Market Wall Backdrop

The pale stone wall is a backdrop. It is in the Market Perimeter. The description is "The walls surrounding Grubber's are high and made of smooth, pale stone."

The printed name is "market wall".

Understand "market wall/walls", "walls", "tall", "smooth" as the pale stone wall. 

Instead of climbing the pale stone wall:
	say "The walls are too high, and you cannot get a handhold.";

Instead of descending the pale stone wall:
	try going down;

Chapter 5 - Generic Market Stalls

Section 1 - Description

The generic stall is a backdrop. It is in Grubbers Market Fruit Stall, Grubbers Market Grocery Stall, Grubbers Market Eastern Junction, Grubbers Market Northwest Junction, Grubbers Market Hat Stall, Grubbers Market Exotic Gems Stall, Grubbers Market Weapons Stall, Grubbers Market Leather Shop, Grubbers Market Herb Stall, Grubbers Market Rope Dealer, Grubbers Market Candle Stall, Grubbers Market Pottery Stall, Grubbers Market Outside Silk Stall, the Alley, the Outer Market Roof, Grubbers Market Teishas Tent, Grubbers Market Center Post, Grubbers Market Top of Center Post.

Rule for printing the description of the generic stall when the location is Grubbers Market Top of Center Post or the location is Outer Market Roof:
	say "The stalls are spread throughout the market below. ";

Rule for printing the description of the generic stall when the location is Grubbers Market Northwest Junction or the location is Grubbers Market Eastern Junction:
	say "This is a relatively clear part of the market; most of the stalls are deeper in. ";

Rule for printing the description of the generic stall when in Grubbers Market Outside Silk Stall:
	say "This part of the market is reserved for Teisha's tent. Most of the stalls are elsewhere. ";

Rule for printing the description of the generic stall when in Grubbers Market Teishas Tent:
	say "There aren't any stalls here inside Teisha's tent. ";

Rule for printing the description of the generic stall when in Grubbers Market Center Post:
	say "You can't really see much of the stalls from back here behind them. ";

Rule for printing the description of the generic stall when in the Alley:
	say "There aren't any stalls back here in the alley. ";

The description is "The stalls are simple constructions of wood and canvas." Understand "stand" as the generic stall. The printed name is "stall". [ The generic stall is enterable. ]

Before examining the generic stall:
	if the location contains wares begin;
		try examining a random nearby wares instead;
	end if;
	if the location contains a stall display begin;
		try examining a random nearby stall display instead;
	end if;

Before stealing the generic stall:
	if the location contains wares:
		try stealing a random nearby wares instead;
	if the location contains a stall display:
		try stealing a random nearby stall display instead;

Instead of entering the generic stall in the presence of a stallkeeper:
	say "The stallkeeper shoos you away. 'Customers on the outside!' he shouts."

Instead of entering the generic stall when it is night:
	say "The stalls are all closed up for the night.";

Instead of pushing the generic stall:
	say "The stalls are too heavy to tip over.";

Section 2 - Various synonyms

[Once again, to allow players to use constructions such as "rope merchant's stall" with a minimum of fuss, we're going to allow the merchandise to double as a synonym for the stall itself, and then use a disambiguation rule to prevent irritating messages.]

Understand "sellers", "merchants", "vendors", ["stallkeepers", ] "dealers" as the generic stall.

Understand "fruit" as the generic stall when the location is Grubbers Market Fruit Stall.
Understand "grocers", "grocery", "groceries", "food" as the generic stall when the location is Grubbers Market Grocery Stall.
Understand "hat", "hats", "milliners" as the generic stall when the location is Grubbers Market Hat Stall.
Understand "gem", "gems", "exotic" as the generic stall when the location is Grubbers Market Exotic Gems Stall.
Understand "weapon", "weapons", "weaponry", "weaponsmiths stall" as the generic stall when the location is Grubbers Market Weapons Stall.
Understand "leather", "goods", "leatherware" as the generic stall when the location is Grubbers Market Leather Shop.
Understand "herb", "herbs", "herbalists stall" as the generic stall when the location is Grubbers Market Herb Stall.
Understand "rope" as the generic stall when the location is Grubbers Market Rope Dealer.
Understand "candles", "candle makers stall", "candlemakers stall" as the generic stall when the location is Grubbers Market Candle Maker Stall.
Understand "pottery", "pot", "pots", "potters" as the generic stall when the location is Grubbers Market Pottery Stall.

Does the player mean doing something with the generic stall when a stallkeeper is nearby:
	if the action requires a touchable second noun or the action requires a carried second noun:
		it is possible;
	otherwise:
		it is very likely;

Rule for clarifying the parser's choice of the generic stall when a stallkeeper is nearby:
	say "(as sold at this stall)[command clarification break]";

Chapter 6 - Huge canvas awnings

Some awnings are a backdrop. They are in the Open-Air Market and the Alley and the Outer Market Roof and Market Square. The description is "The canvas awnings stretch out over the market, providing shade for the merchants and customers below." Understand "canvas", "awning", "shade", "shades" as the awnings.

Instead of doing something other than examining to the awnings, say "The awnings are high out of your reach." instead;

Chapter 7 - Support wires

Some wires are a backdrop. They are in the Open-Air Market and the Alley and the Outer Market Roof and Market Square and Grubbers Market Center Post. The description is "The wires anchor the support posts and hold the canvas awnings in place." Understand "anchor", "cable", "cables", "wire", "thick", "braided" as the wires.

Rule for printing the description of the wires when in the Grubbers Market Center Post:
	say "Looking straight up, you can see four support wires attached to the top of the center post, stretching in four directions. "

Instead of doing something other than examining to the wires, say "The wires are too high up for you to reach, and you can't get to where they are attached on the ground." instead;

Chapter 8 - Market Perimeter Wall

Chapter 9 - Grubber's Market Backdrop

Section 1 - Definition

The backdrop-market is a backdrop. The backdrop-market is in the Open-Air Market and the Alley and the Outer Market Roof and Grubbers Market Teishas Tent and Grubbers Market Center Post and Grubbers Market Top Of Center Post.

The backdrop-market is proper-named. The printed name of the backdrop-market is "Grubber's Market". Understand "Grubbers", "Grubber", "market", "square", "outer ring", "center", "of", "aisle", "aisles", "stalls", "other stalls", "stands", "other stands", "hub", "central hub", "main", "bustle", "bustling" as the backdrop-market.

Section 2 - Descriptions

The description of the backdrop-market is "The market is bustling this morning."

Rule for printing the description of the backdrop-market when in the Alley:
	say "You can't see the market from back here in the alley. ";

Rule for printing the description of the backdrop-market when in the Outer Market Roof:
	say "You can see the northwest corner of the market from up here. ";

Rule for printing the description of the backdrop-market when in Grubbers Market Teishas Tent:
	say "You can't see the market from inside Teisha's tent. ";

Rule for printing the description of the backdrop-market when in Grubbers Market Center Post:
	say "You can't see much of the market from back here behind the stalls. ";

Rule for printing the description of the backdrop-market when in Grubbers Market Top of Center Post:
	say "The market spreads out beneath you in all directions. ";

Section 3 - Smell

Last after smelling the location when the player can see the backdrop-market:
	try smelling the backdrop-market instead;

Instead of smelling the backdrop-market:
	say "The market smells of a mixture of all sorts of odors: dust and sweat, leather and sawdust, spices, fruits, and groceries.";

Section 4 - Sound

Last after listening to the location when the player can see the backdrop-market:
	try listening to the backdrop-market instead;

Instead of listening to the backdrop-market:
	say "The market is a cacophony of sounds, with dozens of merchants hawking their wares and scores of shoppers haggling over the price.";

Section 5 - Entering

Instead of entering the backdrop-market when in the Alley:
	try going southeast;

Instead of entering the backdrop-market when the location is not grounded:
	say "You'll have to get down first.";

Instead of entering the backdrop-market when in Grubbers Market Teishas Tent:
	try going south;

Instead of entering the backdrop-market when in Grubbers Market Center Post:
	say "You'll have to get out from behind Teisha's tent first.";

Instead of entering the backdrop-market:
	say "You're already in the market.";

Section 6 - Exiting

Instead of departing the backdrop-market when in Grubbers Market Northwest Junction:
	try going north;

Instead of departing the backdrop-market when in Grubbers Market Eastern Junction:
	try going east;

Instead of departing the backdrop-market:
	say "There's only two ways out of Grubber's: north from Northwest Junction, or east from East Junction.";

Part 18 - Bottom Of Center Post

Chapter 1 - Description

Grubbers Market Center Post is a room. "This is a cramped little square formed by the backs of three stalls and Teisha's tent. It's barely big enough to stretch your arms out in. Right in the middle is the base of the huge central support post. You can duck back into Teisha's tent to the south." The dead-end description is "The stalls are built close all around you; there's nowhere you can go except south, back to Teisha's tent, or up the pole."

The printed name is "Behind the Silk Tent". Understand "Teishas", "cramped", "little", "square", "behind", "silk", "tent" as Grubbers Market Center Post.

Rule for printing the distant description of Grubbers Market Center Post while in Grubbers Market Teishas Tent:
	say "You can't see much from inside the tent. ";

Rule for printing the distant description of Grubbers Market Center Post while in Grubbers Market Top of Center Post:
	say "That little area behind the tent looks very, very tiny from up here. ";

Instead of going inside in Grubbers Market Center Post:
	try going south;

Instead of facing inside in Grubbers Market Center Post:
	try facing south;

Instead of facing up while in Grubbers Market Center Post:
	say "The top of the center post towers overhead. You might just be able to climb up there, with a bit of luck."

Chapter 2 - Contents (Center Post)

The bottom of the center post is in Grubbers Market Center Post. It is scenery and fixed in place. The initial appearance is "The big support post rises up in the midst of the gloom, towering over the empty stalls." The description is "It's huge, almost too big to put your arms around. Way up at the top, you can see wires stretching out in each of the cardinal directions." The printed name is "center post". Understand "central", "support", "wood", "wooden", "pole", "tall" as the bottom of the center post.

Instead of taking, pushing, pulling, or turning the bottom of the center post, say "The enormous post does not budge."

Instead of climbing the bottom of the center post, try going up instead.

Instead of descending the bottom of the center post, try going down instead.

Instead of taking, pushing, pulling, turning, or attacking the bottom of the center post:
	say "The enormous post does not budge.";

After going up from Grubbers Market Center Post:
	if Pole Escape is happening:
		say "With speed borne of panic, you scamper up the wooden post.";
	otherwise:
		say "[first time]You're totally hidden back here, so there's no one to stop you from climbing. By the time you're high enough for people to see you, you're also high enough that no one will notice anyway; no one ever bothers to look up in Grubber's Market. [only]After a minute or so of grunting and sweating, you reach the top.";
	continue the action;

Part 19 - Top of the post

Chapter 1 - Description

Grubbers Market Top of Center Post is up from Grubbers Market Center Post. "Perched atop the high central post, you can look out over the whole market square. To the north, Lord's Road heads out of the city towards Lord's Keep. Commerce Street and the rest of the city beckon to the east. In every other direction the city is surrounded by rolling meadows and farmlands.[paragraph break]Four anchor cables stretch away in four directions: northwest, northeast, southwest, and southeast." The printed name is "Top of the Post". The distant description of Top of Center Post is "The top of the central post is high, high above you." 

Understand "central", "pole" as Top of Center Post. 

Grubbers Market Top of Center Post overlooks Grubbers Market Fruit Stall, Grubbers Market Northwest Junction, Grubbers Market Hat Stall and Grubbers Market Exotic Gems.

After going down from Grubbers Market Top of Center Post:
	say "You take a deep breath, lower yourself carefully until you're hugging the side of the pole, and let yourself slide down.";
	continue the action;

Before dropping something in Grubbers Market Top of Center Post:
	say "Anything you drop here would just fall to the ground fifty feet below." instead;

Before facing down in Grubbers Market Top of Center Post:
	say "The ground is very, very far below you." instead;

Before facing a planar direction in Grubbers Market Top of Center Post:
	let c be a random cable that points the noun;
	if c is nothing,
		try examining the farmland-view instead;
	let the location yonder be the short name corresponding to a cable of c in the table of landing locations;
	say "[The c] ends near [location yonder], far below." instead;

Instead of smelling Grubbers Market Top of Center Post when in Grubbers Market Top Of Center Post, say "The air is fresh up here, although the scents of the market drift up faintly from below."
Instead of listening to Grubbers Market Top of Center Post when in Grubbers Market Top Of Center Post, say "It is relatively quiet and peaceful up here, although the sounds of the market drift up faintly from below."

The farmland-view is scenery, in Top of Center Post. The printed name is "view". The description is "The view is spectacular, but you don't have time to waste gawking."

Understand "road", "keep", "city", "meadows", "meadow", "farmlands", "farmland", "farms", "farm", "lords road/keep", "Commerce", "street", "Miradan", "Miradania", "rolling", "pastures", "pasture" as the farmland-view.

Instead of doing something other than examining to the farmland-view, say "It's all too far away for you to do more than look at it."

Instead of going to Grubbers Market Center Post from Top of Center Post during Final Chase:
	say "You can't climb down now, the mercenaries will catch you!";

Chapter 2 - Sliding down action

Sliding down it barehanded is an action with past participle slid, applying to one thing.
Understand "slide [cable]" as sliding down it barehanded.
Understand "slide down/along [something]" as sliding down it barehanded.
Check sliding down it barehanded (this is the block sliding rule):
	say "You can't slide down that.";

Sliding down it with is an action with past participle slid, applying to two things.
Understand "slide [cable] with/using [something preferably held]" as sliding down it with.
Understand "slide down/along [something] with/using [something preferably held]" as sliding down it with.
Check sliding down it with (this is the block sliding with rule):
	say "You can't slide down that.";

Hanging it on is an action with past participle hung, applying to two touchable things.
Understand "put [something preferably held] over/around [something]" as hanging it on.
Understand "hang [something preferably held] on/over/around [something]" as hanging it on.
Understand the command "loop" as "hang".
Understand the command "wrap" as "hang".

Check hanging it on (this is the block hanging rule):
	say "You can't see any way to hang [the noun] on that."

Instead of cutting cable:
	say "The cables are far too thick and tough for you to cut."

Instead of untying cable:
	say "The cables are too thick and tough to untie."

Chapter 4 - Contents (cables)

Section 1 - Pointiness

A pointy thing is a kind of thing. A pointy thing has a direction called the way.

A cable is a kind of pointy thing. A cable is usually scenery. The northwest cable, the southwest cable, the northeast cable and the southeast cable are cables and here. Understand "anchor", "braided", "wire", "thick", "rough" as cable.

A cable has a direction called the way. The way of the northwest cable is northwest. The way of the southeast cable is southeast. The way of the southwest cable is southwest. The way of the northeast cable is northeast.

Pointing relates a pointy thing (called the indicator) to a direction (called D) when the way of the indicator is D. The verb to point (it points, they point, it pointed) implies the pointing relation.

Section 2 - Cable

The description of a cable is "[cable description of the item described]".

To say cable description of (c - a cable):
	let dest be the destination corresponding to a cable of c in the Table of landing locations;
	say "The cable is made of thick, braided wire. One end is attached to the top of the pole that you are currently balancing on; the other end is attached to the ground at [if dest is visited]the [dest][otherwise]the [way of c] corner of the market[end if], far below. ";


After printing the description of the southeast cable when the player can see the monkey:
	say "The monkey balances easily on the cable, about 3 feet out from the top of the post. [run paragraph on]"

Understand "nw" as the northwest cable. Understand "sw" as the southwest cable. Understand "ne" as the northeast cable. Understand "se" as the southeast cable. 

Does the player mean hanging something on the southeast cable: it is likely.
Does the player mean sliding down the southeast cable with something: it is likely.
[ This is to go easy on people who try to slide down a cable without specifying direction -- the game automatically selects southeast, which is the closest to where you want to go. ]

Does the player mean doing something with the southeast cable:
	if the player can see the monkey:
		it is very likely;

Before going a direction in Top of Center Post:
	let c be a random cable that points noun;
	if c is not nothing,
		try sliding down c barehanded instead.

Instead of climbing a cable, try going the way of the noun. [which will, in turn, generate a sliding barehanded action.]

Before ascending a cable, say "The cables go down, not up." instead.

Instead of sliding down a cable barehanded, say "The cable is too steep for you to climb down, and the rough wire would tear up your hands. But if you had something you could loop over the cable, you might be able to slide down while hanging underneath."

Before putting something on a cable:
	try hanging the noun on the second noun instead;

Instead of cutting a cable:
	say "The cables are far too thick and tough for you to cut.";

Instead of cutting a cable with something:
	say "The cables are far too thick and tough for you to cut.";

Instead of interfering when the noun is a cable:
	say "The cables are so thick and so taut, they don't even budge. ";
	if the noun is the southeast cable and the player can see the monkey:
		say "The monkey chatters happily at your fruitless efforts to dislodge it. ";
	say paragraph break;

Section 2 - Collected cables

Some collective cables are scenery, in Top of Center Post. The printed name of the collective cables is "cables". The description of the collective cables is "There are four cables, each made of thick, braided wire. They stretch southwest, southeast, northeast, and northwest to the four corners of the market."

Understand "anchor", "braided", "wires", "wire cables", "thick", "rough" as the collective cables.

Before doing something other than examining with the collective cables:
	change the noun to a random cable.

Chapter 5 - Aerial Runway

Before hanging something on a cable:
	try sliding down the second noun with the noun instead.

Does the player mean hanging something on the southeast cable when Final Chase is happening: it is likely.

Definition: a thing is good for sliding with:
	if it is the gray cloak, yes;
	if it is the belt, yes;
	if it is the length of rope, yes;
	if it is the riding reins, yes;
	if it is the harness, yes;
	otherwise no;

Does the player mean sliding down the southeast cable with something when Final Chase is happening: it is likely.

Before sliding down a cable with the green silk cloak:
	say "That would utterly ruin the cloak, which would kind of ruin your plan to use it as a disguise." instead;

Before sliding down a cable with something not good for sliding with:
	say "You can't loop [the second noun] around the cable." instead;

Instead of sliding down a cable with something:
	let the room where the player ends up be the short name corresponding to a cable of the noun in the table of landing locations;
	let the landing place be the destination corresponding to a cable of the noun in the table of landing locations;
	say "You wrap one end of [the second noun] around your hand, then squat down and sling the other end around the anchor cable. You give [it or them for the second noun] a good, hard yank to test your grip. [first time]You take a deep breath. You try to convince your heartbeat to slow down, or at least continue its dreadful hammering in some part of your body other than your throat. You fail. [only]Then[first time], finally,[only] you swing down under the wire and kick off into space.[paragraph break][if the noun is the southeast cable and the monkey is on-stage]The monkey sees you coming and leaps over you, landing deftly on the top of the post. Its angry shrieks fade quickly as you pick up speed.[paragraph break][end if]The wind is exhilarating as you zip down the line. The ground rushes up at you with terrifying speed. [The second noun] whizz[if the second noun is not plural-named]es[end if] hotly against the rough wire, and you vaguely smell smoke, but your impromptu harness holds. [if final chase is happening]Shouts ring out and fingers point as the mercenaries spot you and race to where they think you will land. [end if]Suddenly your feet hit dirt. You let go of [the second noun] and tumble to the hard ground, knocking over a couple of shoppers before you roll to a stop. The next moment you are back on your feet, bruised and breathless.";
	move the second noun to the landing place;
	move the player to the landing place;
	update the character list;

Table of landing locations
cable		destination		short name
northeast cable	Fruit Stall		"the fruit stall"
southeast cable	Hat Stall			"the hat stall"
southwest cable	Exotic Gems		"the gem stall"
northwest cable	Grubbers Market Northwest Junction	"the Northwest Junction"

Chapter 6 - More Monkey Business

Section 6.1 - Description (Monkey & Necklace)

The monkey is an animal. The initial appearance of the monkey is "Balancing on the southeast cable is that monkey[if the player holds a banana][first time]. The monkey sniffs the air excitedly as you come closer, and emits a hopeful chirp[only][end if]." Understand "animal", "silver", "silvery", "fur", "black", "mask", "marking", "markings", "like", "face", "monkeys", "chest", "hand", "hands" as the monkey.

Instead of kissing the monkey, say "Are you crazy? You don't know where that monkey's been."

The description of the monkey is "The monkey has silvery fur and black mask-like markings on its startlingly human face. It seems to regard you with a mixture of curiosity and amusement — as though you were some strange little animal whose antics it found entertaining[if the monkey carries the necklace]. It grips the necklace protectively, tight against its tiny chest[end if]. The monkey is standing on the southeast cable."

Instead of listening to the monkey, say "The monkey chatters and hoots to itself constantly."
Instead of smelling the monkey, say "It won't let you close enough to smell it, but hey, it's a monkey — it probably doesn't smell like roses."

The monkey carries a necklace. The necklace is wearable and merchandise. The description is "It's a beautiful gem, clear blue with a deep indigo star in its center, set in a silver-wrought starburst on a silver chain. It's easily worth the price of any piece of merchandise in the market." Understand "gem", "blue", "indigo", "star", "starburst", "silver", "wrought", "chain", "jewel", "jewelry", "star burst" as the necklace.

Instead of wearing the necklace, say "On second thought, better not. You're still trying to look like a boy, and boys just don't wear this kind of jewelry."

Instead of doing something when the necklace is physically involved and the monkey carries the necklace:
	say "[first time][monkey escape][subsequently]The monkey [one of]screeches at you and backs away[or]jumps back, chattering angrily and holding the necklace out of reach[or]scampers back, clutching the necklace to its chest[or]hops back a few feet on the wire and blows a raspberry at you, as though daring you to come after it[at random].[only]";

Instead of trading the necklace with someone:
	try giving the noun to the second noun instead;

Instead of trading the necklace for something:
	say "You may be just a street urchin, but you have enough savvy to know that you don't flash loot like this if you want to keep it. Better keep it under wraps until you find a use for it... like trading it for something you [i]really[r] need.";

Instead of giving or showing the necklace to someone when the second noun is not Teisha and the second noun is not the player:
	say "You may be just a street urchin, but you have enough savvy to know that you don't flash loot like this if you want to keep it. Better keep it under wraps until you find a use for it... like trading it for something you [i]really[r] need.";

To say monkey escape:
	remove the monkey from play;
	say "You reach for the necklace, and the monkey backs away, screeching angrily.[paragraph break]Then, suddenly, it turns, crouches, and lets go of the wire. As it falls, it swings the necklace over the wire and grabs the other end with its free hand. Hanging onto the thin gold chain, the monkey slides away to the southeast, to the stalls far below, hooting with glee all the way down.";
	update the character list;

Every turn during monkey business:
	if the monkey is off-stage and the location is not Grubbers Market Top of Center Post:
		move the monkey to Grubbers Market Top of Center Post;

Section 6.2 - Monkey actions / reactions

Understand "pet [monkey]" as touching.
Understand "pat [something]" as touching.

Instead of taking, attacking, pushing, pulling, turning or shaking the monkey:
	say "The monkey screams and jumps away from you, chattering with rage and shaking its tiny fist."

Instead of throwing something at the monkey:
	say "That monkey is way too fast. You'd never hit it.";

Instead of taking the necklace when the monkey carries the necklace:
	say "[one of]The monkey screeches at you and backs away[or]The monkey jumps back, chattering angrily and holding the necklace out of reach[or]The monkey scampers back, clutching the necklace to its chest[or]The monkey hops back a few feet on the wire and blows a raspberry at you, as though daring you to come after it[at random]."

Every turn when the monkey is nearby and a random chance of 1 in 5 succeeds during Monkey Business:
	say "[if the player does not hold the banana][one of]The monkey bounces up and down on its wire, screeching happily[or]The monkey turns the necklace over and over in its tiny hands, staring intently into the gem's blue depths[or]The monkey tosses the necklace high in the air, swings itself under the wire and back up again with its tail, and at the last second catches the necklace in its little black hands[or]The monkey hoots at you[or]The monkey sniffs at you and shrieks with amusement[at random][otherwise][one of]The monkey sniffs at you excitedly, and emits a hopeful chirp[or]The monkey hops up and down on the wire, and makes a gesture with its tiny hands as though it were peeling a banana[or]The monkey stares hungrily at your banana[at random][end if]."

Instead of giving or showing food to the monkey when the noun is not the banana:
	say "You hold out [the noun] and wiggle [it or them for the noun] enticingly. The monkey [one of]makes a disgusted face[or]sticks out its tongue[or]turns up its nose[or]shudders in disgust[or]screeches in disgust[at random] and shakes its head. Guess it doesn't like [noun][if the noun is not plural-named]s[end if].";

Instead of waving food in the presence of the monkey:
	try giving the noun to the monkey instead;

Instead of giving or showing the banana to the monkey:
	remove the monkey from play;
	remove the banana from play;
	move the necklace to the player;
	update the character list;
	say "You hold out the banana and wiggle it enticingly. 'Here, monkey. C'mon, little monkey,' you whisper, trying to ignore how stupid it sounds.[paragraph break]The monkey creeps closer, eyes locked on the yellow fruit. It's only about a foot away from your out-stretched hand now. Its little face contorts into bizarre expressions as it wrestles with the conflicting desires to grab the tasty treat, hold onto the shiny trinket, and stay out of your reach, all at the same time.[paragraph break]Suddenly the monkey lunges. You make a grab with your free hand. The monkey grabs the banana just as your fingers hook around the necklace's chain. The monkey screams, jumps six feet straight up, lands on your head, and scampers off down the wire behind you. Your heart leaps into your throat as you lose your balance on top of the pole. You totter, pinwheel your arms... and stay on.[paragraph break]You're breathing hard, but you're safe. And you have the necklace.[paragraph break]Somewhere far below you, a stallkeeper is cursing and wiping smushed banana off his face." instead.

Section 3 - Monkey talk

Rule for initiating conversation with the monkey:
	if MONKEY_QUIP1 is not fired:
		change the chosen opening gambit to MONKEY_QUIP1;
	otherwise:
		change the chosen opening gambit to a random quip that is clustered with MONKEY_QUIP1;

Gibbering is a kind of value. The gibberings are ook, ooh, aah, ah-ah, eek, aieee, ooh-ah.

mq1 is an indexed text which varies.
mq2 is an indexed text which varies.
mq3 is an indexed text which varies.

When play begins:
	change mq1 to "Ooh-ooh-ooh-ooh-ooh!";
	change mq2 to monkey gibbering;
	change mq3 to monkey gibbering;

Rule for firing a quip (called q) clustered with MONKEY_QUIP1:
	say "[monkey response for q]";

To say monkey response for (q - a quip):
	let g1 be menu text of q with closing period replaced by a comma;
	let g2 be monkey gibbering with closing period replaced by a comma;
	say "'[g1]' you [one of]gibber[or]try[or]attempt[or]insist[or]mutter[or]shriek[or]call[or]squeal[at random][one of] enthusiastically. [or], feeling foolish. [or] hopelessly. [or]. [or]. [at random]", paragraph break;
	say "'[one of][g2][or][monkey gibbering] [g2][or][monkey gibbering] [monkey gibbering] [g2][as decreasingly likely outcomes]' ";
	if a random chance of 1 in 2 succeeds:
		say "the monkey [one of]gibbers[or]whoops[or]cackles[or]bellows[or]babbles[or]blabbers[or]jabbers[or]hoots[or]hollers[at random][one of] in reply[or] back at you[or] disparagingly[or], baring its little teeth at you[or] sarcastically[or] with apparent understanding[or] back in mocking imitation[as decreasingly likely outcomes].";
	otherwise:
		say "[one of]gibbers[or]whoops[or]cackles[or]bellows[or]babbles[or]blabbers[or]jabbers[or]hoots[or]hollers[at random] the monkey[one of] in reply[or] back at you[or] disparagingly[or], baring its little teeth at you[or] sarcastically[or] with apparent understanding[or] in mocking imitation[as decreasingly likely outcomes].";

To decide what indexed text is (x - indexed text) with closing period replaced by a comma:
	let n be the number of characters in x;
	if n is not 0:
		if character number n in x is ".":
			replace character number n in x with ",";
	decide on x;

After firing a quip clustered with MONKEY_QUIP1:
	[not allowing duplicates...]
	change mq1 to monkey gibbering;
	change mq2 to monkey gibbering;
	while mq2 is mq1 repeatedly change mq2 to monkey gibbering;
	change mq3 to monkey gibbering;
	while mq3 is mq1 or mq3 is mq2 repeatedly change mq3 to monkey gibbering;

MONKEY_QUIP1 is a quip. The menu text of MONKEY_QUIP1 is "[mq1]".
MONKEY_QUIP2 is a quip. The menu text of MONKEY_QUIP2 is "[mq2]". 
MONKEY_QUIP3 is a quip. The menu text of MONKEY_QUIP3 is "[mq3]".
MONKEY_QUIP_BYE is a quip. The menu text of MONKEY_QUIP_BYE is "Bye."

MONKEY_QUIP1 is clustered with MONKEY_QUIP2 and MONKEY_QUIP3.

Rule for firing unfired MONKEY_QUIP_BYE:
	if a quip that is clustered with MONKEY_QUIP1 is fired:
		let g1 be monkey gibbering;
		let n be the number of characters in g1;
		replace character number n in g1 with "-";
		say "'[g1]-' you begin. 'Er, I mean, bye-bye.'[paragraph break]The monkey just stares back.";
	otherwise:
		say "You stare at the monkey for a good long time, but all it does is stare back, chattering away to itself all the while. 'Never mind,' you mutter finally.";

Rule for firing MONKEY_QUIP_BYE:
	say "[one of]'See you later,' you mutter. The monkey goes back to playing with the necklace, ignoring you completely.[or]'See you later,' you mutter, to no apparent effect.[stopping]";

To decide what indexed text is monkey gibbering:
	let imax be 1;
	let the gibber be an indexed text;
	if a random chance of 3 in 10 succeeds:
		increment imax by 1;
	repeat with i running from 1 to imax:
		if i is not 1:
			change the gibber to "[gibber] ";
		let x be a random gibbering;
		let jmax be 1;
		while jmax is less than 3 and a random chance of 2 in 3 succeeds:
			increment jmax by 1;
		repeat with j running from 1 to jmax:
			if j is 1:
				change the gibber to "[x]" in sentence case;
			otherwise:
				change the gibber to "[gibber]-[x]";
		change the gibber to "[gibber][one of]![or].[or]?[purely at random]";
	decide on the gibber;

Rule for populating a quip clustered with MONKEY_QUIP1:
	let n be a random number between 1 and 3;
	let c be {MONKEY_QUIP1, MONKEY_QUIP2, MONKEY_QUIP3};
	truncate c to n entries;
	add MONKEY_QUIP_BYE to c;
	change the current conversation to c;


Part 18 B - Final Chase Scene

Chapter 1 - Chase beginning

Section 1 - Chase out of Tent

Final chase is a scene. Final chase begins when TE20 is fired. Final Chase ends when the location is Commerce Street.

When Final Chase begins:
	ratchet the current tension to essential;
	say "Suddenly you hear someone shouting just outside: [i]'Hey, any of you dims check inside this tent yet?'[no line break][r][paragraph break]Teisha's face turns white. 'Hurry, Jack!' she hisses, 'Out the back way!'";

Instead of wearing the green silk cloak when the player wears the gray cloak during Final Chase:
	say "You slip out of your old gray cloak and wrap the green cloak around you.";
	now the gray cloak is carried by the player;
	now the green silk cloak is worn by the player.

TIMER_Tent_Escape is a timer. The top end is 2.

Every turn during Tent Escape:
	increment TIMER_Tent_Escape;

Tent Escape is a scene. Tent Escape begins when TE20 is fired. Tent Escape ends disastrously when TIMER_Tent_Escape is fired and the location is not Grubbers Market Center Post. Tent Escape ends safely when TIMER_Tent_Escape is fired and the location is Grubbers Market Center Post.

Before going south in Teishas Tent during Tent Escape:
	stop the action. [ending in disaster]

Before exiting when in Teishas Tent during Tent Escape:
	try going north instead. 

When Tent Escape ends disastrously:
	say "'Jack, what are you doing?!' Teisha cries, 'Get out of—[no line break][i]oof![no line break][r]'[paragraph break]The mercenaries charge in, knocking Teisha and racks of silk garments to the floor. A rock-hard fist closes around your upper arm. 'Gotcha!' the mercenary yells. The other one starts hollering for his companions as they drag you out of the tent.[paragraph break]";
	say mercenary brutality ending;

Section 2 - Chase up pole

TIMER_Pole_Escape is a timer. The top end is 1.

Every turn during Pole Escape:
	increment TIMER_Pole_Escape;

Pole Escape is a scene. Pole Escape begins when Tent Escape ends safely. Pole Escape ends brutally when TIMER_Pole_Escape is fired and the location is not Grubbers Market Top of Center Post. Pole Escape ends safely when TIMER_Pole_Escape is fired and the location is Grubbers Market Top of Center Post.

Before going south in Grubbers Market Center Post during Pole Escape:
	stop the action. [again, ending in disaster.]

When Pole Escape begins:
	say "You can hear Teisha yelling shrilly at the mercenaries behind you — then her tirade is cut off abruptly amidst grunted curses. Someone is fumbling with the back flap of the tent!"

When Pole Escape ends brutally:
	say "The mercenaries burst out of the tent. With nowhere to run, you are quickly overwhelmed. They drag you back out to the market (dimly you see Teisha sitting amidst her scattered silks, nursing a bloody nose) and hold you firmly while calling to their comrades.[paragraph break]";
	say mercenary brutality ending;

To say mercenary brutality ending:
	say "The captain soon arrives, followed by two more of his men. They take your other arm as their leader smiles. 'Good work, dims. Got ourselves a nice little commission with this one.' And before you can react, he takes a sack of black cloth from his belt and jerks it down over your head.[paragraph break]Everything goes dark. You can't breathe. You struggle, but you can no more break the iron grip on your arms than you could uproot a tree. The men holding you curse. Dimly, you hear shouts from the crowd — someone protesting this rough treatment of a child.[paragraph break]'Don't worry, m'lady,' laughs the mercenary leader. 'We'll treat the little rat nice an['] gentle. Like this, see?'[paragraph break]Something huge and heavy crashes into your head, and the rest of your senses are ripped away. Why they wanted you, you never awake to find out.";
	end the game in death;

Chapter 2 - Chase middle

Section 1 - Chase from Pole

Chase from Pole is a scene. Chase from Pole begins when Pole Escape ends safely.
Chase from Pole ends safely when the location is not Top of Center Post.

Pole Problem is a scene. Pole Problem begins when Chase From Pole begins.

When Pole Escape ends safely:
	move the chasing mercenaries to Top of Center Post;
	say "One of the mercenaries tries to climb after you, but can't get more than a few feet up before sliding back. Looks like you're safe for the time being — but they're already heatedly discussing how to get you down, and there are only so many ways you can go from here.";

Section 2 - Chasing mercenary scenery

Some chasing mercenaries are people, male, scenery. "[if Pole Destruction is not happening]The mercenaries are huddled around the base of the pole, discussing how to get you down.[otherwise]The mercenaries are chopping the pole down![end if]"

Understand "person", "people", "soldier", "soldiers", "man", "men", "bravo", "bravos", "guard", "guards", "mercenary", "merc", "mercs" as the chasing mercenaries.

Instead of doing something other than examining or listening to the chasing mercenaries:
	say "You can't do anything to the mercenaries while you're way up here.";

Instead of listening to the chasing mercenaries:
	say "[if Pole Destruction is not happening]You can't quite make out what they're saying from up here.[otherwise]The mercenaries have stopped talking. All you can hear now is the constant [i]chock![no line break][r] of the axe.[end if]";

Every turn during Pole Problem:
	say "[one of]Another of the mercenaries tries his luck climbing the post, but his callused fingers can't get a grip on the wood[or]One of the mercenaries kicks the post in frustration[or]The mercenaries continue to shout and point[or]More mercenaries are squeezing into the tiny square behind Teisha's tent[or]The sounds of heated debate drift up from below. You just make out the word, '[one of]fire[or]ladder[or]arrows[at random]'[or]One of the mercenaries runs back out through Teisha's tent[at random].";

TIMER_Pole_Problem is a timer. The top end is 7.

Every turn during Pole Problem:
	increment TIMER_Pole_Problem;

Pole Problem ends worryingly when TIMER_Pole_Problem is fired.
Pole Problem ends safely when Chase From Pole ends safely.

Section 2 - Final moments

Pole Destruction is a scene. Pole Destruction begins when Pole Problem ends worryingly.

Pole Destruction ends safely when Chase From Pole ends safely.

Pole Destruction ends in disaster when POLE3 is fired.

When Pole Destruction begins:
	change the current script to {POLE1, POLE2, POLE3};

When Pole Destruction ends safely:
	change the current script to {};

When Pole Destruction ends in disaster:
	end the game in death;

POLE1 is a scripted event. The display text is "A mercenary enters the square behind Teisha's tent, carrying an axe.[paragraph break]Uh-oh."
POLE2 is a scripted event. The display text is "There is a solid [i]chock![r] from below as the axe bites into wood, and the entire post shivers."
POLE3 is a scripted event. The display text is "The mercenaries chop ever faster, taking turns as each one grows tired. Soon the post is shaking and swaying dangerously, and it is all you can do to keep your balan-[paragraph break]And that's when the post gives one horrendous lurch, your foot slips, and you go pinwheeling off the top of the post. You just have time to notice the grins on the mercenaries['] faces, and the fact that none of them are trying to catch you, before the ground fills your sight and everything goes black."

Section 3 - Axe

The axe is a thing. The description is "It looks like a pretty big axe. It probably won't take them long to chop down this pole."

Instead of listening to the axe:
	say "The axe's steady [i]chocking[r] noise is very loud, even from up here.";

Instead of doing something other than examining or listening to the axe:
	say "You can't do anything to the axe while you're way up here.";

Rule for deciding the concealed possessions of the chasing mercenaries:
	if the particular possession is the axe, no;
	yes;	

After firing POLE1:
	now the chasing mercenaries carry the axe;

Chapter 3 - Chase End

Market Escape is a scene. Market Escape begins when Chase From Pole ends safely.
Market Escape ends safely when the location is Commerce Street.

When Market Escape begins:
	now every market stall is unblocked.

Every turn during Market Escape:
	ratchet the current tension to important;
	spool through dialogue in the table of Chasing Mercenary descriptions.

Market Escape ends in capture when the table of Chasing Mercenary descriptions is empty.

After going to a market stall during Market Escape:
	say "You [one of]sprint[or]dive[or]weave[or]dodge[or]race[at random] through the crowd.";
	try looking;
	update the character list;

After going to Grubbers Market Outside Silk Stall during Market Escape:
	say "You dash for Teisha's tent, but it's no good — the mercenaries have planted themselves outside it and are scanning the crowd. You backpedal quickly before they can spot you.";
	let x be a random adjacent market stall;
	while x is Grubbers Market Teishas Tent
		repeatedly let x be a random adjacent market stall;
	move the player to x;
	update the character list;

Instead of going to the Alley during Market Escape:
	say "Not that way — the alley is a dead end!";

Instead of going north in Grubbers Market Northwest Junction during Market Escape:
	say "No good — the mercenaries have closed the north gates and aren't letting anyone through. Your only chance is east, further into the city!";

Instead of stealing during Market Escape:
	say "There's no time for that! You've got to run!";

Instead of talking to someone during Market Escape:
	say "There's no time for that! You've got to run!";

Table of Chasing Mercenary descriptions
text
"You hear shouting coming from all quarters of the market. If the mercenaries saw where you landed, they'll be after you in no time.[line break]"
[row marker]
"The shouts are getting closer.[line break]" 
[row marker]
"You hear curses and yells of pain as the mercenaries shove their way through the crowds of shoppers. They're closing in![line break]"
[row marker]
"Someone yells, [i]'There! I see him!'[r][line break]"
[row marker]
"You hear thudding footsteps right behind you![line break]"
[row marker]
"Mercenaries burst through the crowd from three different directions. They grab you and slam you to the ground, knocking the breath from your lungs.[paragraph break]'[i]Gotcha[r], you little runt!' someone growls in your ear.[line break]"

When Market Escape ends in capture:
	say "The captain soon arrives, followed by two more of his men. They take your other arm as their leader smiles. 'Good work, dims. Got ourselves a nice little commission with this one.' And before you can react, he takes a sack of black cloth from his belt and jerks it down over your head.[paragraph break]Everything goes dark. You can't breathe. You struggle, but you can no more break the iron grip on your arms than you could uproot a tree. The men holding you curse. Dimly, you hear shouts from the crowd — someone protesting this rough treatment of a child.[paragraph break]'Don't worry, m'lady,' laughs the mercenary leader. 'We'll treat the little rat nice an['] gentle. Like this, see?'[paragraph break]Something huge and heavy crashes into your head, and the rest of your senses are ripped away. Why they wanted you, you never awake to find out.";
	end the game in death;

Instead of going east from Grubbers Market Eastern Junction when the green silk cloak is not worn by the player during Market Escape:
	say "Wait! You've got to put on the silk cloak, or you'll never sneak pass the mercenaries guarding the market exit!";

After going east from Grubbers Market Eastern Junction during Market Escape:
	say "You reach the edge of the market just ahead of your pursuers and see the mercenary captain standing there. He hasn't seen the commotion yet; he's still casually scanning faces.[paragraph break]You skid to a halt. Everything depends on you looking like you're not in a hurry.[paragraph break]You force yourself to walk slowly. Underneath the hood, sweat is pouring down your cheeks, and you can barely control your breathing. The shouts behind you are getting nearer. The captain is not looking at you, he's looking at the mercenaries pounding up behind you. Does he understand what they're shouting? Just a few more steps...[paragraph break]A bead of sweat rolls off the tip of your nose and drops to the ground, leaving a little dark spot in the dust. Did the captain notice?[paragraph break]Five steps... four steps... three steps...[paragraph break]You hear a confused babble of voices behind you as the captain orders his men to halt and explain what's going on.[paragraph break]You slip through the crowds into Commerce Street...";
	pause the game;
	select theme "MeetingBobby";
	output chapter heading "Chapter 2 - Bobby";
	continue the action;

Book 3 - Commerce Street

Part 1 - Commerce Street and shopfronts

Commerce Street is east of Grubbers Market Eastern Junction. The description is "[timely description of Commerce Street]"

To say timely description of Commerce Street:
	if it is day begin;
		say "Commerce Street runs east to west, between Grubber's Market and Lord's Market. Raised sidewalks allow pedestrians to avoid the mud, horse dung, and occasional teamster's cart rumbling by. The shops here are actual buildings, spaced evenly along the avenue. Storefronts for a bakery, a butcher's shop, and an armory line the north side of the street from west to east. ";
	otherwise;
		say "Commerce Street at night is eerily quiet and empty. Your footsteps seem to echo on the cobbles as you walk past the shuttered shopfronts. ";
	end if.

The distant description of Commerce Street is "Commerce Street lies just [if location is Eastern Junction]east[otherwise if location is East Commerce Street]west[otherwise]south[end if] of here."

The dead-end description of Commerce Street is "There are no shops on the south side of the street."

Rule for printing the distant description of Commerce Street while in Grubbers Market Top of Center Post:
	say "You can just make out Commerce Street to the east, far below. ";

Instead of facing north in Commerce Street, try examining the butchers storefront.
Instead of facing northwest in Commerce Street, try examining the bakery storefront.
Instead of facing northeast in Commerce Street, try examining the armory storefront.

Inside from Commerce Street is nowhere. Instead of going Inside from Commerce Street, say "You'll have to say which shop you want to enter."

Section 2 - Scenery (Sidewalks)

The sidewalk is scenery, in Commerce Street. The description is "The sidewalks are made of flagstones laid over the cobbles of the street." Understand "sidewalks", "flagstone", "flagstones" as the sidewalk.

Some cobblestones are scenery, in Commerce Street. The description is "The cobblestones are slick with mud." Understand "cobbles", "cobble", "cobblestone", "street", "stone", "stones" as the cobblestones. The printed name is "cobbles".

Some mud is scenery, in Commerce Street. The description is "This sort of filth accumulates in most of the streets in Toresal, except for the richer districts." Understand "dung", "filth" as the mud.

Chapter 1 - Stores and Storefronts

Section 1 - Storefronts

A storefront is a kind of thing. Understand "storefront", "store front", "shopfront", "shop front", "shop", "store", "sign", "building", "door" as a storefront.

The bakery storefront is a storefront, scenery and here. "A hanging sign above the door depicts a loaf of bread[if it is day], and the warm smell of fried dough wafts out from the open door, making your stomach rumble[end if]." Understand "baker", "bakers" as the bakery. Instead of entering the bakery: try going northwest instead.

The butchers storefront is a storefront, scenery and here. "A brightly painted pig decorates the sign over this shop[if it is day]. The smell of burned sausage masks the darker odor of butchered animals[end if]." Understand "pig", "butcher", "butchery" as the butchers storefront. Instead of entering the butchers: try going north instead.

The printed name of the butchers storefront is "butcher's storefront".

The armory storefront is a storefront, scenery and here. The description of the armory storefront is "The walls of this shop are built of heavy, blackened bricks — a grim exterior to house grim business. The sign over the door is painted with an axe and shield." Understand "axe", "shield", "armorers", "armoury", "armourers", "armourer" as the armory storefront. Instead of entering the armory storefront: try going northeast instead.

Instead of examining a storefront when it is night, say "All of the shops are shuttered and locked up for the night."

Some shopfronts are a backdrop, in Commerce Street and Lords Market.

Rule for printing the description of the shopfronts when in Commerce Street:
	say "Storefronts for a bakery, a butcher's shop, and an armory line the north side of the street from west to east.";

Rule for printing the description of the shopfronts when in Lords Market:
	say "A fine clothier's, a jewelry store, and a moneylender's shop lie northeast, east, and southeast, respectively.";

Understand "shops", "stores", "shopfronts", "storefronts", "shop/store fronts", "signs", "shop/store signs", "doors", "shop/store doors", "buildings", "shop/store buildings" as the shopfronts.

Section 2 - Stores

A store is a kind of room. 
Instead of going to a store when it is night, say "All of the shops are shuttered and locked up for the night."

Rule for assembling available exits of a store:
	change the viable directions to {outside};

Chapter 2 - Text upon entering

Avoiding Soldiers ends when Final Chase begins.

After going to Commerce Street when Commerce Street is unvisited:
	say "[i]You made it.[no line break][r][paragraph break]You let out a long, shaky breath. You're safe now. Commerce Street is home to a slightly more upscale class of merchant, and business here is not quite as frantic as in Grubber's Market. Transactions tend to be calmer and more deliberate, and involve significantly more money. The Lord's Guard will be far less tolerant of a pack of armed bullies menacing the citizens of the upper class, so it's unlikely that the mercenaries will follow you here. And even if they did, there are too many streets and side alleys for them to block off your escape.[paragraph break]Gratefully, you remove the cloak, which was beginning to stick to your skin with all the sweat. Now it's time to pause, catch your breath, and try to figure out why they were after you in the first place.";
	now the player carries the green silk cloak;
	move the green silk cloak to the satchel;
	if the player carries the gray cloak begin;
		now the player wears the gray cloak;
	end if;
	continue the action;

Chapter 4 - Exiting

Before going west in Commerce Street:
	if it is day:
		say "It would be a good idea to lay low for a while before returning to Grubber's Market." instead;

Before going from Commerce Street:
	if the player is disguised as a girl and the player is still dirty and Escape Through the Sewer has not happened:
		if Meeting Bobby has happened and Bobby's Adventure has not happened:
			say "You'd better put your hat on - Bobby thinks you're one of the boys, and if he sees your long hair out it'll blow your disguise." instead;
		otherwise:
			say "You'd better put your hat on — most of the merchants on this street know you as Jack. Keeping your hair stuffed up and hidden helps the disguise." instead;

Instead of going to the armory shop when the player is scared of Olgan Minor:
	say "The way your last conversation went, you're not sure you're ready to face Olgan Minor again.";

Part 2 - In Germaise's Bakery

Chapter 1 - Description

The Bakers Shop is a store, northwest of Commerce Street. "The air is heavy with the warm and slightly damp smell of freshly baked bread, and the counters are piled high with golden loaves, baguettes, and rolls. You can leave the bakery to the south." The printed name is "Bakery". Understand "bakery" as the Bakers Shop.

South of the bakers shop is Commerce Street.
Outside from the bakers shop is Commerce Street.

Instead of facing outside while in the Bakers Shop, try facing south.
The dead-end description of the Bakers Shop is "You can see no other exits besides the door to the south, leading to Commerce Street."

Instead of smelling the Bakers Shop, say "The store is filled with the smell of baking bread."

Chapter 2 - Contents - Scenery

A bread counter is scenery, privately-named, in the Bakers Shop. Some baked-goods are privately-named, scenery. The baked-goods are on the bread counter. "The bread looks and smells delicious, but right now you need answers more than you need a snack." Understand "bread", "loaves", "baguettes", "rolls", "loaf", "baguette", "roll" as the baked-goods. The printed name of the baked-goods is "bread".

Understand "counter" as the bread counter. The printed name of the bread counter is "counter".

Instead of doing something other than examining to the baked-goods, try examining the baked-goods. 

Some flour is scenery. The flour is on the bread counter. "Germaise likes to throw around great handfuls of flour whenever he's making something, and he and everything else in the bakery is covered with the white, powdery stuff." Understand "white", "powder", "powdery", "stuff", "film", "dust", "dusty", "floury", "thin film", "film of", "thin film of" as the flour.

Instead of doing something other than examining to the flour:
	say "It's just a thin film of flour — you can't really do anything with it.";

An oven is scenery and here. "Most of the shop's back wall is actually the front of Germaise's huge brick oven. A warm, orange glow and the delicious smells of baking bread drift from its wide opening." Understand "huge", "brick", "warm", "orange", "glow", "hot", "kiln", "furnace", "back wall" as the oven.

Instead of doing something other than examining the oven, say "It's behind the counter, and Germaise wouldn't want you messing with it in any case.".

Chapter 3 - Event on entry

After going to the Bakers shop for the first time:
	say "[command clarification break]";
	try looking;
	update the character list;
	if the location has not been the sewer begin;
		say "The man behind the counter nods at you amiably as you enter the shop.";
	otherwise;
		start conversation with Germaise the Baker on GE16;
	end if;

After going to the Bakers shop:
	say "[command clarification break]";
	try looking;
	update the character list;
	if the player has been in the sewer and GE16 is unfired begin;
		start conversation with Germaise the Baker on GE16;
	end if;

Chapter 4 - Leaving

After going from the Bakers Shop:
	if the player has not been in the sewer, say "'Later, Germaise,' you call out as you leave.[paragraph break]'Eh,' grunts Germaise, flapping a flour-dusted hand at you.";
	continue the action;

Chapter 5 - Germaise the Baker

Section 1 - Description

Germaise the Baker is a man and here. "A man dressed in flour-dusted baker's whites stands behind the counter, busily rolling out dough." The description is "Germaise the Baker is a tall, thin man from the south. He has a thick accent and likes to stroke his sparse moustaches with his fingertips while he talks — the lower half of his face is almost always covered with a grayish coating of flour. He's a shameless flirt, always eyeballing the young ladies who come into his store. It's moments like those that make you grateful for your disguise."
The printed name is "Germaise". Understand "man", "proprietor", "shopkeeper", "shop keeper", "merchant", "behind the counter", "behind counter" as Germaise.

Instead of kissing Germaise, say "Your relationship with Germaise isn't that close."

Section 2 - Possessions (Moustache and Dough)

Germaises Moustache is part of Germaise the Baker. The description is "Germaise's moustache looks like a caterpillar with particularly dirty feet walked across his upper lip and left tracks, but he sure is proud of it." Understand "moustaches", "mustache", "mustaches", "caterpillar", "tracks" as Germaises Moustache.

Instead of doing something to Germaises Moustache:
	say "Germaise would probably rather you leave his moustache alone.";

Germaise carries a mound of pastry dough. The description of the mound of dough is "The mound of dough is twice the size of Germaise's head, and he keeps punching it and squeezing it for all he's worth. It looks like a great way to work out your frustration."

Understand "strips", "flour" as the pastry dough.

Instead of doing something to the mound of dough:
	say "'Eh, keep away from the pastry dough, Jack,' Germaise warns you. 'This is delicate business.'"

Section 3 - Idle actions

Every turn when Germaise is nearby and the player is not on the move:
	if a random chance of 1 in 5 succeeds begin;
		say "[one of]Germaise works the dough, pounding and pushing it into shape[or]Germaise tosses another handful of flour onto the dough[or]Germaise cuts the dough into strips, twists the strips into a complicated shape, and slides the whole thing into the glowing mouth of the brick oven behind him. Then he slaps another mound of dough onto the counter and starts the process all over again[or]Germaise peers into the oven, checking on the status of his latest batch[at random].";
	end if;

Chapter 6 - Conversation

Section 1 - Initial conversation

Rule for initiating conversation with Germaise:
	if the player has been in the Sewer,
		change the chosen opening gambit to GE19;
	otherwise change the chosen opening gambit to GE1;

GE1 is a quip. The display text is "You lean casually on the counter, and the baker raises one flour-dusted eyebrow. 'You want something?' he asks."

GE2 is a quip. The menu text is "'How's business?'". The display text is "[first time]'So how's business, Germaise?' you ask.[paragraph break]Germaise sighs. 'I've had the ovens fired up night and day all week trying to fill a rush order for Baron Fossville. It's good for business, don't get me wrong, but...' He slaps dispiritedly at the dough, sending up a puff of flour. 'You have to spend [i]time[r] on a pastry to make it just right. Breaks my heart to do slapdash work like this, but what can you do? The Baron wants quantity, not quality.'[subsequently][rp] 'just trying to get all these pastries done before tomorrow night.'[only]"

GE3 is a quip. The menu text is "'What have you got to sell today?'". The display text is "[first time]Knowing that the question always puts him in a good mood, you ask, 'What are you selling today, Germaise?'[paragraph break]A smile spreads across the baker's flour-streaked face. 'What is it you desire?' he replies. 'I have round breads and flat breads, grain breads and white breads, hard breads and soft breads, long loaves and demi-loaves. I make rolls, muffins, pastries, cakes, and the most delicious fried dough you will ever taste. It is no joke to say that my buns are the most firm and delicious in all of Toresal.'[subsequently][rp] 'I have every kind of bread and pastry you could imagine.'[only]"

GE4 is a quip. The menu text is "'Do you know about the mercenaries in Grubber's Market?'". The display text is "[first time]'So...' you say, trying to play it tricky, 'you hear anything about mercenaries running around Grubber's?'[paragraph break]'Eh?' Germaise looks up, startled. 'Mercenaries in Grubber's?'[paragraph break]'Oh yeah,' you say. 'I, uh, heard they were kicking over stalls, scaring the locals, really roughing up the place. I was just wondering if you knew anything about it?'[paragraph break]'Did they knock over Teisha's tent?' Germaise blurts out. 'Is Teisha all right?'[subsequently]'I only know what I heard from you,' says Germaise.[only]"

GE5 is a quip. The menu text is "'Have you seen Bobby around?'". The display text is "[first time]'You seen Bobby around lately?'[paragraph break]'Seen him?' Germaise snaps. 'I chased him out of here for stealing one of my tarts! You tell him from me, he comes around my bakery again, he gets the wrong end of my bread knife in his you-know-what!'[paragraph break]'So... that'd be a [i]no[r], then?'[paragraph break]Germaise sniffs and pounds at his dough.[subsequently][rp] 'I haven't seen him, and that's a good thing.'[only]"

GE6 is a quip. The menu text is "'Nothing, never mind.'" The display text is "'Oh, nothing,' you say.[paragraph break]Germaise nods and goes back to his dough."

GE7 is a quip. The menu text is "'What's the occasion for the pastries?'" The display text is "[first time]'So what are all the pastries for?' you ask. Somehow, you never envisioned Fossville as someone who was particularly passionate about eating pastries.[paragraph break]Germaise shrugs. 'Fossville should tell me his business? Maybe he just likes pastries. Maybe he's throwing a big party. All the more reason to take extra care over the pastries if that's the case, but what do I know? I'm just the baker.'[subsequently][rp] 'I don't know why he wants all the pastries. I suppose he's entertaining someone.'[only]"

GE8 is a quip. The menu text is "'What do you think of Baron Fossville?'" The display text is "[first time]'What do you think of Baron Fossville?' you ask. 'It sounds like you're not exactly pleased to be getting his custom.'[paragraph break]'No, no, it's not that, not that at all,' Germaise says hastily. 'Fossville runs the city well and his taxes are fair. They say he has a temper, but who am I to judge? Now, the Duke before him...' he seems on the verge of saying something, but then thinks better of it. 'Eh, well,' he mutters, and attacks his dough with renewed vigor.[subsequently][rp] 'he's no worse than most.'[only]"

GE9 is a quip. The menu text is "'What do you think of the Queen?'". The display text is "[first time]'What do you think of the Queen?' you ask.[paragraph break]Germaise shrugs and makes a dismissive [i]pfff[r] noise, blowing a cloud of flour from his moustache. 'I try not to. The King placed a greater value on looks than wisdom, and that is his royal prerogative, but then he went and died and the rest of us had to live with the consequences.'[subsequently][rp] 'she's a poor ruler at best, a Royal fool at worst.'[only]"

GE10 is a quip. The menu text is "'What do you think of the Princess?'". The display text is "[first time]'What do you think of the Princess?' you ask.[paragraph break]Germaise shrugs. 'I hear she's a pretty thing, like her mother once was. That plus the throne makes her quite the prize.'[subsequently][rp] 'I don't know much about her.'[only]"

GE11 is a quip. The menu text is "'What do you know about the Ascension?'".
The display text is "[first time]'It seems like the Ascension is all anyone talks about lately,' you say. 'What's the big deal, anyway?'[paragraph break]Germaise rolls his eyes and flaps his flour-white hands in the air. 'Eh, a big how-do-you-do about nothing, believe me. The Ascension is when the throne is bestowed on the Queen's successor. Officially we common folk get some say in who that will be, but if you believe that, you're daft. Anyone with a dram of sense knows that the throne will go to the Queen's daughter, and to whoever is ruthless enough and lucky enough to wed her. Don't trouble your head about Royal politics, Jack,' Germaise adds, giving his dough a vicious punch. 'It's far above the likes of you and me.'[subsequently][rp] 'it's no use worrying about Royal politics; it's nothing to do with the lives of common folk.'[only]"

GE12 is a quip. The menu text is "'What did you think of the old Duke?'". The display text is "[first time]'It's only been a couple of years since the old Duke died,' you say. 'Do you miss him?'[paragraph break]Germaise gets a faraway look in his eyes. 'Ah, Duke Toresal,' he mutters, 'there was a bright and honorable Lord. He'd have been in line for the throne himself if he hadn't married below his station. And if he hadn't died.' He sighs. 'Eh, well. Those were better days, Jack.'[subsequently][rp] 'he was a good man who died before his time.'[only]"

GE13 is a quip. The menu text is "'Yes, Teisha's fine.'". The display text is "[i]There he goes again[r], you think, trying not to roll your eyes too obviously. 'Yeah, uh, Teisha's just fine. She's great.'[paragraph break]Germaise lets out a sigh of relief. 'Ah good. Such a lovely girl, that Teisha. Such a [i]firm[r] girl. Maybe I'll send her a few pastries as a gift, so she knows I'm thinking about her...'"

GE14 is a quip. The menu text is "'The mercenaries roughed Teisha up.'". The display text is "'No, actually, the mercenaries roughed her up and stomped all over her tent,' you say, relishing the effect that this news has on Germaise, who has had a crush on your friend for as long as you can remember.[paragraph break]'The scoundrels!' Germaise shouts, slamming his fist into the middle of his mound of dough with a dull [i]splat[r]. 'I'll go see her right away, I'll bring her a plate of my best pastries. I'll, uh...' he looks around in dismay. 'I'll do that... as soon as I finish the Baron's order.' He starts working the dough at double-speed."

GE15 is a quip. The menu text is "'One more question.'". The display text is "'You mind if I ask you something else?' you ask.[paragraph break]'As long as it doesn't slow down my pastries,' Germaise replies."

HO1 is a quip. [See Holstenoffer's Conversation, below.]

The response of GE1 is { GE2, GE3, GE4, GE5, GE7, GE8, GE9, GE10, GE11, GE12, GE6 }

After populating GE1:
	if HO1 is unfired begin;
		remove GE5 from the current conversation, if present;
	end if;
	if GE2 is unfired begin;
		remove GE7 from the current conversation, if present;
	end if;

The response of GE2 is { GE7, GE8, GE15 }

The response of GE3 is { GE15 }

The response of GE4 is { GE13, GE14 }

After populating GE4:
	if GE4 was fired begin;
		change the current conversation to { GE15 };
	end if;

The response of GE5 is { GE15 }

The response of GE6 is { }

The response of GE7 is { GE8, GE15 }

The response of GE8 is { GE12, GE15 }

The response of GE9 is { GE10, GE15 }

The response of GE10 is { GE9, GE15 }

The response of GE11 is { GE9, GE10, GE15 }

The response of GE12 is { GE15 }

The response of GE13 is { GE15 }

The response of GE14 is { GE15 }

The response of GE15 is { GE2, GE3, GE4, GE5, GE7, GE8, GE9, GE10, GE11, GE12, GE6 }

After populating GE15:
	if HO1 is unfired begin;
		remove GE5 from the current conversation, if present;
	end if;
	if GE2 is unfired begin;
		remove GE7 from the current conversation, if present;
	end if;

Section 2 - Later conversation

GE16 is a quip. The display text is "Germaise the baker looks up at you and scowls. [first time]You're utterly filthy, of course, and he doesn't appear to be too happy about you fouling up his shop.[only]";

GE17 is a quip. The menu text is "'Have you seen Bobby?'". The display text is "'Have you seen Bobby this morning?' you ask, trying to keep the edge of fear out of your voice.[paragraph break]'No,' Germaise sniffs. 'And I'd like to keep it that way.'"

GE18 is a quip. The menu text is "'Is there a back door here?'". The display text is "'Is there a back door in your shop?' you ask.[paragraph break]Germaise looks at you incredulously. 'No, there isn't, and no, you can't use it. And why would I tell you anyway, eh? So you can tell your cretin friend Bobby so he can break in and steal all my beautiful pastries?' He shakes his head, dislodging fine drifts of flour from his hair. 'Go away, Jack. I am very, very busy.'"

GE19 is a quip. The display text is "It doesn't seem as though Germaise is going to be very helpful. You'd have better luck talking to someone else.";

GE16 is clustered with GE17 and GE18.

The response of GE16 is { GE17, GE18 }.
The response of GE17 is { GE18 }.
The response of GE18 is { GE17 }.

After populating a quip that is clustered with GE16:
	if GE17 is fired, remove GE17 from the current conversation, if present;
	if GE18 is fired, remove GE18 from the current conversation, if present;

Part 3 - In Old Man Holstenoffer's Butcher's Shop

Chapter 1 - Description and scenery

The Butchers Shop is a store, north of Commerce Street. "Chains of linked sausages and thick, red cuts of meat dangle from hooks in the ceiling, and trays of less identifiable bits sit out on the counters. It's better than some butcheries you've seen, but it's still not your favorite place in the city. You can leave to the south."

Rule for printing the name of the Butchers Shop:
	say "Butcher's Shop";

Outside from the butchers shop is Commerce Street.

Instead of facing outside while in the Butchers Shop, try facing south.
The dead-end description of the Butchers Shop is "You can see no other exits besides the door to the south, leading to Commerce Street."

Instead of smelling the Butchers Shop, say "The store is filled with the thick, bloody smell of raw meat."

A counter 2 is privately-named, scenery. The counter 2 is in the Butchers Shop. The printed name is "counter".

Some trays are scenery and merchandise and on counter 2. The description is "The sausage looks tasty enough, and the hanging meat cuts would probably look good once they're cooked. The rest of it just makes you sort of ill."

Understand "tray", "meat", "sausage", "sausages", "carcass", "carcasses", "bits", "tray of", "bargain bits", "bargains", "counter", "gristle", "label", "labeled/labelled", "hanging", "hung", "cut/cuts", "chop/chops", "joint/joints", "raw" as the trays. The printed name is "trays of meat".

Instead of eating, tasting or smelling the trays:
	say "For some reason, this place just kills your appetite."

Instead of taking or stealing the trays:
	say "What are you going to do with a handful of raw meat?"

Instead of searching the trays:
	try examining the trays;

Some hooks are scenery, in the Butchers Shop. The description is "Dozens of thick, brutal hooks hang from the ceiling."

Instead of doing something other than examining to the hooks: say "The hooks are out of your reach.";

Understand "thick", "brutal", "hook" as the hooks.

A chopping block is scenery, in the Butchers Shop. The description is "Holstenoffer's chopping block is cut from a single, gigantic piece of unstained oak. Its surface is stained black with blood, soaked three inches down into the wood's grain."

Understand "stain", "stains", "stained", "oak", "oaken", "wood", "wooden", "gigantic", "unstained", "black", "grain", "board", "cutting", "butcher block/board", "butchers block/board" as the chopping block.

Instead of doing something to the chopping block,
	say "The chopping block is behind the counter, out of your reach — and honestly, you prefer it that way.";

Chapter 2 - Old Man Holstenoffer

Old Man Holstenoffer is a man and here. "Old Man Holstenoffer is leaning over his huge, bloodstained butcher block behind the counter, hacking up some carcass." The description is "Holstenoffer is a squat brick of a man, with a scarred bulldog's face and sailors['] tattoos covering his arms from the wrists all the way up to his rolled-up sleeves. He's friendly enough and loves to gossip, but he has a tendency to gesticulate with his cleaver in hand, which sometimes makes conversation with him a tense and athletic affair." Understand "butcher", "proprietor", "shopkeeper", "shop keeper", "merchant", "tattooed", "scarred", "scars", "scar", "squat", "brick", "of", "of a", "face", "friendly", "rolled", "rolled up", "sleeve", "sleeves" as Holstenoffer.

Instead of kissing Holstenoffer, say "You like Holstenoffer, but you don't like him [i]that[r] much."

Old Man Holstenoffer carries a cleaver. The description of the cleaver is "Holstenoffer calls his cleaver 'Big Molly,' and often talks to it as though it were a pet. The side of the blade is tarnished and dull from years of use, but the edge is sharp enough to hack through thick muscle and bone like they were pudding." Understand "big molly", "molly", "knife", "tarnished", "dull", "edge", "sharp" as the cleaver.

Some tattoos are part of Old Man Holstenoffer. The description is "Holstenoffer's tattoos are a solid blue tangle of swirling designs, covering his arms from his wrists all the way up to his rolled-up sleeves." Understand "tattoo", "solid", "blue", "tangle", "design", "designs", "swirling", "arm", "arms", "sailors" as the tattoos.

Every turn when the player is not on the move and Holstenoffer is nearby and a random chance of 1 in 5 succeeds:
	unless the player has been in the sewer:
		add ATMOS_Holstenoffer to the dramatic possibilities, if absent;

ATMOS_Holstenoffer is a dramatic event. The display text is "[one of]Holstenoffer's cleaver cuts through a particularly tough joint with a gristly, crunching sound.[or]Holstenoffer chops deftly at the raw meat on his block, cutting into narrow strips.[or]Holstenoffer stops to wipe his hands on his blood-smeared apron.[or]Holstenoffer stops suddenly and holds up a slippery-looking chunk of red gristle. 'Huh,' he mutters, 'never seen one of [i]those[r] before.' With a shrug, he tosses it into a tray labeled 'Bargain Bits.'[at random]".

Chapter 3 - Event on entry

After going to the Butchers shop for the first time:
	say "[command clarification break]";
	try looking;
	update the character list;
	unless the player has been in the sewer:
		start conversation with Holstenoffer on HO1;
	otherwise:
		start conversation with Holstenoffer on HO18;

After going to the Butchers shop [subsequent times]:
	say "[command clarification break]";
	try looking;
	update the character list;
	if the player has been in the sewer:
		start conversation with Holstenoffer on HO18;

Chapter 4 - Leaving

After going from the Butchers Shop:
	say "Holstenoffer waves his bloody cleaver at you as you leave. 'Take care of yourself, Jack.'";
	continue the action;

Chapter 5 - Conversation

Section 1 - Initial conversation

Rule for initiating conversation with Old Man Holstenoffer:
	change the chosen opening gambit to HO12;

[HO1 is a quip: see Germaise's Conversation, above.] The display text of HO1 is "Holstenoffer glances over his shoulder as you come in. 'Ey, Jack!' he calls. 'Long time, no see. Bobby was around this morning, looking for yeh. Up to something again, eh?' he laughs."

HO2 is a quip. The menu text is "'What did Bobby want?'". The display text is "[first time]'Bobby was looking for me?' you ask, trying to contain your excitement. 'Did he say what he wanted?'[paragraph break]Holstenoffer shrugs. 'Didn't say. He said he'd be waiting for you over by Lord's Market. Yeh oughtta stick by Bobby more, Jack,' he adds. 'Big strong lad, he is; do a skinny boy like yerself good to learn from him.'[subsequently][rp] 'you'll have to ask him yerself. He's somewhere around Lord's Market, east of here.'[only]"

HO3 is a quip. The menu text is "'Heard any gossip?'". The display text is "[first time]'So, heard any good gossip lately, Holstenoffer?' you ask.[paragraph break]'Well, the mercenaries in Grubber's have got everyone excited,' Holstenoffer says, 'but I expect they'll be old news by tomorrow. Aside from that, all anyone talks about is the upcoming Ascension, yeh know.'[subsequently][rp] 'mercenaries and the Ascension are all anyone's talking about.'[only]"

HO4 is a quip. The menu text is "'What do you know about the mercenaries in Grubber's?'". The display text is "[first time]'Have you heard about the mercenaries in Grubber's Market this morning?' you ask.[paragraph break]Holstenoffer's eyes narrow. 'All I know about mercs is, they're filthy scavengers to a man, with empty purses where their sense of honor oughtta be. Let [']em try coming up Commerce Street,' he growls, tightening his grip on his cleaver, 'They'll find more than some timid stallkeeper here!'[subsequently][rp] 'all I know is they'd better not come into my shop.'[only]"

HO5 is a quip. The menu text is "'What do you think about Baron Fossville?'". The display text is "[first time]'So what do you think of Baron Fossville?' you ask.[paragraph break]The butcher's face suddenly grows dark, and his cleaver [i]thwacks[r] into the wooden block. 'Tell yeh the truth Jack, I laugh about it when I can, but the Baron's a rat and I'm not afraid to say it. He don't care about this city, he don't care for anything but his own purse and his own power, and he ain't half the lord the old Duke was. He and the old Duke were always at odds, yeh know. But Fossville won that quarrel, and not by playing fair, I'd wager.' Holstenoffer shakes his cleaver menacingly. 'It makes a man so angry, he's liable to cut his own hand off if he's not careful.'[subsequently][rp] 'Fossville's a rat and everyone knows it.'[only]"

HO6 is a quip. The menu text is "'What do you think of the Queen?'". The display text is "[first time]'What do you think of the Queen?' you ask.[paragraph break]'Bah,' spits Holstenoffer, 'that old bat couldn't rule so much as her own temper. This whole kingdom went to pot when she took the throne, and we'll be even worse off if she gets to decide who takes the throne after her.'[subsequently][rp] 'her Royal Highness is an idiot. She'll run this kingdom into the ground before she's through.'[only]"

HO7 is a quip. The menu text is "'What do you think of the Princess?'". The display text is "[first time]'What do you think of the Princess?' you ask.[paragraph break]'Well, the apple doesn't fall too far from the tree, yeh know,' grumbles Holstenoffer. 'I figure if the Ascension goes to the Queen's little brat, it'll be just like she never left.'[subsequently][rp] 'I just hope the nobles can decide on someone better to rule this kingdom.'[only]"

HO8 is a quip. The menu text is "'What do you think of the Ascension?'". The display text is "[first time]'What's your opinion of the upcoming Ascension?' you ask. 'Who do you think will take the throne?'[paragraph break]Holstenoffer heaves a sigh and spreads his arms in a wide shrug (forcing you to duck as a bit of bloody suet flies off the end of his cleaver and [i]splats[r] against the wall). 'I'm just a humble butcher, Jack. I know chopping meat, not Royal politics. But I do know that Fossville has his eye on the Queen's daughter, and that means his eye is on the throne, and [i]that[r] means no good for anyone. Yeh know what I think?' he asks, squinting at you and pointing with his cleaver. 'Old Holstenoffer thinks we're all doomed. That's what I think.'[subsequently][rp] 'it's all about who marries the Queen's daughter. Too depressing to think about, really.'[only]"

HO9 is a quip. The menu text is "'What did you think of the old Duke?'". The display text is "[first time]'I guess you liked the old Duke better, huh?' you ask.[paragraph break]Holstenoffer takes a deep breath. 'It's hard for someone your age to understand, Jack,' he says. 'Duke Toresal was a great man. A great, great man.' He starts chopping furiously, as if trying to hide a sudden rush of emotion.[subsequently][rp] 'the old Duke was a great man.'[only]"

HO10 is a quip. The menu text is "'Where did you get the tattoos?'". The display text is "[first time]'Holstenoffer,' you ask, 'where did you get those tattoos?'[paragraph break]Holstenoffer looks proudly down at his arms. 'Got these during my days at sea,' he said. 'I was quite the old salty dog, yeh know, before I came back to the city to be a meat chopper.' Then he frowns. 'Never did get the hang of it, though,' he admits. 'Kept gettin['] seasick. Still, the tattoos are quite the conversation piece, they are.'[subsequently][rp] 'got [']em when I was a sailor.'[only]"

HO11 is a quip. The menu text is "'Thanks.'". The display text is "'Thanks, Holstenoffer,' you say.[paragraph break]'Always a pleasure,' he says, bringing his cleaver down with a crunch."

HO12 is a quip. The menu text is "'Can I ask you something?'". The display text is "'Mind if I ask you a question?' you say.[paragraph break]'Go right ahead,' says Holstenoffer."

HO13 is a quip. The menu text is "'Never mind.'". The display text is "'Nah, never mind, it was nothing.'[paragraph break]'Suit yourself,' Holstenoffer says with a shrug."

HO14 is a quip. The display text is "'Say, Jack,' says Holstenoffer suddenly, 'I haven't seen Widow Shannon down this way in a few days. Everything all right at Maiden House?'"

HO15 is a quip. The menu text is "'She's fine; she's just been busy.'". The display text is "'Oh, everything's fine at Maiden House,' you say, while thinking to yourself, [i]and boring as ever[r]. 'Widow Shannon's just been busy; she hasn't had time to do the shopping.'[paragraph break]'Well, tell her I've been savin['] some choice cuts for her next time she comes down here. Good meat, better than she'll get in Grubber's, and cheaper too. Those orphans need to eat healthy.' He grins. 'And I need to convince Widow Shannon that I'd make her a good husband.'"

HO16 is a quip. The menu text is "'I try to stay away from Maiden House.'". The display text is "You try to stifle a groan. 'To tell you the truth, Holstenoffer, I try to stay away from Maiden House whenever I can.'[paragraph break]Holstenoffer smiles knowingly. 'The Widows breathin['] down yer neck a bit, eh? Well, I know all about that. Plenty a['] times I sneaked outta the bedroom window when I was a wee lad stayin['] with me aunty. Still,' he waves his cleaver at you admonishingly, and you step back quickly to avoid a deep facial scar. 'Still, Maiden House is a good place for urchins like yerself. Keeps yeh outta trouble. And the widows are a fine bunch of women. [i]Especially[r] that Shannon,' he adds, with a wistful look in his eye."

HO17 is a quip. The menu text is "'She's just not into you, Holstenoffer.'". The display text is "You sigh. 'You know, I've told you before, Widow Shannon isn't looking for a husband. It's nothing against you personally, I think she just likes devoting all her time to the orphans.'[paragraph break]Holstenoffer just smiles. 'Oh, I know, Jack, that's what she [i]says[r]. But old Holstenoffer knows a few tricks too, yeh know.' He waves his cleaver in the air nonchalantly; a bit of bloody gristle dangles from the blade's tip. 'You just get her down here, and let Holstenoffer work his charm.'[paragraph break]You know from past experience that there's no use arguing with him about this, so you just smile and nod. 'I'll let her know,' you say."

After populating a quip (called q) when HO14 is unfired and Old Man Holstenoffer is the current conversationalist:
	if the location has not been the sewer begin;
		if q is not HO1 and q is not HO12 begin;
			if a random chance of 1 in 2 succeeds begin;
				carry out the firing activity with HO14;
				carry out the populating activity with HO14;
			end if;
		end if;
	end if;

The response of HO1 is { HO2, HO3, HO4, HO12 }

The response of HO2 is { HO12, HO11 }

The response of HO3 is { HO5, HO6, HO7, HO8, HO12, HO11 }

The response of HO4 is { HO12, HO11 }

The response of HO5 is { HO9, HO12, HO11 }

The response of HO6 is { HO7, HO8, HO12, HO11 }

The response of HO7 is { HO6, HO8, HO12, HO11 }

The response of HO8 is { HO5, HO6, HO7, HO12, HO11 }

The response of HO9 is { HO12, HO11 }

The response of HO10 is { HO12, HO11 }

The response of HO11 is { }

The response of HO12 is { HO2, HO3, HO4, HO5, HO6, HO7, HO8, HO9, HO10, HO13 }

The response of HO13 is { }

The response of HO14 is { HO15, HO16, HO17 }

The response of HO15 is { HO16, HO17, HO12 }

The response of HO16 is { HO17, HO12 }

The response of HO17 is { HO12 }

Section 2 - Later conversation

HO18 is a quip. The display text is "Holstenoffer looks up as you enter his shop[first time], and his eyes widen. 'Goddesses, Jack,' he says, 'you look like you've had the worse of it. Widow Fiona was just in this morning, lookin['] for yeh. Is everything all right[subsequently]. 'Everything all right, I hope[only]?'"

HO19 is a quip. The menu text is "'Have you seen Bobby?'". The display text is "'Have you seen Bobby since last night?' you ask desperately. 'Or heard anything?'[paragraph break]'Well, no,' Holstenoffer says. His wide brow creases in concern. 'What's wrong, Jack? Is Bobby in some kind of trouble?'"

HO20 is a quip. The menu text is "'No, I'm just looking for him.'". The display text is "You [i]want[r] to tell Holstenoffer, but the words catch in your throat. Who can you trust, really? 'Um, no, no trouble,' you say. 'I'm just looking for him, is all.'[paragraph break]Holstenoffer sets his mouth in a grim line. It's clear he doesn't believe you, but Holstenoffer's never been one to pry. 'All right, then,' he says."

HO21 is a quip. The menu text is "'Yes, he's been captured by Fossville's men.'". The display text is "'It was Fossville!' you blurt out before you can stop to think whether you should. 'His mercenaries captured Bobby and me in the woods north of town... I got away, but Bobby's still in jail.'[paragraph break]Holstenoffer's jaw clenches and he shakes his head. 'Aw, stupid, brave lad. And that rotten Fossville. I'm so sorry, Jack. If I can do anything for yeh, just tell me, all right?'"

HO22 is a quip. The menu text is "'Is there a back door here?'". The display text is "[first time]'Listen, is there a back door in your shop?' you ask, craning to see around Holstenoffer's huge frame.[paragraph break]'Yeah...' he says slowly, giving you an odd look. 'Don't lead to nothin['] but a closed-off alley, though. What do yeh want back there for?'[subsequently][rp] 'there's an alley in back. But yer not goin['] back there until you tell me what yer doin['].'[only]"

HO23 is a quip. The menu text is "'Just let me back there.'". The display text is "'Please, Holstenoffer,' you say, 'just let me back there.'[paragraph break]He looks you square in the eye. 'Now look, Jack,' he says, pointing his cleaver at you, 'Yer a good lad and I like yeh, but I can't go lettin['] urchins run through my shop whenever they've got a hankerin['], yeh know? If you want me to help yeh, yer gonna have to tell me what's going on."

HO24 is a quip. The menu text is "'It's to help Bobby.'". The display text is "You take a deep breath. 'I need to get up onto the rooftops, so I can get to... a place.' You're not sure if Holstenoffer would actually [i]disapprove[r] of breaking into Fossville's house, in principle, but he probably wouldn't approve of you doing it yourself. 'Please, Holstenoffer. I need to get up there to help Bobby.'[paragraph break]Holstenoffer looks at you for a few moments, then nods his head. 'All right then. Come around the counter here.'[paragraph break]You follow the burly butcher to the back of his shop, where he unlocks and opens a small metal door. 'Be safe now, Jack,' he says, laying a thick hand on your shoulder. 'Do what yeh have to do.'[paragraph break]A twinge of guilt nags at you. Technically, you don't have any idea whether this letter will help Bobby or not, although you certainly [i]hope[r] it does. But what else can you do? 'Thanks,' you whisper, and step through the door."

The response of HO18 is { HO19, HO22 }.

After populating HO18:
	if HO19 is fired, remove HO19 from the current conversation, if present;

The response of HO19 is { HO20, HO21 }.

The response of HO20 is { HO22 }.

The response of HO21 is { HO22 }.

The response of HO22 is { HO23, HO24 }.

After populating HO22:
	if HO23 is fired, remove HO23 from the current conversation, if present;

The response of HO23 is { HO24 }.

After firing HO24:
	now the current conversationalist is the player;
	move the player to the thin alley;
	update the character list;

Part 4 - Armory

Chapter 1 - Description and scenery

The armory shop is a store, northeast of Commerce Street. The description of the armory is "The tangy smell of oiled steel is nearly overpowering in this small, windowless shop. All of the really interesting weapons — axes, flails, spiked gauntlets and the like — are high up on racks on the back wall, well behind the high counter. However, there is a display of assorted knives and daggers sitting out where you can get a close look at them." The printed name is "Armory".

South of the armory shop is Commerce Street.

Outside from the armory shop is Commerce Street.

Instead of facing outside while in the Armory Shop, try facing south.
The dead-end description of the Armory Shop is "You can see no other exits besides the door to the south, leading to Commerce Street."

The dagger display is scenery and here. The description is "Over a dozen gleaming blades are set out in neat little rows: from long, wicked stilettos, to demure little stickers you could hide inside an opera glove, to gigantic cleavers that would make Holstenoffer next door jealous." Understand "daggers", "knife", "knives" as the dagger display.

Some blades are part of the dagger display. The description is "The blades are all beautiful, polished to a deadly gleam." Understand "blade", "stiletto", "stilettos", "sticker", "stickers", "cleaver", "cleavers", "sword", "swords" as the blades.

Some big weapons are scenery and here. The description is "The sheer variety of ways to grievously injure someone with a sharp, metal instrument leaves you both impressed and vaguely disturbed." Understand "big", "axe", "axes", "flail", "flails", "racks", "rack", "gauntlet", "gauntlets", "spiked gauntlets/gauntlet", "more interesting", "interesting" as the big weapons. The printed name of the big weapons is "big weapons".

Instead of smelling the armory shop:
	say "The tangy smell of oiled steel is nearly overpowering.";

Instead of going outside in the armory shop, try going south.

Instead of doing something other than examining or buying to the dagger display:
	say "'Touch one of those [if Pieter is not nearby]and I'll use it to have your finger off, little thief,' growls Minor.[otherwise] and-'[paragraph break]'Shut your trap, Minor,' snaps Pieter, and Minor falls silent and even takes a step or two back. 'Hurry up and buy something, Jacqueline,' Pieter says.[end if]" instead;

Understand "pick [something]" as buying when the location is a store.
Understand "choose [something]" as buying when the location is a store.

Chapter 2 - Olgan Minor

Section 1 - Description of Olgan

Olgan Minor is a man and here. "Olgan Minor, the proprietor, leans against the counter, watching you intently." The description is "Olgan Minor is short but muscular, with a lined face and graying, military-cut hair. He says nothing as you browse his wares, but his eyes seem to bore right into you.[first time][if the player is still dirty] You are suddenly certain that he knows you're not really a boy — that he knew it the instant you walked into his shop.[end if][only]"

Understand "proprietor", "shopkeeper", "shop keeper", "merchant", "man" as Olgan Minor.

[ After examining Olgan Minor for the first time, say "You've heard that Olgan Minor is not one to trifle with — and now that you've seen him, you believe it." ]

Instead of kissing Olgan Minor, say "You're pretty sure he wouldn't appreciate that."

Every turn when the player is not on the move and a random chance of 1 in 5 succeeds and Olgan Minor is nearby:
	say "[one of]Minor's eyes seem to drill right into you[or]Minor trims his nails with a small knife. The whole time, he never stops staring at you. He never blinks[or]Olgan Minor's constant staring is really starting to creep you out. You find yourself wishing you'd never entered his shop[at random]."

Section 2 - Actions and reactions

Before buying anything when the location is the armory shop and the player does not have the purse:
	say "Minor barks an unpleasant laugh. 'Run along, [i]girl,[r] before you cut yourself.'" instead.

Section 3 - Leaving

After going from the Armory Shop:
	say "You leave the weapons shop with a sense of relief.";
	continue the action;

Section 4 - Conversation

Rule for initiating conversation with Olgan Minor:
	change the chosen opening gambit to OM1;

OM1 is a quip. The display text is "'Um, mind if I ask you something?' you stammer.[paragraph break]The man's lip twitches slightly, but he says nothing. You decide (foolishly, perhaps) to interpret this as a 'yes'."

OM2 is a quip. The menu text is "'Tell me about your weapons.'". The display text is "You make a show of looking around the shop. 'You sure have a lot of interesting weapons, here,' you say. 'Can you tell me something about them?'[paragraph break]'You should find something else to be interested in,' Olgan sneers, 'else you're likely to get cut.'"

OM3 is a quip. The menu text is "'What do you know about the mercenaries in Grubber's Market?'". The display text is "'Do you, uh, know anything about the mercenaries who were in Grubber's Market this morning?'[paragraph break]Olgan smiles unpleasantly. 'They're some of my best customers. Should I let them know you were asking?'[paragraph break]'Er — no, no thanks,' you stammer. 'Not necessary.'"

OM4 is a quip. The menu text is "'Have you seen Bobby lately?'". The display text is "'I, uh, don't suppose you've seen Bobby around?' you ask. 'Tall? Black hair?'[paragraph break]Olgan Minor just curls his lip with contempt."

OM6 is a quip. The menu text is "'What do you think about Baron Fossville?'". The display text is "'So what's your opinion on Baron Fossville?' you ask.[paragraph break]'I don't discuss politics with customers,' Olgan snaps."

OM7 is a quip. The menu text is "'Never mind.'". The display text is "'Uh, okay then,' you mumble. 'I'll just... look around by myself, then.'"

OM8 is a quip. The display text is "'I'm through listening to your chatter, little boy,' Olgan growls. 'Now get out of my store before I decide to test my wares on your soft, pink hide.' He advances on you menacingly, and you find yourself backpedaling towards the door."

OM9 is a quip. The menu text is "'Is there a back door here?'" The display text is "'Is there a back door in your store?' you ask.[paragraph break]Olgan narrows his eyes at you. 'Looking for an easy way in, little thief?' he growls.[paragraph break]'Er, no... I mean, sorry. Never mind,' you stammer."

The response of OM1 is { OM2, OM3, OM4, OM6, OM9, OM7 }

The response of OM2 is { OM2, OM3, OM4, OM6, OM9, OM7 }

The response of OM3 is { OM2, OM3, OM4, OM6, OM9, OM7 }

The response of OM4 is { OM2, OM3, OM4, OM6, OM9, OM7 }

The response of OM6 is { OM2, OM3, OM4, OM6, OM9, OM7 }

The response of OM7 is { }

The response of OM8 is { }

The response of OM9 is { OM2, OM3, OM4, OM6, OM9, OM7 }

OM2 is clustered with OM3, OM4, OM5, and OM6.

After populating a quip (called q) when the current conversationalist is Olgan Minor:
	remove q from the current conversation, if present;
	if HO1 is unfired:
		remove OM4 from the current conversation, if present;
	if the location has not been the sewers:
		remove OM9 from the current conversation, if present;

Rule for firing a fired quip that is clustered with OM2:
	carry out the firing activity with OM8 instead;
	[ Olgan Minor doesn't care to repeat himself!... ]

After firing OM8:
	[...And on being asked to do so, he will kick us out of his shop...]
	now the current conversationalist is the player;
	move the player to Commerce Street;
	update the character list;

To decide whether the player is scared of Olgan Minor:
	[...whereupon we'll be wary of going back in there.]
	if the bodyguard scene is happening, no; [ ok with Pieter as backup ]
	if OM8 is fired, yes; [ not ok if we've been thrown out]
	no; [ ok if we know no better ]

Part 8 - East Commerce Street

Chapter 1 - Description and scenery

East Commerce Street is east of Commerce Street. "The east end of Commerce Street winds through an upper-class residential neighborhood before ending up at Lord's Market. Behind spiked, iron fences stand the 'city cottages' of Toresal's wealthy merchants and nobility, built tall and narrow to make the best use of limited real estate.[paragraph break]Three stately houses are built close together on the north side of the road: Black Gate Estate to the northwest; Red Gate Estate to the north; and the Jacobs family mansion to the northeast. The street itself continues east and west, and the City Park lies to the south.[if Bobby's Hanging is happening][first time][paragraph break]There seems to be some sort of spectacle going on in Lord's Market; pedestrians are crowding the streets, pressing east.[only][end if]"

Understand "upper", "class", "residential", "neighborhood" as East Commerce Street.

After going south to East Commerce Street when the player is clean and TRIG_SANDLER_HINT is unfired:
	say "[command clarification break]";
	try looking;
	fire TRIG_SANDLER_HINT;
	update the character list;

TRIG_SANDLER_HINT is a trigger.

Rule for firing unfired TRIG_SANDLER_HINT:
	say "'I suppose we should go see Mrs. Sandler now,' Shannon says.";

The distant description of East Commerce Street is "East Commerce Street lies just [if location is Commerce Street]east[otherwise if location is Lords Market]west[otherwise]south[end if] of here."

Instead of facing northwest while in East Commerce Street, try examining Black Gate Estate.
Instead of facing north while in East Commerce Street, try examining Red Gate Estate.
Instead of facing northeast while in East Commerce Street, try examining Jacobs Family Mansion.
Instead of facing south while in East Commerce Street, try examining the City Park.

An estatefront is a kind of door. An estatefront is usually scenery, not openable, lockable, locked. Understand "estates", "house", "houses", "cottage", "cottages", "mansion", "mansions", "building", "buildings" as an estatefront.

Rule for assembling available exits of East Commerce Street:
	change the viable directions to {northwest, north, northeast, south};

Section 1 - Black Gate Estate

Black Gate Estate is an estatefront, northwest of East Commerce Street. The description is "Black Gate Estate is the home of Baron Fossville, the current Lord of Toresal since the old Duke's death (though he has yet to be fully confirmed). It is a somber building of dark flagstone and slate, although you've heard the Baron keeps a beautiful rooftop garden."

Instead of going northwest in East Commerce Street, try entering Black Gate Estate.
Instead of opening, knocking on, or entering Black Gate Estate, say "The servants would never let someone like you enter unless you had urgent business with the Baron, which they'd never believe you had anyway.";

Section 2 - Red Gate Estate

Red Gate Estate is an estatefront, openable, north of East Commerce Street. The description is "This is where the Duke and former Lord of Toresal lived, until his death several years ago. Now it stands empty, slowly crumbling. No one has ever made an attempt to claim the property, and the Baron seems content to let it remain vacant." Understand "front", "gates" as Red Gate Estate.

Understand "lock" as Red Gate Estate when the location is East Commerce Street.

The matching key of Red Gate Estate is the large brass key.

Rule for printing the name of Red Gate Estate while implicitly opening:
	say "the front gates";

After printing the description of Red Gate Estate:
	if Red Gate Estate is locked:
		say "The front gates are securely locked. ";
	otherwise if Red Gate Estate is closed:
		say "The front gates are closed. ";
	otherwise:
		say "The front gates are open. "

Instead of opening locked Red Gate Estate when the player encloses the large brass key:
	now the player carries the large brass key; [i.e. get it out of satchel]
	try unlocking Red Gate Estate with the large brass key;

Instead of opening locked Red Gate Estate:
	say "The front gates are securely locked." instead;

Rule for supplying a missing second noun when unlocking Red Gate Estate with:
	if the player encloses the large brass key:
		change the second noun to the large brass key;

Rule for supplying a missing second noun when locking Red Gate Estate with:
	if the player encloses the large brass key:
		change the second noun to the large brass key;

Instead of knocking on Red Gate Estate, say "No one answers."

After unlocking Red Gate Estate with the large brass key for the first time:
	now Red Gate Estate is open;
	say "The lock is rusty and stiff from long disuse, but the key turns... and the door unlocks with a grinding [i]clunk[r].";
	unless currently running the implicitly opening activity:
		try entering Red Gate Estate instead;

Instead of unlocking Red Gate Estate with the piece of wire:
	say "The lock on these gates is too sturdy for you to pick with the little lockpick.";

Section 3 - Jacobs Family Mansion

The Jacobs Family Mansion is an estatefront, northeast of East Commerce Street. The description is "Jacobs the Elder is rumored to be the richest merchant in Toresal. His lavish city residence is built to resemble a miniature castle, complete with turrets, crenellations, and a heavy portcullis over the front door." Understand "castle" as the Jacobs Mansion. The printed name is "Jacobs Mansion".

Some crenellations are scenery, in East Commerce Street. "All the castle-style architecture on Jacobs['] mansion is a bit ostentatious."
Understand "turrets", "turret", "crenellation", "portcullis" as the crenellations.

Instead of going northeast in East Commerce Street, try entering Jacobs Family Mansion.

Instead of opening, knocking on, or entering Jacobs Family Mansion, say "[if Escaping Jail has happened]After your encounter with the younger Jacobs in jail, you're not too keen on meeting him or his father anytime soon[otherwise]You don't have any business with Jacobs, and even if you did, you might think twice before knocking on his door. Rumor has it that many of his dealings are decidedly less than legal[end if].";

Section 4 - City Park

City Park is scenery, in East Commerce Street. The description is "The park is a couple of acres of open meadow and sparse woods in the middle of the city, set aside by the previous Lord to promote health and relaxation among Toresal's citizens. It still sees strollers and the occasional picnicker by day, although it's become infamous as a haunt for cutpurses by night. The park is surrounded by a high stone wall, with a gate on the north side." The printed name is "park". Understand "meadow", "woods", "wood" as City Park.

Instead of going south in East Commerce Street, try entering City Park.

Instead of entering City Park:
	say "[one of]You hesitate. Several street kids have congregated in the middle of the park, chasing each other and playing at knights and villains in the late afternoon warmth. Some of them know you from [if Escape Through The Sewer has happened]the streets or from Maiden House. The last thing you need now is for someone to recognize you and start calling out your name, drawing attention to you[otherwise]Maiden House, and know that you're really a girl. That could lead to an awkward situation if one of them called you out in front of someone who only knows you as a boy. [i]Especially[r] if that someone was Bobby[end if][or]No, too risky. Better visit the park another day[stopping].";

Instead of entering City Park when it is night,
	say "The park is not a safe place at night.";

Section 5 - Iron Fence

The iron fence is scenery, in East Commerce Street. The description is "The fence is too high to climb, and topped with sharp iron spikes to boot." Understand "spike", "spikes", "spiked", "fences" as the fence.

Instead of climbing Red Gate Estate:
	try climbing the fence;

Instead of climbing the fence:
	try examining the fence;

Section 6 - Knock knock knock

Knocking on is an action applying to one touchable thing.
Understand "knock [something]" as knocking on.
Understand "knock on [something]" as knocking on.

Carry out knocking on a person:
	say "I don't suppose [the noun] would care for that." instead.

Carry out knocking on a door:
	say "Knock knock. There is no reply." instead.

Carry out knocking on something:
	say "Nothing happens." instead.

Book 4 - Lord's Market

Part 1 - Market Square

Chapter 1 - Description

Lords Market is east of East Commerce Street. The description is "[timely description of Lords Market]". The printed name of Lords Market is "Lord's Market".

To say timely description of Lords Market:
	if Bobby's Hanging is happening begin;
		say "Lord's Market is packed with people from every part of the city, all of them pressing in, blocking the streets. You've never seen it this crowded before. ";
	else if it is day;
		say "Lord's Market is the high-class mirror to Grubber's Market: a wide, cobbled square, uncrowded and clean, shaded by tall buildings on either side. Here Toresal's upper crust can hunt for bargains inaccessible to the ordinary rabble. Antiques, custom jewelry, and similarly high-priced goods are available behind the tasteful shopfronts. A fine clothier's, a jewelry store, and a moneylender's shop lie northeast, east, and southeast, respectively; you can return to Commerce Street to the west; an alley leads south. ";
	otherwise;
		say "At night, lanterns hung from ornate posts light the empty square of Lord's Market. Their warm yellow glow seems to make the shadows in the alleys that much darker. ";
	end if.

Understand "wide", "cobbled", "square", "uncrowded", "clean", "shaded", "shady" as Lords Market.

The distant description of Lords Market is "Lord's Market lies just [if location is East Commerce Street]east[otherwise if location is Back Alley]north[otherwise]west[end if] of here."

Instead of facing northeast while in Lords Market, try examining the clothiers storefront.
Instead of facing east while in Lords Market, try examining the jewelers storefront.
Instead of facing southeast while in Lords Market, try examining the moneylenders storefront.

Inside from Lords Market is nowhere. Instead of going inside from Lords Market, say "You'll have to say which shop you want to enter."

Chapter 2 - Constrained exits

Before going from Lords Market during Bobby's Hanging:
	say "More people have squeezed in behind you, crushing you into the crowd. You can't get through all the densely packed people." instead;

Before going east from Lords Market when a quip clustered with DS29 is fired:
	say "Dame Sandler asked you not to come back. You're on your own, at least for now." instead;

Chapter 3 - Scenery

Section - Storefronts

The clothiers storefront is a storefront, scenery and here. "A large window, trimmed in fine, dark wood, lets light into the front of the shop. A clothier's dummy dressed in a smartly cut tunic stands in front of a velvet backdrop, advertising the latest fashion to be found inside." Instead of entering the clothiers: try going northeast instead.
Understand "tailor", "clothier", "window", "display", "velvet", "backdrop", "tunic", "clothiers dummy" as the clothiers storefront.

The jewelers storefront is a storefront, scenery and here. "The sign above this store is painted with a necklace of pearls. The windows are blocked with thick (but decorative) iron bars, and the door sports a heavy lock. An engraved brass plaque reads 'Sandler and Son.'" Instead of entering the jewelers: try going east instead.
Understand "jeweler", "plaque", "window", "windows", "iron", "bars", "bar", "barred", "Sandler", "Sandler and Son" as the jewelers storefront.

The moneylenders storefront is a storefront, scenery and here. "A discreet placard next to the door reads 'Chorus Brothers.' The remainder of the windowless storefront is painted blue." Instead of entering the moneylenders: try going southeast instead.
Understand "placard", "Chorus", "Brothers", "blue" as the moneylenders storefront. 

Some servants are scenery, people, and here. The description is "Most of the people wandering about Lord's Market are well dressed servants of the upper class (even for merchandise as nice as this, rich people rarely do their own shopping). Everyone looks to be in a dreadful hurry, with dreadfully important business to attend to, and no one affords you so much as a second glance." The printed name is "people". Understand "people", "crowd", "crowds", "shopper", "shoppers", "servant" as the servants.

Instead of kissing the servants, say "The people here would probably not take kindly to that."

After assembling when in Lords Market:
	remove the servants from the roster of available characters, if present;

Section 2 - Lamps

Some lanterns are scenery, in Lords Market. The description is "[if it is night]The lampposts cast circles of warm yellow light around the perimeter of the market square[otherwise]The lampposts are unlit during the day[end if]." Understand "lamp", "lamps", "lantern", "lanterns", "post", "posts", "lamppost", "lampposts" as the lanterns.

Chapter 4 - Event on entry

After going to Lords Market for the first time:
	say "[command clarification break]";
	try looking;
	update the character list;
	fire EVENT_Fossville;

EVENT_Fossville is a dramatic event. The display text is "As you are standing there getting your bearings, the door to the moneylender's shop suddenly slams open. A tall, dark-cloaked figure storms out, shouting over his shoulder, '...regret refusing my business! I know import—[i]OOF!'[no line break][r] —and before you have a chance to react, he crashes into you. You both hit the cobbles in a tangle of limbs.[paragraph break]Muttering apologies, you try to disentangle yourself, but the man is on his feet in an instant, dragging you up by your elbow. 'Why you insolent little cutpurse—'[paragraph break]A woman standing next to him places her hand on his shoulder. 'Calm yourself, Fossville,' she says. Only her chin is visible beneath the shadow of her hood, but the woman's voice is cool and commanding. 'It's only a foolish street boy. You've already terrified him.'[paragraph break]The man — you realize now that he must be Baron Fossville, Lord of the city since the old Duke died, and one of the most powerful nobles in Toresal — eyes you suspiciously. Even flustered, his sharp features are intense, handsome, and dangerously intelligent. His lips twitch slightly beneath his trim moustache. Finally, he releases you.[paragraph break]'Off with you then, urchin,' he growls, flicking his fingers as though he had just touched something filthy. 'Take your begging elsewhere, and learn to stay out from under the feet of your betters.'[paragraph break]Fossville strides off towards Commerce Street, followed closely by his hooded companion. As she brushes past you, you can hear her talking in a low voice, '...have the money you need, but we must be subtle about it. The Queen can not appear to play favorites...'[paragraph break]And then they are gone."

EVENT_Fossville can be newly-fired. EVENT_Fossville is not newly-fired.

After firing unfired EVENT_Fossville:
	now EVENT_Fossville is newly-fired;

After going somewhere from Lords Market when EVENT_Fossville is newly-fired:
	now EVENT_Fossville is not newly-fired;
	continue the action;

Instead of following Baron Fossville when the location is Lords Market and EVENT_Fossville is newly-fired:
	say "You've already lost him in the crowd.";

Part 2 - Clothier's (Royal Tunic)

Chapter 1 - Description & Scenery

The clothiers shop is a store, northeast of Lords Market. The printed name is "Royal Tunic". The description is "The walls are hung with tasteful curtains, and a deep, soft carpet covers the floor. Several clothier's dummies stand about the room, displaying gorgeous formal court apparel for both men and women. The street lies west."

West of the clothiers shop is Lords Market.

Outside from the clothiers shop is Lords Market.

Instead of facing outside while in the Clothiers Shop, try facing west.
The dead-end description of the Clothiers Shop is "You don't see any other exits besides the door to the west, to Lord's Market."

Some clothiers curtains are scenery, in the Clothiers Shop. "The curtains are made of thick, purple velvet.". The printed name is "curtains". Understand "curtain", "thick", "velvet", "purple" as the clothiers curtains.

The clothiers carpet is scenery, in the Clothiers Shop. The printed name is "carpet".

The description of the clothiers carpet is "The carpet is deep, plush, and colored a rich, vibrant... pink[first time]. Definitely not something you see every day[only]." Understand "deep", "soft", "plush", "rug", "rich", "vibrant", "pink" as the clothiers carpet.

Some dummies are scenery, in the Clothiers Shop. The description is "Armless, headless torsos carved from wood, stuck atop iron tripods, and dressed in fancy clothes. They're actually a bit ghoulish, now that you really look at them." Understand "dummy", "torsos", "torsoes", "torso", "tripod", "tripods", "clothiers" as the dummies.

A floor-length mirror is scenery, in the Clothiers Shop. The description is "In the clothier's tall, floor-length mirror, you can see yourself from head to toe." After examining the floor-length mirror, try examining yourself. Instead of searching the floor-length mirror, try examining the floor-length mirror. Understand "floor length" and "tall" as the floor-length mirror.

Understand "reflection", "my reflection", "reflection of" as yourself when the player can see the floor-length mirror.

Chapter 2 - More scenery (Clothes)

The lovely clothes are scenery, in the Clothiers Shop. The description is "The clothes here are lovely — rich velvets and flowing silks, dyed with brilliant colors and embroidered with lace and gems. [if the player does not have the purse][first time]These are the sorts of costumes that you imagine while lying on your straw mattress back at Maiden House, dreaming about dazzling the Prince at the Royal Ball. Stupid fantasies, you know, but oh — if you could only wear something as beautiful as these, just for one evening...[only][otherwise]You used to daydream about dresses like these. It hardly seems possible that you could actually buy one and wear it now.[end if][if the player is still dirty][paragraph break]Suddenly, you remember that you're supposed to be looking at the [i]men's[r] clothing. You glance nervously at the shopkeeper, but he doesn't seem to find your intense interest in ladies['] formal wear strange in the slightest.[end if]". Understand "velvet", "velvets", "silk", "silks", "costume", "costumes", "dresses", "gowns", "ballgowns", "ball gowns", "fancy", "gorgeous", "formal", "court", "apparel" as the lovely clothes. Understand "dress", "gown", "ballgown", "ball gown" as the lovely clothes when the location does not enclose the ball gown. 

Yourself can be clean or still dirty. Yourself is still dirty.

Chapter 3 - Event on entering

After going to the clothiers shop when the player is still dirty:
	say "[first time]A small bell above the door tinkles as you enter, and a man comes rushing out of the store's back room with a measuring cord in his hands, held up as though ready to begin fitting you immediately. When he sees that you're merely a boy (and by all appearances, not one with much in the way of discretionary income at that), he stops in his tracks and his expression grows distinctly pouty. He manages to keep most of the disappointment out of his voice as he asks, 'May I [i]help[r] you?'[only]";
	continue the action;

After going to the clothiers shop when the player is clean:
	say "[first time]A small bell above the door tinkles as you enter, and a man comes rushing out of the store's back room with a measuring cord in his hands, held up as though ready to begin fitting you immediately. When he sees that you are a clean, respectable, and evidently well-to-do young lady, he practically squeals with delight, clapping his hands and plucking at the fabric of your clothes with soft, nimble fingers.[only]";
	continue the action;

Section 1 - Leaving

After going from the Clothiers Shop:
	say "'Come back soon, sweetie,' calls the clothier as you leave.";
	continue the action;

Chapter 4 - The Clothier

The clothier is a man, in the clothiers shop. The initial appearance of the clothier is "[if the player is still dirty]The clothier stands nearby, eyeing you with some distaste[otherwise]The clothier hovers nearby, waiting to serve[end if]." The description of the clothier is "Tall, thin, and impeccably dressed, although his fashion sense seems a bit...decorative, compared to the more austere look preferred by men of Toresal's upper class. His hairline is receding, and what hair he has left is plastered back onto his scalp with fragrant oil. He looks as though he'd secretly prefer to wipe things down with a scented handkerchief before touching them; somehow, through good breeding and intestinal fortitude, he manages to soldier on despite not doing so." Understand "proprietor", "shopkeeper", "shop keeper", "merchant", "man" as the clothier.

Every turn when the clothier is nearby and a random chance of 1 in 5 succeeds:
	say "[one of]The clothier absentmindedly flicks a bit of lint off of his sleeve[or]The clothier wanders over to one of the dresses on display and fusses with the [one of]sleeve[or]hem[or]neckline[at random][or]The clothier inspects his fingernails[or]The clothier purses his lips and hums something indecipherable[or]The clothier slowly looks you up and down. You get the distinct and not-exactly comfortable feeling that he's 'dressing' you with his eyes[at random].";

Chapter 5 - Actions and interactions

Before talking to the clothier when the player is still dirty:
	say "The clothier sniffs and pretends not to hear you." instead;

Instead of kissing the clothier, say "You should probably get to know the clothier a little better, first."

Instead of taking the lovely clothes:
	try buying the lovely clothes;

Instead of giving the purse to the clothier when the player does not wear the ball gown:
	try buying the lovely clothes;

Instead of giving the purse to the clothier when the player wears the ball gown:
	say "You've already paid the clothier.";

Before buying the lovely clothes when the player is still dirty:
	say "The clothier is not likely to sell anything to a dirty urchin such as yourself. Besides, you're only interested in the dresses, and it would raise awkward questions if you asked him to sell you one." instead;

Understand "pick [lovely clothes]" as buying.
Understand "choose [lovely clothes]" as buying.

Instead of buying the lovely clothes when the player does not have the purse:
	say "You point to one of the dresses, and the clothier quotes you a price that leaves you feeling distinctly queasy. Of course, your old wardrobe consists entirely of cast-offs from the older orphans and whatever interesting bits you were able to scrounge from back-alley trash bins. Who knew [i]real[r] clothes cost so much?[paragraph break]'Remember what Dame Sandler said, Jacqueline,' remarks Pieter. 'Get yourself some money first.'"

Instead of buying the lovely clothes when the player has the ball gown:
	say "You are already wearing a perfectly lovely ball gown.";

After buying the lovely clothes when the player has the purse and the player does not have the ball gown:
	remove Pieter from play;
	remove the daydress from play;
	now the player carries the ball gown;
	now the player wears the ball gown;
	say "After several agonizing minutes of indecision, you point to the dress you want, and hand over several coins.[paragraph break]The clothier flutters his hands and practically giggles, 'Oh, a [i]splendid[r] choice! You'll look like a princess!' He dances around you, taking measurements with his cord and muttering numbers to himself as he goes. Then he takes the dress off its dummy and retreats with it into the back of his shop. 'This will be [i]just[r] a moment,' he calls over his shoulder.[paragraph break]You look at Pieter and shrug.[paragraph break]'Er, I'll wait outside,' he says.[paragraph break]And afterwards, as you look at yourself in the full-length mirror while the clothier kneels at your feet, tugging, plucking, pinning, making adjustments, you realize that he was right — you [i]do[r] look like a princess.";
	update the character list;

Chapter 6 - Conversation

Rule for initiating conversation with the clothier:
	change the chosen opening gambit to CL1;

CL1 is a quip. The display text is "'I have a question...' you start to say.[paragraph break]'And I have [i]answers[r],' says the clothier, bowing deeply. 'Please, dear, ask away.'"

CL2 is a quip. The menu text is "'What's your cheapest dress?'". The display text is "'Can you show me one of your cheaper dresses?' you ask, thinking yourself quite shrewd.[paragraph break]The clother sniffs. 'If you want to look [i]cheap[r], you can buy something from that hussy who runs the silk tent in Grubber's. The question you have to ask yourself, honey, is how much is it [i]worth[r] to you to look [i]fabulous[r].'[paragraph break]You nod, silently wishing you could sink through the floor."

CL3 is a quip. The menu text is "'What would be good to wear to the ball?'". The display text is "[first time]'I'm, um... well, actually I'm going to be attending Fossville's ball tonight,' you say, still a bit giddy at the idea. 'And I need something to wear. What would you suggest?'[paragraph break]The clothier scoffs and rolls his eyes. 'Sister, there's no point in even choosing. They're [i]all[r] good. In fact, I [i]guarantee[r] that if you wear one of my dresses to the ball, [i]no one[r] is going to take their eyes off you. If you manage to find someone who is not treating you like the absolute [i]center of their universe[r], come back here, and I'll give you a full refund.'[subsequently][repeat phrase] 'all of my dresses are fabulous. Pick any you like.'[only]"

CL4 is a quip. The menu text is "'What do you think?'". The display text is "[first time]'You really think this will be all right?' you ask, looking down at the beautiful gown you're now wearing.[paragraph break]'It's fabulous,' raves the clothier. 'It's you. Trust me'.[subsequently][rp] 'you're a princess, darling. You're beautiful.'[only]"

CL5 is a quip. The menu text is "'What do you think of Fossville and the Ascension?'". The display text is "'What do you think about the Ascension coming up?' you ask. 'Do you think Baron Fossville has a chance?'[paragraph break]The clothier raises one (plucked, you suspect) eyebrow. 'Listen honey,' he says, 'I know dresses. That's all I know. Well, it's not [i]all[r] I know. But it's all I'm willing to [i]talk[r] about. Don't mix business with pleasure, and don't mix dresses with politics. That's my motto and I'm sticking to it.'"

CL6 is a quip. The menu text is "'Okay, how about... that one?'".

CL1 is clustered with CL2, CL3, CL4 and CL5.

The response of CL1 is { CL2, CL3, CL4, CL5 }.

The response of CL2 is { CL2, CL3, CL4, CL5 }.

The response of CL3 is { CL2, CL3, CL4, CL5, CL6 }.

The response of CL4 is { CL2, CL3, CL4, CL5 }.

The response of CL5 is { CL2, CL3, CL4, CL5 }.

After populating a quip (called q) that is clustered with CL2:
	remove q from the current conversation, if present;
	if CL2 is fired, remove CL2 from the current conversation, if present;
	if DS49 is unfired:
		remove CL3 from the current conversation, if present;
		remove CL4 from the current conversation, if present;
	if the player has the ball gown:
		remove CL3 from the current conversation, if present;
	otherwise:
		remove CL4 from the current conversation, if present;
	if CL5 is fired:
		remove CL5 from the current conversation, if present;
	unless the player has the purse:
		remove CL6 from the current conversation, if present;

After firing CL6:
	try buying the lovely clothes;

Chapter 7 - A ball gown

The ball gown is a wearable thing. The description is "The dress is pale blue with tiny pearls embroidered into the bodice and dagged sleeves."

Before taking off the ball gown:
	say "This isn't the time or place to get undressed." instead;

Understand "pale", "blue", "dress", "tiny", "pearls", "embroidery", "bodice", "dagged", "sleeves" as the ball gown.

Chapter 8 - Getting changed

[Pieter leaves us during this scene]

Getting changed is a scene. Getting changed begins when the player wears the ball gown, and the location is the clothiers shop, and the bodyguard scene is happening. Getting changed ends when the location is not the clothiers shop.

When getting changed begins:
	remove Pieter from play;
	update the character list;

When getting changed ends:
	say "Pieter blinks and does a double take as you step out onto the sidewalk in your new finery. '[i]Wow[r],' he stammers. 'You look... different. I mean, you looked good before, er, with the other dress, I mean, but this is... [i]wow[r].' He blushes, then grins, then shakes his head. 'Come on, Lady Toresal. We still have much to do.'";
	move Pieter to the location;
	update the character list;

Part 3 - Jeweler (Sandler and Sons)

Chapter 1 - Description & Scenery

The jewelers shop is a store, east of Lords Market. The printed name is "Sandler & Sons Jeweler". The description is "The inside of this store is tasteful but austerely decorated, perhaps to better draw the eye towards the glass display case in the center of the room and the king's ransom in jewelry contained therein. You can leave to the west."

Outside from the jewelers shop is Lords Market.

Instead of facing outside while in the Jewelers Shop, try facing west.
The dead-end description of the Jewelers Shop is "You don't see any other exits besides the door to the west, to Lord's Market."

A display case is scenery, plural-named, a container, closed, not openable, transparent, in the jewelers shop. The description is "Light glitters across diamond-encrusted rings, bracelets, pendants, brooches, and necklaces spread out on black velvet." Understand "cases", "glass", "topped", "top", "pedestal", "pedestals", "velvet", "box" as the display case.

Instead of opening the closed display case:
	say "The cases are locked, of course. And Pieter has his eyes on you constantly."

Inside the display case is some valuable jewelry. The description of the valuable jewelry is "Any one piece is several times more valuable than everything you've ever nicked in your entire life put together. The thought of wearing something that extravagant on your wrist makes your skin tingle." Understand "jewels", "rings", "bracelets", "pendants", "brooches", "necklaces", "necklace", "diamond", "diamonds" as the valuable jewelry. Understand "diamond", "encrusted" as the valuable jewelry. The printed name of the valuable jewelry is "jewelry".

Understand "jewel" as the valuable jewelry when the location does not enclose dress jewelry.

Understand "brooch" as the valuable jewelry when the location does not enclose the silver brooch.
Understand "bracelet" as the valuable jewelry when the location does not enclose the silver bracelet.
Understand "pendant" as the valuable jewelry when the location does not enclose the silver pendant.
Understand "ring" as the valuable jewelry when the location does not enclose the silver ring.

Chapter 2 - Pieter

Pieter is a man, in the Jewelers Shop. The description is "[if Pieter has not been caught]Pieter is loyal to Dame Sandler and tirelessly vigilant. You've traded friendly words with him in the past, but most of the time he stays quiet to preserve a sense of professionalism. He gives you a slight smile and nod when you catch his eye.[otherwise]It looks as though they worked him over pretty hard before tying him up. His wrists and ankles are all bound together behind his back, the ropes digging cruelly into his skin.[end if]". Understand "guard", "man" as Pieter.

The initial appearance of Pieter is "[if caught]Pieter is lying on the floor in front of the fire place, trussed up and gagged.[otherwise if the Bodyguard Scene has not happened]Pieter[one of], the guard,[or][stopping] stands discreetly near the back of the room.[otherwise]Pieter is here, [one of]looking around warily[or]fingering his sword hilt[or]muttering to himself[or]shifting his feet impatiently[or]keeping a close eye on you[at random].[end if]";

Instead of talking to Pieter when the Bodyguard scene has not happened:
	say "'I can't really talk while I'm on duty, Jack,' says Pieter. 'Maybe later.'";

Instead of kissing Pieter:
	if the location is the War Room, say "Pieter seems a little preoccupied at the moment.";
	otherwise say "You like Pieter, but you don't like him [i]that[r] much."

To say Jack stutteringly:
	say "[one of]Jack[or]Jacqueline[or]Jac— er, Jacqueline[at random]";

Chapter 3 - Dame Sandler

Dame Sandler is a woman, scenery, in the Jewelers shop. The initial appearance is "Dame Sandler stands near the central display case. A small, dignified smile plays on her lips." The description is "Though in her elder years, Dame Sandler is still a tall and striking woman. Her long, silver hair is pulled back in a complicated bun, and she has a habit of inspecting her merchandise through a monocle on the end of a long, slender, gold-filigreed handle. Sometimes you wonder what she's doing running a shop (even such a relatively high-class one as this); her demeanor is more that of an aristocrat, if not full royalty[first time].[paragraph break]Although you almost never come to her shop (everything in here is too expensive to buy and too well-guarded to nick), for some reason Dame Sandler has always taken a liking to you, and never runs you out when you visit, even when she has important customers to tend to. It's more of a bemused curiosity than real affection, but feels good to be on a friendly basis with such an intriguing woman[only].";

Understand "woman", "proprietor", "shopkeeper", "shop keeper", "merchant" as Dame Sandler.

Instead of kissing Dame Sandler, say "Although you're pretty sure that Dame Sandler likes you, she doesn't seem like the affectionate type."

Chapter 4 - Events on entry and exit

After going to the jewelers:
	now Dame Sandler is scenery;
	if A New Morning has not happened:
		if DS1 is unfired:
			say "[command clarification break]";
			try looking;
			now Dame Sandler is not scenery;
			update the character list;
			start conversation with Dame Sandler on DS1 instead;
	otherwise:
		if the player is still dirty:
			if DS24 is unfired:
				say "[command clarification break]";
				try looking;
				update the character list;
				start conversation with Dame Sandler on DS24 instead;
		otherwise:
			if DS31 is unfired:
				output chapter heading "Chapter 9 - Preparations";
				try looking;
				now Dame Sandler is not scenery;
				update the character list;
				start conversation with Dame Sandler on DS31 instead;
	continue the action;

Instead of going from the jewelers for the first time:
	if DS49 is fired, continue the action; [e.g. when we have the jewel ]
	if the location has not been the sewer:
		if the player is still dirty and DS14 is unfired and DS23 is unfired:
			start conversation with Dame Sandler on DS14;
		otherwise:
			continue the action;
	otherwise:
		carry out the firing activity with DS30;
		[halt the action - firing DS30 moves the player out.]

Instead of going from the jewelers [later]:
	if DS49 is fired, continue the action; [e.g. when we have the jewel ]
	if the location has been the sewer and DS30 is unfired, carry out the firing activity with DS30;
	[halt the action - firing DS30 moves the player out.]
	otherwise continue the action;

Chapter 5 - Dame Sandler's Conversation

Rule for initiating conversation with Dame Sandler when the player is still dirty:
	change the chosen opening gambit to DS19;

DS1 is a quip. The display text is "Dame Sandler's eyebrows raise slightly as you walk into her store. 'Well, hello there, Squire Jack,' she says, in her typically smooth (and the slightest bit mocking) tone. 'We haven't had the pleasure of your company in a while. You look a bit out of breath — nothing ails you, I hope?'"

DS2 is a quip. The menu text is "'No, I'm fine.'". The display text is "'No, I'm fine,' you say.[paragraph break]'I [i]see[r],' says Dame Sandler, in a tone that suggests she doesn't believe you, but doesn't have any particular objection to you lying.[paragraph break]'Well then, young man,' she continues after a brief pause, 'Was there anything in particular on your mind?'"

DS3 is a transitional quip. The menu text is "'There were mercenaries chasing me this morning.'". The display text is "'There were mercenaries chasing me all over Grubber's Market this morning,' you admit. 'I guess I'm still a bit shaken up.'"
The following quip of DS3 is DS6;

DS4 is a quip. The menu text is "'Have you seen Bobby?'". The display text is "[first time]'Have you seen Bobby around?' you ask, trying to make it sound casual.[paragraph break]'Ah, Bobby,' says Dame Sandler with a knowing smile. 'Now there's a boy who could get you into all sorts of the most... [i]interesting[r] sorts of trouble. Why yes,' she adds. 'I have seen him. He was wandering around the south end of the square, just by the alley.'[subsequently][rp] 'Bobby is in the alley just south of Lord's Market. Do pay attention, dear.'[only]"

DS5 is a transitional quip. The menu text is "'Have you heard about the mercenaries?'". The display text is "'Did you hear about the mercenaries in Grubber's Market this morning?' you ask."
The following quip of DS5 is DS6;

DS6 is a quip. The display text is "'Ah yes, I [i]did[r] hear about that nasty business in Grubber's Market,' Dame Sandler says. 'How terrible. Odd, the way they seemed so interested in you.'"

DS9 is a transitional quip. The menu text is "'What do you mean, [']odd[']?'". The display text is "'What do you mean, [']odd[']?' you ask."
The following quip of DS9 is DS10;

DS10 is a quip. The display text is "'Only that there seems to be no conceivable reason for a group of trained soldiers to be hunting for an orphan of no significant heritage. Questions spring to mind, such as: who hired them? And why now in particular? And is everything about this incident what it seems to be?'[paragraph break]Dame Sandler smiles at you. She doesn't seem to be actually asking you these questions, so much as hinting to you that she already knows the answers."

DS11 is a quip. The menu text is "'I don't know what you're talking about.'". The display text is "Suddenly Dame Sandler's knowing tone starts to seem a little bit creepy. 'I don't know what you're talking about,' you say, cautiously. 'Those mercenaries chasing me were just a coincidence.'[paragraph break]Dame Sandler smiles. 'Of course they were, my dear Jack. Of course they were.'"

DS12 is a quip. The menu text is "'Thanks! I should go.'". The display text is "'Thanks!' you say, not quite able to hide your enthusiasm. 'I should probably go find him...'[paragraph break]Dame Sandler smiles. 'Of course, Squire Jack. Don't let me keep you.'"

DS13 is a quip. The menu text is "'Not really.'". The display text is "'Nothing in particular,' you say. 'Just dropping by.'[paragraph break]'Well, your visits are always a pleasure, Jack,' Dame Sandler says. 'I do enjoy keeping my eye on you.'"

DS14 is a quip. The display text is "Just as you're about to leave, Dame Sandler calls out, 'Oh, Jack, I almost forgot to ask you... what are your thoughts on the coming Ascension?'"

DS15 is a transitional quip. The menu text is "'I don't know anything about it.'". The display text is "You shrug. 'I don't know anything about it. It's all over my head, really.'[paragraph break]'Oh, but it's really very simple,' says Dame Sandler. 'The King — or Queen, in this case — chooses her successor for the throne, usually her direct, lineal descendant, although technically she can choose anyone. Unless [i]someone else[r] has a valid claim to the throne. Then it becomes very, very complicated.'"
The following quip of DS15 is DS18;

DS16 is a transitional quip. The menu text is "'It seems like it's going to fall to the Princess.'". The display text is "'It seems like a done deal, from what I've heard,' you say. 'The Princess will take the throne, and I guess Baron Fossville if she consents to marry him.'[paragraph break]Dame Sandler shakes her head. 'It's never a [']done deal['], Jack, not until the crown is finally placed on someone's head — and sometimes not even then. The throne is the greatest prize in all the kingdom, and no one with any stake in it can afford to let it be a forgone conclusion.'"
The following quip of DS16 is DS18;

DS17 is a transitional quip. The menu text is "'Why should I care?'". The display text is "You shrug. 'What is it to me? That sort of thing is way beyond us common folk, isn't it?'[paragraph break]'Tsk, tsk, Jack,' Dame Sandler sighs. 'Such cynicism in youth is unattractive.'"
The following quip of DS17 is DS18;

DS18 is a quip. The display text is "She smiles in that knowing way of hers. 'Some friendly advice, Jack: the Ascension is the most important event you will witness in your lifetime. It would behoove you to understand more about it. You should ask around; you might be surprised at some of the answers you get.'"

DS19 is a quip. The menu text is "'Can I ask you something?'". The display text is "'Can I ask you a question, Dame Sandler?' you ask.[paragraph break]'But of course, my dear Jack,' she says."

DS20 is a quip. The menu text is "'What do you think of Baron Fossville?'". The display text is "[first time]'What do you think of Baron Fossville?' you ask.[paragraph break]Dame Sandler smirks. 'Fossville is a buffoon, of course, but he is a buffoon with the backing of some very powerful people and a large number of hired swords. He's not one to trifle with lightly. Of course, I [i]never[r] do anything lightly,' she adds, smiling enigmatically.[subsequently][rp] 'Fossville is a powerful man and not one to be trifled with, whatever your opinions of him.'[only]"

DS21 is a quip. The menu text is "'What do you think of the Queen?'". The display text is "[first time]'What do you think of the Queen?' you ask.[paragraph break]'The Queen may be a poor ruler,' Dame Sandler says, 'but she is a shrewd politician. She backs Fossville for now, but she will snip him off like a loose thread if she deems it advantageous.'[subsequently][rp] 'the Queen will support Baron Fossville as long as it's to her advantage, and not a moment longer.'[only]"

DS22 is a quip. The menu text is "'What do you think of the Princess?'". The display text is "[first time]'What do you think of the Princess?' you ask.[paragraph break]Dame Sandler shrugs. 'The Princess has as much riding on the Ascension as Fossville does,' she says. 'Can you blame her? This is her chance to become Queen of Miradania, after all. If you had that sort of opportunity, wouldn't you do anything in your power to grasp it?' And she smiles again, strangely.[subsequently][rp] 'the Princess wants her chance to be Queen. Just as I imagine any girl of her age and position would.'[only]"

DS23 is a quip. The menu text is "'What do you know about the Ascension?'". The display text is "[first time]'What do you know about the Ascension?' you ask.[paragraph break]Dame Sandler merely arches one eyebrow and smiles at you. 'Why, what do you think about it, Jack?'[subsequently]Dame Sandler waves the question away. 'I've already told you all you need to know, Jack. What you learn from that is up to you.'[only]"

The response of DS1 is { DS2, DS3 }

The response of DS2 is { DS4, DS5, DS20, DS21, DS22, DS23, DS13 }

After populating DS2:
	if HO1 is unfired or DS4 is fired: 
		remove DS4 from the current conversation, if present;
	if DS6 is fired:
		remove DS5 from the current conversation, if present;

The response of DS4 is { DS12, DS19 }

The response of DS6 is { DS9, DS11 }

The response of DS10 is { DS11 }

The response of DS11 is { DS4, DS20, DS21, DS22, DS23 }

The response of DS12 is { }

The response of DS13 is { }

The response of DS14 is { DS15, DS16, DS17 }

After firing DS14:
	now DS23 is fired;

The response of DS18 is { DS4, DS5, DS20, DS21, DS22 }

DS18 is clustered with DS11, DS19, DS20, DS21, DS22, DS23.

After populating a quip that is clustered with DS19:
	if HO1 is unfired or DS4 is fired:
		remove DS4 from the current conversation, if present;
	if DS6 is fired:
		remove DS5 from the current conversation, if present;
	[ if DS23 is fired:
		remove DS23 from the current conversation, if present; ]

The response of DS19 is { DS4, DS5, DS20, DS21, DS22, DS23 };

The response of DS20 is { DS4, DS5, DS21, DS22, DS23 }.

The response of DS21 is { DS4, DS5, DS20, DS22, DS23 }.

The response of DS22 is { DS4, DS5, DS20, DS21, DS23 }.

The response of DS23 is { DS15, DS16, DS17 }

First after populating DS23:
	if DS23 was fired,
		change the current conversation to { DS4, DS5, DS20, DS21, DS22 };

Section 2 - Later conversation

DS24 is a quip. The display text is "Dame Sandler looks up as you burst into her shop, and her face only betrays a hint of surprise before settling into a carefully expressionless pose. 'Why Jack,' she says, 'you look an absolute fright. Whatever is the matter?'"

DS25 is a transitional quip. The menu text is "'No, nothing's wrong.'". The display text is "'Um, no, nothing's wrong,' you stammer, knowing you must sound ridiculous looking as bedraggled and filthy as you do.[paragraph break]Dame Sandler looks at you for a moment, then nods. 'Exactly so,' she says, quietly. 'Nothing is wrong.'"
The following quip is DS29.

DS26 is a quip. The menu text is "'Have you seen Bobby?'". The display text is "'Dame Sandler,' you plead, 'have you seen Bobby or heard anything about where he might be?'[paragraph break]She glances quickly at Pieter, who frowns. Then she turns back to you. 'I only know that he was taken up by Fossville's men,' she says, calmly. 'But I imagine you already know that. I... don't know what they plan to do with him. I'm sorry.'[paragraph break]Seeing the look of desperation on your face, Dame Sandler continues, 'Did Bobby give you any... instructions... before you were separated?"

DS27 is a transitional quip. The menu text is "'No.'". The display text is "Something about her question triggers your suspicions, and you pause. 'No,' you say, forcing your voice to sound level and calm. 'He didn't give me any instructions.'[paragraph break]Dame Sandler nods slowly. 'Well then, Squire Jack, you appear to be out of options. The wisest course of action probably would be to find a place to hide... and stay there.'"
The following quip is DS29.

DS28 is a transitional quip. The menu text is "'He told me to break into Baron Fossville's house and steal a letter.'". The display text is "'Yes,' you say nervously, unsure of whether you should be trusting her with this information or not. 'He told me there's an alley behind the shops on Commerce Street where I can climb up to the rooftops; then I'm to break into Baron Fossville's house, and steal a... a letter.'[paragraph break]Dame Sandler's eyebrows arch and she inclines her head slightly. 'A [i]letter[r],' she murmurs. 'How interesting. Well then, Squire Jack, I suppose you had better get to it. The shops on Commerce Street most likely will have back doors leading to the alley; one of them is sure to grant you access if you ask.'"
The following quip is DS29.

DS29 is a quip. The display text is "She stands up and moves over to the door. 'And now, my dear boy, I'm afraid I must ask you to leave. It is not safe for you here, and it is not safe for me to have you here. Please forgive me for being so abrupt; I hope that soon I will be able to explain more to you. But for now, good-bye and good luck. Take care, Jack.'[paragraph break]And she gently pushes you out the door, and locks it behind you."

DS30 is a quip. The display text is "As you are about to leave, Dame Sandler comes close to you and lays her fingertips on your arm. 'Jack,' she says softly, 'I'm afraid I must ask you to not come back, at least not for a little while. It is not safe for you here, and it is not safe for me to have you here. Please forgive me for being so abrupt; I hope that soon I will be able to explain more to you. But for now, good-bye and good luck. Take care, Jack.'[paragraph break]And she gently pushes you out the door, and locks it behind you."

The response of DS24 is { DS25, DS26 }.

The response of DS25 is { DS27, DS28 }.

The response of DS26 is { DS27, DS28 }.

DS29 is clustered with DS30.

After firing a quip clustered with DS29:
	now every quip clustered with DS29 is fired;
	now the current conversationalist is the player;
	move the player to Lords Market;
	update the character list;

Part 4 - Moneylender (Chorus Brothers)

Chapter 1 - Description and scenery

The moneylenders shop is a store, southeast of Lords Market. The description is "The inside of this shop is dark — dark wood paneling on the walls; deep, wine-dark carpeting on the floor; even a pair of huge, leather armchairs over in one corner. The back half of the room is hidden behind a wall of reinforced wood, with a wide window cut through the center." The printed name is "Chorus Brothers".

West of the moneylenders shop is Lords Market.

Outside from the moneylenders shop is Lords Market.

Instead of facing outside while in the Moneylenders Shop, try facing west.
The dead-end description of the Moneylenders Shop is "You don't see any other exits besides the door to the west, to Lord's Market."

Some armchairs are a supporter, and scenery, in the Moneylenders Shop. The description is "The armchairs are overstuffed, upholstered in rich, dark leather." Understand "armchair", "chair", "chairs", "leather", "dark", "rich", "overstuffed" as the armchairs. The printed name is "chairs".

Instead of entering the armchairs:
	say "They look very comfortable, but it would feel awkward sitting in the chair and yelling across the room at the counter.";

The moneylenders carpet is scenery, in the Moneylenders Shop. The printed name is "carpet". Understand "carpeting" as the carpet.

The tellers window is scenery, in the Moneylenders Shop. The description of the tellers window is "The window forms a sort of counter, with an ornately scrolled, brass grating that opens to the inside, allowing customers to speak with the proprietors on the other side. In fact, it appears that the only way to get service here is by standing up at the counter, so it's not exactly clear what the fancy chairs are for." Understand "counter", "wall", "scrolled", "brass", "grate", "grating", "reinforced", "wood" as the tellers window. The printed name of the tellers window is "window".

Chapter 2 - Event on entering

After going to the moneylenders shop:
	if the player is still dirty or CB1 is fired begin;
		now Chorus Brothers are not scenery;
		continue the action;
	otherwise;
		now Chorus Brothers are scenery;
		say "[command clarification break]";
		try looking;
		now Chorus Brothers are not scenery;
		start conversation with the Chorus Brothers on CB1;
		update the character list;
	end if;

Section 1 - Leaving

After going from the Moneylenders Shop when the Chorus Brothers are respectful:
	say "'Come back soon,' intones one of the brothers as you leave. 'Your business is...'[paragraph break]'...always welcome,' finishes the other.";
	continue the action;

Chapter 3 - Chorus Brothers

The Chorus Brothers are people, plural-named, scenery, in the moneylenders shop. The initial appearance is "The Chorus Brothers hover behind their window, staring at you." The description is "It's in some ways fortunate that you're not on a first-name basis with these fellows, because you'd never be able to tell them apart. Their faces are utterly identical, from the tops of their bald, cadaverous heads down to the black, dusty collars of their coats." Understand "brother", "proprietor", "shopkeeper", "shop keeper", "merchant", "men", "proprietors", "shop keepers", "shopkeepers", "merchants" as the Chorus Brothers.

The Chorus Brothers can be respectful or disrespectful. The Chorus Brothers are disrespectful.

Instead of talking to the Chorus Brothers when the player is still dirty:
	say "Both brothers raise their eyebrows (one left, the other right) simultaneously. After a moment, one of them says, 'Forgive us. We are...'[paragraph break]'...[no line break][i]extremely[r] busy,' the other continues. 'No time for...'[paragraph break]'...[no line break]idle chit-chat with penniless ragamuffins,' finishes the first.";

Rule for initiating conversation with the Chorus Brothers:
	change the chosen opening gambit to CB2;

Instead of kissing the Chorus Brothers, say "It would probably be best if you kept your relationship with the moneylenders strictly professional."

Chapter 4 - Conversation

CB1 is a quip. The display text is "A moment after you enter, two faces suddenly appear at the window, each seeming to slide smoothly in from either side. They look at you with identical expressions of curiosity and a sort of mild, almost professional contempt.[paragraph break]'Good afternoon,' says one.[paragraph break]'May we help you?' asks the other."

CB2 is a quip. The menu text is "'I have a question.'". The display text is "'I have a question,' you tell the two brothers.[paragraph break]They nod in perfect, synchronized unison. 'Allow us to answer...' says one.[paragraph break]'...to the best of our ability,' finishes the other."

CB3 is a transitional quip. The menu text is "'I need a loan.'". The display text is "[one of]'I, er... I need a loan,' you say, not at all sure how this sort of business is conducted. 'You know, money.'[or]'I need a loan,' you say again.[stopping]". The following quip of CB3 is CB4.

CB4 is a quip. The display text is "[first time]A pair of condescending smiles, perfectly identical and simultaneous, appear on the brothers['] faces.[paragraph break]'My dear,' begins one, 'you certainly [i]look[r] respectable, but without...'[paragraph break]'...some form of credentials...' adds the other.[paragraph break]'...our hands are tied,' finishes the first.[subsequently]'Like we said,' they say, 'you need credentials.'[only]"

CB5 is a quip. The menu text is "'What kind of credentials do you need?'". The display text is "[first time]'What counts as credentials?' you ask. 'What do you need to see?'[paragraph break]'Ah, well then,' says one, 'some form of collateral perhaps, or someone to act as surety...'[paragraph break]'...even a letter, establishing your social standing. In short, anything that...'[paragraph break]'...would help assure us that we were making...'[paragraph break]'...a sound [i]investment[r].'[subsequently]'As we told you before,' they say, 'there are many different sorts. But a letter would be most ideal.'[only]"

CB6 is a quip. The display text is "The two brothers lean forward eagerly, as though they've been waiting for you to ask.[paragraph break]'We would be [i]delighted[r] to extend to you...'[paragraph break]'...our most prestigious line of credit!'[paragraph break]Over the next few minutes they hand you dozens of sheets of paper covered in dense, tiny script, full of bizarre words like [i]amortization[r], [i]dividends[r], [i]compound interest[r], [i]escrow[r]. Pieter tries to read some of it, with his brow furrowed up and his lips moving slightly; eventually he shakes his head and mutters, 'Just sign all of them.' You dutifully write your name where the brothers show you.[paragraph break]After it's all done, they sweep all the papers into a slim leather folder, and place a small purse of coins on the counter. 'That's [i]it?[r]' complains Pieter, but when you tug open the drawstring, you see the sunlight gleam of gold.[paragraph break]'When you have secured your position, please remember us...' says one brother.[paragraph break]'...not merely as your creditors...'[paragraph break]'...but as your [i]friends[r].'[paragraph break]And both brothers smile in unison, revealing two sets of wide, white teeth."

CB7 is a quip. The menu text is "'What was Fossville doing here yesterday?'". The display text is "[first time]Suddenly you remember running into Baron Fossville — literally — yesterday morning. 'I saw Baron Fossville leaving here yesterday,' you mention casually. 'He seemed pretty upset. Was there some problem with [i]his[r] credentials?'[paragraph break]One of the brothers makes a show of inspecting his fingernails while the other clears his throat. 'We have the utmost...' one of them begins.[paragraph break]'...[no line break][i]respect[r] for Baron Fossville,' continues the other. 'However, he presents certain...'[paragraph break]'...[no line break][i]liabilities[r]. Let us leave it at that.'[subsequently]They shake their heads. 'We're afraid it would not be proper for us to discuss our other clients['] financial matters.'[only]"

CB8 is a quip. The menu text is "'Will this letter work?'". The display text is "'Will this work as credentials?' you ask, holding out the Duke's letter."

The response of CB1 is { CB3, CB5, CB7 }.

The response of CB2 is { CB3, CB5, CB7, CB8 }.

The response of CB4 is { CB5, CB8 }.

The response of CB5 is { CB2, CB8 }.

The response of CB7 is { CB2 }.

CB1 is clustered with CB2, CB4, CB5, CB6 and CB7.

After populating a quip that is clustered with CB1:
	if DS49 is unfired:
		remove CB3 from the current conversation, if present;
	if CB6 is fired:
		remove CB3 from the current conversation, if present;
	if CB4 is unfired or CB6 is fired:
		remove CB5 from the current conversation, if present;
	if CB5 is unfired or CB6 is fired or the player does not have the secret letter:
		remove CB8 from the current conversation, if present;

After firing CB3:
	if the Chorus Brothers are disrespectful:
		change the following quip of CB3 to CB4;
	otherwise:
		change the following quip of CB3 to CB6;

After firing CB6:
	now the player carries the purse;

After firing CB8:
	try showing the secret letter to the Chorus Brothers;

Chapter 5 - Negotiations

A persuasion rule for asking the Chorus Brothers to try giving the purse to the player:
	if the player is still dirty and the Chorus Brothers are disrespectful begin;
		say "The brother on the left sniffs. The brother on the right ever-so-slightly twists up the corner of his mouth.[paragraph break]'I think...' says one.[paragraph break]'...[no line break][i]not[r],' says the other.";
		persuasion fails;
	end if;

A persuasion rule for asking the Chorus Brothers to try giving the purse to the player:
	if the player is clean and the Chorus Brothers are disrespectful begin;
		say "A pair of sympathetic smiles, perfectly identical and simultaneous, appear on the brothers['] faces.[paragraph break]'My dear,' begins one, 'you certainly [i]look[r] respectable, but without...'[paragraph break]'...some form of credit history...' adds the other.[paragraph break]'...our hands are tied,' finishes the first.[paragraph break]'Some form of collateral, someone to act as surety, some proof that you are capable of paying your debts...'[paragraph break]'...would help assure us that we were making...'[paragraph break]'...a sound [i]investment[r].'";
		persuasion fails;
	end if;

Instead of giving the secret letter to the Chorus Brothers:
	try showing the secret letter to the Chorus Brothers.

Instead of showing the secret letter to the Chorus Brothers when the Chorus Brothers are disrespectful:
	now the Chorus Brothers are respectful;
	say "The brothers both reach out, one with his right hand, the other with his left, and delicately take the letter from you. Their eyes flicker back and forth in unison as they scan the words. When they reach the end, four identical eyebrows shoot up.
'How very...'[paragraph break]'...[no line break][i]interesting[r]. It would seem you are a personage of more...'[paragraph break]'...[no line break][i]importance[r] than first we assumed.'[paragraph break]They hand the letter back to you, smiling, and ask in chorus, 'How may we help you?'[paragraph break]";
	if CB3 is fired:
		start conversation with the Chorus Brothers on CB3;
	otherwise:
		start conversation with the Chorus Brothers on CB2;

Instead of showing the secret letter to the Chorus Brothers when the Chorus Brothers are respectful:
	say "'No need to show us...' begins one brother.[paragraph break]'...again, Miss,' continues the other. 'We never forget...'[paragraph break]'...a customer.'";

A persuasion rule for asking the Chorus Brothers to try giving the purse to the player:
	if the player is clean and the Chorus Brothers are respectful begin;
		[this duplicates CB6]
		now the player carries the purse;
		say "The two brothers lean forward eagerly, as though they've been waiting for you to ask.[paragraph break]'We would be [i]delighted[r] to extend to you...'[paragraph break]'...our most prestigious line of credit!'[paragraph break]Over the next few minutes they hand you dozens of sheets of paper covered in dense, tiny script, full of bizarre words like [i]amortization[r] and [i]escrow[r]. Pieter tries to read some of it, with his brow furrowed up and his lips moving slightly; eventually he shakes his head and mutters, 'Just sign all of them.' You dutifully write your name where the brothers show you.[paragraph break]After it's all done, they sweep all the papers into a slim leather folder, and place a small purse of coins in your hand. 'That's [i]it?[r]' complains Pieter, but when you tug open the drawstring, you see the sunlight gleam of gold.[paragraph break]'When you have secured your position, please remember us...' says one brother.[paragraph break]'...not merely as your creditors...'[paragraph break]'...but as your [i]friends[r].'[paragraph break]And both brothers smile in unison, revealing two sets of wide, white teeth.";
		persuasion fails;
	end if;

The purse is a closed, openable container. The Chorus Brothers carry the purse. The description of the purse is "The sack is about the size of your fist. It's very heavy. Even a little bit of gold goes a long way in Toresal, and there's more than just a little bit of gold in here. The drawstring is [if closed]closed[otherwise]open[end if]." The printed name of the purse is "coin purse".

Understand "bag", "sack" as the purse. Understand "coins", "gold", "money", "of" as the purse when the purse is closed.

Understand "loan" as the purse when the Chorus Brothers hold the purse. 

The drawstring is part of the purse. Understand "drawstrings", "string", "strings" as the drawstring. Instead of examining the drawstring, try examining the purse.

Instead of pulling the drawstring when the purse is open, try closing the purse.
Instead of pulling the drawstring when the purse is closed, try opening the purse.

After opening or closing the purse:
	say "You pull the drawstring [if open]open[otherwise]closed[end if].";

Instead of giving the purse to someone: say "Very generous of you, but you're going to need the money for yourself.";

Instead of dropping the purse:
	say "It wouldn't be very wise to leave a bag full of gold coins sitting unguarded. Better keep them on your person for now.";

Instead of putting the purse on something:
	try dropping the purse;

Instead of inserting the purse into something:
	try dropping the purse;

Inside the purse are some gold coins. The description of the gold coins is "Each coin has the Great Seal of the Kingdom of Miradania on one side; the face of some old, dead king that you've never heard of on the other. You've never seen real gold before, and nothing you've ever seen is quite like it. The coins seem to shine with an inner light.";

Understand "money", "great", "seal", "seal of", "of miradania", "kingdom", "kingdom of", "coin" as the gold coins.

Instead of taking the gold coins:
	say "The bag is the safest place for them, for now. There's no need to take them out unless you're planning to purchase something.";

Book 6 - Meeting Bobby

Part 1 - Back Alley & Bobby

Chapter 1 - Description

A Back Alley is south of Lords Market. The description is "A little-known shortcut between Lord's Market and the less affluent neighborhoods of the city's south end. A narrow street to the southwest leads to Maiden House, or you can return to the market square to the north."

Understand "shortcut", "short cut" as the Back Alley.

Lords Market is outside of the Back Alley.

Instead of facing outside while in the Back Alley, try facing north.

The distant description of the Back Alley is "The back alley threads its way between tall buildings to the [if location is Lords Market]south[otherwise]northeast[end if]."

The dead-end description of the Back Alley is "Tall buildings surround you in every direction except north and southwest."

Section 1 - Event on Entry

After going to the Back Alley for the first time:
	say "[command clarification break]";
	try looking;
	now Bobby is not scenery;
	say "At the end of the alley, a group of young urchins — none older than their eighth year — are clustered around something. As you draw closer, you see Bobby crouched in the midst of them. He's holding his arm out, his fist loosely clenched in front of a dirty-cheeked child of six.[paragraph break]'Go on,' says Bobby. 'Go on, take a look.'[paragraph break]The little boy, his eyes wide and solemn, reaches out and pries Bobby's fingers open. His hand is empty. The little boy looks dismayed.[paragraph break]'Ah, but what's this?' asks Bobby. And he reaches forward and draws a copper coin from behind the boy's grimy ear. There is a collective gasp and squeals of delight from the rest of the children. The little boy's eyes look ready to fall out of his head.[paragraph break]Bobby presses the coin into the little boy's hand, then passes out more coins to the others. 'Here,' he says, 'keep it safe in your pocket. Spend it on bread, not candy.' The children take their prizes and scatter.[paragraph break]";
	update the character list;
	start conversation with Bobby on BO1;

Chapter 2 - Bobby

Section 1 - Description, in various circumstances

Bobby is a man, scenery, in the Back Alley. The description is "[bobby's description]."

To say bobby's description:
	if Bobby's Hanging is happening:
		say "He's still wearing the mud- and blood-stained clothes he wore last night, when you snuck into Lord's Keep. His hands are bound behind his back, and a black cloth bag is pulled down over his head";
	otherwise if Bobby's Adventure is happening:
		if the location is not in Greater Lord's Keep:
			say "Bobby's eyes dart towards every shadow, and he pauses often to listen for some noise you can't hear. But when he glances at you, his eyes still twinkle with his happy-go-lucky spirit, and he smirks as though nothing could possibly go wrong";
		otherwise if the location is the Guardhouse:
			say "Bobby is listening intently. His eyes never waver from the guardhouse door";
		otherwise:
			say "Bobby looks more nervous than ever, constantly checking over his shoulder and making little gestures at you to keep moving";
	otherwise if the Skirmish is happening:
		say "Bobby looks none the worse for wear after his hanging. He's still smiling that wild, lopsided smile, even as he parries the Baron's savage blows";
	otherwise:
		say "Bobby is a few years older than you, a tall, broad-shouldered lad with tousled black hair and a rakish grin. He's an orphan, like you, and like you he makes the streets of the city his home and lives by his wits. Unlike you, he doesn't have the safety net of Maiden House to fall back on, but somehow he always seems to do well for himself.[paragraph break]You also happen to think that he's devastatingly handsome, although you would never admit that under the worst torture the King's interrogators could devise. He doesn't even know that you're really a girl, and you're not about to tell him, because if he knew he would certainly stop inviting you along on his exploits";

Instead of kissing Bobby:
	if the location is the War Room, say "Bobby is a little busy right now!";
	otherwise say "You've thought about it. You've even dreamed about it. But the thought of actually doing it makes you go all wobbly-kneed."

Instead of attacking or kicking Bobby:
	say "You'd never dream of hurting Bobby!";

Section 2 - Initial appearance, in various circumstances

The initial appearance of Bobby is "[Bobby initial appearance]"

To say Bobby initial appearance:
	if the player is in the back alley:
		say "Bobby leans carelessly against the alley wall, watching you with a slight, wry smile. ";
	otherwise if the player is in Market Square:
		say "Bobby is standing in the middle of Grubber's Market, under the shadow of the center post. ";
	otherwise if creeping to Lord's Keep is happening:
		say "Bobby is here, waiting for you. ";
	otherwise if the Skirmish is happening:
		say "Bobby and Baron Fossville are surrounded by a whirl of flashing steel as they slash and hack at each other. ";
	otherwise if it is night:
		say "Bobby is here, waiting for you. ";

Section 3 - Meeting Bobby, and Bobby's plan

Meeting Bobby is a scene. Meeting Bobby begins when Bobby is nearby. Meeting Bobby ends when Bobby is off-stage.

Every turn when Bobby is nearby in the back alley and the player is not on the move:
	add ATMOS_Bobby to the dramatic possibilities, if absent;

ATMOS_Bobby is a dramatic event. The display text is "[one of]Bobby whistles a few notes of some popular mummer's tune[or]Bobby smirks at you in that way he has, that makes you want to smile and blush and run and hide, all at the same time[or]You realize you're staring, and quickly find something else to look at. Goddesses, but he's handsome[or]Bobby rakes his fingers back through his tangled hair[at random]."

Instead of going from the Back Alley when BO8 is unfired:
	start conversation with Bobby on BO7;

Instead of going from the Back Alley when BO8 is fired and BO10 is unfired:
	say "Bobby catches your arm. 'C'mon, Jack, tell me: are you in or out?'";

Chapter 3 - Conversation

Rule for initiating conversation with Bobby:
	change the chosen opening gambit to BO11;

Last before choosing a dramatic possibility when the current conversationalist is Bobby:
	change the current tension to unimportant;

BO1 is a quip. The display text is "Bobby stands and stretches, then sees you. The corner of his mouth goes up in that lop-sided grin you know so well. '[']Lo, Jack,' he says. 'Been looking for you. What's happening?'"

BO2 is a quip. The menu text is "'Nothing much.'". The display text is "'Oh, nothing much,' you say. 'Just, you know... doing stuff. Around the city. Staying tricky.'[paragraph break]'Huh,' says Bobby, nodding vaguely.[paragraph break][i]Goddesses[r], you think to yourself, [i]I am such a dim![no line break][r]"

BO3 is a quip. The menu text is "'I was chased by some mercenaries.'". The display text is "'Oh, nothing much,' you say, nonchalantly. 'Unless you count being chased by armed mercenaries through Grubber's Market.'[paragraph break]Bobby grins and snaps his fingers. 'Oh, I [i]heard[r] about that! Extra tricky! Wish I could've been there to help; that would've been a time!'[paragraph break]You look away, wondering if it's obvious, the way you're totally glowing right now."

BO4 is a quip. The menu text is "'What's with the urchins?'". The display text is "'So what’s with all the urchins I saw you with?' you ask.[paragraph break]'Oh, I just got lucky this morning,' Bobby says. 'Some dim was striding around Lord's Market with his purse strings too loose, so I stopped and tightened them for him. By the time I'd finished, he'd already left. So I thought I'd share the wealth.' He sighs and looks uncharacteristically glum. 'They have so little...' he murmurs, then shakes his head. 'Anyway.'"

BO5 is a quip. The menu text is "'Were you at Maiden House?'". The display text is "'I don't usually see you on this side of town,' you remark. 'Were you visiting Maiden House?' You try to make it sound casual, but inside you're practically holding your breath. Because if Bobby was visiting Maiden House, theat means he was coming to visit [i]you[r].[paragraph break]'Nah, not for me, that place,' says Bobby. 'You know how the Widows are, they'd just mother-hen me to death. I can't be cooped up; there's too many things to do, places to see. Trinkets to nick.' He laughs, then glances at you. 'But you know, it's all right for someone younger, like you. Good to have a shelter, somewhere you can run back to when things get sticky.'[paragraph break]You feel your neck growing hot. 'I don't need to [i]run[r] anywhere,' you mutter. You [i]hate[r] it when he brings up your age. He's only two years older than you, anyway![paragraph break]Bobby looks embarrassed. 'Er, yeah, yeah, I know that, Jack,' he says. 'Sorry, mate. I didn't mean anything by it. Let's change the subject.'"

BO6 is a transitional quip. The menu text is "'What did you want to see me for?'". The display text is "'So, I heard you were looking for me,' you say. 'What'd you want to tell me?'[paragraph break]Bobby grins.". The following quip is BO8.

BO7 is a transitional quip. The display text is "'Wait, wait,' Bobby says, catching your arms. 'There was something I wanted to tell you.'". The following quip is BO8.

BO8 is a quip. The display text is "He makes a show of looking carefully over one shoulder, then the other, then leans close to whisper. 'I found this new place, right? It's a big secret, extra tricky. I was thinking of doing a bit of exploring there later tonight. You want to come with?' He smiles that knowing half-smile of his, the one that always means he's about to do something that will be very fun, or land you both in a pile of trouble, or — the usual case — both."

BO9 is a quip. The menu text is "'Where is this place?'". The display text is "'I don't know,' you say. 'Where is this place?'[paragraph break]Bobby rolls his eyes. 'Well, that's kind of the point. If I told you, it would ruin the secret. C'mon, Jack, are you in or out?'"

BO10 is a quip. The menu text is "'I'm in.'". The display text is "Could you [i]even[r] consider saying no? 'Okay, let's do it,' you say, your heart thumping hard. 'I'm in.'[paragraph break]'You're a good bloke, Jack,' says Bobby. 'Now, you don't want the Widows worrying about you, so head on home for now. Once it gets dark, sneak back out and meet me in the Grubber's Market. Right? Good. See you later, Jack.' He claps his hand on your shoulder, then turns and strides, all grace and overconfidence, out of the alley."

BO11 is a quip. The menu text is "'Can I ask you a question?'". The display text is "'Hey, Bobby, you mind if I ask you a question?' you ask.[paragraph break]'Sure,' says Bobby."

BO12 is a quip. The menu text is "'Do you know anything about the mercenaries?'". The display text is "[first time]'Do you know anything about those mercenaries that were chasing me?' you ask.[paragraph break]Bobby shakes his head. 'Sorry, Jack. I was all the way on the other side of town when it happened. Word on the street is, they were looking for someone important. What that has to do with you, I have no idea.'[paragraph break]'Oh, [i]thanks[r],' you say, punching him in the arm.[subsequently][rp] 'I don't know anything.'[only]"

BO14 is a quip. The menu text is "'What do you think about Baron Fossville?'". The display text is "[first time]'What do you think about Baron Fossville?' you ask.[paragraph break]Bobby shrugs. 'I try not to. He's a little man who cares more about his power than about the people in this city. But then, most of them are like that, aren't they?'[subsequently][rp] 'he's just another politician.'[only]"

BO15 is a quip. The menu text is "'What do you know about the Ascension'". The display text is "[first time]'What do you know about this Ascension thing?' you ask.[paragraph break]Bobby looks at you incredulously. 'Ascension? Well, I know a little...' He takes a deep breath, as though gathering his thoughts. 'Okay, so, the King died without naming any heir, right? I don't know why he didn't just name his own daughter, but he didn't. And that makes things interesting, because now that means the people get a say. Well... [i]important people[r], mainly, the Dukes and Barons and whatnot, but even the common blokes like you and me get represented by a People's Regent who gets a bit of a say. And the thing is, the Queen's never been all that popular. She wants to keep the succession in the bloodline, and all the Dukes and Barons want someone who's going to keep the treasuries filled, and the People's Regent is going to be pulling for a good-looking hero type who looks out for the poor, or pretends to at least. And there's no obvious good choice. So now basically everything's up for grabs.'[paragraph break]He turns red when he notices you staring at him. 'Or at least, uh, that's what I hear.'[subsequently]'Come on, Jack,' Bobby says, rolling his eyes, 'I already explained it once. Any more and my throat will dry out.'[only]"

The response of BO1 is { BO2, BO3 }

The response of BO2 is { BO6, BO11 }

The response of BO3 is { BO6, BO11 }

The response of BO4 is { BO6, BO11 }

The response of BO5 is { BO6, BO11 }

The response of BO8 is { BO9, BO10 }

The response of BO9 is { BO10 }

The response of BO10 is { }

After firing BO10:
	remove Bobby from play;
	update the character list;

The response of BO11 is { BO6, BO12, BO14, BO15 }

The response of BO12 is { BO6, BO14, BO15 }

The response of BO14 is { BO6, BO12, BO15 }

The response of BO15 is { BO6, BO12, BO14 }

Part 3 - Entrance to Maiden House

Chapter 1 - Description 
[ *** Code changed for Demo Version ***]

[Purchase Full Version is southwest of the back alley. The description is "You have reached the end of the playable demonstration of [i]Jack Toresal and The Secret Letter[r]. Who is Baron Fossville? What is Bobby's plan? Why are the mercenaries chasing you? What is the Princess doing in Toresal? To find out the answer to all of these questions, you can purchase the full, downloadable, version for Windows XP, Windows Vista, Windows 7, or Intel Mac OS X by surfing to www.textfyre.com."]

Entrance to Maiden House is southwest of the back alley. The description is "The alley dead-ends in a small courtyard surrounded by old, run-down buildings. You can return to the main part of the city to the northeast."

Understand "narrow", "street", "dead end", "cul de sac", "courtyard", "small", "outside" as Entrance to Maiden House.

Instead of facing southwest while in Entrance to Maiden House, say "Looks like you could just scoot around the corner of the southern building into a tiny spur off the main alley."
Instead of facing south while in Entrance to Maiden House, try examining the wooden door.

Rule for printing the distant description of the Hallway while in Entrance to Maiden House:
	say "You'll have to to go inside to get a better look at that. ";

The distant description of Entrance to Maiden House is "A darkened courtyard lies to [the quick best route from the location to Entrance to Maiden House].".

The dead-end description of Entrance to Maiden House is "[description of run-down buildings]".

Some run-down buildings are scenery, in Entrance to Maiden House. The description is "The buildings in this part of town have seen better days." Understand "old", "run down", "tenements" as the run-down buildings.

Entrance to Maiden House overlooks Hallway.

Chapter 2 - The door

A wooden door is an open door, south of the entrance to Maiden House and north of the Hallway. Understand "front", "entrance" as the wooden door. Understand "southern", "south", "to", "building", "tenement", "maiden", "house", "lock" as the wooden door when the location is Entrance to Maiden House.

The wooden door is inside from the entrance to Maiden House, and outside from the Hallway.

Rule for writing a paragraph about the wooden door:
	if the location is Entrance to Maiden House, say "[one of]A battered wooden door provides entrance into the southern building. There's no sign, but you know it well nonetheless — this is the door to Maiden House, your 'home'[or]The door to Maiden House is [if the wooden door is open]open[otherwise]closed[end if][stopping].";
	otherwise say "The front door is [if the wooden door is open]open.[otherwise]closed." 

The description of the wooden door is "There's nothing on the outside to distinguish Maiden House from any of the other run-down tenements in this neighborhood, but you recognize it well. You grew up in this place, and it is filled with memories both good and bad. Well, okay, mostly bad. But you make do with what you have."

Instead of examining the wooden door when the location is the Hallway:
	if Raid on Maiden House has not happened, say "The front door is closed and locked — and it will stay locked until sunup. The Widows give you a good bit of latitude generally, but curfew is one of the few rules they never, ever bend.";
	otherwise say "The front door is closed and locked."

Instead of lockpicking the wooden door:
	if the wooden door is locked:
		if A New Morning has not happened:
			say "You don't have anything to pick the lock with[if Theresa is in the location]. Besides, you couldn't do it while Widow Theresa is here[end if].";
		otherwise:
			say "You don't need to go out that way.";
	otherwise:
		say "The front door isn't locked[if the location is the Hallway]. But you don't need to go out that way anyway[end if].";

Book 5 - Maiden House

Part 1 - Hallway

Chapter 1 - Description

The Hallway is a room. The description is "The main hallway runs through the center of Maiden House, with several doorways leading to rooms on either side. To the southeast and southwest are the laundry and privy, respectively; to the northeast lies the kitchen; and to the northwest is the dormitory where you and the other orphans sleep."

West of the hallway is nowhere. East of the hallway is nowhere.

Understand "main", "center", "Maiden", "House", "hall", "corridor", "passage", "passageway" as the Hallway.

The distant description of the Hallway is "The main hallway lies just to the [quick best route from the location to the Hallway]."

Rule for printing the distant description of the Entrance to Maiden House while in the Hallway:
	say "You'll have to go outside to get a better look at the courtyard outside Maiden House. ";

Instead of facing outside while in the Hallway, try facing north.

The Hallway overlooks Entrance to Maiden House.

After going to the Hallway from the Dormitory when the orphans are asleep:
	fire TRIG_TIPTOE;
	continue the action;

TRIG_TIPTOE is a trigger.

Rule for firing unfired TRIG_TIPTOE:
	say "You tiptoe out into the hallway. Widow Theresa is nowhere in sight.";

When Bobby's Hanging ends:
	remove the orphans from play;
	move Widow Shannon to the Laundry;

Chapter 2 - Widow Theresa

Section 1 - Description

Widow Theresa is a woman, scenery, portable, in the Hallway. The description is "Widow Theresa is a harsh, sour-faced woman, with suspicious eyes and a mannish, angular jaw that she thrusts out when she is angry, which is nearly always. You gather that she was very poor before coming to Maiden House, and she harbors a bitter dislike for anyone whom she perceives as unjustly privileged. For reasons that persistently escape you, that includes you. Staying out of her way is generally the best tactic." The initial appearance of Widow Theresa is "Widow Theresa stalks the hallway, her eye out for trouble."

Understand "maiden" as Widow Theresa.

The long brass key is held by Widow Theresa. The description is "That's the key to the front door. Widow Theresa keeps it on a ribbon around her neck constantly." The printed name is "brass key". Understand "front door key" as the long brass key.

The ribbon is held by Widow Theresa. The description is "It's just a long piece of ribbon."

Instead of doing something other than examining when the noun is the long brass key or the noun is the ribbon:
	say "Widow Theresa has her eye on you constantly, and she won't let you get near [the noun].";

Instead of kissing Widow Theresa:
	say "That would surely be about as pleasant as hugging a cactus.";

Instead of attacking or kicking Widow Theresa:
	say "As much as you'd love to, that would only compound your problems.";

Instead of opening the wooden door when in the hallway and Widow Theresa is in the hallway:
	say "'Don't trouble yourself, Jacqueline,' sneers Widow Theresa. 'It's after curfew, and the door will stay locked.'";

Rule for implicitly opening the wooden door when in the hallway and Widow Theresa is in the hallway:
	try opening the wooden door;

An unlocking rule for the wooden door when in the hallway and Bobby's Adventure has not happened:
	say "It's no use. Widow Theresa keeps the only key, and in fourteen years you've never managed to steal it from her.";

Section 2 - Conversation

TH1 is a quip. The display text is "Widow Theresa turns to you. 'You're filthy,' she says, 'though no more than usual. I realize it's too much to ask you to actually do any of your chores, but you might at least like to wash yourself, if only in consideration for the next poor girl who has to wash your sheets.'"

TH2 is a quip. The menu text is "'And how are you this evening?'". The display text is "'And how are [i]you[r] this fine evening, Widow Theresa?' you say, dropping an exaggerated curtsey.[paragraph break]Her face twitches. 'I'll not be baited by the simple sarcasm of a spoiled brat,' she says. 'Go find someone else to bother.'"

TH3 is a quip. The menu text is "'You can't lock me in!'". The display text is "'You can't lock me in here!' you shout. 'I'm supposed to meet Bobby tonight!'[paragraph break]Widow Theresa somehow manages to look both scandalized at the idea of you keeping a midnight tryst and pleased that she has managed to ruin your entire night already. (It's a look she practices often.) 'That Bobby is a bad influence,' she declares. 'You shouldn't be running amok with older boys in the first place, much less a delinquent like him.'"

TH4 is a quip. The menu text is "'Give me a break, I was chased by mercenaries this morning.'". The display text is "'Come on, Widow Theresa,' you say, 'give me a break. I'm tired; I was chased all over Grubber's Market by armed soldiers this morning.'[paragraph break]She smirks. 'An obvious lie,' she says, 'not up to your usual standards. And even if it were true, they were probably hired by someone you'd stolen from, and it would have served you right if you were caught.'"

TH5 is a quip. The menu text is "'Where are Fiona and Shannon?'". The display text is "You sigh. The last thing you need is to get drawn into another argument with Widow Theresa. 'Where are Widow Fiona and Widow Shannon?' you ask.[paragraph break]'Fiona is in the kitchen, washing the dishes,' she says, 'and Shannon is in the laundry room. Which is to say, they are actually doing something [i]productive[r], so you'll avoid them at all costs, I'm sure.'"

TH6 is a quip. The menu text is "'Let me ask you something.'". The display text is "'Let me ask you something,' you say.[paragraph break]Widow Theresa does not answer at first, but after a moment she glares at you sharply and says, '[i]What?[r]'"

TH7 is a quip. The menu text is "'What do you think of Widow Fiona?'". The display text is "[first time]'What do you think of Widow Fiona?' you ask.[paragraph break]Theresa looks at you for a long time before answering. 'I think,' she says, finally, 'that playing favorites does an injustice to the other children here, but in your case it's even worse because of how little you deserve it. Fiona thinks you're something special. But I think you're just a spoiled brat who thinks she can get away with whatever she likes. And I think that one day, you're going to be caught with your fingers in the wrong pocket, and then you will be very, very sorry.' She sneers and adds in a low voice, 'And I think I'm looking forward to that day.'[subsequently][rp] 'she treats you far better than you deserve.'[only]"

TH8 is a quip. The menu text is "'What do you think of Widow Shannon?'". The display text is "[first time]'What do you think of Widow Shannon?' you ask.[paragraph break]'Shannon?' laughs Theresa. 'I'd say she's too dim to catch another husband, but I'm not sure she even wants one. Don't worry, she'll still be here long after you're dead in a gutter somewhere.'[subsequently][rp] 'she's too dim to be anything but happy here.'[only]"

TH9 is a quip. The menu text is "'What do you think of Baron Fossville?'". The display text is "[first time]'What do you think of Baron Fossville?' you ask.[paragraph break]Widow Theresa gets a wistful look in her eye. 'Baron Fossville is a great man. A great and... handsome man.' Her look hardens. 'It pains me to hear his name coming from your dirty little liar's mouth.'[subsequently][rp] 'I don't want to talk about him with you.'[only]"

TH10 is a quip. The menu text is "'What do you know about the Ascension?'". The display text is "[first time]'What do you know about the Ascension?' you ask.[paragraph break]Widow Theresa sniffs. 'Royal politics are for people of breeding and intellect, which puts it far above your concern, I'm sure.'[subsequently][rp] 'it's not something you would understand.'[only]"

TH11 is a quip. The menu text is "'What do you know about my parents?'". The display text is "'Widow Theresa,' you say suddenly, 'what do you know about my parents?' You're not quite sure why you asked the question; it's certainly never occurred to you to ask her before.[paragraph break]But as soon as the words leave your mouth, you realize your mistake. Widow Theresa would never let such a rich opportunity to hurt you pass unseized.[paragraph break]'I don't know much,' she says, 'but I can make an educated guess. No doubt your father was some vagrant, probably on the run from the law. Too shifty to stay around for [i]you[r], at any rate. As for your mother,' she goes on with a cruel smile, 'no one knows who she was, nor is anyone likely to ever learn. But women like that all tend to end up in the same place in the end, don't they?'[paragraph break]Your face is alternating burning hot and icy cold, and your fists are clenched to keep your hands from shaking. 'Well. Thanks,' you manage to whisper."

The response of TH1 is { TH2, TH3, TH4 }

The response of TH2 is { TH5, TH6 }

The response of TH3 is { TH5, TH6 }

The response of TH4 is { TH5, TH6 }

The response of TH5 is { TH6 }

The response of TH6 is { TH5, TH7, TH8, TH9, TH10, TH11 }

The response of TH7 is { TH5, TH8, TH9, TH10, TH11 }

The response of TH8 is { TH5, TH7, TH9, TH10, TH11 }

The response of TH9 is { TH5, TH7, TH8, TH10, TH11 }

The response of TH10 is { TH5, TH7, TH8, TH9, TH11 }

The response of TH11 is { }

After populating a quip (called q) when the current conversationalist is Widow Theresa and TH5 is fired:
	remove TH5 from the current conversation, if present;

After talking to Widow Theresa:
	say "One conversation with Theresa per day is about as much as you can stand.";

Chapter 3 - Curfew

Section 1 - Event on entry

After going to the Hallway for the first time:
	select theme "MaidenHouse";
	set the scene for night;
	say "You slip quietly through the front door, into the familiar smells of cooking grease and laundry soap; the familiar sight of shabby, warped floorboards lit by dim, flickering tallow lamps. It's nothing fancy, but it's home, and you're familiar enough with the plight of most of Toresal's orphans to know that you're lucky to have it.";
	try looking;
	update the character list;
	now the wooden door is closed;
	now the wooden door is locked;
	now Widow Theresa is not scenery;
	say "Widow Theresa, who probably has been lurking here all day, just waiting for you to return, springs out from her corner and slams the front door shut. 'The evening churchbells are ringing, Jacqueline,' she tells you, her mouth set in a satisfied little line. 'Well past the time when any respectable young lady should be out on the streets.' She locks the door with a long brass key that she wears on a ribbon around her neck.[paragraph break]";
	start conversation with Widow Theresa on TH1;

Section 2 - The onset of night

To set the scene for night:
	change the circadia to nighttime;
	now Market Square is mapped west of Commerce Street;
	now Commerce Street is mapped east of Market Square;
	move the bottom of the center post to Market Square;
	move Bobby to Market Square; [for first journey to Lord's Keep]

To set the scene for day:
	change the circadia to daylight;
	now Grubbers Market Eastern Junction is mapped west of Commerce Street;
	now Commerce Street is mapped east of Grubbers Market Eastern Junction;
	move the bottom of the center post to Grubbers Market Center Post;
	now Lords Road is mapped north of Grubbers Market Northwest Junction;

Part 2 - Dormitory

Chapter 1 - Description

The Dormitory is northwest of the Hallway. "A long room lined with rough bunks, where the children sleep and, if they are too young to run about the city on their own, play. The main hallway lies southeast."

East of the dormitory is the hallway.

Understand "dorm", "dormitories", "dorms" as the dormitory. The distant description of the dormitory is "The dormitory is northwest of here."

Before going when in the dormitory:
	if the bunk bed supports the player:
		try silently getting off the bunk bed;
	if the noun is east or the noun is outside:
		try going southeast instead;

Before exiting when in the dormitory:
	if the bunk bed supports the player:
		continue the action;
	try going southeast instead;

Chapter 2 - Bunk bed

A bunk bed is a supporter, scenery, enterable, in the dormitory. The printed name is "bunk". The description is "The bunks are spare and functional, straw mattresses on wooden frames. It's clean straw, but that's about all that can be said for them."

Instead of sleeping in the presence of the bunk bed when the player is not on the bunk bed:
	try entering the bunk bed;

Instead of going by name to the bunk bed when in the dormitory [ie > GO TO BED]:
	try sleeping;

First instead of boarding when the noun is the bunk bed and Widow Theresa is in the Hallway:
	say "Grumbling, you climb into your bunk and make yourself as comfortable as you can on the lumpy mattress. The other children eventually lie down and fall quiet, and Widow Fiona stops in to blow out the light. Soon all you can hear are the soft snores of children and the occasional creaking of a floorboard. You are determined to keep your eyes open until you are sure Theresa has left the hallway, but soon your muscles, tense from the day's chase, are unknotting... your breath is coming slower, heavier...[paragraph break]You snap awake, heart pounding. How long were you asleep? It seems like it couldn't have been that long, but the thought of missing your rendez-vous with Bobby is agonizing.[paragraph break]As quietly as you can, you slip out of bed.";
	remove Widow Theresa from play;
	remove Widow Shannon from play;
	remove Widow Fiona from play;
	remove the dishes from play;
	remove dirty underpants from play;
	now the orphans are asleep;

Instead of entering the bunk bed when Bobby's Adventure has not happened:
	say "You're not even [i]close[r] to being tired. You couldn't possibly go to sleep until you've seen what Bobby wanted to show you."

Understand "your", "my", "rough", "straw", "mattress", "wood", "wooden", "frame", "spare", "functional" as the bunk bed.

Does the player mean doing something to the bunk bed: it is likely.

Chapter 3 - Other bunks

Some other bunks are scenery, in the dormitory. Understand "beds", "bunk beds", "straw", "mattresses", "wood", "wooden", "frames", "rough", "spare", "functional" as the other bunks.

The description of the other bunks is "The bunks are spare and functional, straw mattresses on wooden frames. It's clean straw, but that's about all that can be said for them."

Instead of entering the other bunks:
	try entering the bunk bed;

Chapter 4 - Other orphans

Some orphans are a person in the Dormitory. The orphans can be asleep or awake. The orphans are awake. Understand "orphan", "child", "children", "childish", "kids", "kid", "urchins", "urchin", "young", "younger", "boys", "girls", "boy", "girl" as the orphans. The description of the orphans is "The kids are mostly younger than you. Some of them you saw earlier with Bobby. They sit up in their beds, whispering secrets to each other, showing off the childish treasures they found during the day: pebbles, pennies, bits of string. It almost makes you wish you could be that young again. Your life was so much less complicated then." The initial appearance of the orphans is "Most of the other orphans are in here, playing and talking before bedtime."

Instead of talking to the awake orphans:
	say "You're not really in the mood for a chat. As you've gotten older, and the other kids your age have drifted away from the orphanage, you find you have little in common with the younger children.";

Instead of talking to the asleep orphans:
	try waking the orphans;

Rule for writing a paragraph about the asleep orphans:
	say "The other children are all sound asleep in their bunks.";

Instead of waking the asleep orphans:
	say "You don't want to wake them. They'll just cause a commotion, and then you'll never be able to sneak out.";

Part 5 - Kitchens

Chapter 1 - Description and scenery

The Kitchen is northeast of the Hallway. "A low-ceilinged room with a fire pit at one end for cooking, a basin at the other end for washing, and rows upon rows of pots and pans hanging from the rafters. The long table at the side of the room is where the children take their meals. You can leave to the southwest."

West of the dormitory is the hallway.

Before going when in the kitchen:
	if the noun is west or the noun is outside:
		try going southwest instead;

Before exiting when in the kitchen:
	try going southwest instead;

Understand "kitchens" as the kitchen. The distant description of the kitchen is "The kitchen is northeast of here."

The table is a supporter, and scenery, in the Kitchen. The description of the table is "The table seats about twenty hungry orphans, with room at the end for two or three scowling widows to enforce discipline. You rarely eat here, yourself. Older kids are encouraged to scrounge around the city for their meals rather than rely on the kitchen, but the Widows make sure that no one goes hungry."

[ "The pots and pans are scenery, here" annoyingly gives us a pots object and a pans object.
]
Some pans are scenery, in the Kitchen. The description of the pans is "Dozens of them, and you are familiar with each and every one. Scullery duty is not exactly your favorite day of the week." The printed name is "pots and pans". Understand "pots", "pot", "pan" as the pans.

Instead of taking the pans:
	say "You're not about to cook anything and they don't need cleaning, so you might as well leave the pots and pans alone.";

The fire pit is scenery, in the Kitchen. The description of the fire pit is "The logs are down to glowing embers now." Understand "fireplace", "log", "logs", "ember", "embers" as the fire pit.

Understand "rafter", "rafters", "low", "ceiling", "ceilinged", "dark", "rough", "wood", "wooden" as the backdrop-ceiling when the location is the Kitchen.

Rule for printing the description of the backdrop-ceiling when in the Kitchen:
	say "The rafters are made of dark, rough wood.";

Instead of doing something other than examining the backdrop-ceiling when in the Kitchen:
	say "The ceiling is low, but it's still higher than you can reach.";

Instead of washing the player in the Kitchen:
	say "The sink is too small to bathe in.";

Section 1 - Basin and plates

The basin is a container and scenery, in the Kitchen. The description is "The basin is filled with grayish, greasy water. It's not clear whether it's making the dishes more or less dirty."

In the basin is the dishwater. It is singular-named, fixed in place. Instead of examining the dishwater, try examining the basin instead. Understand "water" as the dishwater. The indefinite article of the dishwater is "some".

Before drinking, taking, touching, searching, or tasting the dishwater, say "No thanks; that stuff looks nasty." instead.

Some dishes are scenery, in the Kitchen. The description is "The kids did a number on them tonight. Fiona is about halfway through the stack." Understand "plate", "plates", "stack" and "stack of dishes/plates" as the dishes.

The dishes can be clean or dirty. The dishes are dirty.

Section 2 - Actions related to doing the washing up

Washing is an action applying to one touchable thing and requiring light.
Understand the command "clean" as something new.
Understand the command "clear" as something new.
Understand the command "scrub" as something new.
Understand the command "shine" as something new.
Understand the command "wipe" as something new.

Understand "wash [something]" as washing.
Understand "wash up [something]" as washing.
Understand "clean [something]" as washing.
Understand "dry [dishes]" as washing.

Understand the command "clear" as "clean".
Understand the command "scrub" as "clean".
Understand the command "shine" as "clean".
Understand the command "wipe" as "clean".

Check washing (this is the block washing ourselves rule):
	if the noun is the player,
		say "Nothing here to wash in." instead.

Check washing (this is the block washing other people rule):
	if the noun is a person,
		say "I don't suppose [the noun] would care for that." instead.

Check washing (this is the block washing rule):
	say "Washing is your least favorite chore. Now would be a great time to skip it." instead.

Section 3 - Kitchen sink play

Instead of taking the dishes:
	try washing the dishes;

Instead of washing the dishes when Widow Fiona is concerned for our safety:
	say "You try to avoid the subject by busying yourself with the dishes, but Fiona won't have it. 'Jacqueline,' she says, pulling you gently away from the basin, 'look at me. Tell me about these mercenaries.'";

Instead of washing the dishes:
	if the dishes are dirty and FI13 is unfired begin;
		now the dishes are clean;
		say "You take one of the wet dishes from the basin, dry it, and place it on the 'clean' stack.[paragraph break]Fiona smiles at you. 'Thank you, dear.'";
	otherwise;
		now the dishes are clean;
		say "You continue helping with the dishes, drying and stacking.";
	end if;

Chapter 2 - Widow Fiona

Section 1 - Description

Widow Fiona is a woman, in the Kitchen. "[if the location is Kitchen]Widow Fiona is here, washing the last of the dinner dishes[otherwise]Widow Fiona hovers nearby, looking concerned[end if]."

Understand "maiden" as Widow Fiona.

The description is "[if the location is Kitchen]Fiona is a plump and genial woman, always quick with a smile or a bit of advice. The strange thing is, her advice always seems to make good sense, though sometimes you are loathe to admit it. She likes you and occasionally does you a good turn when you're in trouble with Widow Theresa.[otherwise]Widow Fiona's eyes are red and swollen, as if she's been weeping, and a heavy sadness weighs on her usually cheerful features.[end if]"

Instead of kissing Widow Fiona:
	if the location is the Kitchen, say "[one of]You give Widow Fiona a hug and a quick kiss on the cheek. 'Oh, thank you, dear,' she says, hugging you back.[or]'Enough, girl!' laughs Widow Fiona. 'I have dishes to wash!'[stopping]";
	otherwise say "[one of]Fiona wraps her arms around you and hugs you tightly. Her cheeks are wet against yours. 'Oh, child,' she whispers. 'You've been like a daughter to me, you know.'[or]Fiona puts her hands on your shoulders and holds you away. 'I love you, too, Jacqueline,' she says, 'but we haven't time for that now.'[stopping]";

Every turn when Widow Fiona is nearby and the location is the Kitchen and the player is not on the move and a random chance of 1 in 5 succeeds:
	if Widow Fiona is at the sink begin;
		say "[one of]Widow Fiona grunts as she leans over the basin's edge to dunk another gravy-encrusted plate into the soapy water[or]Widow Fiona wipes a plate dry on her apron and stacks it on the counter[or]Widow Fiona inspects a plate closely, [one of]and scrapes at a bit of stuck food with her thumbnail[or]then stacks it on the counter[or]then dunks it back into the basin[at random][or]Widow Fiona gives you a broad, rosy-cheeked smile[at random].";
	end if;

After going to the Kitchen for the first time:
	say "[command clarification break]";
	try looking;
	update the character list;
	start conversation with Widow Fiona on FI1;

Definition: Widow Fiona is concerned for our safety:
	if FI3 is fired and FI7 is not fired, yes;
	no;

Definition: Widow Fiona is at the sink:
	if Widow Fiona is concerned for our safety, no;
	yes;

Instead of going from the Kitchen when Widow Fiona is concerned for our safety:
	say "Fiona holds your arm gently but firmly. 'You're not going anywhere, young lady, until we're done talking about these people who attacked you.'";

After going from the Kitchen when Widow Fiona is in the Kitchen and Widow Fiona is not concerned for our safety:
	say "'Good night, Widow Fiona,' you say as you leave.[paragraph break]'Good night, Jacqueline,' she says.";
	continue the action;

Section 2 - Conversation

Rule for initiating conversation with Widow Fiona when the dishes are dirty and FI13 is unfired:
	change the chosen opening gambit to FI14;

Rule for initiating conversation with Widow Fiona when the dishes are clean or FI13 is fired:
	change the chosen opening gambit to FI15;

FI1 is a quip. The display text is "Fiona looks over her shoulder as you enter the kitchen. 'Good evening, Jacqueline. And how has the day treated you?'"

FI2 is a quip. The menu text is "'Oh, fine.'". The display text is "'Oh, it's been fine,' you say. 'Not much going on.'[paragraph break]'Mm-hmm,' Fiona says, distractedly."

FI3 is a quip. The menu text is "'I was chased by mercenaries.'". The display text is "'It's been kind of crazy, to tell the truth,' you say. 'There were armed mercenaries chasing me through Grubber's Market this morning.'[paragraph break]'[i]What?[no line break][r]' Widow Fiona drops her dishes and rushes over to you. 'Oh, Goddesses, Jacqueline, are you hurt?'"

FI4 is a quip. The menu text is "'Nah, I'm fine.'". The display text is "'Nah, it was tricky,' you say, feigning an insouciance that you don't really feel. 'Just a lot of people running around and yelling about nothing.'[paragraph break]Widow Fiona doesn't seem convinced, however. She checks you all over for injuries, muttering '[i]Goddesses... so close... could have been... all because of...[no line break][r]' Finally, after she's satisfied that you aren't secretly bleeding to death, she looks you directly in the eye. 'Listen, Jacqueline, I'm glad you're all right, but I think it would be a good idea to stay off the streets for a while. Especially Grubber's Market. We'll send Shannon if we need any groceries.'"

FI5 is a quip. The menu text is "'A few bumps and scratches is all.'". The display text is "'A few bruises and scratches as I was getting away,' you say. 'Nothing serious.' You pause, then add, 'I was... pretty scared, though.' Fiona is probably the only other person in the whole city to whom you'd admit that.[paragraph break]She pulls you to her and hugs you tight. 'It's all right now,' she whispers, 'you're safe now.' Then, after she checks you all over to for injuries and satisfies herself that you aren't secretly bleeding to death, she looks you directly in the eye. 'Listen, Jacqueline, I think it would be a good idea to stay off the streets for a while. Especially Grubber's Market. We'll send Shannon if we need any groceries.'"

FI6 is a quip. The menu text is "'Why are you so worried?'". The display text is "Even though she's trying to hide it, you've never seen Widow Fiona so upset. 'Why are you so worried?' you ask. 'Do you know something about those men?'[paragraph break]She looks away, goes back to the basin and starts clattering dishes around. 'No... no. I'm just so relieved that you're not hurt. Please, Jacqueline, promise me you'll stay away from Grubber's Market.'"

FI7 is a quip. The menu text is "'Yes ma'am.'". The display text is "'Yes, ma'am, I will,' you say. You know it's a promise you can't possibly keep, but you also know it will comfort Fiona to hear it.[paragraph break]She takes a deep breath. 'Thank you, Jacqueline.'"

FI8 is a transitional quip. The menu text is "'I saw Bobby on the way home.'". The display text is "'Oh, it was a [i]great[r] day,' you tell her. 'I saw Bobby on the way home just now; we talked for a while.'[paragraph break]Fiona smiles. 'Bobby's a good boy,' she says. She usually adds, [i]and cute, too[r], but today she spares you the teasing. 'Rough around the edges, maybe, but he has a good heart, and he knows this city like no one else. You should listen to what he says... you might learn something important.'[paragraph break]Suddenly you remember the impossible situation that Widow Theresa has put you in. 'Oh, but Widow Theresa has locked me in for the night!' you cry. 'I'm supposed to meet Bobby later on!'". The following quip is FI10.

FI9 is a transitional quip. The menu text is "'Widow Theresa locked me in!'". The display text is "'It's been a [i]horrible[r] day,' you snap. 'Widow Theresa's locked the doors, but I promised Bobby I'd meet him later tonight! Please make her open the doors for me, Fiona, you [i]have[r] to!'" The following quip is FI10.

FI10 is a quip. The display text is "Fiona sighs. 'Now, Jacqueline, you know the curfew rules here. You can stay out all night if you like — although I wish you wouldn't — but once you're in and the evening bells ring, you're in. And I can't let you out after Widow Theresa's locked the doors for the night; it would undermine her authority.'[paragraph break]All you can do is seethe. Just for once, it would be nice if the things Widow Theresa says about Fiona playing favorites with you were actually [i]true[r]."

FI11 is a quip. The display text is "Widow Fiona leans away from the basin, stretching her back. 'How about you help me with some of these dishes, Jacqueline?' she asks."

FI12 is a quip. The menu text is "'I'll pass.'". The display text is "'Uh, I think I'll pass,' you say.[paragraph break]	She sighs. 'Well, since it's not technically your chore night, I won't press the issue. But it wouldn't hurt you to be more helpful around here.'"

FI13 is a quip. The menu text is "'Okay.'". The display text is "'Okay,' you say. You start stacking the dry dishes as Fiona washes them.[paragraph break]'Thank you. You are a dear one,' she says."

FI14 is a quip. The display text is "'Can I ask you a question?' you ask.[paragraph break]'Not unless you're willing to help me with the dishes,' Fiona says, firmly."

FI15 is a quip. The menu text is "'Can I ask you a question?'". The display text is "'Do you mind if I ask you a question?' you ask.[paragraph break]'Of course not, dear,' Widow Fiona says."

FI16 is a quip. The menu text is "'What do you think of Widow Theresa?'". The display text is "[first time]'What do you think of Widow Theresa?' you ask, trying to keep the angry edge out of your voice.[paragraph break]'Widow Theresa can be prickly at times,' Fiona says, keeping her voice carefully neutral, 'but you must be patient with her. She's had a difficult life. Her late husband left her with a heavy debt, and she's never been able to forgive him for that.'[subsequently][rp] 'Theresa has a temper, but she has a good heart.'[only]"

FI17 is a quip. The menu text is "'What do you think of Widow Shannon?'". The display text is "[first time]'What do you think of Widow Shannon?' you ask.[paragraph break]'Oh, Shannon's a sweet one,' Fiona says. 'Don't ever make the mistake of thinking her simple. She's here because she loves the children, and that's all she needs.'[subsequently][rp] 'Shannon is a kind woman and we're blessed to have her.'[only]"

FI18 is a quip. The menu text is "'What do you think of Baron Fossville?'". The display text is "[first time]'What do you think of Baron Fossville?' you ask.[paragraph break]Widow Fiona's expression hardens, which is not something that happens often. 'Fossville is a very ambitious man,' she says. 'And ambitious men tend to be dangerous to those around them, whether they mean to be or not. You shouldn't concern yourself with him, Jacqueline.'[subsequently][rp] 'you needn't concern yourself with Baron Fossville.'[only]"

FI19 is a quip. The menu text is "'What do you know about the Ascension?'". The display text is "[first time]'What do you know about the Ascension?' you ask.[paragraph break]Fiona gives you an odd look. 'The Ascension?' she asks. 'What makes you ask?'[paragraph break]You shrug. 'It's just that I keep hearing about it. Everyone's talking about it, and everyone has a different opinion about it, and it seems terribly important, but I still don't understand it very much.'[paragraph break]She thinks for a moment, then says, 'The reason why everyone is talking about the Ascension is because it's the first time we've had one in a long time. There's no clear successor to the throne, and so everyone is in a tizzy about who has a solid claim and who will be chosen. Probably we'll have a People's Regent, and when the time comes I'll make my voice heard along with everyone else... but the truth is, Jacqueline, is that even with the Regent, people like you and me have very little say in what happens.'[subsequently][rp] 'it's a very important ceremony that only very important people have a say in.'[only]"

FI20 is a quip. The menu text is "'What do you know about my parents?'". The display text is "[first time]'Can you tell me anything about my parents?' you ask.[paragraph break]Fiona looks at you, surprised. 'What brought that question on?'[paragraph break]'I don't really know,' you say, which for once is the unvarnished truth. 'I've just been thinking about them lately.'[paragraph break]She dries her hands off and puts a hand on your shoulder. 'We've told you before, your mother came to us and stayed only long enough to leave you on our doorstep. No one knows where she is now. As for your father...' She hesitates. 'The only thing I can tell you with absolute certainty is that he loved you very much. Your parents weren't bad people, Jacqueline, they were put in a hard circumstance and had to make a terrible sacrifice. You were never unwanted. Just... unfortunate.'[subsequently][rp] 'your parents loved you, Jacqueline.'[only]"

The response of FI1 is { FI2, FI3, FI8, FI9 }

The response of FI2 is { FI15 }

The response of FI3 is { FI4, FI5 }

The response of FI4 is { FI6, FI7 }

The response of FI5 is { FI6, FI7 }

The response of FI6 is { FI7 }

The response of FI7 is { FI15 }

The response of FI10 is { FI15 }

The response of FI11 is { FI13, FI12 }

The response of FI12 is { }

The response of FI13 is { FI15 }

The response of FI14 is { }

The response of FI15 is { FI16, FI17, FI18, FI19, FI20 }

The response of FI16 is { FI17, FI18, FI19, FI20 }

The response of FI17 is { FI16, FI18, FI19, FI20 }

The response of FI18 is { FI16, FI17, FI19, FI20 }

The response of FI19 is { FI16, FI17, FI18, FI20 }

The response of FI20 is { FI16, FI17, FI18, FI19 }

Before firing FI13:
	now the dishes are clean;

FI2 is clustered with FI7, FI10.

After populating a quip that is clustered with FI2 when the dishes are dirty:
	carry out the firing activity with FI11;
	carry out the populating activity with FI11;

Part 6 - Privy

Chapter 1 - Description 

The Privy is southwest of the Hallway. "Not much more than a narrow closet, with a hole in the floor and a bench over it. At least it's more private than an alley."

East of the privy is the hallway.

Instead of going from the Hallway to the Privy when Widow Theresa is in the Hallway:
	say "[one of]'Not so fast,' snaps Widow Theresa, blocking your path. 'Don't think I don't know what you're up to.'[paragraph break]'But I really have to go to the bathroom!' you protest, knowing that it would do no good even if it were true.[paragraph break]Theresa smirks. 'Then you can do it with the door open, while I stand and watch. No? Well then, I guess you can hold it until the morning. Until then, you're not going anywhere but to bed.'[or]It's no use; Widow Theresa is watching you like a hawk. You'll probably have to wait until everyone goes to sleep before making your escape.[stopping]";

Before going when in the privy:
	if the noun is outside:
		try entering the privy window instead;

Before exiting when in the privy:
	if the player is not on the bench:
		try entering the privy window instead;

Understand "narrow", "closet", "bathroom", "water closet", "privies", "restroom", "toilet", "loo", "rest room", "WC", "W C" as the privy. 

The privy overlooks Behind Maiden House.

Instead of facing west while in the Privy, try searching the privy window.
Instead of facing outside while in the Privy, try searching the privy window.

The distant description of the Privy is "[if the location is Hallway]The privy lies to the southwest[otherwise]You can't see much through the grimy window[end if]."

Does the player mean doing something with the Hallway when the location is the Privy: it is likely.

Instead of smelling the privy:
	try examining the hole;

Chapter 2 - Privy hole

A hole is a container and scenery, in the privy. The description is "The less said about that, the better."

Instead of searching, touching or smelling the hole, try examining the hole.

Before inserting something into the hole, say "You never know; you might want it back someday. Besides, the last thing you want is to accidentally clog that hole up." instead.

Before entering the hole, say "It's way too small for you to fit into. Thank the Goddesses." instead.

The bench is an enterable supporter, scenery, in the privy. The description is "The bench allows you to use the privy without a lot of ungainly squatting." Understand "privy" as the bench.

Instead of looking under the bench:
	try examining the hole;

After standing on or entering the bench:
	say "You get onto the bench. It wobbles under your feet, and you grab the window sill to steady yourself.";

Instead of sitting on the bench:
	say "You don't feel the urge right now.";

Understand "use [bench]" as sitting on.

Chapter 3 - Window

The privy window is an open door, privately-named, west of the privy, east of Behind Maiden House. "A narrow window over the bench provides some ventilation.". The description is "[if the location is the Privy]The window has been stuck halfway open for as long as you can remember. No one's ever bothered to fix it, because no matter how cold it gets, no one who's been in that privy would ever consider closing the window for even an instant. There's a bit less than two hand-widths of clearance between the pane and the sill — just enough for you to scrape through.[otherwise]The privy window is at ground level on the outside. Through the dirty glass, you can look down into the hole in the bathroom floor. Not that you'd want to.[end if]"

Understand "window", "privy window", "narrow", "stuck", "half", "halfway", "half way", "open", "closed", "shut", "pane", "sill", "clearance", "glass", "dirty" as the privy window.

Outside from the Privy is the privy window. Inside from Behind Maiden House is the privy window.

Instead of opening or closing or raising or lowering the privy window:
	let the verb be "close";
	if we are opening,
		let the verb be "open";
	say "[if the location is the privy]The window is adamantly stuck halfway; the pane won't budge an inch, up or down.[otherwise]You can't [verb] the window from the outside.[end if]"

Instead of going through the window when in Behind Maiden House:
	say "Although you've done so many times in the past, at the moment there's no need to sneak [i]into[r] Maiden House.";

Instead of searching the privy window, say "Through the window's grime-smeared glass, you can see [if the location is the Privy]the privy room inside Maiden House[otherwise]a tiny alleyway behind Maiden House[end if]."

After going to the privy:
	now the privy window is not scenery;
	continue the action;

After going to Behind Maiden House:
	now the privy window is scenery;
	continue the action;

Section 1 - Escaping from Maiden House

Instead of climbing the privy window, try entering the noun.

Before going through the privy window when in the privy:
	if the player is on the bench begin;
		move the player to the privy, without printing a room description;
	end if;

After going through the privy window from the privy when Leaving Maiden House has not happened:
	say "[first time]You sneak a furtive glance back at the hallway[if the player was not on the bench], then step up onto the bench. It wobbles under your feet, and you grab the window sill to steady yourself.[paragraph break]Then,[otherwise], then,[end if] holding your breath to keep from making a sound, you pull yourself up and through. The window is at ground level outside, and you wriggle out onto the slick cobblestones.[paragraph break]You're free! Of course, the widows will find out you're gone soon enough — Maiden House is not a very big place, after all. But you've snuck out before, and they know you can take care of yourself. At worst, you'll pull extra laundry duty for a few days.[only]";
	select theme "EmptyGrubbers";
	continue the action;

Part 7 - Laundry

Chapter 1 - Description and scenery

The Laundry is southeast of the Hallway. "The air in here is filled with steam and the sharp stink of lye soap. Moisture trickles down the walls, and puddles of water splash underfoot. The main hallway lies northwest."

West of the laundry is the hallway.

Before going when in the laundry:
	if the noun is outside:
		try going northwest instead;

Before exiting when in the laundry:
	try going northwest instead;

Understand "washing", "wash" as the laundry.

The distant description of the Laundry is "[if the location is the Hallway]The laundry lies southeast of here[otherwise]The laundry is just outside[end if]."

Instead of smelling the laundry, say "The harsh soap the widows like to use has a strong smell that burns your nostrils."

After going to the Laundry for the first time:
	say "[command clarification break]";
	try looking;
	update the character list;
	start conversation with Widow Shannon on SH1;

After going from the Laundry when Widow Shannon is in the Laundry and Raid on Maiden House has not happened:
	say "'See you later, Widow Shannon,' you say as you leave.[paragraph break]Widow Shannon smiles at you over her shoulder. 'Take care, Jacqueline.'";
	continue the action;

Chapter 2 - Scenery

Section 1 - Washtub and contents

A huge wooden washtub is a container, scenery, in the Laundry. The description is "The wooden tub is huge enough to bathe in (and it has been used for that purpose on occasion). It's slopping over with soap-clouded water, pouring steam into the humid air." Understand "wooden", "wood", "tub", "laundry" as the washtub.

Instead of entering the washtub, say "You don't need a bath right now. And you [i]definitely[r] don't need a bath in [i]that[r]."

Instead of washing the player in the presence of the washtub:
	try entering the washtub.

Some washing water is inside the washtub. It is fixed in place. The description of the water is "The water is gray and coated with greasy-looking suds." The printed name is "water".

Instead of taking, drinking or tasting the washing water:
	say "The grimy underpants of two dozen hygiene-challenged urchins have been steeping in that water all day.";

Some dirty underpants are inside the washtub. The description is "The clothes look as gray as the water they're floating in. It's hard to see how they ever actually get clean." The printed name of the underpants is "dirty laundry". Understand "wet", "clothes", "laundry", "gray", "grey", "nappies" as the underpants.

Instead of doing something other than examining the underpants:
	say "Better leave the clothes alone. They're in that tub for a reason.";

Section 2 - Moisture

Some moisture is scenery, in the Laundry. Understand "puddle", "puddles", "steam", "damp", "humid", "air" as the moisture. The description of the moisture is "The constant steam makes everything in this room damp."

Instead of smelling the moisture, try smelling the location.

Section 3 - Clothes line

The clothesline is scenery in the Laundry. The description is "Dripping clothes hang from a simple clothesline stretched across the room." Understand "line", "rope", "stretched", "clothes line" as the clothesline.

The hanging clothes are scenery in the Laundry. The description is "The hanging clothes still look grungy, but at least they smell a bit nicer." Understand "dripping", "grungy", "clean", "shirt", "shirts", "shifts", "shift", "trousers", "pants", "sock", "socks" as the hanging clothes.

Instead of taking or stealing the hanging clothes:
	say "If you took those clothes, someone in Maiden House would end up having to go naked. You can't have that on your conscience.";

Chapter 2 - Widow Shannon

Section 1 - Description

Widow Shannon is a woman, in the Laundry. The initial appearance is "[if Raid on Maiden House has happened]Widow Shannon is here, looking scared but determined.[otherwise if the location of Widow Shannon is the Laundry]Widow Shannon is bent over the big washtub in the corner, scrubbing out the toddlers['] nappies.[otherwise]Widow Shannon stands nearby.[end if]".

Understand "maiden", "woman" as Widow Shannon.

The description is "[if Shannon's Company has happened]Shannon is the youngest of the widows in Maiden House. She is plain-looking, and you've always thought of her as not particularly bright... but she is resourceful and devoted, and your respect for her has grown. She seems energetic and happy to be on this adventure with you.[otherwise if Raid on Maiden House has happened]Although Shannon's face is pale, there is a look of resolve in her features that you have never seen there before. This is her protective side: she will not let anyone — not Fossville, not his armed thugs — harm one of [i]her[r] children.[otherwise]Shannon is the youngest of the widows in Maiden House. She is plain-looking, none too bright, and, to all appearances, utterly uninterested in ever remarrying, which is perhaps just as well. She devotes herself to the children, particularly the toddlers, and does most of the cleaning and cooking for Maiden House.[end if]"

Instead of kissing Widow Shannon:
	if Raid on Maiden House has not happened, say "Widow Shannon laughs as you give her a hug and a kiss on the cheek. 'Thank you, Jacqueline,' she says. 'I'd return it, but my hands are full of soapsuds!'";
	otherwise say "[one of]Widow Shannon hugs you tightly. 'You're a brave girl, Jacqueline,' she whispers. 'Now let's get going.'[or]'No time for that now, Jacqueline,' Shannon says.[stopping]";

Every turn when in the Laundry and Widow Shannon is nearby and the player is not on the move and Raid on Maiden House has not happened and a random chance of 1 in 4 succeeds:
	[ See Section 10.2.1.2, "Making the secret closet secret" ]
	add ATMOS_Shannon_Laundry to the dramatic possibilities, if absent;

ATMOS_Shannon_Laundry is a dramatic event. The display text is "[one of]Widow Shannon picks up a thick wooden stick and jabs at the clothes soaking in the tub, stirring them about[or]Widow Shannon steps back from the tub for a moment, wiping the sweat off her brow[or]Widow Shannon pulls a [one of]shirt[or]shift[or]pair of trousers[or]diaper[or]sock[at random] from the grayish water, wrings it out, and hangs it up on the line behind her[or]Widow Shannon gives you a friendly nod[at random]."

Section 2 - Conversation

Rule for initiating conversation with Widow Shannon:
	change the chosen opening gambit to SH7;

SH1 is a quip. The display text is "Widow Shannon looks over her shoulder and gives you a friendly nod. 'Hello, Miss Jacqueline,' she says."

SH2 is a quip. The menu text is "'Hello, Widow Shannon.'". The display text is "'Hello, Widow Shannon,' you say, without much enthusiasm. 'How's the, uh... washing today?'[paragraph break]Shannon smiles. 'Hard work builds patience and character, Jacqueline,' she says.[paragraph break]'So I've heard,' you mutter, rolling your eyes when Shannon looks away."

SH3 is a quip. The menu text is "'You need a hand with the washing?'". The display text is "'You need a hand with the washing?' you ask.[paragraph break]'No, no,' Shannon says happily. 'It's my day for it, anyway.'[paragraph break]You breathe an inward sigh of relief. You were trying to be polite, but not [i]that[r] polite."

SH4 is a quip. The menu text is "'Can you unlock the front door?'". The display text is "'Widow Shannon,' you say, picking your words carefully, 'I was wondering... do you think you could open the front door for me, so I can go out? I know the bells have rung, but just this once? Please?'[paragraph break]Shannon frowns. 'Widow Theresa's got the only key,' she says. 'What do you want to go out for?'"

SH5 is a quip. The menu text is "'Oh, nothing.'". The display text is "'Oh, nothing,' you say quickly. 'Just to... do some things... that I forgot.'[paragraph break]Shannon gives you a suspicious but good-humored look. 'I'm sorry, Miss Jacqueline, but curfew is curfew. Widow Theresa wouldn't give me the key if I asked, and even if she did, I'd not relish being on her waspish side if I were to let you out.'"

SH6 is a quip. The menu text is "'I'm meeting Bobby later tonight.'". The display text is "'I'm supposed to meet Bobby later tonight,' you tell her.[paragraph break]Shannon laughs and gives you a knowing wink. 'Oh, it's Bobby, is it? He [i]is[r] a cute one.' She shakes her head. 'I'm sorry, Miss Jacqueline, but curfew is curfew. Widow Theresa wouldn't give me the key if I asked, and even if she did, I'd not relish being on her waspish side if I were to let you out. However...' she adds, in an innocent-sounding voice, 'if a clever girl like you [i]were[r] to find some way around the obstacles to make her midnight tryst, I'm sure I'd never notice a thing.'"

SH7 is a quip. The menu text is "'Can I ask you a question?'". The display text is "'Can I ask you a question?' you ask.[paragraph break]'Of course, Miss Jacqueline,' says Shannon."

SH8 is a quip. The menu text is "'What do you think of Widow Theresa?'". The display text is "[first time]'What do you think of Widow Theresa?' you ask.[paragraph break]Widow Shannon scrunches up her nose and starts scrubbing the clothes harder when you mention Theresa. 'Well,' she says, 'it's important to obey your elders, Jacqueline. I'm sure Widow Theresa has your best interests in her heart.'[subsequently][rp] 'just try to be a good girl and do what she tells you.'[only]"

SH9 is a quip. The menu text is "'What do you think of Widow Fiona?'". The display text is "[first time]'What do you think of Widow Fiona?' you ask.[paragraph break]'Oh, Miss Fiona is [i]terribly[r] smart,' Shannon says, with a touch of awe in her voice. 'You mind her and listen to what she says, Jacqueline. She knows what's best.'[subsequently][rp] 'she's the smartest person I know.'[only]"

SH10 is a quip. The menu text is "'What do you think of Baron Fossville?'". The display text is "[first time]'What do you think of Baron Fossville?' you ask.[paragraph break]'Fossville?' Shannon replies, somewhat confused, 'Why, he's the new Lord of Toresal, since the old Duke died. I thought everyone knew that.'[subsequently][rp] 'he's the Lord of the city.'[only]"

SH11 is a quip. The menu text is "'What do you know about the Ascension?'". The display text is "[first time]'What do you know about the Ascension?' you ask.[paragraph break]'The [i]Ascension?[r]' Shannon laughs. 'Goddesses, Miss Jacqueline, where's your head been today? That Royal politics is far above the likes of me. I keep my feet on the ground, and my hands in the washing, where they belong.'[subsequently][rp] 'I don't pay attention to that high-faluting stuff.'[only]"

SH12 is a quip. The menu text is "'What do you know about my parents?'". The display text is "[first time]'Widow Shannon, did you ever know my parents?'[paragraph break]'Why, your mother was a [i]beautiful[r] woman,' she says immediately. 'And your father was a [i]great[r] man.' This is what Widow Shannon says every time you ask about your parents; you're pretty sure that she never saw either one of them in her life.[subsequently][rp] 'your parents were both beautiful, good people.'[only]"

The response of SH1 is { SH2, SH3 }.

The response of SH2 is { SH4, SH7 }.

The response of SH3 is { SH4, SH7 }.

The response of SH4 is { SH5, SH6 }.

The response of SH5 is { SH7 }.

The response of SH6 is { SH7 }.

The response of SH7 is { SH4, SH8, SH9, SH10, SH11, SH12 }.

The response of SH8 is { SH4, SH9, SH10, SH11, SH12 }.

The response of SH9 is { SH4, SH8, SH10, SH11, SH12 }.

The response of SH10 is { SH4, SH8, SH9, SH11, SH12 }.

The response of SH11 is { SH4, SH8, SH9, SH10, SH12 }.

The response of SH12 is { SH4, SH8, SH9, SH10, SH11 }.

SH2 is clustered with SH3, SH7, SH8, SH9, SH10, SH11 and SH12.

After populating a quip (called q) that is clustered with SH2:
	if SH4 is fired or the location is not the Laundry [if the raid on Maiden House has happened, we don't need to ask her to open the door], remove SH4 from the current conversation, if present;

Section 3 - Later Conversation

Rule for initiating conversation with Widow Shannon during Shannon's Company:
	change the chosen opening gambit to SH13;

SH13 is a quip. The menu text is "'Can I ask you something?'" The display text is "'Can I ask you something, Shannon?' you ask.[paragraph break]'Of course, Miss Jacqueline,' she says." 

SH14 is a quip. The menu text is "'Did you know about Fiona?'" The display text is "[first time]'Did you know about Fiona being a special tutor?' you ask. 'Did you know about me... and about my father?'[paragraph break]'Oh, no, Fiona never let on that you were special. Well,' Shannon says, looking a bit embarassed, 'I always thought you were special, Jacqueline. But that was just my own feelings telling me things, the way they do sometimes.'[subsequently][rp] 'Fiona never told Theresa or me anything. We just thought you were another one of the orphans.'[only]" 

SH15 is a quip. The menu text is "'Why is Dame Sandler so important?'" The display text is "[first time]'Why did Fiona want me to see Dame Sandler?' you ask. 'What does she have to do with all of this?'[paragraph break]'I don't know,' says Shannon, 'but I do know that Widow Fiona has known her for a long time. Fiona had dealings with many people in the city that we never knew about. Sometimes she'd go off for the whole day and not come back until late at night.' She smirks. 'Widow Theresa always hated that.'[subsequently][rp] 'I don't know, but if Fiona says she's a friend, then we can trust her.'[only]" 

SH16 is a quip. The menu text is "'What if you get caught?'" The display text is "[first time]'It's very nice of you to come with me, Shannon,' you say, 'but I'm worried if we get caught by Fossville's men.' Your stomach clenches as you remember what happened to Bobby. 'I don't... I don't want anything to happen to you.'[paragraph break]Widow Shannon pats your arm. 'Oh, Jacqueline, I couldn't leave you to face all this alone. And don't you worry — if those mercenaries come after me, I'll run straight to Holstenoffer's shop. I'd like to see how tough they are once they're in swinging range of his cleaver.'[paragraph break]You try to imagine an enraged Holstenoffer protecting his lady-love, and you aren't sure whether it makes you want to smile or shudder.[subsequently][rp] 'If we get caught, I'll go to Holstenoffer for protection.'[only]" 

The response of SH13 is {SH14, SH15, SH16}.
The response of SH14 is {SH15, SH16}.
The response of SH15 is {SH14, SH16}.
The response of SH16 is {SH14, SH15}.

Section 4 - Stick and Soap

The wooden stirring stick is carried by Widow Shannon. Understand "thick", "wood" as the wooden stirring stick. The description of the stirring stick is "Widow Shannon always uses that stick to stir the laundry." The printed name of the stirring stick is "stick".

The cake of lye soap is carried by Widow Shannon. Understand "cake", "harsh", "stink", "sharp", "burning", "strong" as the lye soap. The description of the lye soap is "Shannon is using a big cake of lye soap to scrub the laundry." Instead of smelling the lye soap, try smelling the location. The printed name of the cake of lye soap is "lye soap".

Part 8 - Behind Maiden House

Behind Maiden House is a room. "This tiny spur of an alleyway runs between the back of Maiden House and the high stone wall surrounding the park. You can make your way around to the Maiden House Entrance to the northeast."

Rule for printing the name of Behind Maiden House while asking which do you mean:
	say "the alley behind Maiden House";

Understand "tiny", "narrow", "spur", "corner", "alley", "alleyway", "back of" as Behind Maiden House.

Instead of facing north while in Behind Maiden House, try examining the mortar wall.
Instead of facing east while in Behind Maiden House, try searching the privy window.

The dead-end description of Behind Maiden House is "There's nowhere to go in that direction."

The mortar wall is a backdrop. The mortar wall is in East Commerce Street and Behind Maiden House. The description is "The stone wall is about ten feet high." 

The printed name is "stone wall". Understand "flagstone", "flagstones", "stone", "high" as the mortar wall.

Instead of climbing the mortar wall:
	say "The wall is too high, and the stones are too smooth to get a fingerhold on.";

Instead of going up in Behind Maiden House, try climbing the mortar wall. Instead of going north in Behind Maiden House, try climbing the mortar wall.

Behind Maiden House is southwest of Entrance to Maiden House.

Part 9 - Maiden House as region

The Hallway, The Dormitory, Kitchen, the Privy, the Laundry and the Secret Closet are in a region called Maiden House.

Book 5B - Journey with Bobby

[ Looking again, this 'Book' is a bit of a coding mess. However it is self-contained and should stand up to refactoring. ]

Part 1 - Grubber's Market, night

Chapter 1 - Description

Market Square is a room. "At night, the normally crowded marketplace is an empty maze of shadows, abandoned stalls and rolled-up canvas. The Lord's Road lies north, and Commerce Street lies back east." The printed name is "Grubber's Market"

Understand "Grubbers", "empty", "maze", "shadows", "shadow" as Market Square

The distant description of Market Square is "Grubber's Market, dark and empty, lies to the [quick best route from location to Market Square]."

Some abandoned stalls are scenery, in the Market Square. The description is "It's a little bit eerie, seeing the whole market empty and packed away like this." Instead of entering the abandoned stalls, say "The stalls are all closed for the night." Understand "closed", "empty", "stall", "canvas", "rolled up", "rolled", "shadow", "shadows" as the abandoned stalls.

After assembling available exits of Market Square:
	change the viable directions to {north, east, up};

Chapter 2 - Restrained exits

Before going up in Market Square [e.g. by climbing the center pole, which has been moved here --]:
	say "[no climbing]" instead.

Before going up while the location is the Alley and Bobby's Adventure is happening [e.g. by climbing up the crates, because I don't want to deal with overlooking the Northwest Junction or the Lord's Road during a night scene --]:
	say "[no climbing]" instead.

To say no climbing:
	say "[if Bobby is nearby]Bobby grabs your belt and pulls you back to the ground. 'We don't have time for that,' he whispers.[otherwise if Pieter is nearby]Now is not the time for climbing around![otherwise]You don't have time for climbing around; Bobby is waiting for you.[end if]";

Before going east from Market Square during Bobby's Adventure:
	say "[if Bobby is nearby]'Where you going, Jack?' Bobby asks. 'You're not losing your nerve, are you?'[otherwise]You can't back out now; you'd be mortified if Bobby thought you'd lost your nerve.[end if]" instead.

Chapter 3 - Events on exit

After going north from Market Square when BO26 is unfired:
	say "You're too filled with nervous excitement to chit-chat; you want to get on with the adventure! With a grin, you walk right past Bobby without answering towards the gate out of town.[paragraph break]Bobby cocks an eyebrow at you. 'Well, okay then,' he mutters, and hurries to catch up. ";
	now BO26 is fired;
	continue the action;

Chapter 4 - Further Conversations with Bobby (Adventure with Bobby)

BO23 is a quip. The display text is "When you get closer, he walks over and claps you on the shoulder. 'Had a spot of trouble getting away from the Widows, eh, Jack?' he whispers."

BO24 is a transitional quip. The menu text is "'Not really.'". The display text is "You shrug and say, 'Not really.'[paragraph break]Bobby raises his eyebrows. 'All right, then.'[paragraph break]You find yourself wishing you had made up some story about a daring, sneaky escape, so that Bobby would be more impressed."
The following quip is BO26.

BO25 is a transitional quip. The menu text is "'I had to sneak out.'". The display text is "You nod. 'Widow Theresa locked the front door; I had to sneak out the privy window.'[paragraph break]Bobby chuckles. 'The old privy window routine, eh? Tricky, Jack.' You can't help but smile yourself."
The following quip is BO26.

BO26 is a quip. The display text is "'Okay then, we're ready to go,' says Bobby. 'We're off to the wood outside Lord's Keep; that place I wanted to show you is there. Now follow me, keep your hood up and your head down, and land your feet soft.'[paragraph break]Without waiting for you to answer, Bobby heads north, out the city gates."

BO27 is a quip. The display text is "Your head is spinning. 'Wait, wait,' you say, 'I have to ask you something first.'[paragraph break]'Okay, but make it quick,' says Bobby."

BO28 is a quip. The menu text is "'This tunnel really goes under Lord's Keep?'". The display text is "'This tunnel really goes all the way under [i]Lord's Keep?[no line break][r]' you ask. 'Are you [i]serious?[no line break][r]'[paragraph break]Bobby grins. 'Sure does. Right up to the chapel, if I'm not mistaken.'"

BO29 is a quip. The menu text is "'How did you find out about this?'". The display text is "'If this thing is so old and forgotten, how did you learn about it?' you ask.[paragraph break]'Oh, I just keep my ear to the ground,' says Bobby vaguely. 'You learn things, you hear rumors, you check up on them. You know. Now quit standing around flapping your mouth and [i]get in there[r],' he adds, waving you down towards the hole."

BO30 is a quip. The menu text is "'I don't know about this.'". The display text is "'I don't know about this, Bobby...' you say. You can't help imagining the sorts of things that might be waiting down there in the dark.[paragraph break]Bobby sighs. 'Come on, Jack, this is an [i]adventure[r].' Then he adds, in a voice that stings you much more than you let show, 'Don't chicken out on me now, Jack.'"

BO31 is a quip. The menu text is "'Fine, I'm going.'". The display text is "'Fine, I'm going down your dark, creepy hole,' you mutter.[paragraph break]'Thattaboy, Jack,' Bobby laughs, as you duck your head and climb down into the darkness."

The response of BO23 is { BO24, BO25 }.

The response of BO27 is { BO28, BO29, BO30, BO31 }.

The response of BO28 is { BO29, BO30, BO31 }.

The response of BO29 is { BO28, BO30, BO31 }.

The response of BO30 is { BO28, BO29, BO31 }.

BO28 is clustered with BO29 and BO30.

After populating a quip clustered with BO28:
	if BO28 is fired, remove BO28 from the current conversation, if present;
	if BO29 is fired, remove BO29 from the current conversation, if present;
	if BO30 is fired, remove BO30 from the current conversation, if present;

After firing BO26:
	move Bobby to Lords Road [originally we just moved him to the Clearing, but since we've implemented a following action, we need to move him to each stage of the journey to Lord's Keep so that he can serve as a target for the player];
	update the character list;

Part 2 - Bobby's Adventure (Creeping to Lord's Keep)

[ We need to know if we're on the way there, or on the way back; and we do so using scenes. ]

Bobby's adventure is a scene. Bobby's adventure begins when the location is Market Square.

When Bobby's Adventure begins:
	start conversation with Bobby on BO23;

Creeping to Lord's Keep is a scene. Creeping to Lord's Keep begins when the location is Market Square. Creeping to Lord's Keep ends when the location is the Guardhouse. 

Bobby's route is a list of objects [rooms] that varies. [It's defined at the bottom of the source, though, because room names have to be defined before they're used.]

Bobby's trail is a list of objects [directions] that varies. 

Bobby's trail is { [from market] north, [from Lord's Road] east, [from pasture] north, [from stream crossing] north, [from the woods] south [except this is a placeholder: the split sapling points the real way], [then from the clearing] down, [from the tunnel] north, [from the secret wall] north, [from the chapel] southwest, [from the lower bailey] up, [and finally from the upper bailey] north };

Instead of following Bobby during Bobby's adventure:
	if Bobby is in the location,
		say "But Bobby's right here!" instead;
	say "[conditional paragraph break]";
	if the location is the Woods:
		let Bobby's wake be the way of the split sapling;
	otherwise:
		let Bobby's wake be entry 1 of Bobby's trail;
	try going Bobby's wake.

First after going during Creeping to Lord's Keep:
	let the proper way be entry 1 of Bobby's route;
	if the room gone to is the proper way:
		remove entry 1 from Bobby's route, if present;
		remove entry 1 from Bobby's trail, if present;
	if the current conversationalist is Bobby:
		carry out the terminating conversation with activity with Bobby;
	continue the action;

A leading rule for Bobby during Bobby's Adventure:
	[We can sensibly follow Bobby at this point --]
	rule succeeds;

Part 3 - Lord's Road

Lords Road is north of Market Square and southwest of the Crossing. "The wide and well-paved Lord's Road runs from Grubber's Market straight north to Lord's Keep, a mile or so from the city. To the east are rolling pastures; to the west, broken ground and scrub." The printed name is "Lord's Road".

Understand "wide", "paved", "well paved", "west", "western", "edge", "bound", "boundary", "straight" as Lords Road.

Rule for printing the distant description of Lords Road while in Outer Market Roof:
	say "Lord's Road runs straight north beyond the Market Gates. ";
Rule for printing the distant description of Lords Road while in the Pasture:
	say "Lord's Road bounds the western edge of the pasture. ";

Instead of facing southeast while in Lords Road, say "That way stands the high outer wall of Toresal."
Instead of facing southwest while in Lords Road, say "That way stands the high outer wall of Toresal."
Instead of facing west in Lords Road, say "There's nothing in that direction but rocks and the occasional thornbush."
Instead of facing northwest in Lords Road, say "There's nothing in that direction but rocks and the occasional thornbush."

After going north to Lords Road for the first time:
	move Bobby to Pasture;
	say "[command clarification break]";
	try looking;
	say "Bobby jerks his thumb down the road, to the north. 'This road leads straight to the front gate of Lord's Keep; we want to stay away from there. I know a better way,' he says with a knowing smile, and strikes off towards the open field to the east.";
	update the character list;

Instead of going south in Lords Road during Bobby's Adventure:
	say "You can't back out now; you'd be mortified if Bobby thought you'd lost your nerve."

Instead of going northeast in Lords Road during Bobby's Adventure:
	say "Bobby went east, to the pasture. You should follow him, or you're likely to get lost."

Instead of going southeast in Lords Road, try facing southeast.
Instead of going southwest in Lords Road, try facing southwest.
Instead of going northwest in Lords Road, try facing northwest.
Instead of going west in Lords Road, try facing west.

[ See Southern Gate for the rule that blocks you from going there during Bobby's Adventure. ]

Chapter 1 - Scenery

The broken ground is a backdrop, in Lords Road and Southern Gate. The description is "The land to the west of Lord's Road is rocky and dry, no good for growing much of anything other than thornbushes." Understand "land", "rocks", "rock", "rocky", "dry", "fallow", "scrub", "bushes", "bush", "thorn", "thorns", "thornbush", "thornbushes", "painful" as the broken ground.

Instead of taking, pulling, pushing, squeezing, rubbing, turning, searching, or touching the broken ground, say "A hand full of painful thorns is not what you need right now."

The city wall is a backdrop, in Lords Road and the Pasture. The description is "The outer walls of Toresal are built of heavy, white stone." Understand "Toresal", "walls", "heavy", "white", "stone", "stones", "outer", "white" as the city wall.

Instead of climbing the city wall, say "The city walls are far too high for you to climb."

Part 3 - Pasture

Chapter 1 - Description

A Pasture is east of Lords Road and southeast of the Southern Gate. "This wide, open field is used by the city for tourneys and other holiday events, or by cattle-sellers driving their livestock to market. Tonight it is empty except for the occasional cow pie. To the north you can see the dark, rough edge of a forest." 

Understand "wide", "open", "pastures", "field", "empty", "endless", "farm", "farms", "farmless", "rolling", "air", "fresh air" as the Pasture.

Instead of facing east in the Pasture, say "There's nothing out that way but endless farmland."
Instead of facing northeast in the Pasture, say "There's nothing out that way but endless farmland."

The dead-end description of the Pasture is "That way lies the outer wall of the city."

Instead of going east in the Pasture, try facing east.
Instead of going south in the Pasture, try facing south.
Instead of going southeast in the Pasture, try facing southeast. 
Instead of going southwest in the Pasture, try facing southwest.

Instead of going west in the Pasture during Bobby's Adventure:
	say "You can't back out now; you'd be mortified if Bobby thought you'd lost your nerve."

Chapter 2 - Cow Pies

Some cow pies are a backdrop. They are in Pasture and in Crossing. The description is "Last time you checked, cow pies pretty much all looked the same."

Understand "pie", "pies", "patty", "patties", "dung", "manure", "cow pie" as cow pies.

Instead of smelling or taking the cow pies, say "Um, [i]yuck[r]."

Instead of smelling in the presence of cow pies,
	say "Mostly you smell cow pies.";

Chapter 3 - Event on entering

After going to the Pasture for the first time:
	move Bobby to Crossing;
	say "[command clarification break]";
	try looking;
	say "You feel nervous and exposed out here in all this empty space; you're more used to the narrow alleys and enclosing walls of the city. The urge to hunch over and make yourself small is near-irresistible.[paragraph break]Bobby, meanwhile, strides out into the middle of the moonlit field, standing straight and swinging his arms wide. 'Fresh air, Jack!' he calls out, laughing. 'Enjoy it! Take a deep breath!' He heads north, still laughing.";
	update the character list;

Chapter 4 - Taking breaths, taking deep breaths, and breathing

Understand "breathe" as smelling.
Understand "breathe in" as smelling.
Understand "breathe deeply" as smelling.
Understand "breathe in deeply" as smelling.
Understand "take a breath" as smelling.
Understand "take a deep breath" as smelling.

Part 4 - Crossing / Stream

[ This is only called "northern pasture" in the game source, to avoid conflicts with the Stream object below. ]

[ And now we're changing it right back to "Crossing," because now that you can refer to adjacent rooms, I don't want the game asking "which do you mean, the Pasture or the Northern Pasture?" -- MSG ] 

The Crossing is north of the Pasture. "A small stream bubbles out of the woods and cuts through the pasture's north end. The edge of the forest lies to the north, just across the water. The city is far away to the south." The printed name of Crossing is "Stream Crossing". 

Understand "stream", "woods", "forest", "dark", "rough", "edge", "trees", "end", "edge", "bound", "boundary", "north", "northern" as the Crossing.

The distant description of the Crossing is "A dark forest bounds the northern edge of the pasture."

Instead of facing northwest in the Crossing, say "There's no place to cross the stream in that direction."
Instead of facing northeast in the Crossing, say "There's no place to cross the stream in that direction."

Instead of going northwest in the Crossing, try facing northwest.
Instead of going northeast in the Crossing, try facing northeast.

Instead of facing east in the Crossing, say "There's nothing out that way but endless farmland." 
Instead of facing southeast in the Crossing, say "There's nothing out that way but endless farmland."

Instead of going east in the Crossing, try facing east.
Instead of going southeast in the Crossing, try facing southeast.

After going north to the Crossing for the first time:
	select theme "Stream";
	move Bobby to Woods;
	say "[command clarification break]";
	try looking;
	say "Bobby hops nimbly across, bouncing from rock to rock as though he were playing a game of Circles-and-Stones in the park. He doesn't get so much as the hem of his cloak damp. When he gets to the opposite bank, he turns and beckons you to follow before disappearing into the shadowy woods.";
	update the character list;

Instead of going south in the Crossing during Bobby's Adventure:
	say "You can't back out now; you'd be mortified if Bobby thought you'd lost your nerve."

Instead of going southwest in the Crossing during Bobby's Adventure:
	say "You can't back out now; you'd be mortified if Bobby thought you'd lost your nerve."

Chapter 1 - Scenery and Stream

The stream is scenery, in Crossing. The description is "The water is clean and clear, rippling over a shallow bed of stones and splashing over the larger rocks. It's only about ten feet wide here; you could probably pick your way across." Understand "clean", "clear", "rippling", "ripple", "ripples", "shallow", "splashing", "water", "brook", "river" as the stream.

Section 1 - Cross the stream

Before entering the stream:
	try going north instead.

Before going north from Crossing for the first time:
	[BUG: Extra space after this text]
	say "The first couple of rocks are easy enough, but the third one wobbles when you put your weight on it. You end up pinwheeling your arms desperately, contorting yourself in every direction to avoid a tumble and a thorough soaking. Finally, with arms outstretched and lower lip clamped between your teeth, you pick your way across the remainder of the stream with all the grace of a drunkard on a greased tightrope.[paragraph break]At the edge of the woods, Bobby is nearly doubled over with silent laughter. He claps you on the back. 'You're a good sport, Jack,' he says. 'C'mon, we're nearly there.'";

Before going north from Crossing in the presence of Pieter:
	ratchet the current tension to important;
	say "[first time]Now that you've had a bit of practice, you're a bit more confident on the rocks this time. Carefully lifting your skirts (you're still getting used to moving around in a dress), you pick your way over the stream with little difficulty.[paragraph break]Pieter, on the other hand, stumbles halfway across and lurches into the water, soaking his trousers up to the knee. You do your best to stifle your laughter as he sloshes his way to shore, cursing and grumbling something about 'gratitude' and 'youth today.'[only]";

Section 2 - Drink the stream 

Instead of drinking or tasting the stream, say "The water is fresh and shockingly cold."

Section 3 - Swimming and Diving

Swimming in is an action applying to one visible thing. Diving into is an action applying to one visible thing.

Understand "swim [something]", "swim in [something]", "swim across [something]" as swimming in.

Understand "dive into [something]", "dive in to [something]" as diving into.

Instead of swimming in or diving into the stream, say "The water is only about shin-deep here; better for wading than for swimming."

Check swimming in (this is the block swimming rule):
	say "You can't swim in that!";

Carry out diving into something:
	try swimming in the noun instead;

Chapter 2 - Other scenery - stepping stones

Some stepping stones are scenery, in Crossing. The description is "The stones just barely poke over the surface of the water, rounded and slippery."

Understand "bed", "bed of", "rock", "rocks", "stone", "large", "larger" as the stepping stones. 	
Before entering the stepping stones:
	try going north instead;

Before climbing the stepping stones:
	try going north instead;

Chapter 3 - Still More Scenery - the Distant City

The distant city is scenery, in the Crossing. Understand "Toresal", "walls", "wall", "town", "silhouette" as the distant city. The description of the distant city is "You can just make out the silhouette of the city walls, far to the south."

Instead of doing something other than examining the distant city, say "The city is too far away."

Part 5 - In The Woods

The Woods are north of the Crossing. "You are in the woodlands just southeast of Lord's Keep, following the game trails that wind their way through strands of pale birch and black spruce. There is just enough moonlight trickling through the upper branches to see your way by."

Understand "forest", "woods", "tree", "trees", "edge", "edge of", "dark", "thick" as the Woods.

The distant description of the Woods is "You can only see a little way in through the thick trees."

Instead of facing a planar direction in the Woods, say "The trees are dark and thick in that direction."

After going to the Woods for the first time:
	change the way of the split sapling to south;
	while the way of the split sapling is south repeatedly change the way of the split sapling to a random orthogonal direction;
	remove Bobby from play;
	say "[command clarification break]";
	try looking;
	say "Bobby motions you over to where he's standing, pushing back the underbrush. 'Look here, Jack,' he says. 'These trails go every which way, and it's easy to get lost. But if you keep an eye out for this one sapling,' — he points to a small tree split halfway down its length — '...you'll find the right trail.' He heads off through the trees to the [way of the split sapling].";
	update the character list;

After going to the Woods in the presence of Pieter:
	remove the split sapling from play;
	try looking;
	update the character list;
	ratchet the current tension to important;
	say "The game trails criss-cross through the woods in every direction, and they all look the same. When you were here the night before, Bobby quickly found a shortcut to the clearing. Where was it?";

Section 1 - Scenery (Trees)

Some trees are a backdrop. They are in the Woods and the Clearing. The description is "The trees grow thick and close all around you." Understand "forest", "woods", "tree", "birch", "birches", "spruce", "spruces", "branch", "branches", "thick", "dark", "close" as the trees.

Instead of climbing the trees, say "This really isn't the time for tree-climbing."

Instead of going up in the presence of the trees:
	try climbing the trees;

Instead of going nowhere when in the Woods:
	if the noun is not up and the noun is not down:
		say "The trees are too thick in that direction.";
	otherwise:
		continue the action;

Instead of searching or examining the trees in the presence of Pieter when the split sapling is off-stage:
	try searching the underbrush.

Section 2 - Scenery (Game Trails)

Some game trails are scenery, in the Woods. The description is "The trails are little more than narrow gaps in the underbrush, leading in every which direction[if the split sapling is in the location]. One trail leads [way of the split sapling], past a split sapling[end if]." Understand "trail", "path", "paths", "gap", "gaps", "narrow", "track", "tracks" as the game trails.

Instead of searching or examining the game trails in the presence of Pieter when the split sapling is off-stage:
	try searching the underbrush.

Understand "follow [game trails]" as following.

Instead of following the game trails:
	if the split sapling is on-stage:
		try going the way of the split sapling;
	otherwise:
		try going a random orthogonal direction;

Section 3 - Scenery (Underbrush)

The underbrush is scenery, in the Woods. The description is "The underbrush is thick and tangled." Understand "thick", "tangled", "under", "brush", "bush", "bushes", "undergrowth" as the underbrush.
Understand "split", "sapling" as the underbrush when the split sapling is not on-stage.

Instead of searching or examining the underbrush in the presence of Pieter when the split sapling is off-stage:
	move the split sapling to the location;
	now the split sapling is not scenery;
	ratchet the current tension to important;
	say "You search the underbrush carefully. At first it all looks the same, but after several tense minutes... you find it! The split sapling that Bobby showed you last night, right next to a trail leading [way of the split sapling]."

Section 4 - Split Sapling

The split sapling is scenery, in the Woods.
The split sapling has a direction called the way.

The description of the split sapling is "The sapling is split down half its length, forming a distinctive 'Y' shape. It's right next to a path leading [way of the split sapling]." The initial appearance of the split sapling is "A split sapling grows right next to a path leading [way of the split sapling]."

Understand "small", "tree", "Y", "shaped", "shape", "distinct", "distinctive" as the sapling.

Instead of going an orthogonal direction in the Woods:
	if the noun is the way of the split sapling begin;
		move the player to the Clearing;
	otherwise;
		say "If you go wandering off among these trails without Bobby's help, you'll never find your way out of the woods.";
	end if;

Instead of going an orthogonal direction in the Woods in the presence of Pieter:
	if the split sapling is on-stage:
		if the noun is the way of the split sapling:
			move the player to the Clearing;
			move Pieter to the Clearing;
		otherwise:
			say "If you go wandering off on those trails again, you'll only get lost. Better stick to the [way of the split sapling] path that Bobby took last night.";
	otherwise:
		say "You try following one of the game trails, but it just seems to wind around without really getting anywhere";
		if a random chance of 1 in 2 succeeds:
			ratchet the current tension to interesting;
			say ". [paragraph break]'[one of]It looks like we're back where we started,[or]Didn't we pass this tree before?[no line break][or]These trails all lead in circles,[or]Are we there yet?[no line break][or]These woods seem to go on forever,[or]Where did you say this fountain was?[no line break][or]I'm completely turned around,[at random]' [one of]says[or]grumbles[or]mutters[or]pants[at random] Pieter.";
		otherwise:
			say ".";

Part 6 - Clearing & Fountain

Chapter 1 - Description and Scenery

Clearing is a room. The description of Clearing is "The woods open out here in a small, moonlit clearing. The ruins of an old pavilion lie here, pale pavestones poking up like bones from the grass."

Understand "open", "small", "moonlit" as the Clearing.

The Clearing overlooks the Woods. Instead of facing a planar direction in the Clearing, try inspecting the Woods.

Rule for initiating conversation with Bobby when in the Clearing:
	change the chosen opening gambit to BO27;

After firing BO31:
	try going down;

Instead of going a planar direction in the Clearing during the Bodyguard scene:
	say "You can't turn back now — and besides, it would take you all night to find your way back out of those woods.";

Section 1 - Fountain

The fountain is in the Clearing. "In the center of the clearing stands a crumbling fountain, topped by a cracked statue of the Goddess Brigid.[if the secret entrance is part of the statue] One side of the stone plinth supporting the statue has opened up, revealing a narrow opening into darkness.[end if]". The description of the fountain is "The fountain is shaped like a large bowl, topped with a classical-style statue of Brigid pouring water from an urn. Of course, the fountain is dry now, its stone cracked, the bowl filled with bird droppings and dead leaves. But at one time it must have been very beautiful[if the secret entrance is part of the statue].[paragraph break]One side of the stone plinth supporting the statue has opened up, revealing a narrow opening into darkness[end if]." Understand "stone", "bowl", "crumbling", "crumbled", "cracked", "cracks", "crack", "dry", "old", "ruined" as the fountain.

Instead of searching the fountain:
	say "The bowl of the fountain is filled with bird droppings and dead leaves[if the secret entrance is part of the statue]. One side of the stone plinth supporting the statue has opened up, revealing a narrow opening into darkness[end if]."

Instead of entering the fountain when the secret entrance is not part of the statue:
	say "There's nothing in there but bird droppings and dead leaves."

Instead of searching the statue, try examining the statue.

After entering the fountain for the first time:
	open the secret entrance.

Section 3 - Ruined pavilion

The ruined pavilion is scenery, in the Clearing. Understand "ruin", "ruins", "old", "pale", "pavestone", "pavestones", "stone", "stones", "rubble", "clearing", "grass", "bone", "bones" as the pavilion. The description is "Whatever once stood here was probably very beautiful. Now it's just rubble in the grass." 

Section 2 - Statue and urn

The statue is part of the fountain. The description is "The statue's features are worn away and obscured by moss, but you've seen enough statues of Brigid to know that she's almost always depicted carrying water. She cradles her urn like a child, tipping it gently over the bowl of the fountain[if the secret entrance is part of the statue]. One side of the stone plinth supporting the statue is open, revealing a narrow tunnel leading down into darkness[end if]." 

Understand "Brigid", "classic", "classical", "cracked", "stone", "cracks", "crack", "style", "old", "sculpture", "feature", "features", "statues", "worn", "obscure", "obscured", "moss" as the statue.

Understand "plinth", "base" as the statue when the secret entrance is not part of the statue.

Instead of pushing, pulling, turning, or shaking the statue:
	say "The statue does not budge.";

Instead of rubbing or washing the statue:
	say "You rub away a bit of the moss, but the statue looks just the same underneath.";

Some dried leaves are scenery, in the fountain. Understand "dry", "leaf", "detritus", "droppings", "bird droppings", "bird dropping", "dropping" as the dried leaves.

The hollow urn is a container, part of the statue. Understand "bowl", "hollow", "empty" as the urn. The description is "The urn is hollow, and probably had water flowing out of it when the fountain worked.[paragraph break]Looking closely, you notice a small metal bolt sticking out of the bottom of the urn."

Before inserting something into the urn:
	say "The urn is tipped over. Anything you put in there would just fall out." instead.

Instead of searching the urn:
	try examining the urn;

The metal bolt is part of the urn. The description is "Just a little knob of metal, sticking out of the bottom of the urn." Understand "stud", "button", "knob", "bottom", "little", "bottom of urn", "underside", "underside of urn" as the metal bolt.

Instead of pulling or turning the metal bolt:
	say "The knob doesn't move that way. It feels like you could press it in, though."

Instead of pushing the metal bolt when the secret entrance is part of the statue:
	say "The metal knob pushes in, then pops out with a [i]click[r]. Nothing much happens."

After pushing the metal bolt in the presence of Pieter:
	open the secret entrance; 
	ratchet the current tension to important;
	say "The metal knob pushes in, then pops out again with a [i]click[r]. Suddenly there is a deep, solid [i]thunk[r], and a scraping sound coming from the stone beneath you. Just as it did before, part of the statue's base swings inward.[paragraph break]Pieter raises his eyebrows. 'Huh,' he says, 'I never knew about [i]that[r].'"

Section 3 - Secret entrance

The secret entrance is a thing. Understand "door", "plinth", "base", "opening", "hole", "tunnel", "dark", "darkness", "narrow", "rectangle", "stone", "rectangular", "space", "black", "cold", "draft", "air", "damp", "damp smelling" as the secret entrance. The description is "The tunnel leads down into darkness."

The initial appearance of the secret entrance is "One side of the stone plinth supporting the statue is open, revealing a narrow tunnel leading down into darkness."

Instead of smelling the secret entrance, say "The air coming out of that tunnel smells damp and old, like something long buried."

Instead of searching the secret entrance, say "You can't see anything down there."

Instead of inspecting Underneath the Fountain in the clearing, try searching the secret entrance.

Instead of entering the fountain when the secret entrance is part of the statue:
	try entering the secret entrance;

Instead of entering the secret entrance, try going down.

To close the secret entrance:
	now the secret entrance is part of Limbo;
	change the down exit of the Clearing to nothing;

To open the secret entrance:
	now the secret entrance is part of the statue;
	change the down exit of the Clearing to underneath the fountain;

When play begins:
	close the secret entrance;

Chapter 2 - Events on entering

Section 1 - On the way there

After looking in the Clearing for the first time during Creeping to Lord's Keep:
	select theme "Fountain";
	open the secret entrance;
	say "'Here we are.' Bobby jumps up into the bowl of the fountain, dried leaves crackling beneath his boots. Then he reaches around the statue of Brigid as though trying to embrace her. His hands fumble at something on the underside of the urn.[paragraph break]Suddenly there is a deep, solid [i]thunk[r] and a scraping sound, and part of the base of the statue moves.[paragraph break]Bobby hops down and pushes against the statue's base. A rectangle of stone about two feet square swings inward. The space behind it is utterly black, and a cold draft of damp-smelling air rises out of it.[paragraph break]'It runs all the way to Lord's Keep,' says Bobby as he brushes off his hands. 'I don't think anyone knows about it. Maybe the old Duke did, but now that he's dead it's basically unknown. It's not been used for decades, I'd guess. Well anyway, until now.'[paragraph break]He holds out his hand. His lop-sided smile is absolutely charming. 'After you, kind sir.'";
	move Bobby to the location;
	update the character list;

Section 2 - On the way back

After going to the Clearing during Returning From Lord's Keep:
	say "You crawl out of the hole in the base of the statue, into the dirt and leaves littering the bottom of the fountain. Apart from your scrabbling, it's quiet out here. Even the crickets have stopped. Finally, you get to your feet and look around.[paragraph break]Bobby is lying face-down in the grass, just a few yards away. One of the mercenaries from Grubber's Market is kneeling with his knee in the small of Bobby's back, binding his wrists together with twine. Standing around the outside of the fountain are several more mercenaries, looking relaxed and smug. Some of them are chuckling to themselves.[paragraph break]A gaunt man wearing red robes walks up to you. His mouth is half-hidden by long, drooping, black moustaches, but underneath them he is smiling like a snake. 'Well now, young...sir? Or is it [']miss[']? So much confusion, all to catch one little mouse. You led us a merry chase this morning, little mouse, but I think you will not scurry away from us again.'[paragraph break]Leaves crunch behind you. You jump for it, but too slow — rough hands haul you back, and a sack of coarse, black cloth is pulled over your head.[paragraph break]'Good night, little mouse.'[paragraph break]Something hard and heavy strikes the back of your head, and the night turns black as pitch.[line break]";
	close the secret entrance; 
	pause the game;
	output chapter heading "Chapter 3 - Jail";
	move the player to Jail-Cell;
	update the character list;

Chapter 3 - Restrained exits

Instead of going an orthogonal direction in the Clearing during Bobby's Adventure:
	say "You can't leave now, Bobby will think you're chickening out!";

Part 7 - Underneath the fountain

Chapter 1 - Description

Underneath The Fountain is a room, privately-named, down from the Clearing. The description is "The walls and floor are old, damp, and spotted with some sort of whitish fungus, and the air has a wet, sour smell to it. Steps lead up to the fountain above you. To the north, a long, low-ceilinged tunnel leads into the gloom."

Understand "walls", "wall", "old", "damp", "spotted", "air", "wet", "sour", "tunnel", "low", "underground", "low ceilinged", "ceiling", "gloom", "water", "slow", "faint", "drip", "drips", "dripping" as Underneath the Fountain when the location is Underneath the Fountain.

The distant description of Underneath the Fountain is "The tunnel leads back to the south." The dead-end description of Underneath the Fountain is "The tunnel walls close in on you from every direction except to the north."

Instead of going outside in Underneath the Fountain:
	try going up;

Instead of going inside in the Clearing:
	try going down;

Rule for printing the distant description of the Clearing while the location is Underneath the Fountain:
	say "You can't see any part of the clearing from down here. ";

Instead of smelling Underneath the Fountain when in Underneath the Fountain:
	say "It smells damp and sour down here. You'll be glad to get back into the fresh air.";

Instead of listening to Underneath the Fountain when in Underneath the Fountain:
	say "You hear the slow, faint dripping of water coming from somewhere."

Instead of talking to Bobby in the Tunnels:
	say "Bobby raises his hand wordlessly, as if to say, [i]not now[r].";

Instead of talking to Bobby in Lord's Keep during Bobby's Adventure:
	say "Bobby shakes his head and puts his finger to his lips."; 

Section 1 - Scenery (Fungus)

Some white fungus is scenery, in Underneath the Fountain. The description is "The fungus grows in little clusters all over the damp walls." Understand "mold", "clusters", "cluster", "white", "whitish", "little", "sticky" as the fungus.

Instead of taking, touching, smelling, eating, or tasting the fungus:
	say "Just the thought of touching the sticky fungus makes you shudder.";

Section 2 - Scenery (Steps)

Some steps are scenery, in Underneath the Fountain. The description is "The stairs lead back up to the clearing above." Understand "stairs", "staircase" as the steps. Instead of climbing the steps, try going up. Instead of descending the steps, say "The steps lead up, not down."

Chapter 2 - Event on entering

The torch is a thing. The description is "The torch's flame throws weird, shivering shadows across the walls."

After going down to underneath the fountain [from the Clearing] for the first time:
	select theme "Tunnel";
	say "You grope your way down into the tunnel, carefully sliding your feet over steps you cannot see. Echoes fill the darkness: the scrape of your shoes on stone, the drip of water, your own loud breathing.[paragraph break]Behind you, you hear Bobby fumbling with something under his cloak; the [i]click[r] of stone on metal; and then a sickly orange glow spreads across the walls of the tunnel. Your own shadow wobbles hugely in front of you.[paragraph break]You turn around and see Bobby holding a torch. He grins at you. 'Tricky, isn't it?'";
	now Bobby carries the torch;
	try looking;
	move Bobby to the location;
	update the character list;

After going south to underneath the fountain when Bobby is on-stage during Returning From Lord's Keep:
	say "Stealthily, you creep back up the long tunnel to the stone steps under the fountain. 'I'll go up first,' whispers Bobby, 'to make sure the coast is clear.' He dunks his torch in a puddle of mud and bounds lightly up the steps. For a moment you can see him silhouetted against the starlight; then he moves away from the opening and disappears from view.[paragraph break]You wait for ten heartbeats. Behind you, you hear the distant [i]plip[r] of dripping moisture, far back in the dark tunnel. Above you, you can hear crickets chirping in the nearby forest.[paragraph break]Ten more heartbeats. 'Bobby?' you call out in a hoarse whisper. Maybe he can't hear you, but you're too nervous to raise your voice. 'Bobby? Are you up there?'[paragraph break]There is no answer.";
	remove Bobby from play;
	update the character list;

Chapter 4 - Exits

Instead of going up from Underneath the Fountain during Creeping to Lord's Keep,
 	say "You [i]definitely[r] can't chicken out now, not while Bobby is right here.";

Before going north from Underneath the Fountain:
	[BUG: Extra space after this text]
	say "Ducking your head to avoid the low ceiling, you follow the tunnel for some distance.";

Part 8 - Secret Passage

Chapter 1 - Description

The Secret Passage is north from Underneath the Fountain. The printed name is "Tunnel End". The description is "[if the blank stone wall is closed]The tunnel ends here at a blank, stone wall.[otherwise]The wall at the north end of the tunnel has swung open, revealing a secret entrance to the castle.[end if]"

Understand "tunnel", "end", "dead end", "of", "walls", [ "wall" [causes conflict with the secret door wall] ] "subterranean", "gloom", "gloomy" as the Secret Passage when the location is the Secret Passage.

Understand "south", "southern", "south wall", "southern wall" as the Secret Passage when the location is the Chapel.

Rule for printing the distant description of the Secret Passage when in Underneath the Fountain:
	say "The tunnel continues on to the north. ";

Section 1 - Scenery (Empty metal bracket)

The metal bracket is a container, scenery, in the Secret Passage. The description is "It's a small metal bracket for holding a torch, bolted into the wall of the tunnel." Understand "rust", "rusted", "rusty", "small", "bolt", "bolts", "bolted" as the metal bracket.

Chapter 2 - Events on entering

Section 1 - On the way there

After going to the secret passage for the first time:
	say "[command clarification break]";
	try looking;
	now the blank stone wall is open;
	move Bobby to the location;
	update the character list;
	say "Bobby steps past you and brushes his fingertips across the stonework, holding the torch close. Eventually he finds a loose brick and pulls it free, revealing a rough, rectangular hole.[paragraph break]You wonder how many times Bobby has been down here. He exudes such easy confidence as he reaches into the hole, as though he has done this many times before.[paragraph break]There is a [i]click[r]. Bobby looks over his shoulder at you and winks. And then the north wall swings away, revealing shadowed spaces beyond.";

Instead of going south from the secret passage during Creeping to Lord's Keep:
 	say "You [i]definitely[r] can't chicken out now, not while Bobby is right here.";

Section 2 - On the way back

After going to the secret passage from the Chapel when Bobby is on-stage during Returning from Lord's Keep:
	say "[command clarification break]";
	try looking;
	move Bobby to the location;
	update the character list;
	now the blank stone wall is closed;
	ratchet the current tension to important;
	say "Bobby reaches back and pulls the secret door shut, carefully easing it the last few inches so that it doesn't slam. He puts the loose brick back in its place, just as before, and tugs his torch free of the bracket where he'd left it.[paragraph break][i]We're out[r], you realize with a sense of overwhelming relief. [i]We made it. It's like we were never here.[no line break][r][paragraph break]'Some adventure, eh, Jack?' laughs Bobby. His voice is disturbingly loud after the long minutes of terrified silence in the keep. 'Eavesdropping on the Lord's own guardsmen! That's a story that'll have the little ones wide-eyed at the Maiden House, eh?' He claps you on the back, and you manage a smile. It was fun, and you want Bobby to think you had fun, but it will be a while before you feel up to something that dangerous again."

After going south from the secret passage when Bobby is off-stage during Returning From Lord's Keep:
	say "Stealthily, you creep back up the long tunnel.";
	continue the action;

Chapter 3 - Secret door

The blank stone wall is a door, scenery, north of secret passage, and south of the Chapel. Understand "passage" as the blank stone wall when the blank stone wall is open. Understand "secret", "entrance", "door", "stonework", "bricks", "brickwork" as the blank stone wall.

Understand "tunnel" as the blank stone wall when the location is the chapel.

Understand "brick" as the blank stone wall when the brick is off-stage. 

Instead of searching the blank stone wall, try touching the blank stone wall.

Instead of examining the blank stone wall when the rectangular hole is not part of the blank stone wall during the Bodyguard Scene:
	try touching the blank stone wall instead;

The description of the blank stone wall is "[secret door description]".

To say secret door description:
	if the blank stone wall is open begin;
		say "The wall has swung open, revealing a secret entrance to the castle. ";
	otherwise;
		say "It's just a blank, featureless, stone wall[if the rectangular hole is part of the blank stone wall]... except for the rough, rectangular hole where one of the bricks is missing. [otherwise]. You know that one of the bricks must be loose, but they all look the same to you. [end if]";
	end if;

Section 1 - Interactions when with Bobby

Instead of closing the open blank stone wall during Bobby's Adventure:
	say "Better let Bobby handle the secret door. If you close it, you'll never figure out how to get it open again."

Before going north in the secret passage during Returning from Lord's Keep:
	try opening the blank stone wall instead;

Before opening the closed blank stone wall during Bobby's Adventure:
	say "You're more than ready to go home now, and you're not sure you could find the loose brick again in any case." instead;

Instead of touching the closed blank stone wall when Bobby is nearby during Bobby's Adventure:
	say "'Leave that for now,' whispers Bobby. 'We need to get going.'"

Section 2 - Interactions when with Pieter

Instead of closing the open blank stone wall during Journey To The Ball:
	say "Better leave it open. You might need a quick getaway later on, and you can't be sure that the wall won't lock behind you if you close it."

After touching the blank stone wall when the rectangular hole is not part of the blank stone wall during Journey To The Ball:
	now the rectangular hole is part of the blank stone wall;
	ratchet the current tension to important;
	say "Carefully, you run your fingertips across the wall, trying to remember where Bobby found the loose brick the last time you were here.[paragraph break]Ah-ha! You find it — the faint outline of a rectangular hole, with a brick resting loosely in the gap.";

Instead of opening the closed blank stone wall:
	if the short lever is part of the rectangular hole:
		try pulling the short lever;
	otherwise:
		say "The wall has no obvious means of opening. You seem to remember Bobby doing something with a loose brick...";

Chapter 4 - Hole in wall, and brick

[ The hole starts off out-of-play, and must be discovered by "touch wall" or similar. ]

The rectangular hole is a container. In the rectangular hole is the brick. Understand "rough", "rectangle", "gap" as the rectangular hole.

Instead of examining the rectangular hole: try searching the rectangular hole.

After searching the rectangular hole:
	if the rectangular hole contains the brick begin;
		try examining the brick instead;
	otherwise;
		say "In the back of the hole you see a short lever." instead;
	end if.

Understand "loose" as the brick when the rectangular hole contains the brick.

The description of the brick is "[if the rectangular hole contains the brick]The brick is loose here.[otherwise]You see nothing special about the brick.[end if]"

Instead of pulling the brick when the brick was in the rectangular hole:
	try taking the brick instead;

After taking the brick when the brick was contained by the rectangular hole:
	reveal the lever;
	say "You manage to get your fingers in around the edge of the brick and pull it out." instead;

Instead of inserting something into the rectangular hole when the noun is not the brick:
	say "You can't quite fit [the noun] into the hole; it keeps bumping into something back in there.";

Instead of pushing the brick when the brick is in the rectangular hole:
	say "The brick does not push in. You might be able to pull it out, though.";

Instead of turning the brick when the brick is in the rectangular hole:
	say "The brick does not turn. You might be able to pull it out, though.";

After inserting the brick into the rectangular hole:
	conceal the lever;
	say "The brick slides easily back into the hole." instead.

Chapter 5 - Hidden lever

The short lever is a thing. Understand "metal" as the short lever.
The description is "It's just a short, metal lever."
Instead of pushing or pulling the lever when the blank stone wall is open:
	say "Nothing happens."

Instead of pushing or pulling the lever when the blank stone wall is closed:
	now the blank stone wall is open;
	ratchet the current tension to important;
	say "The lever moves slightly, then snaps back with a [i]click[r]. The wall swings away, revealing shadows beyond.[paragraph break]Pieter looks impressed. 'You're full of secrets and surprises, aren't you?' You can't bring yourself to tell him that you learned all your secrets from Bobby."

To reveal the lever:
	now the short lever is part of the rectangular hole.

To conceal the lever:
	now the short lever is part of Limbo.

Part 9 - The Tunnels (region)

The Tunnels is a region. Underneath the Fountain and the Secret Passage are in The Tunnels.

Part 10 - Chapel

Chapter 1 - Description and Scenery

The Chapel is a room. The description is "The chapel of the Goddesses is a spacious, octagonal room, empty of furniture. The wood-paneled walls glow warmly, reflecting the light from dozens of devotional candles; the high ceiling disappears in the shadows overhead. A corridor leads southwest, and a secret passage in the south wall leads into subterranean gloom."

Understand "temple", "shrine", "Goddesses", "of the Goddesses", "spacious", "octagon", "octagonal", "8 sided", "eight sided", "empty", "wall", "walls", "wood", "panel", "panels", "paneled", "bare", "glow", "glowing", "warm", "warmly", "high", "ceiling", "shadow", "shadows" as the Chapel.

Rule for printing the distant description of the Chapel when in the Secret Passage:
	say "The tunnel opens into a dimly lit room to the north. ";

Instead of inspecting the Secret Passage in the Chapel, try examining the blank stone wall.

Rule for printing the distant description of the Chapel when in the Lower Bailey:
	say "The chapel lies to the northeast. ";

The dead-end description of the Chapel is "Wood-paneled walls surround you on all sides; the only exits are southwest and south, through the secret passage."

Some candle-shelves are scenery, and supporters, and here. The description is "Little shelves built into the otherwise bare walls hold a number of devotional candles." Understand "little", "shelf", "shelves" as the candle-shelves.

Some devotional candles are scenery, and lit, and on the candle-shelves. The description is "The candles sit on little shelves built into the otherwise bare walls. It is traditional to light a candle when offering a prayer to one of the Goddesses; each of these wavering points of light is someone's hope, a wish, a dream." Understand "candle", "light", "candlelight", "wavering", "points", "point", "of light", "flame", "flames" as the devotional candles.

Instead of taking or extinguishing the devotional candles:
	say "That would be sacrilege!";

Instead of burning the devotional candles:
	say "There are plenty of candles lit already. You don't have time to fool with them right now."

Instead of praying in the Chapel:
	say "Surely here in this Chapel, the Goddesses would hear your prayers. But you can't tell if they're listening or not.";

Chapter 2 - Event on entering

After going to the Chapel for the first time:
	select theme "Chapel";
	move the torch to the metal bracket;
	say "Bobby jams his torch into a rusted bracket on the tunnel wall and steps through the secret door, gesturing you to follow. A prickle runs down your back as you step through after him. Are you really sneaking into the [i]Lord's Keep?[no line break] [r]You're not even sure what the punishment for that is... something a lot worse than a few days in jail, certainly.";
	try looking;
	move Bobby to the Chapel;
	update the character list;

Chapter 3 - Exiting

Before going to the Secret Passage from the Chapel during Creeping to Lord's Keep:
	say "[first time]'Come on,' Bobby whispers, pulling at your arm, 'You don't want to go yet, we just got here. Let's explore some more.' His eyes twinkle, and he gives you that smile that always says, [i]trust me this will be fun[r], but usually means, if running pell-mell for safety with your heart in your throat and the Lord's Guard on your heels is your idea of 'fun'[subsequently]Bobby shakes his head and urges you towards the southwest corridor[only]." instead.

Instead of going to the Lower Bailey from the Chapel during Returning From Lord's Keep:
	say "You've heard all you need to hear; it's too dangerous to stick around any longer.";

Part 11 - Lower Bailey

Chapter 1 - Description & Scenery

The Lower Bailey is southwest of the Chapel. The description is "[if Journey to the Ball is happening]The lower bailey has been cleaned, with gravel spread over the larger mud puddles. Guests to the ball are still trickling in from the south. The doors to the inner keep stand open to the north, and music and laughter drift out into the evening air.[otherwise]A thick wedge of shadow falls across the courtyard at the base of the Keep's outer wall, providing a relatively safe place to hide. The rest of the courtyard is flooded with moonlight, and bored-looking guards stroll back and forth over the gravel. You can return to the Chapel to the northeast.[end if]"

Understand "castle", "keep", "courtyard", "yard", "wedge", "thick", "shadow", "base", "of" as the Lower Bailey.

The distant description of the Lower Bailey is "The keep's lower bailey is [quick best route from location to Lower Bailey] of here."

Rule for printing the distant description of the Lower Bailey when in the Upper Bailey:
	say "The lower bailey is spread out beneath you, half in moonlight, half in shadow. ";

The dead-end description of the Lower Bailey is "The thick stone walls of the keep surround you on all sides."

Understand "mud", "puddle", "puddles" as the backdrop-floor when the location is the Lower Bailey.

Some stone steps are a staircase. The initial appearance is "Steep stone steps climb up to the top of the wall[if Creeping To Lord's Keep is happening]. You could make a dash up them, if you timed it right[end if]." The description is "Steep stone steps climb up to the top of the wall." Understand "steep", "stone", "step", "stairs", "staircase" as the stone steps.

The stone steps are in the Upper Bailey and the Lower Bailey.
The head of the stone steps is the Upper Bailey.
The foot of the stone steps is the Lower Bailey.

Chapter 2 - Guards, Guards!

Some patrolling Lords Guards are people, scenery, male, in the Lower Bailey. The description of the patrolling Lords Guards is "The Lord's Guards are dressed in polished mail and tabards emblazoned with the Toresal coat of arms, and each one wears a hefty sword at his hip. Fortunately, they haven't noticed you. Yet." The printed name is "Lord's Guards".

Instead of doing something other than examining to the patrolling Lords Guards:
	say "There must be less painful, though probably few quicker, ways of getting yourself killed.";

Some polished mail are held by the patrolling Lords Guards. Understand "things", "tabard", "tabards", "sword", "swords", "coat of arms" as the polished mail. The printed name of the polished mail is "guards things".

Instead of doing something to the polished mail:
	say "You can't get near enough to do that without attracting the attention of the guards.";

Chapter 3 - Event on entry

After going to the Lower Bailey for the first time:
	say "[command clarification break]";
	try looking;
	say "Bobby moves up quietly behind you. He puts his mouth very, very close to your ear and whispers, 'Up there,' pointing to the steps leading up to the top of the wall.";
	move Bobby to the location;
	update the character list;

Chapter 4 - Event on exiting

After going up from the Lower Bailey:
	say "You wait, watching the nearest guard until he turns and begins walking away, then sprint lightly up the steps. ";
	continue the action;

Instead of going northeast from the Lower Bailey during Creeping To Lord's Keep:
	say "Bobby tugs your sleeve and shakes his head. Again, he points up the stairs.";

Instead of going up from the Lower Bailey during Returning From Lord's Keep:
	say "You've heard all you need to hear; it's too dangerous to stick around any longer.";

Instead of going up from Lower Bailey during Journey to the Ball:
	say "Climbing up to the upper bailey would only attract unwanted attention. The ball is where you need to be.";

Instead of going south in Lower Bailey during Journey to the Ball:
	say "You can't go that way without attracting the attention of the guards.";

Instead of going north from the Lower Bailey during Bobby's Adventure:
	say "You can't go that way without attracting the attention of the guards.";

Part 12 - Upper Bailey

The Upper Bailey is up from the Lower Bailey. The description is "At the top of the wall you can lean out over the parapets and take in all the lands surrounding Toresal. It's a nice view, but torches set every few yards along the wall make this a dangerous spot to linger in. Stairs lead down into the courtyard below, and a dark archway opens to the north."

Understand "wall", "walls", "outer", "top", "of", "walkway", "stone", "stones", "parapet", "parapets", "narrow" as the Upper Bailey.

Rule for printing the distant description of the Upper Bailey when in the Lower Bailey:
	say "You can see a walkway at the top of the keep's outer walls. ";

Rule for printing the distant description of the Upper Bailey when in the Guardhouse:
	say "The walkway of the upper bailey is south of here. ";

Instead of facing down while in the Upper Bailey, try inspecting the Lower Bailey.

The dead-end description of the Upper Bailey is "It's a rather narrow walkway, and doesn't really go anywhere other than north."

Chapter 1 - Event on Entry

After going to the Upper Bailey for the first time:
	say "[command clarification break]";
	try looking;
	say "Bobby comes up the steps right behind you and crouches against the wall. After a quick look around to make sure the coast is clear, he nods towards the archway to the north.";
	move Bobby to the location;
	update the character list;

Chapter 2 - Event on exit

Instead of going down from Upper Bailey during Creeping To Lord's Keep:
	say "Bobby shakes his head, and again points north.";

Instead of going north from Upper Bailey during Returning From Lord's Keep:
	say "You've heard all you need to hear; it's too dangerous to stick around any longer.";

Chapter 3 - Scenery

Some torches are scenery, in the Upper Bailey. Instead of doing something to the torches, say "If you get anywhere near those torches, you'll be spotted in no time. Best to stay low and in the shadows." Understand "torch", "fire", "flames", "flame" as the torches.

The view of Toresal is scenery, in the Upper Bailey. The description is "[first time]The city is a pile of shadows in the distance, lit here and there with pinpricks of light: a guardsman carrying a torch, perhaps, or some night-owl sitting by the window with a lantern. Seeing it makes you realize just how absurd is your current predicament: you would be [i]safe[r] back in the city, snug in your bunk in Maiden House, or even skulking around the alleyways you know so well. Instead, you're breaking into Lord's Keep, sneaking along its walls, one slip-up away from getting yourself arrested or worse.[subsequently]It's an impressive view, no doubt, but you really don't have time to admire it right now.[only]". 

Understand "city", "skyline", "of", "shadow", "shadows", "distance", "pinprick", "pinpricks", "light", "town" as the view of Toresal.

Instead of doing something other than examining to the view of Toresal:
	say "Unfortunately, the city is too far away.";

Instead of facing southwest in the Upper Bailey:
	try facing south instead;
Instead of facing southeast in the Upper Bailey:
	try facing south instead;
Instead of facing south in the Upper Bailey:
	try examining the view of Toresal;

Part 13 - Guardhouse

Chapter 1 - Description

The Guardhouse is north of the Upper Bailey. "A narrow antechamber, with an open archway to the south and an iron-reinforced door to the north. Arrow slits to either side lend the door an important and dangerous air."

Understand "arch", "archway", "narrow", "antechamber", "dark", "darkness" as the Guardhouse.

The distant description of the guardhouse is "Through the archway you can make out some sort of guardhouse."

The dead-end description of the guardhouse is "The antechamber is enclosed on all sides except north and south."

Instead of facing north while in the Guardhouse, try examining the guardhouse door.

Instead of listening to the Guardhouse when in the Guardhouse:
	try listening to the arrow slit instead;

After assembling available exits of the Guardhouse:
	add north at entry 1 in the viable directions, if absent;

Chapter 2 - Event on entry

After going north to the Guardhouse for the first time:
	select theme "Guardhouse";
	say "[command clarification break]";
	try looking;
	say "Bobby creeps forward and presses himself against the wall next to one of the arrow slits. He hunches closer, peering around the slit's edge. Then he looks up at you and smiles. He points at you, points at the arrow slit, then taps his ear. [i]Listen[r], he's telling you.";
	move Bobby to the location;
	update the character list;

Chapter 3 - Keeping Bobby quiet

Every turn when in the Guardhouse:
	do nothing;
	[ now Bobby is silenced. ]

Chapter 4 - Scenery (Guardhouse Door)

North of the Guardhouse is a closed scenery door called the guardhouse door. The printed name of the guardhouse door is "door". The description of the guardhouse door is "The door is big and tough-looking, with bands of riveted iron across its thick wooden planks."

Understand "iron", "reinforced", "big", "tough", "tough looking", "band", "bands", "riveted", "rivet", "rivets", "thick", "wood", "wooden", "plank", "planks", "lock" as the guardhouse door.

Instead of going north in the Guardhouse, try opening the guardhouse door.

Instead of knocking on or opening the guardhouse door, say "Bobby waves you off with an incredulous look. [i]Are you NUTS?[no line break][r] his eyes seem to ask."

Chapter 5 - Scenery (Arrow slit)

The arrow slit is a container, scenery, in the Guardhouse. The description is "The arrow slits are rectangular holes in the wall, about two inches high and six inches wide. Someone on the inside could stick the end of a crossbow in there and fire at anyone out here, all the while being totally protected by the wall. Fortunately, no one is manning the slits right now.[paragraph break]From here, you can see flickering firelight through the arrow slit, and hear voices on the other side."
Understand "slits", "rectangle", "rectangles", "rectangular", "holes", "hole", "wall", "flickering", "light", "fire", "firelight", "voice", "voices" as the arrow slit.

Instead of searching the arrow slit:
	say "Bobby puts his hand on your chest, holding you back. He shakes his head. [i]Not so close. Too dangerous.[r]"

Section 1 - "Listen at" the slit

Understand "listen at [something]" as listening.

Chapter 6 - More conversations overheard

Instead of listening to the arrow slit for the first time:
	spool through dialogue in the table of overheard guardhouse conversation;

Instead of listening to the arrow slit for the second time:
	spool through dialogue in the table of overheard guardhouse conversation;

Instead of listening to the arrow slit:
	say "The guards' conversation has moved on to other things, and Bobby is motioning that you should leave.";

Table of overheard guardhouse conversation
text
"You can hear several voices coming from the room on the other side of the arrow slit — guards relaxing on break, probably.[paragraph break]'...and I says, I don't care [i]who[r] hired you, people are tryin['] to do honest business here, and you're steppin['] on toes. Take yer men and hit the road, or else you can continue searchin['] for yer urchin boy in the city jail. And this tough fellow, him so hard with his sword and his scars and all, he just starts to cry, blubberin['] like a frightened scullery maid!'[paragraph break]'Haw! Yer lyin['], so you are!'[paragraph break]'Goddesses witness it, [']tis the truth! I could hardly credit it meself, and I weren't that impressed with the man to begin with. Go on, I says, and gave him a boot to the fundament to send him on his way.'[paragraph break]There is general laughter. You glance at Bobby, but he motions you to keep listening.[conditional paragraph break]"
[row marker]
"'You seen tomorrow's detail yet?'[paragraph break]'Yeah, dress-up duty. Polishing helmets so we can look all pretty for the lords and ladies coming down to Baron Fussypants['] ball.'[paragraph break]'Not just lords and ladies, I heard. Word has it the Queen herself is on the guest list.'[paragraph break]'Oh, pull the other one.'[paragraph break]'Goddesses witness it. They say she's comin['] down with her daughter in tow, that's why Fossville's hostin['] the ball in the first place. He's tryin['] to get in the Queen's good graces, so's he can marry the girl.'[paragraph break]'What, the Queen?'[paragraph break]'No, you dim. He wants to marry the [i]Princess[r]. Our Lord Fussypants is trying to position himself for the throne.'[paragraph break]'Well, he's welcome to it, long as I'm paid at week's end. Of course, I'm not looking forward to spending a night standing at attention for a horde of fancy, feathered fops to goggle and gaggle at, but I guess it's better than latrine duty...'[paragraph break]Bobby pulls you away. He's grinning, his eyes wide and his eyebrows cocked. [i]Isn't it great?[no line break][r][paragraph break]But you're too nervous to care. There are who knows how many men of the Lord's Guard right on the other side of that door. Someone could walk out at any second. You grab Bobby's arm and give it a firm yank — [i]It's time to go.[r][conditional paragraph break]"

Chapter 7 - Restraining exit

Before going south in the Guardhouse:
	if the table of overheard guardhouse conversation is not empty,
		say "Bobby motions you back, pointing at the arrow slit again. There's something he obviously wants you to hear." instead;

Part 14 - The Lord's Keep, as a region

The Greater Lord's Keep is a region. The Chapel, the Lower Bailey, the Upper Bailey, the Guardhouse, the Entrance Hall, the Ballroom and the War Room are in the Greater Lord's Keep.

The Lord's Keep is a region. The Chapel, the Lower Bailey, the Upper Bailey and the Guardhouse are in the Lord's Keep.

Part 15 - Returning from Lord's Keep

Returning from Lord's Keep is a scene. Returning from Lord's Keep begins when Creeping to Lord's Keep ends.

Returning from Lord's Keep ends when the location is a holding cell.

Bobby's Adventure ends when Returning from Lord's Keep ends.

After assembling during Returning From Lord's Keep:
	if Bobby is on-stage,
		add Bobby to the roster of available characters, if absent;

Every turn when the player is on the move during Returning From Lord's Keep:
	if Bobby is on-stage and the current tension is at most unimportant:
		say "Bobby [one of]follows you[or]comes along quietly[or]quickly brings up the rear[or]follows right behind you[at random].";
		move Bobby to the location;
	continue the action;

Instead of following Bobby when the player can see Bobby during Returning from Lord's Keep:
	if the table of overheard guardhouse conversation is empty:
		say "Bobby motions for you to go first, pointing." instead;
	continue the action;

Instead of following Bobby when the location is Underneath the Fountain during Returning from Lord's Keep:
	try going up;

Book 6 - Jail

[ Moved this region declaration up here, because Inform gets cranky if you refer to a region before it's declared. Also, split the "Jail Region" into "Prison Region" (because there's too much namespace conflict with the word 'jail' already) and the "Sewers Region" (see Book 7, below) ]

The Prison Region is a region. The Jailhouse, Jail-Cell, Jail-Cell-2, Jail-Cell-3, and Drain Room are in the Prison.

Part 1 - Jailhouse

Escaping Jail is a scene. Escaping Jail begins when the player is in Jail-Cell. Escaping Jail ends when the location is the sewer.

The Jailhouse is a room. The description of the jailhouse is "The jailhouse is hardly more comfortable than the cells themselves, just a crude, small room of unfinished stone, with a wooden bench and table for the guards. Cells are south, west, and north. A foul smell comes from a tiny alcove to the southwest, and a passageway to the east leads out." Understand "main", "crude", "small", "jail", "gaol", "prison", "unfinished", "stone" as the Jailhouse.

The dead-end description of the Jailhouse is "There's no way out that direction."

Rule for printing the distant description of the Jailhouse when in the Drain Room:
	say "The main jailhouse room is northeast of here. ";

When escaping jail begins:
	select theme "InJail";
	Bobby wakes up in two turns from now;
	repeat with contraband running through things carried by the player:
		if the contraband is not the baggy threadbare clothes, remove the contraband from play;

Section 1 - Scenery

The jailhouse-table is a supporter, scenery, in the Jailhouse. Understand "wood", "wooden", "furniture", "rough", "crude", "crudely", "carved", "table" as the jailhouse-table. The description of the jailhouse-table is "The furniture is rough and crudely carved."

A jailhouse-bench is a supporter, scenery, enterable, in the Jailhouse. The printed name of the jailhouse-bench is "bench". Understand "wood", "wooden", "furniture", "rough", "crude", "crudely", "carved", "bench" as the jailhouse-bench. The description of the jailhouse-bench is "The furniture is rough and crudely carved."

Instead of entering the jailhouse-bench:
	say "You don't have time to rest — you need to get out of here!";

Section 2 - The Way Out

[ Somewhere along the way, "the way out" got conflated with the way to the drain room, but they need to be separate directions for this scene to make since. Since we never actually *go* anywhere with Jacobs except to instant death, however, it will serve to make an empty room here with a distant description that can be inspected. --MSG ]

The way-out [it's hyphenated because I'm not confident that it won't conflict with the direction "out" in the code] is a room, east of the Jailhouse. Understand "passage", "passageway", "other", "area", "areas", "of", "prison", "jail", "gaol", "way", "out" as the way-out. The way-out is outside of the Jailhouse. The printed name is "way out".

The distant description of Way-out is "The east passage leads out to other areas of the prison... and hopefully to a way out."

Part 2 - Three cells

Chapter 1 - The holding cell kind

Section 1 - Description

A holding cell is a kind of room. The description of a holding cell is "[cell description]". The printed name of a holding cell is "Holding Cell".

To say cell description:
	let d be north;
	if the location is Jail-Cell-2:
		let d be south;
	if the location is Jail-Cell-3:
		let d be east;
	say "A dank, cramped, foul-smelling cell. A pair of ragged burlap blankets and a few lumps of filthy straw litter the floor. The door is to the [d]. ";

Understand "dank", "small", "cramped", "foul", "foul smelling", "dirty", "filthy", "cell", "jail", "prison", "gaol" as a holding cell.

Section 2 - Restrained Exit

Instead of going east from the Jailhouse: say "There are more guards out there, and you don't know the way. You'll never escape from this jail all by yourself." instead.

Instead of following the prisoner when the location is the jailhouse and the prisoner is off-stage:
	try going east;

Chapter 2 - The holding cell door kind

Section 1 - Description

A cell door is a kind of door. The description of a cell door is "[The item described] is made of stout wood reinforced with bands of iron. A tiny viewing window in the upper half of the door allows guards to look in and prisoners to look out. [The item described] is [if the item described is open]open[otherwise]closed[end if]."

The printed name of a cell door is "cell door". A cell door has a direction called tmpdirection. The tmpdirection of a cell door is usually north. A cell door is usually locked. A cell door is usually scenery. A cell door is always lockable.

Understand "stout", "wood", "wooden", "reinforced", "bands", "band", "metal", "iron", "lock", "door", "cell door" as a cell door.

The matching key of a cell door is usually the piece of wire.

[ Unfortunately we're not allowed to use "Understand the tmpdirection property as describing a cell door" because I7 can't parse direction names in typed commands in this way. Disappointing. ]

Before printing the name of a cell door (called x):
	if the location is the Jailhouse:
		say "[tmpdirection of x] ";

Section 2 - Looking through a cell door (and seeing the other side)

Instead of searching a cell door:
	try looking through the noun instead;
	[as in, "look in north cell"] 

Instead of looking through a cell door:
	if closed,
		say "You peer through the closely spaced bars. ";
	if the location is a holding cell begin;
		say "You can see the jailhouse main room on the other side[if open] of the door[end if].";
	otherwise;
		let the cell in question be the other side of the noun;
		let the body count be the number of people in the cell in question;
		say "You can see a cramped, filthy cell on the other side[if open] of the door[end if]. ";
		[
		And the following code is a good argument for a feature request.
		]
		if the body count > 0 begin;
			let x be the first thing held by cell in question;
			while x is not a person
				repeatedly let x be the next thing held after x;
			repeat with i running from 1 to body count begin;
				if i is 1 begin;
					say "[A x]";
				otherwise if the next thing held after x is nothing;
					say "[if i is not 2],[end if] and [a x]";
				otherwise;
					say ", [a x]";
				end if;
				let x be the next thing held after x;
				if x is not nothing begin;
					while x is not a person
						repeatedly let x be the next thing held after x;
				end if;
			end repeat;
			if the body count is 1 begin;
				say "[is or are for the first thing held by the cell in question] ";
			otherwise;
				say " are ";
			end if;
			say "inside. " ;
		end if;
		say "[paragraph break]";
	end if;

Section 3 - The viewing window kind

A viewing window is a kind of container. The description of a viewing window is "The viewing window is just a tiny square, no more than two handspans to a side, fitted with tough, closely spaced iron bars." The printed name of a viewing window is "viewing window". Understand "tiny", "square", "small", "bar", "bars", "barred", "tough", "iron", "close", "closely spaced" as a viewing window.

After printing the name of a viewing window (called win):
	if the location is the Jailhouse:
		say " in the [tmpdirection of holder of win] door";

Instead of looking through a viewing window (called win):
	try looking through the holder of win;

Section 4 - Reaching through windows

[ I don't actually want to implement this as an action... it doesn't generalize well, it would create too many special cases, and it adds nothing to gameplay. However, it is a reasonable thing for someone to type when trying to get out of jail, so we'll add some error messages to handle it.]

Understand "reach" as a mistake ("[reach message]").
Understand "reach [something]" as a mistake ("[reach message]") when Skirmish is not happening.
Understand "reach in/through/into/between/for [something]" as a mistake ("[reach message]") when Skirmish is not happening.
Understand "reach [something]" as taking when Skirmish is happening.
Understand "reach in/through/into/between/for [something]" as taking when Skirmish is happening.
Understand "reach arm/hand in/through/into/between [something]" as a mistake ("[reach message]").
Understand "reach in/through/into/between [viewing window]" as a mistake ("[lockpicking message]").
Understand "reach arm/hand in/through/into/between [viewing window]" as a mistake ("[lockpicking message]").
Understand "put arm/hand in/through/into/between [viewing window]" as a mistake ("[lockpicking message]").

To say reach message:
	say "You don't need to 'reach' anything in this game. If you want to see what's inside something, just LOOK IN it. ";

To say lockpicking message:
	say "If you want to try to unlock the door, just type UNLOCK DOOR. ";

Chapter 3 - Construction of the Jail Cells

Section 1 - Three Cells

Jail-Cell is a holding cell.
Jail-Cell-2 is a holding cell.
Jail-Cell-3 is a holding cell.

The Jailhouse overlooks Jail-Cell, Jail-Cell-2, and Jail-Cell-3.
Jail-Cell overlooks the Jailhouse.
Jail-Cell-2 overlooks the Jailhouse.
Jail-Cell-3 overlooks the Jailhouse.

Instead of inspecting a holding cell (called target):
	let dir be the quick best route from location to target;
	try facing dir.

Instead of facing south in the Jailhouse, try searching cell-door-1.
Instead of facing north in the Jailhouse, try searching cell-door-2.
Instead of facing west in the Jailhouse, try searching cell-door-3.

Rule for assembling available exits of Jail-Cell:
	change the viable directions to { north };

Section 2 - Three tricky doors

A locked cell door called a cell-door-1 is south of the jailhouse and north of Jail-Cell. The tmpdirection is south. A viewing window called a Viewing Window 1 is part of cell-door-1. Understand "south" as cell-door-1 when the location is the Jailhouse. Understand "south" as Viewing Window 1 when the location is the Jailhouse.

Cell-door-1 is outside from Jail-Cell.

Instead of facing north in Jail-Cell, try searching cell-door-1.

A locked cell door called a cell-door-2 is north of the jailhouse and south of Jail-Cell-2. The tmpdirection is north. Understand "north" as cell-door-2 when the location is the Jailhouse. A viewing window called a Viewing Window 2 is part of cell-door-2. Understand "north" as Viewing Window 2 when the location is the Jailhouse.

Instead of facing south in Jail-Cell-2, try searching cell-door-2.

A locked cell door called a cell-door-3 is west of the jailhouse and east of Jail-Cell-3. The tmpdirection is west. Understand "west" as cell-door-3 when the location is the Jailhouse. A viewing window called a Viewing Window 3 is part of cell-door-3. Understand "west" as Viewing Window 3 when the location is the Jailhouse.

Instead of facing east in Jail-Cell-3, try searching cell-door-3.

Before going through a cell door from the Jailhouse, say "[one of]It's such a huge relief just to be [i]out[r] of a jail cell; you definitely have no desire to get back into one.[or]No, you'd rather stay out of jail cells for a while.[or]You're not going back in there.[stopping]" instead;

Section 3 - Scoping

After deciding the scope of the player while in the Jailhouse:
	if the bald headed man is in Jail-Cell-2 begin;
		place the bald headed man in scope;
		place the skinny man in scope;
	end if;

Before talking to someone when the location is the Jailhouse:
	ignore the can't reach inside rooms rule;

Part 3 - Our Cell

Chapter 1 - Scenery

Section 1 - Blankets

Some burlap blankets are scenery, in Jail-Cell. The description is "The blankets are threadbare, filled with holes, and stained with Goddesses-know-what. You can hardly believe that anyone would actually cover themselves with something like this." Understand "ragged", "blanket", "threadbare", "hole", "holes", "holey", "stained", "stain", "stains" as the blankets.

Instead of doing something other than examining to the blankets, say "You'd just as soon leave the blankets where they are." 
Instead of smelling the blankets, say "You can smell the blankets just fine from where you're standing."

Instead of smelling Jail-Cell:
	say "It smells terrible in here.";

Section 2 - Filthy Straw

Some straw is scenery, in Jail-Cell. The description is "From the smell and the look of it, this straw is all prisoners ever get in the way of a toilet." Understand "damp", "pile", "piles", "pile of", "piles of", "lump", "lumps", "lump of", "lumps of", "filthy", "filth", "disgusting", "nasty", "nasty stuff" as the straw. 

After printing the description of the straw when the piece of wire is part of the straw:
	say " You can't see the thing that Jacobs threw from here: you'll have to poke around in the nasty stuff to find it. ";

Instead of smelling the straw,
	say "You can smell it just fine from where you're standing."

[Instead of taking or raising the straw when the piece of wire is part of the straw:
	try searching the straw;

Instead of looking under the straw when the piece of wire is part of the straw:
	try searching the straw;]

Instead of searching, taking, raising, or looking under the straw when the piece of wire is part of the straw:
	now the player carries the piece of wire;
	say "Holding your breath, you gingerly poke around in the damp, filthy straw, and eventually you find the thing that Jacobs threw in there: it's a short piece of stiff wire."

Instead of doing something other than examining or smelling to the straw:
	say "The straw is disgusting; you'd rather not even get near it.";

Rule for deciding the concealed possessions of the straw:
	if the particular possession is the piece of wire,
		yes;

Understand "poke around/about in/through [something]" as searching.

Chapter 2 - Event on entering

Before looking in Jail-Cell for the first time:
	say "Slowly the world comes back to you. You feel something cold and hard against your cheek; something gritty and scratchy under your hand. You hear footsteps on stone, muttering voices that fade in and out. Someone laughs, a raw, cruel sound. A loud squeak, and an echoing, metallic crash. The footsteps fade away.[paragraph break]You open your eyes. It's still dark.[paragraph break]No, it's just [i]dim[r]. Dim light trickles in from somewhere above you.[paragraph break]You sit up, and a tender spot on the back of your head throbs. Red and purple splotches bloom in front of your eyes, then fade away. After a moment or two, your head clears, and you can see where you are.";

Chapter 3 - Bobby's Voice

Bobby's Voice is a scene. Bobby's Voice begins when Bobby is in the Jailhouse. Bobby's Voice ends when Bobby is off-stage.

After deciding the scope of the player when talking to during Bobby's Voice:
	place Bobby in scope;

Section 1 - Introduction

[ I was going to use a new object for this bit, but... well, why not use the real, authentic Bobby? ]

At the time when Bobby wakes up:
	move Bobby to the Jailhouse; [Just for convenience. Really he ought to be in his cell, but never mind: he won't be described anywhere because we don't mention occupants of the Jailhouse when looking through the viewing window.]
	carry out the assembling activity;
	add Bobby to the roster of available characters, if absent;
	carry out the character-listing activity;
	start conversation with Bobby on BO16, even if not present;

Section 2 - Further conversation with Bobby - In the Jailhouse

BO16 is a quip. The display text is "Suddenly you hear a voice whisper your name. [i]'Jack...'[r][paragraph break]At first you think it's the creepy guy in the corner, but he hasn't moved. You're still staring at him when the whisper comes again, louder this time: [i]'Jack...'[r] It's coming from somewhere outside the cell."

BO17 is a transitional quip. The menu text is "'Bobby? Is that you?'". The display text is "You rush up and press your face against the viewing window, but you can't see anything out there except an empty room. 'Bobby?' you call, trying to keep the fear out of your voice. 'Bobby, is that you? I'm locked in here, Bobby, help me!'". The following quip is BO18.

BO18 is a quip. The display text is "The voice chuckles, then coughs weakly. 'I'm in the cell next to yours, Jack. I'm locked in, too. Sorry.'[paragraph break]Your heart sinks a little at that, but at least being trapped in a cell with Bobby nearby is a [i]little[r] bit better than being trapped in a cell with no one around to help you at all... right?[paragraph break]'Jack, listen,' Bobby says. His voice is ragged and hoarse; you're worried that the mercenaries might have hurt him badly. 'I'm going to get us out of this mess, don't worry. But in case I'm not able to meet up with you after, you need to listen carefully.'"

BO19 is a transitional quip. The menu text is "'What do you mean?'". The display text is "'Not able to meet up with me?' you whisper, dismayed. 'What do you mean? What-' but Bobby cuts you off.". The following quip is BO20.

BO20 is a quip. The display text is "'Jack, [i]listen to me[r]. Go to Fossville's estate in the city. Use the rooftops. There's an alley behind the shops on Commerce Street where you can climb up.'[paragraph break]'You want me to break into [i]Baron Fossville's house?[no line break][r]' you whisper back, incredulously. 'Bobby, I don't understand-'[paragraph break]'He has secrets,' Bobby goes on, 'that you need to know. Secrets about your [i]father[r].'[paragraph break]You pause, stunned.[paragraph break]'Look for a letter-' says Bobby, but he is cut off by a door slamming open elsewhere in the room. Guards move past your cell door; one of them slams a wooden cudgel against the bars right in front of your face, and you stagger back."

BO21 is a transitional quip. The menu text is "'Bobby, what's happening?'". The display text is "'Bobby?' you cry, '[i]Bobby?[no line break][r] What's happening? What letter?'". The following quip is BO22.

BO22 is a quip. The display text is "You hear a metallic rattling, the squeak of hinges. Then several meaty [i]thuds[r], mixed with grunts and cries of pain. The guards reappear, this time dragging Bobby with them. He looks up as he passes the viewing window, and you get a brief glimpse of his face — bruised, bloody, but still smiling. You'd swear he was trying to wink at you, except that his eye is swollen shut.[paragraph break]'Fossville, Jack!' he yells as they bundle him off. 'The rooftops! [i]Look for the letter![no line break][r]'[paragraph break]Somewhere a door slams one final time... and then everything is silent."

The response of BO16 is { BO17 }.

The response of BO18 is { BO19 }.

The response of BO20 is { BO21 }.

Section 3 - Structure of conversation

The redundant script is a list of objects that varies.
The redundant script is { [NOT BO16,] BO18, BO20, BO22}.

Every turn during Bobby's Voice:
	let the event be entry 1 of the redundant script;
	if the event is unfired:
		start conversation with Bobby on the event, even if not present;
	if the number of entries in the redundant script is at least 1:
		remove entry 1 from the redundant script;
	
[ One of those cases where we carry on a conversation with a character who is not physically present. A number of ways to achieve it; fiddling with "rule for terminating" seems to be the best bet. ]

Rule for terminating conversation with Bobby during Bobby's Voice:
	if the current conversation is { }:
		[ conversation is genuinely over ]
		now the current conversationalist is the player;

Section 4 - Outro

After firing BO22:
	remove Bobby from play;
	update the character list;
	Jacobs starts talking in one turn from now;

Chapter 4 - Jacobs

A person can be free or caught. A person is usually free.

Section 1 - Description

The prisoner is an improper-named man in the Jail-Cell. The initial appearance of the prisoner is "[initial appearance of Jacobs]"

Rule for printing the name of the prisoner:
	if the prisoner is proper-named:
		say "Jacobs";
	otherwise:
		say "hulking shape";

The description of the prisoner is "[The item described] is a huge brute, with a face that looks young, but already grizzled and rough beyond his years. His wide, watery eyes jitter constantly. His lip is curled in a constant half-sneer, as though his confinement in jail is merely a passing diversion for him. His clothes, though stained and rumpled, seem too rich to belong to a common thug."

Understand "man" as the prisoner.
Understand "hulking", "huge", "brute", "grizzled", "rough", "sneering", "sneer" as the prisoner.
Understand "bloodshot", "eye", "eyes", "blood shot" as the prisoner.
Understand "shape", "figure" as the prisoner when the prisoner is improper-named.
Understand "Jacobs" as the prisoner when the prisoner is proper-named.

Instead of kissing the prisoner, say "Somehow that doesn't seem very prudent."

Section 2 - Initial appearance, at various moments

To say initial appearance of Jacobs:
	if the location is Jail-Cell and cell-door-1 is locked:
		if JA8 is fired:
			say "Jacobs sits in the corner, apparently in a deep sulk. ";
		otherwise:
			say "[if improper-named]A hulking shape squats in the corner, staring at you with bloodshot eyes[otherwise]Jacobs stands nearby[end if]. ";
	otherwise:
		say "Jacobs stands nearby. ";

Instead of doing something other than examining or talking to the prisoner:
	say "He looks way too scary to mess with.";

Section 3 - Jacobs's Conversation 

JA1 is a quip. The display text is "He doesn't answer you at all; he just keeps staring with those creepy, bloodshot eyes."

JA2 is a quip. The display text is "Suddenly the shadowy man in the corner puts his huge, thick-fingered hands on his knees and pushes himself up. 'Well,' he grunts in a thick, rough voice, 'now that the guards are busy takin['] care of [i]him[r]...' the man stretches, glances at you, and smiles. It is not a friendly smile.[paragraph break]'Hi, kid. Name's Jacobs.'"

JA3 is a transitional quip. The menu text is "'My name's Jack.'". The display text is "'Um... hi,' you manage to say. 'My name's, uh... Jack.'"
The following quip is JA5.

JA4 is a transitional quip. The menu text is "'Stay away from me!'". The display text is "Without meaning to, you find yourself backing into the far corner of the cell. 'Stay away from me,' you say, trying to sound brave. 'I have... I have powerful friends!'"
The following quip is JA5.

JA5 is a quip. The display text is "The man named Jacobs glances at you, a flicker of amusement crossing his dull, reddish eyes, but doesn't otherwise respond."

JA6 is a transitional quip. The menu text is "'Do you know any way to get out of here?'". The display text is "'Do you, uh...,' you clear your throat, '...do you know some way we could get out of this place?'[paragraph break]Jacobs raises an eyebrow. 'Well now, little runt. I suppose I do.'"
The following quip is JA7.

JA7 is a quip. The display text is "The hulking man walks over to the cell door and squints at it for a while, standing with his hands on his hips. 'My father'll have me out of here by tomorrow, of course,' he mutters, 'but I don't like to wait that long.' He barely looks at you — in fact, you're not sure that he's talking [i]to[r] you at all. It's more like he's just muttering to himself.[paragraph break]He bends over and digs something out of the side of his boot, then stands up and thrusts his hand through the viewing window. He curses and strains to push his arm through, but the bars are too narrowly spaced — he can't reach out past his elbow. 'Damn,' he mutters, 'they must have changed the bars since last time...'"

JA8 is a quip. The display text is "'[i]Aaah![no line break][r] Goddesses curse it!' Jacobs jerks his hand back, rubbing his wrist.[paragraph break]'Ah, forget it!' he snarls, and tosses something small across the room. It skitters across the floor and lands in a pile of straw. Jacobs goes back to his corner and sits down heavily. 'Guess I'm in here for the duration after all.'"

After firing JA8: 
	now the piece of wire is part of the straw;

[ JA9 is a quip. The menu text is "'What is that thing?'". The display text is "'What is that thing?' you ask, squinting at the tiny wire — although you're pretty sure you already know.[paragraph break]'What are you, dim?' snarls Jacobs. 'It's a lockpick. Now take it and help me get this door open.'"

JA10 is a transitional quip. The menu text is "'I'd rather not.'". The display text is "Picking locks has never been your strong suit, and the idea of this brute breathing over your shoulder while you try it gives you stomach-flips. 'Uh-uh,' you say, holding up your hands and backing away. 'I'd rather not, if it's all the same.'"
The following quip is JA11.

JA11 is a quip. The display text is "'Oh, for the love of — [i]fine![no line break][r]' he yells. He throws the lockpick to the floor and stomps back to his corner. 'I guess I'll just wait for my father, then. A hundred cells in this twice-cursed jail, and I get stuck in the one with a dim-witted, muling runt and a tiny window...'"

JA12 is a transitional quip. The menu text is "'Okay.'". The display text is "'Um, okay,' you say, and pluck the wire from his thick, calloused fingers."
The following quip is JA13.

JA13 is a quip. The display text is "'That's the spirit, runt!' Jacobs says. Suddenly he comes over and puts his huge hands around your waist, hoisting you up as though you weighed no more than a sack of feathers. He feels strong enough to snap you in half.[paragraph break]'You ever pick a lock before, runt?'"

JA14 is a quip. The menu text is "'Yes.'". The display text is "You manage a nod. It's [i]sort of[r] true... Bobby showed you a few tricks once, and you've practiced on the front door of Maiden House a couple of times. You've never had to do it when your life depended on it, reaching through a tiny, barred window, with a giant, crazy-eyed brute breathing down your neck. But perhaps it's best not to get hung up on details.[paragraph break]'Good,' Jacob says, pushing you up against the viewing window. 'This one should be easy, long as you can reach it.'"

JA15 is a quip. The menu text is "'No.'". The display text is "'No,' you lie.[paragraph break]'Well, doesn't matter,' Jacobs mutters, pushing you up against the viewing window. 'These jail locks aren't complicated. Just reach down there and jiggle around a bit.'" ]

JA16 is a quip. The display text is "Jacobs turns to you. 'I heard what your friend in the other cell said, before they dragged him out,' he says. 'Sounds like Fossville's taken quite an interest in you.'"

JA17 is a transitional quip. The menu text is "'Yes, he has.'". The display text is "You nod. 'I guess he has,' you say, 'but I have no idea why.'"
The following quip is JA19.

JA18 is a transitional quip. The menu text is "'I don't know what you're talking about.'". The display text is "Something tells you not to trust this man. 'I don’t know what you're talking about,' you say, trying to sound tough and sullen. 'I don't know what [i]he[r] was talking about, either. Why would Fossville be interested in me?'[paragraph break]Jacobs gives you a shrewd look. You're not sure if he believes you or not."
The following quip is JA19.

JA19 is a quip. The display text is "The big man seems to consider something. 'You can come with me if you want,' he says. 'I'll take you somewhere safe.'[paragraph break]A shaky, wheedling voice from the north cell calls out: 'Don't you listen to him, girl! That's Jacobs the Elder's son; he'd as soon sell you to slavers as help you.'[paragraph break][i]'Shut up, you!'[no line break][r] yells Jacobs, pounding on the cell door in a sudden rage."

JA20 is a transitional quip. The menu text is "'No, thanks.'". The display text is "'No offense,' you say, 'but I'll find my own way out, thanks.'"
The following quip is JA21.

JA21 is a quip. The display text is "'Fine, then,' Jacobs snarls, 'good luck getting past the guards, runt.' He turns on his heel and stalks out, leaving you alone with the other prisoners."

JA22 is a quip. The menu text is "'Sure, let's go.'". The display text is "'Smart kid,' Jacobs says, following you closely. You can hear the other prisoner shouting something behind you, but soon their voices fade as you hurry down the corridor, leaving the cells far behind you.[paragraph break]Jacobs leads you down dim, torchlit halls until you reach a wooden door. Jacobs motions you to be silent, then puts his ear up to the wood. After a moment he nods, opens the door, and then shoves you through it into the next room.[paragraph break]Bright lantern-light blinds you, and when your eyes clear you see a dozen men of the Lord's Guard standing in a semicircle around you... and in the middle, the man in the red robes.[paragraph break]'Little mouse...' murmurs the man in the red robes, '...always finding some new hole to crawl through.'[paragraph break]Behind you, Jacobs puts his heavy hand on your shoulder and grips it tight. 'Like I said, runt,' he laughs, 'Fossville has taken a special interest in you. And aiding the Baron in his interests can be very profitable.'[paragraph break]The red-robed man and the guards close in on you. This time, you do not escape."

JA23 is a quip. The display text is "Jacobs snorts. 'You must be dim, you pathetic boozer.' To you he says, 'We don't have time for him. I can take care of the guards; he can barely unlace his breeches to take a leak. If you want to do the smart thing, then let's go now.' And he jerks his thumb toward the east passage."

JA24 is a quip. The display text is "'Hey mister...' you start, nervously.[paragraph break]'What?' snaps Jacobs.".

JA25 is a quip. The menu text is "'How are you going to get out of here?'". The display text is "'How are you going to get out of here?' you ask.[paragraph break]Jacobs chuckles, not very pleasantly. 'I'm going to wait,' he says, 'until my father gets me out the old-fashioned way, by bribing the guards. Too bad for you — I doubt they'll throw you into the bargain.'".

JA26 is a quip. The menu text is "'Who is your father?'". The display text is "'So who is your father anyway?' you ask.[paragraph break]The man cocks an eyebrow at you. 'You've never heard of Jacobs the Elder? Big mansion on East Commerce Street? He only owns half the city.' Jacobs sneers. 'Not that it makes any difference for you, at this point.'".

JA27 is a quip. The menu text is "'What were you trying to do with the door?'". The display text is "'What were you trying to do just now?' you ask. 'With the door and everything?'[paragraph break]Jacobs sighs. 'I was [i]trying[r] to pick the lock. But the lock's on the outside, and my arm's too big to fit through the bars. Which means now I'm just killing time.'".

JA28 is a quip. The menu text is "'Do you mind if I try picking the lock?'". The display text is "'You mind if I give it a try?' you ask, jerking your thumb towards the door.[paragraph break]Jacobs looks at you incredulously. 'You ever picked a lock before, runt?'[paragraph break]You nod, thinking of the few times you've practiced with Bobby.[paragraph break]He shrugs, leans his head back against the wall, and closes his eyes. 'Do whatever you want, runt. Just be quiet about it.'".

JA29 is a quip. The menu text is "'What's this wire?'". The display text is "'What is this thing?' you ask, holding up the wire.[paragraph break]'What are you, dim?' snorts Jacobs. 'It's a lockpick, what do you think? And it's worthless, because the lock's on the outside of that door, and I can't get my arm through the bars. So you can stick it up your nose, for all I care.'".

JA30 is a quip. The menu text is "'Never mind.'". The display text is "'Uh, never mind,' you say. Jacobs grunts and looks away at nothing in particular.".

JA31 is a quip. The menu text is "'Okay.'". The display text is "'Um, okay,' you say. Jacobs doesn't even bother to respond.".

The response of JA1 is { }.

The response of JA2 is { JA3, JA4, JA6 }.

The response of JA5 is { JA6 }.

The response of JA7 is { }.

The response of JA8 is { [ JA9, JA10, JA12 ] }.

[ The response of JA9 is { JA10, JA12 }.

The response of JA11 is { }.

The response of JA13 is { JA14, JA15 }.

The response of JA14 is { }.

The response of JA15 is { }. ]

The response of JA16 is { JA17, JA18 }.

The response of JA19 is { JA22, JA20 }.

The response of JA21 is { }.

The response of JA22 is { }.

The response of JA23 is { JA22, JA20 }.

The response of JA24 is { JA25, JA27, JA28, JA29, JA30 }.

The response of JA25 is { JA26, JA27, JA28, JA29, JA31 }.

The response of JA26 is { JA27, JA28, JA29, JA31 }.

The response of JA27 is { JA25, JA26, JA28, JA31 }.

The response of JA28 is { JA25, JA26, JA31 }.

The response of JA29 is { JA25, JA26, JA28, JA31 }.

Section 4 - Structure of Jacobs's Conversation

Rule for initiating conversation with the prisoner:
	change the chosen opening gambit to JA1;

At the time when Jacobs starts talking:
	if JA2 is unfired, start conversation with the prisoner on JA2;

[And here's a rule in case the player starts talking to Jacobs before he starts talking:]

Rule for initiating conversation with the prisoner when BO22 is fired and JA2 is unfired:
	change the chosen opening gambit to JA2;

After firing JA2:
	now the prisoner is proper-named;
	update the character list;

Every turn when JA7 is fired and TIMER_lockpicking is unfired:
	increment TIMER_lockpicking;

TIMER_lockpicking is a timer. The top end is 3.

Rule for firing TIMER_lockpicking:
	start conversation with the prisoner on JA8;

Instead of talking to the prisoner when JA7 is fired and JA8 is unfired:
	say "Jacobs ignores you completely.";

Instead of talking to Jacobs when JA30 is fired and the player is in the Holding Cell: say "Jacobs grunts noncommittally."

Instead of talking to Jacobs when JA31 is fired and the player is in the Holding Cell: say "Jacobs grunts noncommittally."

[ After firing JA8:
	now the prisoner carries the piece of wire;

Instead of talking to the prisoner when JA8 is fired and JA11 is unfired and JA14 is unfired and JA15 is unfired and the player does not have the piece of wire:
	say "Jacob grunts. 'Take the lockpick, runt. Ain't got all day.'";

Every turn when JA8 is fired and TIMER_taking_lockpick is unfired:
	increment TIMER_taking_lockpick;

TIMER_taking_lockpick is a timer. The top end is 4.

Rule for firing TIMER_taking_lockpick:
	if JA11 is unfired and JA13 is unfired:
		carry out the firing activity with JA11;
		now the current conversation is { };
		carry out the terminating conversation with activity with the prisoner;

After firing JA11:
	move the piece of wire to the Jail-Cell.

Instead of talking to the prisoner when JA11 is fired and JA13 is unfired:
	say "Jacobs pointedly ignores you." instead;

After firing JA12:
	now the player carries the piece of wire.

After taking the piece of wire when in the Jail-Cell and the piece of wire was not handled:
	start conversation with the prisoner on JA13;

After firing JA13:
	now the player is caught;

Instead of talking to the prisoner when cell-door-1 is locked and (JA14 is fired or JA15 is fired):
	say "Jacobs pointedly ignores you.";
]

Rule for initiating conversation with the prisoner when JA8 is fired and the location is the holding cell and JA30 is unfired and JA31 is unfired:
	change the chosen opening gambit to JA24;

After populating JA24:
	unless the player has the piece of wire:
		remove JA29 from the current conversation, if present;  

JA27 is clustered with JA28, JA29.

After populating JA25:
	if a quip clustered with JA27 is fired:
		remove JA27 from the current conversation, if present;
	if JA28 is fired:
		remove JA28 from the current conversation, if present;
	unless the player has the piece of wire:
		remove JA29 from the current conversation, if present;

After populating JA26:
	if a quip clustered with JA27 is fired:
		remove JA27 from the current conversation, if present;
	if JA28 is fired:
		remove JA28 from the current conversation, if present;
	unless the player has the piece of wire:
		remove JA29 from the current conversation, if present;

After populating JA27:
	if JA25 is fired:
		remove JA25 from the current conversation, if present;
	if JA26 is fired or JA25 is unfired:
		remove JA26 from the current conversation, if present;
	if JA28 is fired:
		remove JA28 from the current conversation, if present;

After populating JA28:
	if JA25 is fired:
		remove JA25 from the current conversation, if present;
	if JA26 is fired or JA25 is unfired:
		remove JA26 from the current conversation, if present;

After populating JA29:
	if JA25 is fired:
		remove JA25 from the current conversation, if present;
	if JA26 is fired or JA25 is unfired:
		remove JA26 from the current conversation, if present;
	if JA28 is fired:
		remove JA28 from the current conversation, if present;

Last every turn when in the Jailhouse and the prisoner is nearby and JA16 is unfired: 
	start conversation with the prisoner on JA16;

After firing JA16:
	Jacobs offers his support in two turns from now;

At the time when Jacobs offers his support:
	if JA19 is unfired and the prisoner is on-stage,
		start conversation with the prisoner on JA19;

After firing JA19:
	Olmer pleads for freedom in one turn from now;
	Jacobs withdraws his offer of support in two turns from now;

At the time when Olmer pleads for freedom:
	if OL1 is unfired and the bald headed man is in a holding cell:
		start conversation with the bald headed man on OL1, even if not present;

At the time when Jacobs withdraws his offer of support:
	if JA21 is unfired:
		fire JA21;

After firing JA21:
	if OL1 is unfired and the bald headed man is in a holding cell:
		unless we are unlocking cell-door-2 with the piece of wire:
			start conversation with the bald headed man on OL1, even if not present;
	otherwise if the current conversationalist is the prisoner:
		carry out the terminating conversation with activity with the prisoner;
	remove the prisoner from play;
	update the character list;

Rule for populating JA21:
	if the current conversationalist is the prisoner or the current conversationalist is the player:
		change the current conversation to { };

After firing JA22:
	end the game in death;

Section 5 - Scripted Actions

Jacobs's lockpicking attempt is a scene. Jacobs's lockpicking attempt begins when JA7 is fired. Jacobs's lockpicking attempt ends when the table of Jacobs's frustrated mutterings is empty.

TIMER_Jacobs_Lockpicking is a timer. The top end is 1.

Every turn during Jacobs's lockpicking attempt:
	if TIMER_Jacobs_Lockpicking is fired:
		spool through dialogue in the table of Jacobs's frustrated mutterings;
	increment TIMER_Jacobs_Lockpicking;

Table of Jacobs's frustrated mutterings
text
"Jacobs['] face reddens with effort. 'C'mon... c'mon...' he mutters.[conditional paragraph break]"

Instead of looking through or searching a viewing window during Jacobs's lockpicking attempt:
	say "You can't; Jacobs is blocking your view.";

Section 6 - A small piece of wire

Picking a Lock is a scene. Picking a lock begins when the wire is enclosed by the Jail-Cell. Picking a Lock ends when cell-door-1 is unlocked.

The piece of wire is a thing. Understand "lockpick", "lock pick", "pick", "bit", "short", "stiff" as the piece of wire. The description is "It's a short bit of stiff wire, like the ones you've sometimes seen Bobby carrying around."

[ A procedural rule during Picking a Lock:
	ignore the can't take people's possessions rule; [ because we have to take the wire off of Jacobs ] ]

[ Understand "unlock [something]" as unlocking it with; ]

Rule for supplying a missing second noun while unlocking something with when the player can see the piece of wire:
	say "(with [the piece of wire])[command clarification break]";
	change the second noun to the piece of wire;

Instead of giving or showing the piece of wire to the prisoner when in jail-cell:
	say "Jacobs rolls his eyes. 'I don't want it, you dim. It's no use to me in here.'";

Section 7 - Successful lock-picking

An unlocking rule for cell-door-1 when the player has the piece of wire:
	rule succeeds with result the piece of wire;

An unlocking rule for cell-door-1:
	rule fails;

After unlocking cell-door-1 with the wire during Picking A Lock:
	now the piece of wire is in the Jailhouse;
	now cell-door-1 is open;
	Jacobs leaves his cell in one turn from now;
	say "You slip your arm through the bars and reach down for the lock. It's a stretch — you have to get up on your tip-toes and strain your shoulder painfully against the little window  — but soon you feel the metal of the lockplate, and your fingers find the tiny, sharp-edged hole in the middle. You jam the end of the lockpick in and start wiggling it around. Almost immediately, you realize that this is not going to work. The angle is too awkward, and you can't see what you're doing, and you can't feel the tumblers the way Bobby showed you when you practiced on the front door of Maiden House...[paragraph break]Then you feel the wire bump against something inside the lock. You manage to lever the end of the wire under something and push... you feel it start to give... you push a little harder, and it starts to pivot... you push harder, and it starts to slip off the end of your pick...[paragraph break]And then there is a [i]snap[r], and the pick jumps out of your fingers and tumbles to the ground outside the cell door.[paragraph break]Jacobs suddenly stands up. He walks over, puts his hand on the door... and pushes it open.[paragraph break]'Well,' he says, looking you over with a sly expression, 'you are a valuable runt, after all.'";

At the time when Jacobs leaves his cell:
	now cell-door-1 is open;
	try the prisoner going north.

Chapter 5 - Jacobs's Deception

Section 1 - Scene beginning

Jacobs's Deception is a scene. Jacobs's Deception begins when the prisoner is in the Jailhouse and the player is in the Jailhouse.

When Jacobs's Deception begins:
	start conversation with the prisoner on JA16;

Every turn during Jacobs's Deception:
	increment TIMER_Jacobs_Deception

TIMER_Jacobs_Deception is a timer. The top end is 3.

Jacobs's Deception ends when the prisoner is off-stage or TIMER_Jacobs_Deception is fired.

Chapter 6 - Olmer

A bald headed man is a man, improper-named, in Jail-Cell-2. The description is "A short, bald-headed man, with a week's worth of stubble on his neck and a nose bright red from burst blood vessels. His breath reeks of cheap spirits, and his watery eyes shift constantly, as though always looking for a way out of his current predicament."
The printed name is "[if improper-named]bald-headed man[otherwise]Olmer[end if]"

Understand "Olmer" as the bald headed man when the bald headed man is proper-named. Understand "short", "head", "stubble", "men", "prisoner", "prisoners" as the bald headed man.

Instead of kissing the bald headed man, say "He seems nice enough, but you really don't know him very well... and besides, he stinks."

Does the player mean talking to the bald headed man:
	it is likely;

Section 2 - Olmer's Conversation

Rule for initiating conversation with the bald headed man:
	if the location of the bald headed man is Jail-Cell-2:
		change the chosen opening gambit to OL9;
	otherwise:
		change the chosen opening gambit to OL10;

OL1 is a quip. The display text is "'Do us a kindness, girl, and free us as well,' whines the man in the north cell. 'We ain't done nothin['] terrible, and we can show you a way out, too, one that doesn't include leaving you dead in an alley for your shoes.'";

OL2 is a quip. The display text is "'Miss?' wheezes the man in the north cell[if OL2 was fired] again[end if]. 'Are you there? Will you take pity on two poor drunkards and use that fancy lock-picker of yours?'"

OL3 is a quip. The menu text is "'Hang on for a minute.'". The display text is "[first time]'Just hang on for a minute,' you tell him. 'I need to look around.'[paragraph break]'You are wise beyond your years, miss,' the man says, sounding a bit disappointed. 'I'm sure you'll do the right thing.'[subsequently]'We'll wait right here,' calls the man.[only]"

OL4 is a quip. The menu text is "'All right, I'll let you out.'". The display text is "'All right, hold your horses,' you say. 'I'll let you out.'"

OL5 is a quip. The menu text is "'Who are you?'". The display text is "Through the viewing window, you eye the red-faced man warily. To tell the truth, he doesn't inspire much more confidence than Jacobs did. 'Who are you?' you ask. 'What's your name?'[paragraph break]'Please forgive me, miss,' he wheezes. 'I am called Olmer, and this is my associate, Darrens.' The skinny man standing behind him smiles and nods enthusiastically."

OL6 is a quip. The menu text is "'Why doesn't your friend talk?'". The display text is "[first time]You nod towards the red-faced man's skinny friend. 'The tall one,' you ask, 'why doesn't he ever talk?'[paragraph break]'Oh, Darrens wasn't always a mute,' Olmer explains. 'One o['] the Lord's Guard caught him nickin['] from the fruit stall in Grubber's a few years back. Kicked him in the head [']til the blood came out his ears. His eyes stayed crossed for near on a week, and he's never been quite right since.'[paragraph break]Darrens nods sadly.[subsequently][rp] 'he's mute.'[only]"

OL7 is a quip. The menu text is "'What are you doing in prison?'". The display text is "[first time]'What did you do to end up in prison?' you ask.[paragraph break]'Oh, nothin['] worse than the Lord's Guard gets up to themselves on payday,' wheezes Olmer. 'A weakness for the drink is our curse. Every now and again the Goddesses see fit to bless me with an opportunity to learn from my shameful mistakes.' He gives you a wide, gap-toothed grin.[subsequently][rp] 'we had the misfortune to be drunk in the vicinity of a Guardsman.'[only]"

OL8 is a quip. The menu text is "'What have you got against Jacobs?'". The display text is "[first time]'What have you got against Jacobs?' you ask.[paragraph break]'That Jacobs is a brute and a scoundrel,' Olmer says. 'He'd sell his own mother to slavers if he thought there'd be a profit in it, and if his pirate of a father didn't get to her first.'[subsequently][rp] 'Jacobs is an unscrupulous brute.'[only]"

OL9 is a quip. The display text is "'Hey, you still in there?' you call through the viewing window. 'Can I ask you something?'[paragraph break]'We are at your service, miss,' comes the wavering voice."

OL10 is a quip. The display text is "'Let me ask you something,' you say.[paragraph break]'Of course,' wheezes Olmer."

OL11 is a quip. The menu text is "'Where does the drain lead?'". The display text is "[first time]'That drain,' you ask, 'where does it lead?'[paragraph break]'Down to the sewers, it does miss,' says Olmer excitedly. 'Tunnels, leading all under the city. A crafty bloke without too delicate a sensibility could escape to anywhere. But it's very easy to get lost; you'll need Darrens and me to find your way.'[subsequently][rp] 'that drain leads down to the sewers.'[only]"

The response of OL1 is { OL3, OL4, OL5 }.

The response of OL2 is { OL3, OL4, OL5 }.

The response of OL3 is { }.

The response of OL4 is { }.

The response of OL5 is { OL3, OL4, OL6, OL7, OL8, OL11 }.

The response of OL6 is { OL3, OL4, OL7, OL8, OL11 }.

The response of OL7 is { OL3, OL4, OL6, OL8, OL11 }.

The response of OL8 is { OL3, OL4, OL6, OL7, OL11 }.

The response of OL9 is { OL3, OL4, OL5, OL6, OL7, OL8, OL11 }.

The response of OL10 is { OL6, OL7, OL8, OL11 }.

The response of OL11 is { OL3, OL4, OL6, OL7, OL8 }.

After firing OL4:
	now the player carries the piece of wire;
	try lockpicking cell-door-2; 

After firing OL5:
	now the skinny man is proper-named;
	now the bald headed man is proper-named;

OL5 is clustered with OL6, OL7, OL8, OL9, OL10, OL11.

After populating a quip that is clustered with OL5:
	if the bald headed man is not in Jail-Cell-2:
		remove OL3 from the current conversation, if present;
		remove OL4 from the current conversation, if present;
	if the drain room is not visited:
		remove OL11 from the current conversation, if present;
	if JA19 is unfired:
		remove OL8 from the current conversation, if present.

After populating OL9:
	if OL5 is unfired:
		remove OL6 from the current conversation, if present;
		remove OL7 from the current conversation, if present;
		remove OL8 from the current conversation, if present;
		remove OL11 from the current conversation, if present;

Rule for terminating conversation with the bald headed man when the bald headed man is in a holding cell:
	if the number of entries in the current conversation is 0:
		[ conversation is genuinely over ]
		now the current conversationalist is the player;

Chapter 7 - Darrens

A skinny man is a man, improper-named, in Jail-Cell-2. The description is "He's a tall, wobbly scarecrow of a man, with a huge nose, ears like jug handles jutting from the sides of his head, and a throat-apple the size of a child's fist that bobs up and down constantly. He never speaks; just smiles and nods whenever anyone says or does anything. You suspect he may be a few coins short of a full purse." The printed name is "[if improper-named]skinny man[otherwise]Darrens[end if]"

Understand "Darrens" as the skinny man when the skinny man is proper-named. Understand "wobbly", "scarecrow", "dim", "tall", "men", "prisoners" as the skinny man.

After talking to the skinny man:
	say "[if the skinny man is proper-named]Darrens[otherwise]The skinny man[end if] just waggles his head and smiles at you.";

Instead of kissing the skinny man, say "He seems nice enough, but you really don't know him very well... and besides, he stinks."


Chapter 9 - Freeing Olmer & Darrens

Last before unlocking cell-door-2 with the piece of wire when the prisoner is nearby:
	now the player carries the piece of wire;
	carry out the firing activity with JA21;
	continue the action;

After unlocking cell-door-2 with the piece of wire:
	now cell-door-2 is open;
	now the skinny man is proper-named;
	now the bald headed man is proper-named;
	move the skinny man to the Drain Room;
	move the bald headed man to the Drain Room;
	update the character list;
	say "Picking a lock is [i]much[r] easier when you're standing on the ground and can see what you're doing. Twenty seconds, and you have the cell door open.[paragraph break]'Oh, thank you, miss, thank you kindly. Goddesses['] blessings upon you.[no line break][if OL5 is fired] [otherwise] Allow me to make introductions,' the man wheezes. 'I am called Olmer, and this is my associate, Darrens.' The skinny man standing behind him smiles and nods enthusiastically.[paragraph break][quotation mark][end if]Now,' says Olmer, 'if you will follow us, we will make our escape.'[paragraph break]Olmer and Darrens go southwest.";

Does the player mean lockpicking cell-door-2:
	it is very likely;

Does the player mean following the bald headed man:
	it is likely;

Does the player mean following the prisoner:
	if the location is the jailhouse:
		if the bald headed man is in the Drain Room:
			it is unlikely;
		if the prisoner is off-stage:
			it is very likely;

Definition: a person is a drunkard:
	if it is the skinny man, yes;
	if it is the bald headed man, yes;
	no;

A leading rule for the prisoner when the player is in the Prison:
	rule succeeds;

A leading rule for a drunkard person (called the reprobate):
	if the player is in the Prison Region:
		rule succeeds;

Instead of following a drunkard when the player is in the jailhouse and the noun is in the Drain Room:
	try going southwest;

Part 3 - Drain Room

Chapter 1 - Description

The Drain Room is southwest of the Jailhouse. "This tiny alcove reeks worse than the privy back at Maiden House, and that's saying something. The main room is back to the northeast."

Understand "tiny", "alcove", "reeking", "foul" as the Drain Room.

Instead of smelling the Drain Room:
	say "The smell in [if Drain Room is not the location]t[end if]here is overpoweringly foul.";

Instead of smelling in the Drain Room:
	say "The smell in here is overpoweringly foul.";

Instead of listening to the Drain Room when in the Drain Room:
	try listening to the drain hole instead;

Instead of going down in the Drain Room:
	try entering the drain hole;

The distant description of the Drain Room is "The tiny alcove to the southwest smells [i]really[r] bad."

The dead-end description of the Drain Room is "There's nowhere to go in here except northeast, back to the main room."

Instead of facing down in the Drain Room, try examining the drain hole.

After going to the Jailhouse from the Drain Room when the bald headed man is in Jail-Cell-2:
	say "[command clarification break]";
	try looking;
	update the character list;
	start conversation with bald headed man on OL2, even if not present;

Chapter 2 - Contents - Grate

A metal grate is in the Drain Room. The metal grate can be moved or unmoved. "[if unmoved]Set into the stone floor is a metal grate[otherwise]The metal grate is lying off to one side[end if]." The description is "[if unmoved]The thick bars of metal cover a rectangular hole that drops down into darkness. Around the edges of the grate, the stonework is loose and crumbling[otherwise]The metal grate is lying off to one side[end if]." Understand "thick", "bar", "bars", "of", "metal", "cover" as the metal grate.

Instead of raising the unmoved metal grate:
	now the grate is moved;
	now the drain hole is not scenery;
	say "It's a strain, but you're just barely able to lift the heavy grate and drop it to the side with a dull clang[if the bald headed man is nearby]. Olmer smiles and gestures towards the foul-smelling hole. 'After you,' he wheezes[end if].";

Instead of raising the moved metal grate:
	say "That grate is too heavy to carry anywhere, and you don't feel like lifting it again.";

Before opening, pushing, pulling, or taking the metal grate:
	try raising the metal grate instead.

Before entering the unmoved metal grate:
	try entering the drain hole instead.

Before searching the unmoved metal grate:
	try searching the drain hole instead.

Instead of listening to the unmoved metal grate:
	try listening to the drain hole instead;

Section - the crumbling stonework

The loose stonework is scenery, in the Drain Room. The description is "The stonework around the [if the metal grate is moved]hole[otherwise]metal grate[end if] is loose and crumbling[if the metal grate is unmoved]. You might be able to pull the grate free, if you really strained[end if]." Understand "crumbling", "stone", "crumbly" as the loose stonework.

Chapter 3 - Contents - Hole

A drain hole is scenery, in the Drain Room. The initial appearance is "A rectangular hole in the middle of the stone floor leads down into foul-smelling darkness." The description is "The hole is too dark and too deep for you to see the bottom, although you think you can hear the sound of running water coming from far below." Understand "shaft", "rectangle", "rectangular", "dark", "darkness", "foul", "foul smelling" as the drain hole.

Instead of searching the drain hole:
	try examining the hole;

Instead of entering the drain hole when the metal grate is unmoved:
	say "You can't squeeze between the bars of the grate.";

Instead of listening to the drain hole:
	say "You can faintly hear the sound of running water coming from below.";

Instead of entering the drain hole:
	say "[i]Well, it can't be that much worse than rotting in prison,[r] you think. You sit down and dangle your feet over the edge of the hole. You can hear the sound of running water, and smell the sharp stink of sewage. You offer a quick prayer to Brigid — [i]Please give me a chance to clean up before seeing Bobby again[r] — take a deep breath, and drop down into darkness.";
	output chapter heading "Chapter 4 - Sewer";
	move the player to the sewer;
	update the character list;

Report listing exits when in the Drain Room:
	say "There is an exit northeast from here[if the metal grate is moved], or [first time](though you can hardly imagine it) [only]down through the drain[end if]." instead;

Chapter 4 - Event on entering

After looking in the Drain Room when the prisoner is on-stage:
	remove the prisoner from play;
	say "You can hear Jacobs muttering angrily in the room behind you, then heavy footsteps that quickly fade away.";

Discussion of Drainage is a scene. Discussion of Drainage begins when the player is in the drain room and the bald headed man is in the drain room. Discussion of Drainage ends when the location is the Sewer.

When Discussion of Drainage begins:
	say "'The guards use this place to relieve themselves,' Olmer explains, 'and sometimes for beatin['] prisoners when they don't feel like moppin['] the cells after. [if the metal grate has been moved]I see you've already made some progress,' Olmer adds, noting the moved grate. 'All's left is to reap the fruits of your generous labor.'[paragraph break]He smiles and gestures towards the foul-smelling hole. 'After you,' he wheezes.[otherwise]My back's not much for physical labor, but a spry one like yourself should have little trouble.' He motions for you to lift up the grate.[end if]";

Book 7 - The Sewers

Escape Through The Sewer is a scene. Escape Through The Sewer begins when the location is the Sewer. Escape Through The Sewer ends when the location is Access Tunnel.

The Sewers Region is a region. The Sewer and Access Tunnel are in the Sewers Region.

Part 1 - The Sewer room

Chapter 1 - Description

The Sewer is a room. The description is "The tunnels down here are made of ancient, unmortared brick, slimy with centuries of filth and mold. Dim light trickles down through narrow vents in the ceiling. Side tunnels split off in numerous directions, twisting away into darkness."

Understand "sewers", "tunnel", "tunnels", "side", "dark", "darkness" as the Sewer.

Instead of facing a planar direction in the Sewers Region, say "These tunnels seem to go in every direction."

Instead of facing down in the Sewers Region, try examining the sewer water.

Instead of facing up in the Sewers Region, try examining the ceiling vents.

Instead of listening to the location while in the Sewers Region:
	say "You hear the soft lap and slosh of sewer water through the tunnels."

Instead of smelling the location while in the Sewers Region:
	say "The smell down here is simply unbelievable. You have to breathe through your mouth just to keep from passing out."

Section 1 - Contents

The sewer water is a backdrop, in the Sewers Region. The description is "The water is sluggish and cold, knee-deep in most places. [i]Things[r] float past you in the ooze — garbage, dead rats, and worse. You try to avoid the worst of it, but it's already soaked through your shoes. You feel like you'll never be clean of it." Understand "sluggish", "cold", "deep", "knee deep", "dead", "sewage", "ooze", "garbage", "rat", "rats" as the sewer water.

Instead of searching, drinking, smelling or tasting the sewer water, say "The thought makes you gag."
Instead of touching the sewer water, say "You can't help touching it — it's soaked through your clothes."

The ancient bricks are a backdrop, in the Sewers Region. The description is "The tunnel walls are slimy with filth and mold." Understand "old", "brick", "unmortared", "slimy", "slime", "filthy", "filth", "moldy", "mold", "wall", "walls", "tunnel" as the ancient bricks.

Instead of climbing the ancient bricks, say "You can't get a foothold on the slimy bricks."
Instead of touching or smelling or tasting or eating the ancient bricks, say "You'd rather not touch the walls at all."

The ceiling vents are a backdrop, in the Sewers Region. The description is "The ceiling vents let in a tiny trickle of light, barely enough to see by." Understand "vent", "grating", "gratings", "light", "dim", "trickle", "trickles", "of" as the ceiling vents.

Instead of doing something other than examining with the ceiling vents, say "The vents are high out of reach."

Before going down when in the Sewer:
	say "You can't go that way." instead;
Before going up when in the Sewer:
	say "You can't go that way." instead;

Section 2 - Glyphs

A glyph is a kind of pointy thing. [like the cables in Grubbers Market, the glyphs each indicate a direction.]
A glyph is usually scenery.
A glyph has a symbol color called the color.
A glyph has a sewer-symbol called the symbol.

Understand the color property as describing a glyph.
Understand the symbol property as describing a glyph.

Understand "symbol", "shape", "glyph" as a glyph.

The north glyph is a glyph in the Sewer. The way of the north glyph is north.
The east glyph is a glyph in the Sewer. The way of the east glyph is east.
The south glyph is a glyph in the Sewer. The way of the south glyph is south.
The west glyph is a glyph in the Sewer. The way of the west glyph is west.

[A line of grammar for "following" the glyphs, since this is explicitly suggested by Olmer.]

Understand "follow [glyph]" as following.

Instead of following a glyph: try going the way of the noun.

Section 3 - Collective Glyphs

Some painted symbols are in the Sewer. "Painted on the walls are a number of odd symbols." The description is "[first time]The symbols are a number of crude shapes, no bigger than your hand. Each one is painted on the wall next to one of the side tunnels, as if to mark the way. [only][Glyph report]". Understand "odd", "strange", "paint", "shapes", "glyphs" as the painted symbols.

Listing the names of glyphs is an activity.

To say glyph report:
	carry out the listing the names of glyphs activity;

Rule for listing the names of glyphs:
	say "You can make out [a list of glyphs]. ";

Rule for printing the name of a glyph (called target):
	say "[color of target] [symbol of target]";

Rule for printing the description of a glyph (called target):
	say "The [one of][color of target] glyph[or][color of target] [symbol of target][or][symbol of target][purely at random] is [one of]painted[or]marked[purely at random] on the wall [one of]next to[or]beside[or]by[purely at random] [the way of target] tunnel."

After printing the name of a glyph (called target) while listing the names of glyphs:
	 say " next to the [way of the target] tunnel";

Chapter 2 - Event on entering

The magic color is a symbol color that varies.

To decide which glyph is the magic glyph:
	let the glyph list be {north glyph, east glyph, south glyph, west glyph};
	repeat with current glyph running through the glyph list begin;
		if the color of the current glyph is the magic color begin;
			decide on the current glyph;
		end if;
	end repeat;
	[all four colors are assigned to glyphs, so the loop won't reach this point.]

After looking in the Sewer for the first time:
	select theme "Sewer";
	if the bald headed man is in the Drain Room:
		remove the bald headed man from play;
		remove the skinny man from play;
		change the magic color to a random symbol color;
		update glyphs;
		say "Olmer drops down into the muck next to you, followed by Darrens. 'Now then, miss, cast your eye at these glyphs here,' whispers Olmer. His voice echoes off into the blackness as he brushes the rough stone with his fingertips. 'These are marked down by such poor blokes as have to take refuge down here on a more regular basis. They tells you where the tunnels lead, if you but know their language.' Darrens nods in solemn agreement.[paragraph break]'To get back to your neighborhood, you'll want to follow the [magic color], see?' Olmer points off down [the way of the magic glyph] tunnel. 'Darrens and I, we must be off elsewhere, so we'll take our leave of you now. But follow the [magic color] and you can't go wrong.'[paragraph break]'It was very kind of you to set us free, young lady.' He shakes your hand. (His hands are slimy with filth, but then so are yours.) 'We've only our meagre gratitude with which to repay you, but we won't forget your generosity.' Darrens nods vigorously and shakes as well.[paragraph break]And then the two of them slosh off, leaving you alone.";

Chapter 3 - Sewer Puzzle

Section 1 - Sewer puzzle implementation

The sewer move count is a number that varies. The sewer move count is 0.
The sewer safety count is a number that varies. The sewer safety count is 0.

To update glyphs:
	[ We need to use all four colors, and assign each one a unique symbol. The recent addition of lists to I7 makes this exceptionally easy - if you're used to a list processing language like Haskell or LISP. ]
	let the symbol list be { square, diamond, circle, triangle, crescent, star, cross, arrow };
	let the color list be { red, blue, green, yellow };
	let the glyph list be { the north glyph, the south glyph, the east glyph, the west glyph };
	sort the symbol list in random order;
	sort the color list in random order;
	repeat with the current glyph running through the glyph list begin;
		change the color of the current glyph to entry 1 of the color list;
		change the symbol of the current glyph to entry 1 of the symbol list;
		rotate the color list;
		rotate the symbol list;
	end repeat;

Before going in the Sewer:
	if the noun is orthogonal:
		increment the sewer move count by 1;
		if the way of the magic glyph is the noun:
			increment the sewer safety count by 1;
		if the sewer move count is 2:
			if the sewer safety count is 2:
				move the player to the Access Tunnel instead;
			otherwise:
				run the sewer death ending;
				stop;
		update glyphs;
		move the player to the Sewer instead;

A leading rule for a drunkard person (called the reprobate):
	if the player is in the Sewer and the reprobate is off-stage:
		rule succeeds;

Instead of following a drunkard when the player is in the sewer and the noun is off-stage:
	say "Olmer said to follow the [magic color][first time], and I don't want to get lost down here[only].";

Section 2 - Winning the sewer puzzle

The Access Tunnel is a room. "The tunnels down here are made of ancient, unmortared brick, slimy with centuries of filth and mold. Dim light trickles down through narrow vents in the ceiling. Side tunnels split off in numerous directions, twisting away into darkness."

Understand "sewer", "sewers", "tunnel", "tunnels", "side", "dark", "darkness" as the Access Tunnel.

Instead of facing up in the Access Tunnel, try examining the hatch.

The hatch is scenery in the Access Tunnel. "That hatch probably leads back to the streets above." Understand "trap", "door", "trapdoor", "ceiling" as the hatch.

Does the player mean doing something to the hatch: it is likely.

Instead of doing something other than examining to the hatch:
	say "You can't reach the hatch from down here.";

A ladder is in the Access Tunnel. "A ladder is bolted to the wall here." The description is "The ladder leads up to a hatch in the ceiling above you."

Instead of climbing the ladder:
	say "You eagerly pull yourself up the rungs and shove open the hatch. [i]Daylight![no line break][r] You've never been so glad to see it. [paragraph break]From the look of the sky, it's early morning. You're standing in an empty alleyway, pretty close to Maiden House if you have your landmarks right. After a couple of turns, you figure out where you are, and soon find yourself back in familiar environs.";
	output chapter heading "Chapter 5 - Black Gate Estate";
	move the player to Commerce Street;
	update the character list;

Instead of going up in the Access Tunnel:
	try climbing the ladder;

Before going in the Access Tunnel:
	if the noun is orthogonal:
		run the sewer death ending;

Rule for assembling available exits of the Sewer:
	change the viable directions to { north, south, east, west };

Rule for assembling available exits of the Access Tunnel:
	change the viable directions to { north, south, east, west, up };

Section 3 - Losing the sewer puzzle

To run the sewer death ending:
	say "Suddenly you find yourself in a tunnel where there are no symbols painted anywhere. You try to retrace your steps, but you must have taken a wrong turn somewhere, because now you can't find any symbols anywhere. All of the tunnels look alike; you are completely turned around and don't even know which direction you are stumbling in.[paragraph break]The next tunnel you take leads you to an area with no vents — it is pitch black. You slip as you turn around and fall into the water. Something slick and wriggly brushes past your face, and you thrash to your feet.[paragraph break]You can't see any light, anywhere. You don't know what direction you just came from. Cold, smelly water numbs your feet. Despair and paralyzing panic close in on you as you realize that you have no means of finding your way through this labyrinth.[paragraph break]You are stuck down here, blind in the stinking blackness, forever.";
	end the game in death;

Book 8 - Black Gate Estate

Part 1 - A New Morning

A New Morning is a scene. A New Morning begins when Escape Through The Sewer Ends.

When New Morning begins:
	now the wooden door [to Maiden House] is open;
	now the wooden door is unlocked;
	remove the cake of lye soap from play;
	remove the wooden stirring stick from play; [Shannon has finished the Laundry.]
	remove the orphans from play;
	set the scene for day; [remapping Grubber's Market, etc.]

Before going west from Commerce Street when the location has been the sewer:
	if it is night:
		continue the action;
	otherwise:
		if the Bodyguard Scene has not happened:
			say "It might be a good idea to avoid Grubber's Market for a while. Once they figure out you escaped from your cell, they'll be searching for you everywhere; they looked in Grubber's once, they'll look there again." instead;			
		otherwise if the player does not have the plain dagger:
			say "'Wait a minute,' says Pieter. 'We need to get you a weapon first. We can't leave for the ball until you have one.'" instead;

Before going south from Lords Market when the location has been the sewer and the secret letter has not been handled:
	say "It's probably best to stay away from Maiden House at the moment. The Widows will be on the lookout for you, and it's not like they'd do anything bad to you or anything, but if they catch you they won't let you out of the orphanage until you're an old crone. And you have important work to do. First, Fossville's house; then you can go back to Maiden House." instead;

Before going northwest in East Commerce Street when the location has been the sewer:
	say "[if the secret letter is off-stage]No, you'll never get in through the front gate. [i]Use the rooftops[r], Bobby said.[otherwise]You've already gotten what you need from Fossville's house. Better not press your luck.[end if]" instead;

Part 2 - Closed Alley behind Commerce Street

Chapter 1 - Description

The thin alley is a room. The description is "The walls of the alley are so close together you can barely squeeze your shoulders through; the rough brick and mortar catches at your clothes. The ends of the alley are walled off. There's no way out except back through the little door you came in." The printed name of the thin alley is "Closed Alleyway".

Chapter 2 - Scenery (metal door)

A little metal door is a door, scenery, not openable, south of the thin alley. "Holstenoffer's shop is behind that little metal door." Understand "butchers", "butcher", "holstenoffer", "holstenoffers", "shop", "store", "lock" as the little metal door.

Through the little metal door is the Butchers Shop.

Instead of opening or entering the little metal door:
	say "Bobby told you to find the alley, get onto the rooftops. Now that you're here, there's not much point in going back.";

Before going south in the thin alley:
	try opening the little metal door instead;

Instead of facing south in the Thin Alley, try examining the little metal door.

Instead of facing up in the Thin Alley, say "The sky is a narrow rectangle three stories above your head."

Understand "narrow", "rectangle", "rectangular" as the backdrop-sky when the location is Thin Alley.

The dead-end description of the Thin Alley is "There's nowhere else to go in this little place. Just back the way you came... or up."

Rule for printing the distant description of the main rooftop when in the thin alley:
	say "You can't see much of the rooftops from down here. ";

The distant description of the thin alley is "The alley is way, [i]way[r] down below you."

Chapter 3 - Scenery (walls)

Some rough walls are scenery, in the thin alley. The printed name is "walls". The description is "The walls are rough brick and mortar, towering up a full three stories over your head." Understand "wall", "mortar", "brick", "bricks" as the rough walls.

Instead of climbing the rough walls:
	say "Rough as they are, the walls are too steep to climb.";

Instead of descending the walls, try going down. 

Chapter 4 - Rain gutter

The rain gutter is a thing, fixed in place, in the thin alley. "A rusted rain gutter clings to the wall." The description is "The pipe is old and rusty, and only barely still attached to the brick wall in some places... still, it might hold your weight." Understand "rusted", "rusty", "pipe" as the rain gutter.

Instead of going up while in the Thin Alley, try climbing the rain gutter.

Instead of climbing the rain gutter:
	say "You give the pipe a few experimental tugs, then take a deep breath and start shimmying up. The rain gutter squeaks and groans in protest. Your fingers cramp and your toes scrabble to find purchase on the rough bricks. Finally, three stories up, you grab the edge of the rooftop and haul yourself over.";
	select theme "RoofTops";
	move the player to the main rooftop;
	update the character list;

Understand the command "shimmy" as "climb".

Part 3 - Rooftop Journey

The Rooftops Region is a region. The Main Rooftop, Rooftop Edge, Roof of Black Gate Estate, Patio, Rooftop Garden are in the Rooftops Region.

Chapter 1 - Rooftop

Section 1 - Description

The main rooftop is a room. "The rooftops are like a miniature mountain range up here, with peaks and valleys sloping away into the distance. Chimneys and stovepipes stand sentry like the keeps and towers of castles. [first time]Bobby has exclaimed the virtues of rooftop travel before, and finally you see what he was going on about. It feels like you could explore forever up here.[only]". The printed name is "Rooftop"

Understand "miniature", "mountain", "range", "mountains", "peak", "peaks", "valley", "valleys", "landscape", "roof", "roofs" as the main rooftop.

The distant description of the main rooftop is "There are more rooftops to the east."

The dead-end description of the main rooftop is "The rooftops seem to stretch off forever in every direction, but you need to focus on getting to Fossville's mansion right now."

Instead of going down in the main rooftop:
	say "It took quite an effort to get up here. You're not climbing back down until you find what Bobby told you to find... the letter in Fossville's mansion."

Instead of going up in the main rooftop:
	say "You're on the rooftops already; this is as high as you can go."

Instead of going a planar direction in the main rooftop when the noun is not east:
	say "You need to focus on getting to Fossville's mansion, and if memory serves, that's east of here."

After assembling available exits of the main rooftop:
	add down to the viable directions, if absent;

Chapter 2 - Rooftop Edge

The Rooftop Edge is east from the main rooftop. "The rooftop landscape ends here at a wide street dividing Commerce Street from the more affluent residential district that borders it. Far, far below, you can see all the little people going about their business."

Understand "miniature", "mountain", "range", "mountains", "peak", "peaks", "valley", "valleys", "landscape", "roof", "roofs", "end" as the Rooftop Edge.

The Rooftop Edge overlooks Commerce Street.
Rule for printing the distant description of Commerce Street when in Rooftop Edge:
	say "All the people way down there on Commerce Street look like ants from up here.";

Understand "people", "crowd", "crowds", "pedestrians", "pedestrian", "pavement", "ants", "ant", "little", "wide", "residential", "affluent", "district", "business", "drop", "gap" as Commerce Street when the location is Rooftop Edge.

The distant description of Rooftop Edge is "The rooftops come to an end somewhere to the east."

The dead-end description of Rooftop Edge is "The rooftops seem to stretch off forever, but you need to focus on getting to Fossville's mansion right now."

Instead of facing northeast in Rooftop Edge, say "The rooftops end that way in a three-story drop."
Instead of facing southeast in Rooftop Edge, say "The rooftops end that way in a three-story drop."

Instead of facing east in Rooftop Edge, try examining the distant balcony.

Instead of inspecting the Roof of Black Gate Estate, try examining the distant balcony.

Instead of going a planar direction (called the path) in Rooftop Edge:
	if the path is northeast:
		try facing northeast instead;
	otherwise if the path is southeast:
		try facing southeast instead;
	otherwise if the path is not east:
		say "The thought of exploring the roofs is tantalizing, but you need to focus on Bobby's mission first." instead;
	continue the action;

Instead of going up in the Rooftop Edge:
	say "You're on the rooftops already; this is as high as you can go."

Instead of going down in the Rooftop Edge:
	say "The only way down from here is a long, deadly fall."

After assembling available exits of the rooftop edge:
	add down to the viable directions, if absent; [as a joke -- see above]

Section 1 - Distant balcony

A distant balcony is in the Rooftop Edge. "To the east, across the gap, you spy a balcony." The description is "It's an ornately decorated balcony, jutting out from what looks like a rooftop garden. Judging from the layout of the streets below you, that must be Baron Fossville's house."

Understand "ornate", "ornately", "decorated", "decorative" as the distant balcony.

After going east from Rooftop Edge when Escaping Jail has happened:
	say "You peer across the gap at the balcony, judging the distance. About ten feet. Maybe a little bit more than ten feet. And it's slightly below you, which is a plus.[paragraph break]Still, it's a [i]really[r] far jump.[paragraph break]This is where Bobby told you to go. [i]A letter... secrets about your father[r], he said. The thought of facing Bobby when he gets out, after they beat him up so bad, and telling him you were too scared to do what he asked you to do...[paragraph break]You take a few steps back, run... and [i]jump[r].[paragraph break]The railing slams into your chest, knocking the breath out of you. Your feet pedal madly in empty air. You wrap your arms around the railing, trying to grab on, but you're slipping...[paragraph break]Then your foot finds the edge of the balcony, and you push yourself up and over.";
	continue the action;

Instead of jumping in the Rooftop Edge, try going east.
Instead of climbing or entering the distant balcony, try going east.

Instead of doing something other than examining, climbing, or entering the distant balcony, say "Unless you manage a very lucky jump, the balcony is beyond your reach."

Understand "jump [direction]" as going.
Understand "jump to [any proximate room]" as pseudogoing.
Understand "jump over/across to [any proximate room]" as pseudogoing.

Understand "jump to [distant balcony]" as entering.
Understand "jump over/across to [distant balcony]" as entering. 
Understand "jump on/onto [something]" as entering.

Section 2 - Scenery (chimneys and smoke)

The chimneys are a backdrop, in main rooftop and rooftop edge. The description is "Cheery smoke billows up from several of the chimneys." Understand "chimney" as the chimneys. Understand "stovepipe", "stovepipes", "pipes", "pipe", "sentries", "sentry", "keep", "keeps" as the chimneys.

Instead of ascending the chimneys, try going up instead.

Instead of entering or climbing the chimneys:
	say "The chimneys are all too narrow for you to climb down.";

Instead of smelling the chimneys:
	say "Smells like coal-smoke: grimy and bitter.";

Instead of smelling the location in the presence of the chimneys:
	try smelling the chimneys;

Instead of inserting something into the chimneys:
	say "You'd just lose your [noun], and rouse the suspicions of whomever lives below to boot.";

Instead of searching the chimneys:
	say "You can't see anything in there, and get a faceful of soot for your trouble.";

The smoke is a backdrop, in main rooftop and rooftop edge. The description is "It's the black coal-smoke that hangs over most districts in Toresal.";

Understand "coal", "black", "cheery", "grimy", "bitter" as the smoke.

Instead of smelling the smoke:
	try smelling the chimneys;

Instead of doing something other than examining or smelling to the smoke:
	say "It's just insubstantial smoke.";

Section 3 - Scenery (Street and crowds, far below)

[ Some crowds of pedestrians are scenery, in the Rooftop Edge. "They look like ants from up here." Understand "people", "crowd", "pedestrian", "person", "ants" as the crowds of pedestrians. The printed name is "people".
Instead of doing something other than examining to the crowds of pedestrians:
	say "The people are too far away.";

The residential district is scenery, in the Rooftop Edge. "The street is a good fifty feet straight down." Understand "street", "streets", "pavement", "East Commerce" as the residential district. The printed name is "street".
Instead of doing something other than examining to the residential district:
	say "The street is too far away.";

Allowing the Rooftop Edge to overlook Commerce Street *should* make these unnecessary. -- MSG ]

Part 4 - Black Gate Rooftop

Roof of Black Gate Estate is east of Rooftop Edge. "You're standing on a balcony, complete with ornate railing, that runs along the roof's western edge. To the northeast is a garden; to the southeast, a patio." The printed name is "Balcony"

Understand "balcony", "edge", "ornate", "ornately", "decorated", "decorative" as the Roof of Black Gate Estate.

A railing is scenery, in the Roof of Black Gate Estate. The description is "The curved, elegant railing is made of white marble and gilt iron." Understand "rail", "curved", "elegant", "ornate", "ornately", "decorated", "decorative", "white", "marble", "gilt", "gold", "golden", "gilded", "iron", "railings" as the railing.

Instead of going west in Roof of Black Gate Estate, say "If at all possible, you'd much rather find an escape route that doesn't involve death-defying leaps over fifty-foot drops. Besides, you can't leave until you find the letter Bobby was talking about."

Instead of jumping in Roof of Black Gate Estate, try going west.

Rule for printing the distant description of Rooftop Edge when in Roof of Black Gate Estate:
	say "You can see the rooftops stretching away to the west, beyond the big drop. ";

Roof of Black Gate Estate overlooks Commerce Street.

Rule for printing the distant description of  Commerce Street when in Roof of Black Gate Estate:
	say "All the people way down there on Commerce Street look like ants from up here.";

Understand "people", "crowd", "crowds", "pedestrians", "pedestrian", "pavement", "ants", "ant", "little", "wide", "residential", "affluent", "district", "business", "drop", "gap" as Commerce Street when the location is Roof of Black Gate Estate.

The distant description of Roof of Black Gate Estate is "To the [quick best route from location to Roof of Black Gate Estate], the roof ends at an ornate balcony."

Instead of facing northwest in the Roof of Black Gate Estate, try facing west.
Instead of facing southwest in the Roof of Black Gate Estate, try facing west.

Instead of smelling the Roof of Black Gate Estate when in the Roof of Black Gate Estate:
	try inspecting the rooftop garden instead; 

Instead of smelling the Rooftop Garden when the location is not the Rooftop Garden:
	try inspecting the rooftop garden instead;

Instead of listening to the Roof of Black Gate Estate when in the Roof of Black Gate Estate:
	try inspecting the Patio instead; 

Instead of listening to the Patio when the location is not the Patio:
	try inspecting the patio instead;

Part 5 - Search for the Letter (scene)

The Search is a scene. The Search begins when the player is in the Roof of Black Gate Estate. The Search ends when the player is in East Commerce Street.

Part 5 - Rooftop Patio

Section 1 - Description

The Patio is southeast of Roof of Black Gate Estate. "The patio is an area of elegant paving stones surrounding a small fountain. Cushioned divans and low marble tables allow guests to enjoy refreshment while reclining under the open air. The garden is north of here, and the balcony is northwest." 

Understand "area", "roof", "rooftop", "elegant", "paving", "stone", "stones", "pavestones", "pavestone" as the Patio.

Understand "faint", "trickle", "trickling", "fountain", "water" as the Patio when the location is not the Patio.

The distant description of the Patio is "The faint trickle of a fountain comes from the patio to the [quick best route from location to Patio]."

Section 2 - Scenery

The patio furniture is scenery in the Patio. The description is "The furniture is certainly luxurious, but it's not what you're here for." Instead of doing something other than examining to the patio furniture, try examining the patio furniture instead. Understand "divan", "divans", "table", "tables", "cushioned", "cushion", "cushions", "marble", "luxurious" as the patio furniture.

The ornamental fountain is scenery in the Patio. The description is "Water trickles over a jumble of smooth stones into a shallow pool. The sound of it is soothing." The printed name is "fountain". Understand "water", "shallow", "pool", "jumble", "smooth", "stone", "stones", "trickle", "trickling", "soothing" as the ornamental fountain.

Instead of listening to the ornamental fountain, say "It's nice, but you don't have time to sit here and listen to the water.";

Instead of listening to the Patio when in the Patio, try listening to the ornamental fountain.

Instead of drinking or tasting the ornamental fountain, say "It's not for drinking, and besides, you don't have time to linger here."

Part 6 - Rooftop Garden

Section 1 - Description

The Rooftop Garden is north of the patio, northeast of Roof of Black Gate Estate. "The garden is an impressive piece of greenery in a city otherwise dominated by stone. Most of the flowers are not in bloom this late in the season, but there are autumn roses, and some nice hanging lilies climbing up a lattice. The patio is south, and the balcony lies to the southwest. Nearby, a flight of steps leads down into the lower floors of the house."

Understand "roof", "piece", "piece of" as the Rooftop Garden.

Understand "rose", "roses", "flower", "flowers", "faint", "smell", "perfume", "scent", "of" as the Rooftop Garden when the location is not the Rooftop Garden.

The distant description of the Rooftop Garden is "A faint scent of roses wafts over from the garden to the [quick best route from location to the Rooftop Garden]."

Rule for printing the distant description of the Rooftop Garden when in the Third Floor Landing:
	say "The garden is upstairs from here. ";

Section 2 - Scenery

Some flowers are scenery in the rooftop garden. The description is "The flowers are beautiful, but you don't have time to admire them right now." Understand "greenery", "flower", "autumn", "rose", "roses", "hanging", "lily", "lilies" as the flowers.

Understand "pick [flowers]" as taking.

Instead of smelling the flowers, say "Sadly, there is no time to stop and smell the flowers right now."
Instead of smelling the Rooftop Garden, try smelling the flowers.

A lattice is scenery in the rooftop garden. "It's a decorative wooden lattice, for the flowers to climb up." Understand "wood", "wooden", "decorative" as the lattice.

Instead of climbing the lattice, say "The lattice doesn't lead anywhere, and isn't strong enough to hold your weight anyway."

Some flight of steps is a staircase, in the Rooftop Garden, Third Floor Landing, Second Floor Landing. Understand "step", "stairs", "staircase", "stairway", "stair way", "stair case", "stair" as the flight of steps.

Rule for printing the description of the flight of steps:
	if the location is:
	-- the Rooftop Garden: say "The stairs lead down to the lower floors of the house.";
	-- otherwise: say "The stairs go up and down from here.";

The head of the flight of steps is the Rooftop Garden.

After going down from Rooftop Garden for the first time during the Search:
	say "Holding your breath, you tiptoe quietly down the stairs. A prickle runs up your spine; you are now an intruder. If the Baron catches you here, he'll have you tossed back into a jail cell — and that's the [i]best[r] case scenario. If what Bobby told you is true, and Fossville knows secrets about you that he's trying to hide, it's more likely he'll just slit your throat.[paragraph break]Have to move quickly, then — search the house, find this letter, and get out.";
	select theme "SecretLetter";
	continue the action; [TESTME]

Part 8 - Third Floor Landing

Chapter 1 - Description

The Third Floor Landing is a Landing, down from the Rooftop Garden. The description is "This is the main stairway that winds up and down the back of Black Gate Estate. A hallway leads south." Inside from the Rooftop Garden is the Third Floor Landing.

Rule for printing the distant description of the Third Floor Landing when in the Music Room:
	say "You can see the main stairway to the north. ";
Rule for printing the distant description of the Third Floor Landing when in the Rooftop Garden:
	say "The stairs lead down. ";
Rule for printing the distant description of the Third Floor Landing when in the Second Floor Landing:
	say "The stairs lead up. ";

Part 9 - Music Room

Chapter 1 - Description

The Music Room is a room. "The walls of this room are painted with the Fossville coat of arms, intertwined in a pattern of gilded vines. Your footsteps are muffled by a deep blue carpet. The room continues south, and stairs lie to the north." Understand "hall", "hallway" as the Music Room when the location is the Third Floor Landing.

The distant description of the Music Room is "The music room lies [quick best route from location to the Music Room]."

bg_musicroom_door is a doorway, south of the Third Floor Landing, north of the Music Room.

Chapter 2 - Scenery (Harp)

Section 1 - Playing music

A musical instrument is a kind of thing. Understand "instrument", "musical" as a musical instrument.
Improvising on is an action applying to one thing.

Understand "play [something]" as improvising on.

Check improvising on (this is the can't play music on any old thing rule):
	if the noun is not a musical instrument begin;
		say "And how do you propose to do that?" instead;
	end if;

Check improvising on (this is the block improvising rule):
	say "You have never been musically gifted." instead.

Section 2 - Description

A harp is a musical instrument, on the small wooden platform. "A harp stands on a raised wooden platform." Understand "string", "strings", "frame", "soundboard", "kielwood", "copper", "beautiful", "huge", "tall", "rich", "dark", "shiny", "shining" as the harp.

The description is "It's a beautiful instrument, and huge, taller than you are. The frame and soundboard are carved of rich, dark kielwood; the strings shine copper in the dim light."

Section 3 - Actions

Understand "pluck [harp]" as improvising on.
Understand "twang [harp]" as improvising on.
Understand "strum [harp]" as improvising on.

Instead of improvising on the harp:
	say "[one of]Though you long to hear it play a note, you can't quite bring yourself to pluck the strings. What if someone hears you?[or]Well, maybe if you do it very softly... you swallow the lump in your throat and brush the strings lightly with your fingers. A cascade of crystal-clear notes wafts through the room. The sound lasts for only a few moments before fading away, and you could barely hear it over the thudding of your heart, but it was very beautiful.[or]No way. You've pushed your luck enough already.[stopping]";

Instead of taking the harp:
	say "The harp is much too heavy for you to drag around.";

Before pushing the harp to something:
	try taking the harp instead;

Instead of listening to the harp:
	say "The harps's strings are still.";

Instead of pushing, pulling, or turning the harp:
	say "The harp is too heavy to move.";

Instead of attacking the harp:
	say "That would make too much noise.";

Section 4 - Harp platform

The small wooden platform is a supporter, in the Music Room. The description is "It's a small wooden platform, holding up the harp." Understand "wood" as the platform.

Rule for writing a paragraph about the small wooden platform:
	say "[initial appearance of the harp][paragraph break]";
	now the harp is mentioned;
	now the small wooden platform is mentioned;

Chapter 3 - Coat of Arms

The coat of arms is scenery, in the Music Room. The description is "Fossville's crest depicts a slender, rodent-like animal, maybe a stoat or a ferret, standing on its hind legs against a field of yellow. It carries a milkweed flower clamped in its jaws. You're not sure what it's supposed to symbolize, but it looks a bit ostentatious, painted on the walls of his own house." 

Understand "wall", "walls", "paint", "painted", "Fossville", "pattern", "gilded", "vine", "vines", "ostentatious", "crest", "slender", "rodent", "animal", "rampart", "rampant", "stoat", "ferret", "field", "yellow", "hind", "legs", "milkweed", "flower", "jaws", "symbols", "symbol", "ostentatious", "or" as the coat of arms.

Chapter 4 - Carpet

The blue carpet is scenery, in the Music Room. The description is "The deep, soft carpet muffles your footsteps." Understand "deep", "soft" as the blue carpet.

Part 10 - Audience Area

The Audience Area is a room. "Several rows of benches are set up on this end of the room, so that guests may sit and enjoy a recital. The room continues to the north, and a narrow wooden door lies south."

The distant description of the Audience Area is "The audience area lies [quick best route from location to Audience Area] from here."

bg_audiencearea_door is a doorway, south of the Music Room, north of the Audience Area.

Some auditorium benches are an enterable supporter, scenery, in the Audience Area. "The benches are for audience members to sit and appreciate the music during recitals." The printed name is "benches". Understand "furniture", "row", "rows", "of", "bench", "chair", "chairs" as the auditorium benches. 

Instead of entering or sitting on the auditorium benches:
	say "You don't have time to rest now.";

After deciding the scope of the player while in the Audience Area: place the harp in scope. 

Instead of doing something other than examining or listening to the harp when the location is not the Music Room, say "You cannot reach the harp from here."

Instead of doing something when the second noun is the harp and the location is not the Music Room, say "You cannot reach the harp from here." 

Instead of going inside when in the Audience Area:
	try going the narrow wooden door;

[ Part 11 - Balcony

The Third Floor Balcony is a room. "The balcony looks out over East Commerce Street, below, and beyond it, the park."

The Third Floor Balcony overlooks the Audience Area. The distant description of the Third Floor Balcony is "A balcony lies beyond the doors to the south." 

The park view is scenery, in the Third Floor Balcony. The description is "It's a lovely view, but you don't have time to enjoy it right now." Understand "east", "commerce", "street" as the park view. The printed name is "view".

Instead of doing something other than examining to the park view, say "It's too far away."

Instead of going inside when in the Third Floor Balcony:
	try going the glass-paneled doors; ]

Part 11 - Supply Closet

Chapter 1 - Description

The Supply Closet is a room. "A dusty nook used by the servants to store mops and cleaning supplies." Understand "dusty", "nook" as the supply closet.

Chapter 2 - Exits

A narrow wooden door is a door, scenery, south of the Audience Area, north of the Supply Closet. The description is "It's just a plain wooden door, leading [if in the Audience Area]into a[otherwise]out of the[end if] closet." Understand "plain", "wood" as the narrow wooden door.

Instead of going outside when in the Supply Closet:
	try going the narrow wooden door;

Chapter 3 - Scenery

Section 1 - Mops

Some mops are scenery, in the Supply Closet. "The mops and other supplies are stacked up in the back of the closet." Understand "mop", "supplies", "cleaning supplies", "stack", "stacked", "stack of", "stacks of" as the mops.

Instead of searching, looking under, or raising the mops:
	say "There is no secret letter hidden among the mops.";

Instead of taking the mops:
	say "The last thing you need to be lugging around is a mop.";

Section 2 - Winch

The winch is fixed in place, in the Supply Closet. "Fixed to the floor in the center of the closet is a strange-looking winch." 

Rule for printing the description of the winch:
	say "It's shaped like a very large spool, with a handle attached to one side for turning it. A sturdy rope is [if the gaudy chandelier is raised]tightly wound around it several times[otherwise]attached at one end to the winch[end if]. The other end disappears into a small hole in the floor.";

Understand "large", "strange looking", "strange", "strange-looking", "spool", "drum" as the winch.

Instead of turning the winch:
	redirect the action from the winch to the crank handle, and try that instead;

Section 3 - Winch Crank

A crank handle is part of the winch. The printed name is "handle". The description is "It's a crank, allowing you to turn the winch, which winds or unwinds the rope."

Instead of pushing or pulling or turning the crank handle when the gaudy chandelier is raised:
	now the gaudy chandelier is lowered;
	say "The handle is very stiff, but with some effort you manage to turn it several times, lowering the rope through the hole in the floor.";

Instead of pushing or pulling or turning the crank handle when the gaudy chandelier is lowered:
	now the gaudy chandelier is raised;
	say "The handle is very stiff, and whatever is on the other end of the rope is very heavy, but with a great deal of effort you manage to wind the winch several times, pulling the rope up through the hole in the floor.";

Section 4 - Rope

The chandelier rope is privately-named, part of the winch. The printed name is "rope". Understand "sturdy", "rope", "cord", "tightly", "tautly", "wound", "tight", "taut" as the chandelier rope. The description is "The rope is [if the gaudy chandelier is raised]tightly wound around[otherwise]attached to[end if]the drum of the winch. The other end disappears into a small hole in the floor."

Instead of pulling, pushing, lowering, raising the chandelier rope:
	say "The rope is extremely taut and won't budge. If you want to raise or lower it, you'll have to turn the winch.";

Section 5 - Small Hole

The small hole is scenery, in the supply closet. "The hole is just slightly bigger around than the rope running through it."

Instead of searching the small hole:
	say "Most of the hole is taken up by the diameter of the rope, and what little space there is around the edges is all dark.";

Instead of inserting something into the small hole:
	say "Most of the hole is taken up by the diameter of the rope. There's not enough room to fit anything else in there.";

Part 12 - Second Floor Landing

Section 1 - Description

The Second Floor Landing is a Landing, down from the Third Floor Landing. "This is the main stairway that winds up and down the back of Black Gate Estate. A hallway leads south.[paragraph break]You can hear people moving around below." 

Instead of facing down in the Second Floor Landing, say "The stairs lead down."
Instead of inspecting the Second Floor Landing in the Third Floor Landing, say "The stairs lead down." 
Instead of inspecting the Second Floor Landing in the Living Room, say "You can see the main stairs to the north."

After assembling available exits of the Second Floor Landing:
	add down to the viable directions, if absent;

Section 2 - Event on entering

After going down to the Second Floor Landing for the first time:
	say "[command clarification break]";
	try looking;
	say "You hear a door slam open somewhere on the floor below.[paragraph break]'Blast it all!' someone yells. You recognize Baron Fossville's voice from running into him yesterday. '[i]Servants![no line break][r] Bring me spiced wine — hot — and something to eat. No, you dim fool, down here — I'll be leaving again within an hour. Bring it [i]now[r], unless you'd rather be whipped!'[paragraph break]The sounds of sudden, frantic activity drift up the stairs: footsteps, chairs scraping on wooden floors, glasses and dishes clinking.";
	update the character list;

Section 3 - Constrained exits

Before going down in Second Floor Landing:
	say "You can't go down to the first floor — Fossville is down there!" instead;

Section 4 - Fossville's voice

Fossville's voice is scenery, in the Second Floor Landing. Understand "Baron", "Fossville", "Fossvilles", "voice", "voices", "activity", "footstep", "footsteps", "muttering", "mutters" as Fossville's voice.

Instead of listening to the Second Floor Landing when in the Second Floor Landing, try listening to Fossville's voice.

Instead of listening to Fossville's voice, say "You can still hear muttering voices down there, but you can't quite make out the words."

Instead of doing something other than listening to Fossville's voice, say "You'd have to go downstairs to see what's going on."

Part 13 - Living Room

Chapter 1 - Description

The Living Room is a room. "This is a wealthy bachelor's parlor, furnished with comfortable chairs and portraits of Fossville's illustrious ancestors hanging from the walls. A hallway leads north, and sliding doors lead south."

bg_livingroom_door is a doorway, south of the Second Floor Landing, north of the Living Room.

Understand "wealthy", "bachelor", "bachelors", "parlor", "parlour" as the Living Room. Understand "hall", "hallway" as the Living Room when the location is the Second Floor Landing.

The distant description of the Living Room is "The living room lies [quick best route from the location to the Living Room] of here."

Chapter 2 - Scenery (chairs)

Some chairs are scenery, in the Living Room. The description is "A quick search reveals that the letter isn't hidden under the cushions." Understand "furniture", "furnishings", "furnishing", "chair", "cushion", "cushions", "comfortable" as the chairs.

Instead of searching the chairs, try examining the chairs.
Instead of looking under the chairs, try examining the chairs.

Chapter 3 - Scenery (portraits)

Section 1 - Description

Some portraits are scenery, in the Living Room. The description is "You can spot the family resemblance, certain features that repeat themselves in the faces of Fossville's relatives. The sharp nose, the aristocratic jaw. Perhaps you are only imagining it, but there seems to be a certain cruel gleam in their eyes, as well. You wonder how closely the current Baron resembles his forefathers." Understand "portrait", "paintings", "painting", "pictures", "picture", "Fossvilles", "ancestors", "ancestor", "relatives", "relative", "frame", "frames", "face", "faces", "of", "sharp", "nose", "aristocrat", "aristocrats", "aristocratic", "jaw", "cruel", "gleam", "eye", "eyes", "forefathers", "forefather" as the portraits.

Section 2 - New action (Looking Behind)

Looking behind is an action applying to one touchable thing.
Understand "look behind [something]" as looking behind.

Report looking behind:
	say "You find nothing of interest.";

Instead of searching or looking behind the portraits when the player does not have the secret letter:
	say "You check behind each of the paintings in turn, but find no letters glued to the backs of frames."

Chapter 4 - Scenery (sliding doors)

Some sliding doors are a closed door, scenery, south of the Living Room and north of the Master Bedroom. The description is "The sliding doors are [if open]open[otherwise]closed[end if]." Understand "track", "tracks", "rattling", "rattle", "rattly", "door" as the sliding doors.

Before going through the closed sliding doors:
	silently try opening the sliding doors;

After opening or closing the sliding doors:
	say "The doors rattle slightly in their tracks — not very loud, but enough to make you cringe."

Part 14 - Master Bedroom

Chapter 1 - Description

The Master Bedroom is a room. "The bedroom, as you might have expected, is dominated by an enormous bed and not much else. Sliding doors lead north, back to the parlor, and another doorway leads south."

The distant description of the Master Bedroom is "The master bedroom lies to the [quick best route from location to the Master Bedroom]."

Chapter 2 - Scenery (Bed)

The four-poster bed is a supporter, scenery, in the Master Bedroom. The description is "The bed is the most impressive piece of furniture you've seen in the house yet. [first time]In addition to being simply gigantic, it's... fancier... than what you would have imagined a man like Fossville might own. [only]Four carved mahogany pillars hold up a silken canopy; the coverlet is delicately embroidered with the Fossville coat of arms; the pillows are like bulging, feather-stuffed clouds, piled nearly higher than your head[first time]. Whatever else might be said about Baron Fossville, he certainly likes to sleep in comfort[only]." Understand "furniture", "carved", "pillars", "pillar", "mahogany", "canopy", "silk", "silken", "cover", "coverlet", "sheet", "sheets", "pillow", "pillows", "headboard", "mattress", "enormous", "big", "impressive", "piece of", "gigantic", "fancy", "bulging", "feather", "stuffed" as the four-poster bed.

Instead of searching or looking under the four-poster bed:
	say "You check under the bed, behind the headboard, even under the thick, overstuffed mattress. No letter."

Instead of entering the four-poster bed:
	say "You're tired, but it's neither the time nor the place to take a rest."

Instead of sleeping in the presence of the four-poster bed:
	try entering the four-poster bed.

Part 15 - Library

Chapter 1 - Description

The Library is a room. "Tall bookshelves line all four walls, and a chandelier hangs from the high ceiling. A doorway leads north to the bedroom."

bg_library_door is a doorway, south of the Master Bedroom, north of the Library.

Instead of going outside in the Library:
	try entering bg_library_door instead;

The distant description of the Library is "A library is south of here."

Before going from the Library when the secret letter is revealed and the player does not carry the letter:
	say "Don't forget the letter!" instead;

Chapter 2 - Scenery

Section 1 - Desk

[ The writing desk is a supporter, scenery, in the Library. "The writing surface is a huge, polished slab of red oak. Documents, ledgers, and half-finished letters are strewn across it in disarray." Understand "writing", "surface", "slab", "polished", "red", "oak", "wood", "wooden", "huge" as the writing desk. The printed name is "desk".

Instead of searching the desk: try searching the papers.

Some papers are scenery, on the desk. Understand "paper", "document", "documents", "ledger", "ledgers", "letters", "half finished", "business", "accounts" as the papers.

Instead of examining or taking the papers: try searching the papers.

Instead of searching the papers:
	say "Shuffling through the papers, you find much about Fossville's various holdings and business accounts, but nothing incriminating, and nothing that looks like it would be the letter Bobby told you about." ]

Section 2 -Bookshelves

Some bookshelves are a supporter, scenery, in the Library. "The shelves reach almost to the ceiling, each row sagging beneath the weight of dozens of heavy books." [ "Most of them look new and unread. One shelf, however, is loaded with old, well-worn tomes." ]

Understand "bookshelf", "shelf", "shelves", "book shelf", "book shelves", "row", "rows", "saggy", "sagging" as the bookshelves.

[ Does the player mean examining the bookshelves when we have not examined the bookshelves: it is likely. ]

Instead of searching the bookshelves:
	try examining the bookshelves.

[ Some new books are scenery, on the bookshelves. The description of the new books is "The newer books are on all different subjects, arranged in no particular order. All of them are in perfect condition. It's as though they were purchased only to fill up shelves and make the owner look more intelligent."

Understand "perfect", "condition", "newer", "book", "unread" as the new books.

Instead of taking, searching, reading, looking behind, looking under, or pulling the new books:
	say "[if the secret letter is off-stage]There are so many books here, it would take you forever to go through them all. You don't know where to begin.[otherwise] No need to rummage around anymore — you have the letter. It's time to get out of here![end if]"; ]

Some old books are scenery, on the bookshelves. The description of the old books is "The books are all on government and politics, dry stuff like that. They look worn and well-read."

Understand "older", "book", "government", "politics", "political", "worn", "well", "read", "used", "tome", "tomes", "dusty" as the old books.

[ Instead of taking, searching, reading, looking behind, looking under, or pulling the old books:
	if the secret letter is off-stage:
		move the secret letter to the location;
		say "On a sudden impulse, you pick a random book from the shelf of older books. A folded sheet of parchment slips out from the pages and see-saws to the floor."; 
	otherwise:
		say "No need to rummage around anymore — you have the letter. It's time to get out of here!"; ]

Section 3 - Chandelier

A gaudy chandelier is a container, open, not openable, fixed in place, in the Library. The gaudy chandelier can be raised or lowered. The gaudy chandelier is raised.

Understand "dangling", "crystal", "crystals", "fancy", "looping", "looped", "arm", "arms", "branching", "branches", "curling", "curled", "curlicue", "curlicues", "curlicued", "curly", "curlycue", "curlycues", "curlicued", "candle", "holder" as the gaudy chandelier.

Rule for printing the name of the gaudy chandelier:
	say "chandelier";

Rule for writing a paragraph about the lowered gaudy chandelier:
	say "In fact, the chandelier dangles practically to the floor, at the end of a long rope that goes up through the ceiling. The servants must lower it like this when they need to clean it.";

Rule for printing a locale paragraph about the raised gaudy chandelier:
	now the gaudy chandelier is mentioned;

Rule for printing a locale paragraph about the lowered gaudy chandelier:
	say "In fact, the chandelier dangles practically to the floor, at the end of a long rope that goes up through the ceiling. The servants must lower it like this when they need to clean it.";
	now the gaudy chandelier is mentioned;

Understand "long", "rope" as the gaudy chandelier when the gaudy chandelier is lowered.

Instead of doing something when the raised gaudy chandelier is physically involved:
	say "The chandelier is high out of your reach.";

Instead of pushing or pulling the lowered gaudy chandelier:
	say "You give the chandelier a nudge, and it sways, tinkling faintly.";

Instead of raising the lowered gaudy chandelier:
	say "The chandelier is too heavy to lift. You'll have to raise it with the winch in the closet upstairs.";

Instead of lowering the lowered gaudy chandelier:
	say "The chandelier is already lowered as far as it will go.";

Instead of climbing or entering the lowered gaudy chandelier:
	say "You wouldn't trust that thing to hold your weight, and you shudder to imagine the noise it would make if the rope broke.";

Instead of examining the lowered gaudy chandelier when the secret letter is in the gaudy chandelier:
	try searching the gaudy chandelier;

Instead of searching the lowered gaudy chandelier when the secret letter is in the gaudy chandelier:
	say "[one of]Odd — r[or]R[stopping]olled up and stuffed into one of the empty candle holders is a sheet of parchment.";
	now the secret letter is revealed.

Instead of searching the lowered gaudy chandelier when the secret letter is not in the gaudy chandelier:
	say "Nothing else seems to be hidden in the gaudy chandelier.";

Chapter 4 - The Secret Letter

The secret letter is a thing, in the gaudy chandelier. It can be revealed or unrevealed. It is unrevealed.

Rule for deciding the concealed possessions of the raised gaudy chandelier:
	yes;

Understand "sheet" as the secret letter when the location is the Library.
Understand "rolled", "rolled up", "rolled-up", "roll", "roll of" as the secret letter when the secret letter is in the gaudy chandelier.
Understand "folded" as the secret letter when the secret letter has been handled.
Understand "parchment", "page", "of", "paper", "writing", "script" as the secret letter.

The description of the secret letter is "The parchment is thin and the ink faded, but the writing is still quite legible, a strong, graceful script that flows down the page.[paragraph break][i]Your Royal Highness[r], it begins,[paragraph break][i]It is with a sad but hopeful heart that I write to you, for I must reveal deeds that I am not proud of, and yet perhaps I may thereby put an end to an injustice that has gone on far too long.[paragraph break]My lawful marriage was determined by political expediency rather than by the urgings of my own heart, as you well know. My wife was ever a dutiful companion, but never truly happy. I do not blame her. For my part, I did my best to ensure that she lived a lifestyle fitting for a Duchess, and that she wanted for nothing save my love.[paragraph break]Were this the full extent of my shame, I would have no need to write to you, for a loveless marriage is hardly a rare thing in our kingdom. However, it is to confess yet a further disgrace that I must pen this letter. I was not merely cool in my affections, but an unfaithful husband as well. I took a mistress, a woman on whom I bestowed all the tenderness I was unable to give my wife. And with this mistress, I had a child.[paragraph break]I kept the child's existence a secret, as much to spare my wife the ignominy as to avoid the political complications that would otherwise have ensued. From my private finances I established an orphanage here in the city, and paid a tutor from the Royal College to pose as one of the headmistresses, to raise and educate the child properly. In secret I have sponsored this child for ten years.[paragraph break]I feel it is past time for this secrecy to end. I have grown ill with a mysterious sickness these past months, and I fear that my life will soon come to a close. My wife was never able to provide me with a lawful heir, and yet I would not see my family name extinguished merely because I had not the courage to face up to my failings as a husband.[paragraph break]Therefore it is my wish that the crown recognize this child, who bears no fault for the circumstances of her birth, as my sole and lawful beneficiary, to inherit my title and estate in full upon my death, or upon her sixteenth birthday should I die before she reaches the age of majority. I hereby, by my will and by royal law, and in full possession of my faculties, declare my heir:[paragraph break]
[fixed letter spacing]          [variable letter spacing][i]Jacqueline Toresal[paragraph break]Signed and witnessed therewith,[paragraph break]Lord William, Duke of Toresal[r][paragraph break]"

After examining the secret letter for the first time:
	update the current image;
	say "Your hand is trembling as you fold the letter. You tell yourself that it's a coincidence, just someone who happens to have the same first name as you — but in your heart you know it must be true.[paragraph break]And yet... it poses so many more questions than it answers. The letter is addressed to the queen, and dated three years ago, just a few months before the Duke's death. Why was it never posted? And what is it doing in Baron Fossville's library?[paragraph break]A noise from the bedroom startles you out of your puzzling. Now that you have what you came for, you need to get out of here — fast.";

The printed name of the letter is "[if we have examined the letter]letter[otherwise]folded sheet of parchment[end if]"

After taking the letter when we have not examined the letter:
	say "Taken.[paragraph break]";
	try examining the letter;

Before examining the secret letter when the player does not have the secret letter:
	now the player carries the secret letter;
	say "You pick up the letter. [run paragraph on]";

Part 16 - The Butler

Chapter 1 - Description

The Butler is a man. The initial appearance is "[if the player is caught]The butler grips your arm firmly[otherwise]A butler is standing here, eyeing you suspiciously[end if].". The description is "The butler is tall and somewhat gaunt, with a heavy brow and a sour, twisted cast to his mouth. He looks like he doesn't like kids very much, [i]especially[r] when they're somewhere they shouldn't be." Understand "man", "servant", "tall", "gaunt", "sour", "twisted", "heavy", "brow", "old", "older" as the Butler.

Chapter 2 - Caught by the butler

After going north to the Master Bedroom when the player carries the letter:
	say "You leave the library in a rush — and run smack into someone coming the other way.[paragraph break]It's all you can do to bite back a scream. You try to duck around the person and run, but he grabs your arm and pulls you back. 'Little thief!' a voice hisses, 'What are you doing here? What do you have?'[paragraph break]He turns you around to face him. He's an older man, wearing a servant's uniform — Fossville's butler.";
	move the Butler to the location;
	try looking;
	update the character list;
	now the player is caught;
	the butler drags us away in 1 turn from now;

Chapter 3 - Butler's actions and reactions

Instead of going when the player is caught and the butler is nearby (this is the butler prevents you from going anywhere rule):
	say "The butler is stronger than he looks, and his grip on your arm is firm.";

Instead of attacking something when the player is caught and the butler is nearby (this is the butler prevents you from attacking him rule):
	say "He may look old, but this butler has your arm in some kind of death pinch. Every time you struggle, he gives it a painful twist.";

Instead of other-than-communicating when the player is caught and the butler is nearby (this is the butler prevents you from doing anything rule):
	say "The butler gives your arm a vicious shake. 'Be still, child, or I'll forget my charity and turn you over to the master.'";

The butler prevents you from attacking him rule is listed after the butler prevents you from going anywhere rule in the instead rulebook. The butler prevents you from doing anything rule is listed after the butler prevents you from attacking him rule in the instead rulebook.

Instead of talking to the Butler for the first time:
	say "'Hush,' the butler snaps, cutting you off. 'I've no great desire to see the master inflict his cruelties upon a child, but I'll not lose my position over it. Keep your lips sealed if you want to leave this place unscathed.'";

Instead of talking to the Butler [subsequent times]:
	say "The butler silences you with a stern look.";

Instead of giving the letter to the Butler:
	try showing the noun to the second noun;

Instead of showing the letter to the Butler for the first time:
	say "The butler stares intently at the letter in your hand for a long moment. His eyes flicker towards the library, then settle back on you. 'They say no dark deed goes unpunished,' he mutters. 'I'll not stand between a man and the just fruits of his ill-gotten gains. Put that away, child; I never saw it, and I don't want to know what's in it.'";

Instead of showing the letter to the Butler:
	say "The butler pointedly does not look at it.";

Instead of giving the letter to the Butler:
	try showing the letter to the Butler.

Chapter 3 - Butler's Script

At the time when the butler drags us away:
	move the butler to the Second Floor Landing;
	move the player to the Second Floor Landing, without printing a room description;
	spool through dialogue in the table of Butler's dialogue;
	the butler scene continues in 0 turns from now;

At the time when the butler scene continues:
	the butler kicks us out in 0 turns from now;
	spool through dialogue in the table of Butler's dialogue;

At the time when the butler kicks us out:
	spool through dialogue in the table of Butler's dialogue;
	now the player is free;
	select theme "Gallows";
	output chapter heading "Chapter 6 - Gallows";
	move the player to East Commerce Street;
	update the character list;

Table of Butler's dialogue
text
"The butler drags you into the stairwell, then stops, his thin, bald head cocked, listening. From downstairs you hear more voices.[paragraph break]'...difficult [i]is[r] it to keep one half-witted street urchin locked in a cage for a single night?' That's the Baron again, you realize. 'Because it is evidently far more difficult than I imagined. Perhaps I am not paying you enough for your heroic efforts so far?'[paragraph break]You hear a low, mumbled response.[paragraph break]'No, I daresay you don't,' snaps the Baron, his voice acid with sarcasm. 'I cannot express how galling it is that I still require your services; were it otherwise, I would sooner turn you all over to the Lord's Guards and let them deal with you like the useless curs you are.'[paragraph break]The butler glances at you, one bushy eyebrow raised. You're beginning to suspect that Baron Fossville is not particularly well liked by his servants.[conditional paragraph break]"
[row marker]
"'Gather your men at Lord's Market,' says Fossville. 'We'll deal with the spy first. Once that business is finished, we pay a visit to the Widows. Though I suppose I should warn you — you may encounter several unarmed, elderly women there. Let us hope they pose no greater challenge to your mercenaries than a fourteen-year-old girl.'[paragraph break]The second voice mumbles another response.[paragraph break]'See that you do. I will be there shortly.'[paragraph break]You hear footsteps, and then a heavy door slamming.[paragraph break]The butler holds his hand up, palm out. [i]Wait.[r][conditional paragraph break]"
[row marker]
"Several agonizing minutes later, you hear the door slam a second time. The butler waits a few minutes more, then drags you downstairs. He takes you through the kitchens to a side door, and shoves you unceremoniously out onto the street.[paragraph break]'Go now, and don't come back,' he says. 'Next time, it won't be me that catches you. And if it is me, it'll go that much worse for you. A bit of advice, girl,' he adds, just before turning away. 'Keep yourself out of the Baron's way. He's not one to let a trifling of blood get in between him and his ambitions.'[paragraph break]He slams the door.[conditional paragraph break]"

Part 17 - Black Gate Estate, as a region

[Only used to define interiors by, so only includes interiors -- no rooftop locations.]

The Black Gate Estate Region is a region.

The Third Floor Landing, the Music Room, the Audience Area, [the Third Floor Balcony,] the Supply Closet, the Second Floor Landing, the Living Room, the Master Bedroom and the Library are in the Black Gate Estate Region.

Book 9 - Bobby's Execution

Chapter 1 - Preparation

The occasionally crowded place is a region. East Commerce Street and Lords Market are in the occasionally crowded place.

Section 1 - Scene Start

Bobby's Hanging is a scene. Bobby's Hanging begins when the player carries the secret letter.

Bobby's Hanging ends when B_H3 is fired.

When Bobby's Hanging begins:
	move the gallows to Lords Market;
	move the crowd of spectators to the occasionally crowded place;
	remove the servants from play; [ the everyday shoppers in Lords Market ]
	update the character list;
	set pronouns from the location;

Instead of going west from East Commerce Street during Bobby's Hanging:
	say "The crowd is too thick in that direction; everyone is heading east, towards Lord's Market.";

Every turn when in East Commerce Street and a random chance of 1 in 4 succeeds during Bobby's Hanging:
	say "More people wander past, heading east towards Lord's Market.";

After going to Lords Market during Bobby's Hanging:
	the execution begins in 1 turns from now;
	continue the action.

[We used to make the execution not happen until the player actually examined the gallows. However, on the off-chance that someone doesn't examine the gallows, they will stand there all day waiting for something to happen, and nothing ever will. That's stupid.]

Section 2 - New Scenery (Gallows)

The gallows is a supporter. Understand "structure", "wooden", "platform", "scaffold", "scaffolding", "raised" as the gallows. The printed name of the gallows is "sort of wooden structure".

Instead of examining the gallows for the first time:
	change the printed name of the gallows to "gallows";
	say "It's a raised platform, with some sort of scaffolding built over it, and a rope dangling from...[paragraph break]Suddenly your mouth goes dry. It's a gallows.";

The description of the gallows is "The gallows stands stark against the late morning sky.";

Instead of doing something other than examining to the gallows during Bobby's Hanging:
	say "There are too many people in your way, and you can’t push through.";

Instead of doing something other than examining to the gallows [at any other time]:
	say "You can't bring yourself to touch it. You don't even want to get near it.";

Instead of doing something other than examining to something supported by the gallows during Bobby's Hanging:
	say "You can't reach him. There are too many people in the way.";

Rule for writing a paragraph about the gallows:
	say "[first time]There is some sort of wooden structure[subsequently]A gallows has been[only] erected in the center of the square.";

Section 3 - Scenery (Crowd)

The crowd of spectators are a backdrop. The description is "The crowd is made up of people from every part of the city. Men and women, aristocrats and servants, merchants and beggars, all rubbing shoulders[if the location is Lords Market] and craning to see[end if]." Understand "people", "person", "pedestrians", "pedestrian", "man", "men", "woman", "women", "aristocrat", "aristocrats", "servant", "servants", "merchant", "merchants", "beggar", "beggars", "spectator" as the crowd of spectators.

Before communicating when the noun is the crowd of spectators:
	say "No one is paying any attention to you; all eyes are riveted to the scene in the middle of the square." instead;

Instead of talking to the crowd of spectators:
	say "No one pays any attention to you. Everyone is too intent on [if the location is East Commerce Street]getting to Lord's Market.[otherwise]watching the scene unfold in the middle of the square.[end if]";

Instead of doing something other than examining or talking to with the crowd of spectators, say "[if the location is East Commerce Street]You probably shouldn't attract attention to yourself, at least until you see where everyone is going.[otherwise]You can't. There are too many people pressing in on you; you can barely move.[end if]";

Chapter 2 - Baron Fossville

At the time when the execution begins:
	move Bobby to the gallows;
	move Baron Fossville to the gallows;
	change the current script to { B_H1, B_H2, B_H3 };

Baron Fossville is a man. The description is "[if Bobby's Hanging is happening]The Baron looks a bit haggard, but he projects an energy into the crowd that makes him seem bigger than he is. This is a performance to him, you realize; and he is playing it for all it is worth.[otherwise if the Skirmish is happening]The Baron's face is contorted with rage. He slashes wildly at Bobby, forcing him back through sheer strength of blows.[otherwise]Now that you're up close, the cracks in Fossville's façade show themselves clearly. His thick, black hair is disheveled. His face is haggard. He's tried to hide the lines and the dark circles under his eyes with extra make-up, but it just makes him look more ghoulish[first time]. With some satisfaction, you realize that you must have been really running the Baron ragged the last couple of days. No wonder he hasn't made an appearance at his own party[only].[end if]"

The initial appearance of Baron Fossville is "Baron Fossville stands between you and the fireplace, casting his shadow across the entire room."

Instead of listening to Baron Fossville:
	say "You don't have much choice.";

Instead of kissing Baron Fossville, say "What, the man who murdered your father? You'd sooner kiss a pig."

B_H1 is a scripted event. The display text is "A man steps up onto the wooden platform. By squeezing between two spectators and craning your neck, you can just make out that it is Baron Fossville addressing the crowd.[paragraph break]'Citizens of Toresal,' he shouts, 'as Lord of this City, it is my duty to administer justice within its gates, and punish those who transgress against the Queen's Law. More often than not, this duty is a heavy burden. Though I may wish to be lenient, consideration for the sanctity of the law and the safety of the Queen's subjects must always outweigh my softer instincts. Today, this burden weighs particularly upon me, for today I must administer the most stringent punishment allowed by the law.'[paragraph break]Soldiers prod a second man up onto the platform. The black bag covers his face, but you recognize him by the clothes he's wearing — it's Bobby!"

B_H2 is a scripted event. The display text is "'This man before you stands accused of plotting high treason against the city of Toresal,' shouts the Baron. 'The evidence against him has been weighed, and he has been found guilty by the Honorable Magistrate Hester Rudup.'[paragraph break]Fossville gestures to a third man standing on his other side, a gaunt man wearing crimson robes — the man who captured you last night at the fountain."

After firing B_H2:
	move Hester Rudup to the gallows;

B_H3 is a scripted event. The display text is "'Despite the grief it causes me, the penalty for this most heinous crime is clear.' Fossville pauses; the crowd is hanging on his every word. 'By the power vested in me and in accordance with the Queen's Law... I hereby sentence this man to hang by the neck until dead.'[paragraph break]The Baron turns to one of his soldiers at the platform's base and nods his head.[paragraph break]Later, you cannot remember whether you really heard the [i]clunk[r] of the trapdoor falling open, the rope snapping taut, or whether you only imagined it. You remember screaming, clawing at the spectators in front of you, and being pushed back by rough, angry hands. You stumbled and fell onto the cobblestones. You never saw his body drop.[paragraph break]Everything goes black."

Chapter 3 - Hester Rudup

Hester Rudup is a man. The description is "He looks much the same as he did when you encountered him last night, although now he is wearing his magistrate's chain of office over the red robes. He stares coolly out over the crowd." Understand "magistrate", "honorable magistrate", "gaunt man" as Hester Rudup.

Chapter 4 - Scene End

When Bobby's Hanging ends:
	remove Bobby from play;
	remove Baron Fossville from play;
	remove Hester Rudup from play;
	remove the crowd of spectators from play;
	remove the gallows from play; 
	move the servants [back] to Lords Market;
	remove Widow Theresa from play;
	move Widow Fiona to the Dormitory;
	pause the game;
	output chapter heading "Chapter 7 - Mercenaries Again";
	[ move the player to the Dormitory, without printing a room description; ]
	move the player to the bunk bed, without printing a room description;
	update the character list;

Book 10 - Raid on Maiden House

Part 1 - Bedside Consolation

Chapter 1 - Scene begins

Bedside Consolation is a scene. Bedside Consolation begins when Bobby's Hanging ends. Bedside Consolation ends when FI25 is fired.

When Bedside Consolation begins:
	select theme "MaidenHouse";
	say "You are lying on something soft. Blankets, a thin mattress — you're back in Maiden House, lying in your own bunk. Fiona is sitting on the edge of the bed, watching you with a tender expression. Her eyes are red and raw, though, like she's been crying. For a moment, you feel strange, disconnected. You wonder what's wrong.[paragraph break]And then it comes back to you. It's Bobby. Bobby is dead.[paragraph break]";
	update the current image;
	set pronouns from Widow Fiona;
	start conversation with Widow Fiona on FI21;

Instead of going from the Dormitory during Bedside Consolation:
	say "You try to leave, but you're still disoriented, light-headed, and the floor seems to tilt beneath your feet[first time]. 'Wait,' Fiona says gently, putting her hand on your shoulder and guiding you back to the bed. 'Just wait. Rest. You're still in shock.'[subsequently].[only]";

Instead of showing the secret letter to Widow Fiona during Bedside Consolation:
	say "Widow Fiona nods and presses the letter back into your hand. 'I know, child,' she says, 'I know all about it. But there'll be time to speak more of that later.'"

Instead of giving the secret letter to someone:
	try showing the secret letter to the second noun instead;

Chapter 2 - Consolation Conversation

FI21 is a quip. The display text is "You feel like there's something important you should say to Fiona, but you can't think of what it is."

FI22 is a quip. The menu text is "'How did I get here?'". The display text is "'How... how did I get here?' you ask. Your throat is dry; your voice barely a croak.[paragraph break]Fiona strokes your forehead. Her fingertips are cool on your skin. 'Shannon found you,' she whispers, 'after the crowd dispersed. She brought you back here.'"

FI23 is a quip. The menu text is "'I'm sorry I snuck out.'". The display text is "'I... I... um...' You cough. There's something caught in your throat. There's something burning behind your eyes, making everything blurry... 'I'm sorry I...'[paragraph break]'Shh,' whispers Fiona. She lays her hand gently on the side of your face. 'Shh. It's all right.'"

FI24 is a transitional quip. The menu text is "'Bobby is dead. They killed Bobby.'". The display text is "'Bobby...' you manage to say, but then your throat closes up around the words, and it hurts. It's like choking on something that's swelling, getting bigger and bigger inside you..."
The following quip is FI25.

FI25 is a quip. The display text is "...and then something breaks inside, and it all comes gushing out. Fiona's arms are around you, and your tears are soaking into her blouse as the sobs rack their way up from the deepest part of your gut, and she is whispering, 'Oh child... oh my dear child...'[paragraph break]'[i]Buh-buh-Bobby...[no line break][r]' you wail, feeling stupid and weak and scared and guilty and oh, so alone. How did this happen? You were talking to him just yesterday. You snuck out for a lark, and now he's... he's...[paragraph break]'Hush,' Fiona says softly, stroking your hair. 'I know, child. I know.'[paragraph break][i]I never told him my name![r] you want to scream. You never told him your real name, and you never told him how you felt about him, and now he's dead, and he never really knew you at all.[paragraph break]But then you remember the letter, and all the things Bobby told you in the Jail... what if he [i]did[r] know you? What if he knew you even better than you knew yourself?"

FI26 is a quip. The display text is "Widow Fiona takes a deep breath, then turns to you and smiles weakly. 'Now, then. We don't have much time before the Baron's men come back, and I'm sure you have questions.'"

FI27 is a quip. The menu text is "'You're the tutor, aren't you?'". The display text is "Suddenly a piece of the mystery clicks into place. 'You're the tutor, aren't you?' you ask. It isn't really a question. 'The old Duke... my father... he hired you to keep an eye on me, didn't he.'[paragraph break]Fiona nods. 'Yes, that's right. The Duke had this orphanage built and placed you in it, and he brought me down from Aubrimere to look after you and see to your education. That was almost fourteen years ago, and I've been watching over you ever since.'"

FI28 is a quip. The menu text is "'Tell me about my father.'". The display text is "'Then it's true,' you say, half to yourself. 'The Duke of Toresal really was my father. And you knew him!'[paragraph break]Fiona nods again. 'I'm sorry I could not tell you sooner, Jacqueline,' she says.[paragraph break]'Tell me about him,' you say.[paragraph break]She sighs. 'To tell you everything you want to know would take too long, and the Baron's men will be back soon. Please, Jacqueline, there's so much you need to know now; there will be time to learn more once you're out of danger.'"

FI29 is a quip. The menu text is "'What will happen to Widow Theresa?'". The display text is "'What will happen to Widow Theresa?' you ask. You're actually surprised to discover that you even care. But you've never seen Widow Fiona that angry at anyone before, and it was a little frightening.[paragraph break]Her face hardens. 'She won't have much trouble being hired on as a servant or nanny,' she says. 'But I'll not waste precious time worrying about the fate of a woman who'd turn a child over to armed mercenaries.'"

FI30 is a quip. The menu text is "'What do I do now?'". The display text is "You take a deep breath. 'What am I supposed to do now?' you ask.[paragraph break]'First, you must leave this place. The Baron's men will be back, and they won't be fooled so easily again. You'll need to go quickly, out the privy window.' Although her manner is urgent, Fiona smiles a bit at that. 'It's a route you're accustomed to, to be sure.'[paragraph break]She takes a large brass key from her pocket and presses it into your hand. 'It's time you learned something of your past, Jacqueline. This is yours by right. It is the key to Red Gate Manor, and it opens the way to your history. Once you are away and safe, go there and find the answers that I can't give you now.'[paragraph break]'Finally, when all that is done, go and speak to Dame Sandler in Lord's Market. She is a friend, and more importantly her allegiances are not known to Baron Fossville. She will set you on the road to reclaiming your birthright.'[paragraph break]Fiona pulls you close and hugs you tight. 'Oh child,' she whispers, 'I have loved you like a daughter for so long. Go now, and quickly. Be safe.'"

FI31 is a quip. The menu text is "'I need to ask you something.'" The display text is "Widow Fiona, I need to ask you a question,' you say.[paragraph break]'Ask quickly, child, and I will tell you what I can,' she responds." 

The response of FI21 is { FI22, FI23, FI24 }.

The response of FI22 is { FI23, FI24 }.

The response of FI23 is { FI22, FI24 }.

After populating FI22:
	if FI23 is fired begin;
		remove FI23 from the current conversation, if present;
	end if;

After populating FI23:
	if FI22 is fired begin;
		remove FI22 from the current conversation, if present;
	end if;

The response of FI24 is { }.

The response of FI25 is { }.

The response of FI26 is { FI27, FI28, FI29, FI30 }.

The response of FI27 is { FI28, FI29, FI30 }.

The response of FI28 is { FI27, FI29, FI30 }.

The response of FI29 is { FI27, FI28, FI30 }.

FI26 is clustered with FI27, FI28, FI29 and FI31.

After populating a quip that is clustered with FI26:
	remove the list of fired quips clustered with FI26 from the current conversation;

The response of FI30 is { }.

The response of FI31 is { FI27, FI28, FI29, FI30 }.

After firing FI30:
	now the player carries the large brass key;

Rule for initiating conversation with Widow Fiona during Bedside Consolation:
	 change the chosen opening gambit to FI21;

Rule for initiating conversation with Widow Fiona during Leaving Maiden House:
	change the chosen opening gambit to FI31;

After talking to Widow Fiona when FI30 is fired:
	say "Fiona holds up her hand. 'There is no time, Jacqueline,' she says gently. 'You need to get going.'";

Instead of going from the Hallway when FI30 is unfired during Leaving Maiden House:
	say "Fiona puts a hand on your arm. 'Wait, Jacqueline. There are things you need to know before you leave.'";

The large brass key is a thing. The printed name is "brass key".

Part 2 - The Raid

Raid on Maiden House is a scene. Raid on Maiden House begins when Bedside Consolation ends.

Raid on Maiden House ends when Hidden in the Closet ends.

Chapter 2 - The Raid begins

When Raid on Maiden House begins:
	now the wooden door is closed;
	now the wooden door is locked;
	say "Suddenly, someone pounds on the front door. The sobs freeze in your throat as a voice outside shouts, 'Open in the name of Baron Fossville!'[paragraph break]Fiona looks horrified. 'It's the Baron!' she whispers. 'I didn't think he'd come here so soon... quick, Jacqueline! Go to the laundry room!'"

After going to the Hallway when Widow Fiona is in the Dormitory during Raid On Maiden House:
	say "[command clarification break]";
	try looking;
	update the character list;
	try Widow Fiona going southeast;

Instead of opening or unlocking something with when the noun is the wooden door during the Raid on Maiden House:
	say "Are you insane? Fossville's men are right outside!";

Chapter 3 - Banging on the door

The Banging is a scene. The Banging begins when Raid on Maiden House has happened and the time since Raid on Maiden House began is 1 minute. The Banging ends when the location is the Secret Closet and the player is not on the move. The Banging ends in capture when the time since the Banging began is at least 9 minutes and the location is not the Secret Closet.

Every turn during the Banging, say "The pounding continues[one of], rattling the door in its frame.[or]. 'Open this door, widow, before we break it down!'[or]. 'Open up!'[or].[at random]";

Before going from the Hallway during The Banging:
	if the room gone to is not the Laundry begin;
		say "'Jacqueline, where are you going?' hisses Fiona. 'Get into the laundry room, quickly!'" instead;
	end if;

Instead of talking to Widow Fiona during The Banging:
	say "'There's no time for that now,' she cries, 'just [i]go![no line break][r]'[paragraph break]";

Instead of showing the Secret Letter to Widow Fiona during The Banging:
	try talking to Widow Fiona instead;

Instead of talking to Widow Shannon during The Banging:
	say "'No time, no time!' whispers Shannon. She beckons you towards the secret door. 'Get inside!'"

Before going northwest from the Laundry when the secret closet door is open during The Banging:
	say "'Where are you going, Jacqueline?' cries Shannon. 'Get inside the closet!'" instead;

Chapter 4 - The Banging ends in Capture

When the Banging ends in capture:
	say "Suddenly the knocking pauses. Everyone seems to hold their breath for one silent moment.[paragraph break]Then the front door crashes open and the mercenaries rush in. Children scream. Fiona stands in their way, shouting something you cannot hear. Scarred arms shove her brutally aside. Theresa points at you, crowing, [i]'There she is!'[no line break][r] but the men don't even look in her direction.[paragraph break]The mercenary captain looms over you. His lip pulls back from chipped, yellow teeth.[paragraph break]'You're beginning to damage my credibility, [i]boy,[r]' he growls.[paragraph break]And then his fist crashes into the side of your head, and everything goes black.";
	End the game in death.

Part 2 - Secret Closet

After going southeast to the Laundry [in Maiden House] during Raid on Maiden House:
	now Widow Shannon is scenery; [bad initial appearance]
	try looking;
	now Widow Shannon is not scenery;
	now the secret closet door is open;
	now the secret closet door is not scenery;
	say "As you enter the room, Widow Shannon rushes over and pushes against one of the boards in the northern wall. It moves aside, revealing a narrow, dark opening.[paragraph break]'Quick, Jacqueline,' she whispers, 'in here!'";
	update the character list;

Instead of hiding when in the Laundry and the secret closet door is open during Raid on Maiden House:
	try going the secret closet door;

After assembling available exits when in the Laundry and the secret closet door is closed:
	remove north from the viable directions, if present;
	remove inside from the viable directions, if present;

Chapter 1 - Description

The Secret Closet is a room. "It is pitch black. The walls press close to either side; you can barely breathe." Understand "dark", "darkness", "tiny", "alcove", "hiding/hidden place", "secret", "closet" as the secret closet.

Understand "wall", "walls" as the Secret Closet when the location is the Secret Closet.

The dead-end description of the Secret Closet is "Darkness presses in on you from every direction."

The Secret Closet overlooks the Laundry.
Instead of facing outside while in the Secret Closet, try facing south.

The secret closet door is a closed door, scenery. North of the secret closet door is the Secret Closet. South of the secret closet door is the laundry. The initial appearance is "A gap between boards in the northern wall leads into a tiny, dark closet."

Understand "gap", "dark", "narrow", "opening", "boards", "board" as the secret closet door when the secret closet door is open.

Inside from the Laundry is the secret closet door. Outside from the Secret Closet is the secret closet door.

The description of the secret closet door is "[if the location is the Laundry]A dark gap in the wall leads to a secret closet.[otherwise]Shannon has replaced the board, sealing you in."

Before writing a paragraph about the secret closet door when in the Secret Closet:
	now the secret closet door is mentioned;

Before writing a paragraph about the secret closet door when in the Laundry:
	if the secret closet door is not open,
		now the secret closet door is mentioned;

After going to the Secret Closet during Raid on Maiden House:
	now the secret closet door is scenery;
	now the secret closet door is closed;
	say "You squeeze in, and Shannon pulls the board back into place, shutting you into darkness.";
	continue the action;

To decide whether mercenaries are raiding:
	if The Banging is happening, yes;
	if the number of filled rows in the table of widows meeting mercenaries is at most 1, no;
	if Hidden in the Closet is happening, yes;
	no;

Instead of opening the secret closet door when mercenaries are raiding:
	say "The Baron's men would catch you the instant you left this hiding place.";

Before going south in the Secret Closet when the secret closet door is closed and mercenaries are raiding:
	try opening the secret closet door instead;

Rule for implicitly opening the secret closet door:
	try silently opening the secret closet door; [not even printing a clarification]

After going through the secret closet door from the secret closet during Hidden in the Closet:
	if the number of filled rows in the table of widows meeting mercenaries is 1:
		spool through dialogue in the table of widows meeting mercenaries;
		now the secret closet door is not scenery;
	continue the action;

Instead of listening to the Secret Closet when in the Secret Closet during Hidden in the Closet:
	say "The sound of your own harsh breathing fills the tiny alcove[first time]. If they weren't shouting so much, you're sure the mercenaries outside would be able to hear you[only].";

Instead of singing in the Secret Closet when mercenaries are raiding:
	say "You dare not make a sound.";

Instead of examining in the Secret Closet when the secret closet door is closed:
	say "It's pitch black in here!";

Instead of pseudolooking the Secret Closet when the location is the Secret Closet:
	try touching the Secret Closet; [TESTME]

Instead of touching the Secret Closet [walls]:
	say "The walls are so close, it feels like a coffin in here.";

Section 2 - Making the secret closet secret 

Before going north from the laundry when the secret closet door has not been open:
	say "You can't go that way." instead;

Before going inside from the laundry when the secret closet door has not been open:
	say "You can't go that way." instead;

Before doing something to the secret closet door when the secret closet door has not been open:
	say "You can't see any such thing." instead; [ To make it properly secret, we should try to stifle the every turn rule that makes Widow Shannon do the laundry -- because every turn rules don't run when the noun is not properly understood -- but I don't think anyone will notice. ]

Before doing something when the second noun is the secret closet door and the secret closet door has not been open:
	say "You can't see any such thing." instead; 

Chapter 2 - Eavesdropping on Mercenaries (the Raid overheard)

Hidden in the closet is a scene. Hidden in the closet begins when the Banging ends.

Hidden in the closet ends when the table of widows meeting mercenaries is empty.
Hidden in the closet ends early when the location is not the secret closet.

Every turn during Hidden in the Closet:
	if the location is the secret closet:
		spool through dialogue in the table of widows meeting mercenaries.

When Hidden in the Closet begins:
	now the secret closet door is scenery;

When Hidden in the Closet ends:
	now the secret closet door is open;
	now the secret closet door is not scenery;
	now the wooden door [of Maiden House] is open;
	if the player is in the secret closet:
		move the player to the Laundry;
	update the character list;

Table of widows meeting mercenaries
text
"The pounding suddenly stops. Heavy footsteps tromp into the building, making the floorboards shudder beneath your feet.[paragraph break]'Where is she?' growls a rough voice. It's hard to tell from two rooms away, but it sounds very similar to the mercenary captain you heard yesterday morning, at the Grubber's Market.[conditional paragraph break]"
[row marker]
"'Where is who, sir?' That must be Fiona.[paragraph break]'Don't play games with me, woman,' snaps the first voice. 'I've had my fill of it. The old Duke isn't here to protect you anymore, and if one of my men were to be careless and injure someone, it would rest but lightly on the Baron's conscience. Now, we know she came here after the hanging. [i]Where is she?[r]'[paragraph break]'I can tell you, good sir,' says a third voice.[paragraph break]Bitter anger wells up in your throat, even as fear turns your stomach cold. [i]That was Theresa![r][conditional paragraph break]"
[row marker]
"'[no line break][i]Widow Theresa-[no line break][r]' Fiona begins, in a strangled voice, but Theresa interrupts her.[paragraph break]'Oh, do spare us your indignation, Fiona,' she snaps. 'I've stood by for years while you protected and coddled that child, and I tell you I've had my fill of it. Now she has done something unconscionable — obviously, or else the good Baron would not be looking for her — and I for one will not stand in the way of the law merely so that you can continue to bestow favoritism on one ungrateful little brat.'[paragraph break]There is a long silence. You can feel your cheeks burning. Theresa has always disliked you, but you never thought she would be so treacherous as to turn you over to the Baron's men.[conditional paragraph break]"
[row marker]
"Finally, Theresa speaks: 'If you look in the laundry room, you will find a hidden closet in the north wall. The child is hiding there.'[paragraph break]'My thanks, madam,' grunts the mercenary captain. 'I'll make sure the Baron hears of your cooperation when we-'[paragraph break]Suddenly Shannon is screaming, '[i]Jacqueline! Jacqueline, wait![r]' Shouts. Running footsteps. What is going on? You hear footsteps coming down the hall, towards the laundry room... closer, closer... but then they suddenly turn [i]away[r] from the laundry room, towards... the privy?[paragraph break]'Jacqueline... oh, Goddesses.' That's Shannon's voice.[paragraph break]'What is it?' yells the mercenary captain. 'Where did the girl go?'[paragraph break]'She... she got away from me, Miss Fiona,' wails Shannon. 'I was trying to get her into the hiding place, just like you said, but she slipped past me and wriggled out the privy window. You know how fast she can get through that window, Miss Fiona. She could be anywhere in the city by now. I'm so sorry, Miss Fiona, so sorry.'[paragraph break]You can't help but grin. You've always thought of Shannon as being a trifle dim-witted, and now you'll have to revise your opinion.[conditional paragraph break]"
[row marker]
"The mercenary captain utters a curse so foul that you actually wince. 'Don't just stand there, you bleeding dims,' he yells, 'get out there and find her! You two, keep an eye on the alley in case she comes back. Go! Go! Find her or I'll have your hides!' The footsteps run back up the hall, growing fainter this time, then vanishing altogether as they cross the threshold and exit Maiden House.[paragraph break]You let out a long, shaky breath.[conditional paragraph break]"
[row marker]
"The door opens, and Shannon takes your hand as you stumble out of the stifling darkness.[paragraph break]'That was too close,' she says.[conditional paragraph break]"

Part 3 - Leaving Maiden House / Aftermath

Leaving Maiden House is a scene. Leaving Maiden House begins when Hidden in the Closet ends. Leaving Maiden House ends when the location is Behind Maiden House.
 
Instead of opening or unlocking something with when the noun is the wooden door during Leaving Maiden House:
	say "'Not that way,' says Fiona. 'They'll be watching the front door for sure. Sneak out the back, through the privy window.'"

Instead of talking to Widow Shannon during Leaving Maiden House:
	say "'We can talk later,' Shannon says, gently pushing you towards the door. 'Right now you should go talk to Widow Fiona.'"

[After talking to Widow Fiona during Leaving Maiden House:
	say "'We can talk later,' Shannon says, gently pushing you towards the door. 'Right now you should go talk to Widow Fiona.'" [not sure what this is about...]]

Instead of going through the secret closet door during Leaving Maiden House:
	say "Ugh. You're not going in there again; that was almost as bad as the jail cell.";

After going to the Hallway when FI26 is unfired during Leaving Maiden House:
	now Widow Fiona is scenery;
	say "[command clarification break]";
	try looking;
	now Widow Fiona is not scenery;
	say "Widow Fiona and Widow Theresa are standing in the middle of the hallway, staring each other down. Theresa looks sullen but defiant; Fiona is livid with rage.[paragraph break]They are silent for a long, tense moment. Then Widow Theresa looks away.[paragraph break]'Get out,' chokes Fiona. Her arm is extended, one pale finger pointing at the front door. 'Get out and never set foot in this orphanage again. Perhaps when the nights grow cold you may rely on the charity of your precious Baron.'[paragraph break]Theresa looks at you once — a bitter, helpless glare — and then turns on her heel and walks out, slamming the door behind her.[paragraph break]";
	update the character list;
	now the wooden door [of Maiden House] is closed;
	start conversation with Widow Fiona on FI26;

Book 11 - Red Gate Estate

Part 1 - The company of Widow Shannon

Chapter 1 - Shannon's entry

Shannon's company is a scene. Shannon's company begins when leaving Maiden House ends. Shannon's Company ends when the location is Lords Market and the player is clean.

When Leaving Maiden House ends:
	move Widow Shannon to Behind Maiden House;
	update the character list;

After going through the privy window during Leaving Maiden House:
	select theme "RedGateEstate";
	output chapter heading "Chapter 8 - Red Gate Estate";
	say "You pull yourself through the window and once again clamber up into the alley. You can hear a commotion from the direction of Lord's Market; it looks as though the Baron's mercenaries are still out searching the streets for you, but they'll double back and put a watch on Maiden House soon enough.[paragraph break]There is a scrabbling, grunting sound behind you, and you turn to see Shannon trying to push her way through the window. Confused but not wanting to just stand there and watch her struggle, you give her a hand up.[paragraph break]'Fiona thought I should maybe come with you,' she says, after she's finished smoothing her skirts and catching her breath. 'In case you needed any help.'";
	move Widow Shannon to the location;
	continue the action;

Chapter 2 - Making Shannon follow the player

Every turn during Shannon's company:
	if Widow Shannon is on-stage,
		move Widow Shannon to the location;
	[ if Widow Shannon is silenced,
		now Widow Shannon is not silenced. ]

After assembling during Shannon's Company:
	if Widow Shannon is on-stage,
		add Widow Shannon to the roster of available characters, if absent;

After going to somewhere when Widow Shannon is on-stage [and Widow Shannon is not silenced] during Shannon's company:
	if the current tension is at most unimportant,
		say "Shannon follows [one of]silently[or]close by[purely at random].";
	continue the action;

Chapter 4 - Cutting through the park

Instead of going northeast from Behind Maiden House during Shannon's Company:
	say "Shannon lays a hand on your elbow. 'Wait, Jacqueline,' she whispers. 'The mercenaries will be watching the entrance. We'd best cut through the park. I'll help you climb the wall.'"

Instead of climbing the mortar wall when in Behind Maiden House during Shannon's Company:
	say "Shannon laces her fingers together and gives you a boost up to the top of the wall; then you reach down and help pull her up. You both drop down onto the grass on the other side, and then it's a quick sprint across the open space of the park. Fortunately, no one is about after the awful spectacle in Lord's Market, and you make it to the relative safety of East Commerce Street without anyone spotting you.";
	move Shannon to East Commerce Street;
	move the player to East Commerce Street;
	update the character list;

Chapter 5 - Various destinations

Instead of going East from East Commerce Street when the player is still dirty during Shannon's Company:
	say "'We should stay away from Lord's Market for a while, I think,' says Shannon. 'Fossville's men will still be looking for you.'";

Instead of going West from East Commerce Street during Shannon's Company:
	say "'We shouldn't wander the streets any more than we have to,' Shannon says. 'Fossville's men will still be looking for you.'"

After going to East Commerce Street when the player is clean during Shannon's Company:
	select theme "PreparingForBall";
	continue the action;

When Shannon's Company ends:
	remove Widow Shannon from play;
	say "'Well,' says Shannon, 'I've seen you safely to Dame Sandler. I'd better get back to Maiden House; the Baron's men might get suspicious if I'm gone too long. Take care, Jacqueline.' She hugs you tightly, sniffling a little, and then hurries off to the south.";
	update the character list;
	try going east;

Chapter 6 - Red Gate Estate, as a region

The Red Gate Estate region is a region.
The Foyer, [the Dining Room, the Red Gate Kitchen,] the Great Hall, the Red Gate Estate Second Floor Landing, the Office, the Red Gate Estate Third Floor Landing, the Red Gate Master Bedroom, the Red Gate Cellar, and the Bathroom are in the Red Gate Estate region.

The silence is a backdrop, in the Red Gate Estate region. Understand "stillness", "quiet", "eerie", "expectant", "insubstantial", "echo", "echoes", "echos" as the silence.

Instead of listening to the silence, say "The silence is eerie, almost expectant, as though the house were holding its breath, waiting."

Instead of listening to the location while in the Red Gate Estate Region, try listening to the silence.

Instead of doing something other than listening to the silence, say "It's just insubstantial silence."

Part 2 - Foyer

Chapter 1 - Description

The Foyer [of Red Gate Estate] is north of Red Gate Estate. "The entrance hall is large, high-ceilinged, and empty. A doorway leads north into a large hall, and the street lies south." The printed name is "Entrance Hall".

Understand "entrance", "hall", "large", "high", "ceiling", "ceilinged", "empty", "echoing", "marble", "shadow", "shadows", "shadowed", "interior" as the Foyer.

Understand "marble" as the backdrop-floor when the location is the Entrance Hall.

The distant description of the Foyer is "The foyer lies [quick best route from location to Foyer] from here."

The Foyer overlooks East Commerce Street.

East Commerce Street overlooks the Foyer.

Rule for printing the distant description of the Foyer when in East Commerce Street:
	if Red Gate Estate is open:
		say "You cannot see into the shadowed interior from here. ";
	otherwise:
		say "The front door of Red Gate Estate is closed. ";

Rule for printing the distant description of East Commerce Street when in the Foyer:
	say "The bustle of the street outside does not reach in here. Here, all is quiet. ";

Instead of talking to Widow Shannon in the Red Gate Estate region:
	say "Something about this place — the stillness, the solemn expectancy — makes you reluctant to break the silence with idle chatter.";

Chapter 2 - Events on entry and exit

First after going through Red Gate Estate:
	ratchet the current tension to important;
	say "[first time]You pause for one last glance over your shoulder at the streets of Toresal, so familiar from your years of skulking, scrounging, and sometimes even sleeping on them. Then you pass over the threshold, into Red Gate Estate.[only]";
	continue the action;

Every turn when the location is the Foyer for the first time:
	if the player is on the move,
		say "The house is eerie in its stillness. The walls are bare; your footsteps echo on the marble floors.";

Instead of going south from the Foyer when the player is still dirty:
	say "'Wait,' says Shannon. 'Are you sure you've found everything you were meant to find here?'";

Instead of going to the Foyer when the Bodyguard Scene is happening:
	say "Pieter puts his hand on your arm. 'I know you are anxious to visit your father's house again,' he says quietly, 'but we really should get to the ball as soon as possible.'"

[
Part 3 - Dining Room

Chapter 1 - Description

The Dining Room is a room. "The long table in the center of the hall is covered in sheets, as are the rows of chairs to either side. Doorways lead north and south." The printed name is "Dining Hall". Understand "hall" as the Dining Room.

The dining_room_door is a doorway, north of the Foyer, south of the Dining Room. 

The distant description of the Dining Room is "A dining room lies to the [quick best route from location to Dining Room]."

Part 4 - Kitchen

Chapter 1 - Description

The Red Gate Kitchen is a room. "The kitchen seems particularly barren, empty of its usual clutter of pots and pans and utensils. The firepit in the southwestern corner is cold and dark. The dining room lies south, and a wide staircase in the back of the room leads up to the second floor." The printed name is "Kitchen". Understand "barren", "empty" as the Red Gate Kitchen.

The rg_kitchen_door is a doorway, north of the Dining Room, south of the Red Gate Kitchen. 

The firepit is scenery in the Red Gate Kitchen. Understand "fireplace", "fire", "pit", "ash", "ashes", "cold", "dark" as the firepit. "Nothing but ashes there now." Instead of searching the firepit, try examining the firepit instead.

Rule for printing the distant description of the Red Gate Kitchen when  in the Dining Room:
	say "The kitchen lies north of here. ";
Rule for printing the distant description of the Red Gate Kitchen when in the Red Gate Estate Second Floor Landing:
	say "The kitchen is downstairs. ";

]

Part 3A - Great Hall

Chapter 1 - Description

The Great Hall is a room, north of the Foyer. "This room runs nearly the entire length of this floor, but its size only draws attention to its emptiness. The walls are bare plaster; what little furniture remains is stacked in the corners and covered with dusty sheets. [first time][paragraph break]You can't help but imagine how this place once was, in its full vibrancy. The cream of the city once filled this room, entertained and served by the Duke of Toresal — your father. They respected him, honored him, even loved him. And now it's all gone. [paragraph break][only]A wide stairway sweeps up to the second floor, while a narrower stairway leads down to a cellar. The foyer lies south."

Understand "large" as the great hall.

Chapter 2 - Scenery

Section 1 - Portrait

A portrait of the old Duke is fixed in place, in the Great Hall. "[one of]Whoever took most of the furniture away for some reason decided to leave a large portrait of the Duke himself hanging on the far wall[or]The portrait of the Duke stares down at you from the far wall[stopping]."

The description of the portrait of the old Duke is "The Duke's features are strong and stern, yet softened by kindness, and although the canvas is faded with the years, his eyes are still a rich, vivid blue. In the painting he is smiling slightly, but as he stares out at the empty ruin of his home — at [i]you[r], his lost, beleaguered, and only heir — he seems weighed down by sadness."

Understand "painting", "picture", "canvas", "feature", "features", "strong", "stern", "kind", "kindness", "faded", "fading", "eye", "eyes", "rich", "vivid", "blue", "smile", "smiling", "sad", "sadness", "of Toresal", "father", "my/your/jacks father/dad" as the portrait of the old Duke.

Rule for printing the name of the portrait of the old Duke:
	say "portrait of the Duke";

After printing the description of the portrait of the old Duke when we have not examined the portrait of the old Duke:
	say paragraph break;
	say "'His eyes...' whispers Shannon, with a slight catch in her throat. 'Jack, dear, you have his eyes.' ";

Instead of doing something when the portrait of the old Duke is physically involved:
	say "The portrait is hung too high for you to reach.";

Section 2 - Old Furniture

The old furniture is scenery, in the Great Hall. The description is "The tables and chairs are old and show signs of neglect, but they are finely crafted, made of rich, dark wood."

Understand "chairs", "chair", "table", "tables", "fine", "finely", "crafted", "expensive", "neglected", "dusty", "enshrouded", "shrouded", "sheet enshrouded", "rich", "dark", "wood", "wooden" as the old furniture.

Instead of sitting on the old furniture:
	say "You'd have to remove the dustsheets first, and there will be time for that later.";

Instead of standing on the old furniture:
	say "Standing on the old furniture is probably not a good idea.";

Some dustsheets are scenery, in the Great Hall. The description is "The sheets are fine linen, smudged gray with dust." Understand "shroud", "shrouds", "white", "sheets", "sheet", "linen", "linens", "dusty" as the dustsheets. The printed name of the dustsheets is "sheets".

Does the player mean doing something other than reading the dustsheets: it is likely.

Instead of looking under the dustsheets, try examining the old furniture.

The dust is a backdrop, in the Red Gate Estate region. The description is "The dust covers everything, muting all colors under a film of gray." Understand "film", "grey", "gray", "of" as the dust.

Instead of rubbing the furniture, try rubbing the dust.
Instead of rubbing the dustsheets, try rubbing the dust.
Instead of rubbing the dust:
	say "It would take you months to get this place clean.";

Instead of touching the dust:
	say "The dust leaves a little gray smudge on the tip of your finger.";

Instead of taking, raising, pulling, pushing, searching the dustsheets:
	say "There's little point; it would just fill the air with dust. There will be time to clean this place later, when this craziness is over.";

Understand "wipe [something]" as rubbing.

Part 4A - Cellar

The Red Gate Cellar is a room. "It is cool and quiet down here, amidst the stone walls of the cellar. A narrow stairway leads back up."  Understand "basement" as the Red Gate Cellar.

A narrow stairway is a door, scenery, open, not openable, down from the Great Hall, up from the Red Gate Cellar. "Narrow, stone steps leading [if the location is the Red Gate Cellar]up[otherwise]down[end if]."

Understand "narrow", "stone", "stairs", "staircase", "step", "steps" as the narrow stairway.

Instead of climbing the narrow stairway:
	try entering the narrow stairway;

Chapter 2 - Furnace

The furnace is a container, fixed in place, closed, openable, in the Red Gate Cellar. "Against the back wall stands a pot-bellied iron furnace, [if the furnace is cold]cold and still[otherwise]giving off a bright orange glow[end if]."

Understand "boiler", "grate", "grating", "pipe", "pipes", "heater", "iron", "metal", "big", "pot ", "belly", "bellied", "pot-belly", "pot-bellied", "thick" as the furnace.

The furnace can be cold or hot. The furnace is cold.

Rule for printing the description of the cold furnace:
	say "The metal grating is [if the furnace is closed]closed[otherwise]open, and the interior is dark[end if].";

Rule for printing the description of the hot furnace:
	say "The furnace is [if the furnace is closed]closed, but bright orange light flickers through the grating[otherwise]open, and bright orange light pours out from it[end if]. Thick, metal pipes leading away from the furnace carry the heat to other parts of the house.";

After opening the furnace for the first time:
	say "The grating squeaks open. The inside of the furnace is empty and dark.";
	if player is not carrying the pile of broken wood:
		if brokenwood is true:
			say "'Maybe we should use the broken furniture wood', suggests Shannon.";
		otherwise:
			say "'We could break up some of the furniture upstairs,' suggests Shannon. 'It would be nice to have some warmth in here.'";
	
Instead of touching the hot furnace:
	say "You snatch your hand away. It's hot!";

Instead of touching the cold furnace:
	say "The metal is cold.";

Chapter 3 - Furniture makes wood

BrokenWood is a truth state that varies.

Instead of attacking the old furniture:
	say "You pull a couple of chairs out from under the dust sheets and start whacking them against the hard floor, stomping on them, yanking the legs free.  It's tiring work... but also somehow exhilarating. After all the awful things that have happened to you, it's nice to be able to take your frustrations out on this old furniture. Soon you have a respectable pile of firewood.";
	now brokenwood is true;
	now the player is carrying the pile of broken wood;

Instead of attacking the old furniture when the pile of broken wood is on-stage or the furnace is hot:
	say "You've probably smashed enough furniture for today.";

The pile of broken wood is a thing. The description is "Just a bundle of sticks — that used to be a nice chair."

Does the player mean doing something with the pile of broken wood: it is very likely.

Understand "fire wood", "firewood", "bundle", "bundle of", "sticks", "sticks of" as the pile of broken wood.

Instead of inserting the pile of broken wood into the furnace:
	remove the pile of broken wood from play;
	now the roaring fire is in the furnace;
	now the orange light is in the cellar;
	now the furnace is hot;
	say "You pile the sticks into the furnace, and Shannon, always helpful, produces a match. Soon you have a roaring fire.[paragraph break]'There, that should warm things up a bit,' says Shannon."

Chapter 4 - Fire

The roaring fire is scenery. "The fire crackles cheerfully inside the belly of the furnace." 

Understand "flames", "flame", "burning", "crackling", "cheerful", "smoke" as the roaring fire.

Instead of listening to the roaring fire:
	say "The flames crackle cheerfully.";

Instead of smelling the roaring fire:
	say "It smells like good, clean wood-smoke.";

Instead of doing something when the roaring fire is physically involved:
	say "Messing with fire is a good way to get burned.";

The orange light is scenery. "The warm glow flickers out from the belly of the furnace." 

Understand "flickering", "glow", "warm", "glowing", "bright" as the orange light.

Instead of doing something when the orange light is physically involved:
	say "It's just insubstantial light.";

Part 5 - Second Floor Landing

Chapter 1 - Description

The Red Gate Estate Second Floor Landing is a Landing, up from the Great Hall. "The shadowed stairway leads up and down. A doorway leads south." The printed name is "Second Floor Landing". 

Understand "shadowed", "shadowy", "shadow", "shaded", "shady" as a Landing when the location is in Red Gate Estate and the location is not the item described.

Understand "wide" and "sweeping" as the Red Gate Estate Second Floor Landing when the location is the Great Hall.

Rule for printing the distant description of the Red Gate Estate Second Floor Landing:
	if the location is:
		-- Great Hall: say "The stairs lead up to the second floor. ";
		-- Red Gate Estate Third Floor Landing: say "The stairs continue down. ";
		-- Office: say "You can see stairs north of here. ";

Part 6 - Office

Chapter 1 - Description

The Office is a room. "The room was clearly ransacked some time ago. What few books are left lie scattered about the floor. The desk is pushed against one wall, the dustsheet that once covered it ripped away. You can leave to the north." 

The distant description of the Office is "An office lies south of here."

rg_office_door is a doorway, south of the Red Gate Estate Second Floor Landing, north of the Office.

A procedural rule when in the Red Gate Estate Region:
	ignore the describe what's on scenery supporters in room descriptions rule;

Chapter 2 - Scenery (Shelves and desk)

Some dusty bookshelves are a supporter, fixed in place, in the Office. The description is "The bookshelves have been completely looted[if the wooden box is on the dusty bookshelves], except for a wooden box almost hidden up on the topmost shelf[end if]." The printed name is "bookshelves". Understand "ransacked", "empty", "looted", "top", "topmost", "highest", "uppermost", "bowed", "warped", "sagging", "worn", "scuffed", "shelf", "shelves", "bookshelf", "book shelf", "book shelves" as the dusty bookshelves.

After printing the description of the dusty bookshelves:
	now the wooden box is noticed;

Rule for writing a paragraph about the dusty bookshelves when the wooden box is unnoticed:
	say "The empty shelves are bowed slightly, permanently warped from spending so many years supporting the weight of as many books as they could hold. And beneath the dust, the wood is worn, scuffed — a sign that those books were not merely for show, but taken down and read, again and again. It occurs to you that the Duke must have loved his books. You're not sure why you are so certain of this, but certain you are. Of all the desecrations visited upon his home since his death, you think this room would have made him the saddest.[paragraph break]Up on the top shelf, missed by the looters, you spy a wooden box.";
	now the wooden box is noticed;

Rule for writing a paragraph about the dusty bookshelves when the wooden box is noticed and the wooden box is on the dusty bookshelves:
	say "Nearly hidden on one of the top bookshelves is a wooden box.";


Chapter 3 - A wooden box containing some letters

The wooden box is a closed, openable, privately-named container. The wooden box is on the dusty bookshelves. The description is "The box is about twelve inches by ten inches by six, carved of some beautiful, honey-colored wood and polished smooth. On the lid is engraved the crest of Duke Toresal."

Understand "wood", "old", "wooden", "box", "honey", "colored", "coloured", "smooth", "polished", "carved", "beautiful", "lid" as the wooden box when the wooden box is noticed.

A thing called the engraved crest of Duke Toresal is part of the wooden box. The engraved crest of Duke Toresal is privately-named. Understand "crest", "engraving", "Duke", "Dukes", "Crest", "Toresal", "Toresals", "engraved" as the engraved crest of Duke Toresal when the wooden box is not on the dusty bookshelves.

The wooden box can be noticed or unnoticed. The wooden box is unnoticed.

Instead of doing something to the wooden box when the wooden box is on the dusty bookshelves and the Duke's desk does not support the player:
	say "You can't reach the wooden box from where you're standing."

After opening the wooden box:
	say "Nestled in the box's velvet-lined interior are bundles of papers[first time] — [subsequently], [only]letters and documents of the old Duke.";

After taking the wooden box:
	now the dusty bookshelves are scenery;
	continue the action;

Instead of showing the open wooden box to Shannon when the box has been open:
	say "'The Duke's old correspondences,' breathes Shannon. 'What a treasure, Jacqueline!'";

Instead of giving the wooden box to Shannon when the box has been open:
	say "'No, you should keep those, Jacqueline,' says Shannon firmly.";

Some collected documents are in the wooden box. Understand "legal", "document", "financial", "records", "record", "details", "detail", "papers" as the collected documents. The printed name of the collected documents is "documents"

Understand "letters" as the collected documents when the old love letters are not in the wooden box.

The description of the collected documents is "Some of them are legal documents indicating the disposition of the old Lord's estate. There are also financial records of the city's Treasury, details of various funds and improvements he planned to implement before he died. There are letters from noblemen and noblewomen from every corner of Miradania, addressing the Duke in terms of the utmost deference and respect.[paragraph break]At the bottom of the stack are several letters written on small, worn parchment[first time]. [description of old love letters][subsequently].[only]";

After printing the description of the collected documents:
	move the old love letters to the wooden box;
	move the collected documents to the wooden box; [for a nicely ordered listing]

Some old love letters are a thing.

Understand "clumsy", "penmanship", "handwriting", "short", "scribbled", "small", "worn", "parchment" as the old love letters.

The description of the old love letters is "The penmanship is clumsy, like the way yours looked when Fiona was first teaching you your letters. Each is short, no more than a few scribbled sentences:[paragraph break][i]My Lord. I had some time to myself in the garden and thought of you. I hope to see you again soon if your duties will let you get away. You can see I've not put my name to this like you asked but you know who it is. Your Love.[paragraph break]Dear Love. When will I see you again? Your duties keep you too long from me. But that is why I love you so, you are a good man who never forsakes his duty. Come see me soon. Your Love.[paragraph break]My precious Duke. I pressed Estelle with subtle questions but I am certain she knows nothing. I am discreet and would never give you cause to worry. Our secret is safe. Your Love.[no line break][r][first time][paragraph break]Your mother's words, you realize. Secret letters that she sent to your father, before you were born. He would have had no pictures of her, of course, nor any documents containing clues to her identity. These pieces of paper are all you will ever have of her.[only]"

Instead of taking something that is in the wooden box:
	say "Better to leave them in the box for now. If you take them out, they could get scattered and lost.";

Procedural rule while showing something contained by the wooden box to someone:
	ignore the carrying requirements rule.

Instead of showing something to Widow Shannon when the noun is in the wooden box:
	say "Shannon looks close to tears as you show her the letters[first time]. 'Your father was such a compassionate man,' she whispers. 'I just think it's terrible that he was forced to hide away everything that he truly loved[only].";

Chapter 4 - An old desk

The Duke's desk is a scenery supporter, enterable, in the Office.

The description of the Duke's desk is "It was a handsome thing once, carved from mahogany and kielwood. Now it is covered with years['] worth of dust."

Understand "dusty", "old", "writing", "handsome", "thing", "carved", "wood", "wooden", "mahogany", "kielwood", "furniture" as the Duke's desk.

Instead of pushing, pulling, turning, or shaking the Duke's desk:
	say "It's too heavy to move.";

Instead of washing or rubbing the Duke's desk:
	say "It would take more time than you have right now to restore the old desk.";

After entering, climbing, sitting on or standing on the Duke's desk:
	say "You climb up onto the desk. You can now see the wooden box clearly, within easy reach.";

Chapter 5 - Scattered books

Some scattered books are scenery, in the Office.

The description of the scattered books is "Most of the books are damaged — pages missing, covers torn off. A few have been nibbled at by mice."

Instead of taking or stealing the scattered books:
	say "None of these books is worth anything now.";

Instead of reading or searching the scattered books:
	say "You flip through a few of the old tomes, but find nothing of interest.";

Instead of pushing, pulling, turning, or shaking the scattered books:
	say "You nudge the piles of books around, but find nothing of interest.";

Understand "book", "page", "pages", "cover", "covers", "torn", "missing", "nibbled", "eaten", "pile", "piles", "of", "old", "tome", "tomes" as the scattered books.

Part 7 - Third Floor Landing

The Red Gate Estate Third Floor Landing is a Landing, up from the Red Gate Estate Second Floor Landing. "The stairs end here at the third floor. A doorway leads south." The printed name is "Third Floor Landing". 

Rule for printing the distant description of the Red Gate Estate Third Floor Landing:
	if the location is Red Gate Master Bedroom, say "You can see stairs to the north. ";
	otherwise say "The stairs lead up. ";

Part 8 - Red Gate Master Bedroom

Chapter 1 - Description

The Red Gate Master Bedroom is a room. "Most of the furniture from this room has been carted off. Only the bed remains, just a bare mattress covered with yet another plain, dusty sheet. Doorways lead north and south." The printed name is "Master Bedroom".

rg_bedroom_door is a doorway, south of the Red Gate Estate Third Floor Landing, north of the Red Gate Master Bedroom.

The distant description of the Red Gate Master Bedroom is "A bedroom lies [quick best route from location to Red Gate Master Bedroom] of here."

Chapter 2 - Scenery

The Red Gate bed is a supporter, fixed in place, in the Red Gate Master Bedroom. The description is "No one has slept in it since the Duke died, years ago." The printed name is "bed". Understand "bare", "mattress" as the Red Gate Bed.

Rule for writing a paragraph about the Red Gate bed:
	say "Even before the events of the last two days, you had heard of the Duke, of course. And although there are many theories about exactly what brought him low — illness; treachery; heartbreak when his wife, the Duchess, left him — all agree that he passed away in his sleep. This, then, is where he died. This very room, in this very bed. This mattress, cold, stripped, and covered with dust, is the closest in time you will ever come to the man who was your father, the Duke of Toresal.[paragraph break][i]And Bobby is dead now, too. And maybe Widow Fiona and Widow Shannon, if the Baron figures out that they helped me. And who else then? Teisha? Olmer and Darrens? All because of this stupid letter... I never asked for this. [r] [paragraph break]You close your eyes for a moment, fighting off a dreadful sense of futility.";
	now the Red Gate bed is scenery;

Instead of searching or looking under the Red Gate bed:
	say "Nothing under there but more dust.";

Instead of entering the Red Gate bed:
	say "It would hardly be comfortable, lying on a bare mattress in this forlorn, empty house.";

Instead of sleeping in the presence of the Red Gate bed:
	try entering the Red Gate Bed;

Part 9 - Bathroom

Chapter 1 - Description

The Bathroom is a room. "The walls of this small room are cold, bare marble. The only exit is to the north." The distant description of the Bathroom is "A bathroom lies to the south."

rg_bathroom_door is a doorway, south of the Red Gate Master Bedroom, north of the Bathroom.

Understand "small", "cold", "bare", "marble", "wall", "walls" as the Bathroom.

Chapter 2 - Bathtub

The bathtub is scenery in the bathroom. The description is "The basin is carved from a single block of granite, polished smooth as glass. It looks almost big enough to swim in." The printed name is "basin". Understand "bath", "tub", "basin", "granite", "stone", "big", "block", "block of", "carved", "polished", "smooth", "lip", "huge" as the bathtub.

Chapter 3 - Faucets

Some faucets are fixed in place, in the bathroom. "A pair of strange-looking devices protrudes from the south wall, hanging over the lip of a huge stone basin." Understand "strange", "strange looking", "device", "devices", "wheel", "wheels", "faucet", "brass", "ivory", "fitting", "fittings", "spout", "pair", "plumbing", "pipe", "pipes", "protruding", "spoke", "spokes", "spoked", "curved", "hand sized", "running water", "water", "curving", "gold", "golden", "cold", "hot", "tap", "taps", "pair of", "carved" as the faucets.

Instead of examining the faucets when the player is still dirty for the first time:
	say "The devices are made of brass and ivory and protrude from carved fittings in the wall, at about waist height. They consist of a curved spout flanked by two hand-sized, spoked wheels...[paragraph break]Suddenly you figure it out — these are faucets! Running-water pipes, or 'plumbing' as some call them, are a very recent invention, incredibly rare in Toresal. You've never actually seen faucets up close before.[paragraph break]'Oh, a bath with water-pipes! What a luxury,' exclaims Shannon. 'I've never had a bath that I didn't have to fill myself from a kettle. Back and forth and back and forth — it's either that or take it cold. Kind of takes the relaxation out of it, if you ask me,' she sighs ruefully. 'The Lord Duke was a man of taste and distinction on top of everything else. True nobility, he was.'[paragraph break]And it occurs to you, as you look at your distorted reflection in the golden, curving pipes, you've never had a bath with running water, either. In fact, you can't remember the last time you had [i]any[r] sort of bath, not even the quick spit-baths in a wooden tub full of tepid, cloudy water that are the norm in Maiden House. You've spent your life coated with the grime of the streets more often than not, reeking of sweat and alleyways, hardly noticing it because the smell is around you constantly, and around everyone you know.[paragraph break]But, you realize for the first time, [i]that's not really who you are[r]. You are the Duke's daughter, a child of nobility. By all rights, you should have been sleeping on feather mattresses, smelling of rosewater. This luxury is your heritage... and you've never known a single day of it in your life."

The description of the faucets is "The faucets are made of brass and ivory and protrude from carved fittings in the wall, at about waist height. The curved spout is flanked by two wheels: one for cold water, the other (though you can scarcely imagine it) for hot."

Instead of switching on the faucets:
	try turning the faucets instead.

Instead of switching off the faucets:
	say "The faucets are already turned off.";

Instead of turning the faucets:
	say "A groaning sound reverberates behind the walls, and then a spout of clean, clear water gushes out of the spout and splashes into the tub."

Instead of turning the faucets when the player is still dirty:
	try entering the bathtub instead; 

Instead of drinking the faucets:
	say "You turn the faucet on and cup your hand under the water to drink. It tastes cold and metallic."

Instead of taking off the baggy threadbare clothes in the presence of the bathtub:
	try entering the bathtub instead;

Chapter 4 - Taking a bath

Bathing is an action applying to one thing.
Understand "bathe" as bathing.
Understand "bathe [yourself]" as bathing.
Understand "wash" as bathing.
Understand "wash [yourself]" as bathing.
Understand "get washed" as bathing.
Understand "get bathed" as bathing.
Understand "use [bathtub]" as bathing.
Understand "take [bathtub]" as bathing.
Understand "take bath" as bathing.
Understand "take a bath" as bathing.
Understand "have [bathtub]" as bathing.
Understand "have bath/wash" as bathing.
Understand "have a bath/wash" as bathing.

Carry out bathing:
	try washing the noun instead;

Rule for supplying a missing noun when bathing:
	change the noun to the player;

Instead of bathing the bathtub:
	try washing the player;

Instead of washing the player in the presence of the bathtub:
	try entering the bathtub;

Instead of entering the bathtub when the furnace is cold:
	say "You turn on the faucets, thinking how nice it would be to take a bath — but the water is freezing! No matter how far you turn the 'hot' faucet, the water is as cold as if it had been drawn from a deep well.[paragraph break]'The furnace must be off,' muses Shannon. 'Perhaps we can get it started again.'";

Instead of entering the bathtub when the player is still dirty:
	repeat with garment running through wearable things had by the player begin;
		remove the garment from play;
	end repeat;
	move the old clothes to the location;
	now the player is clean;
	now the player carries the daydress;
	say "You plug the drain and begin filling the enormous tub with water.[paragraph break]Shannon nods with approval but says nothing; somehow she seems to know what you were thinking. By the time you've gotten out of your sodden clothes and into the water, she's managed to scare up a cake of soap from somewhere. She smiles as she hands it to you.[paragraph break]The water is scalding hot and exquisite. You rub the soap into your reddening skin, feeling everything wash clean away. Not just the dirt and the sweat, but [i]everything[r]: the fear and confusion of being hunted for two days straight; the moldy tunnel under Lord's Keep; the prison; the sewers. Even your grief and guilt over Bobby's death — it doesn't go away completely, not all of it, but it's not so raw any more, not so sharp.[paragraph break]You dry off with a sheet pulled from the furniture. As you reach for your old clothes, Shannon stops you and holds out a dress.[paragraph break]'Where in the world—?' you ask.[paragraph break]'The Duke's wife was a little lady, just about your size, really,' Shannon says. 'Not beautiful, but handsome in her way. And good taste in clothes.' She puts the dress into your hands.";

Instead of entering the bathtub when the player is clean:
	say "Once is enough for today."

Chapter 5 - Old clothes for new

Your old clothes are a thing, plural-named. The description is "Your old clothes are threadbare and crusted with filth." Understand "filthy", "rags", "threadbare", "filth", "crusty", "crusted", "encrusted" as the old clothes.

Instead of doing something other than wearing or examining the old clothes:
	say "Now that you're finally clean, you don't want anything to do with those filthy rags. You don't even want to touch them.";

Instead of wearing the old clothes:
	say "Now that you're finally clean, you don't think you could bear to put those filthy rags on again.";

Instead of washing the old clothes:
	say "If you washed those clothes now, they'd probably just fall apart.";

The daydress is a wearable thing. The description is "It's nothing fancy — a simple skirt and blouse of finespun linen, with an embroidered bodice. The sort of day dress you might see a somewhat well-to-do lady wearing to the market. It is finer than anything you've ever worn in your life." The printed name is "dress". Understand "simple", "finespun", "embroidered", "embroidery", "fine", "skirt", "blouse", "linen", "bodice", "day dress" as the daydress. Understand "dress" as the daydress when the location does not enclose the clothes.

Does the player mean buying the daydress:
	it is very unlikely;

After wearing the daydress:
	say "You slip the dress over your head, and Shannon helps you with the laces. The linen feels soft and light on your skin. She was right about the size, too; it fits fine[first time].[paragraph break]Finally, you look in the mirror. A complete stranger looks back at you: fair of skin, with dark eyes and high cheekbones. She wears fine clothes, and the tangles have been brushed out of her hair. She looks, you realize, quite pretty.[paragraph break][i]This isn't Jack anymore,[r] you think to yourself. [i]This is Jacqueline[r][only].";

Instead of taking off the daydress when the daydress is worn:
	say "This isn't really the proper time or place to get undressed." instead;

Section 2 - Concerning nakedness

Definition: a person is naked:
	if the number of things worn by it is 0, yes;
	otherwise no;

Before going from the bathroom when the player is naked:
	say "You can't leave here naked!" instead.

Book 12 - Ball

Part 1 - Events at Dame Sandlers

Chapter 1 - Actions and Reactions

After talking to Dame Sandler when DS49 is fired:
	say "Dame Sandler interrupts you with an upheld finger. 'There's no time, child. You must hurry.'"

Instead of giving the secret letter to Dame Sandler:
	try showing the secret letter to Dame Sandler instead;

Instead of showing the secret letter to Dame Sandler:
	say "That is proof of your heritage, Jacqueline,' Dame Sandler says. 'Guard it well; it is the most valuable thing you possess.'"

Before going from the jewelers shop when DS31 is fired and DS49 is unfired:
	say "Dame Sandler takes your elbow, gently but firmly. [if DS44 is unfired]'Please don't go yet, dear; what I have to tell you is dreadfully important.'[otherwise]'Wait, Jacqueline. There are a few things yet you should know.'" instead;

Before going from the jewelers shop when DS31 is fired and DS49 is fired and the player has no dress jewelry:
	say "'Jacqueline,' Dame Sandler scolds, 'aren't you forgetting something?' She nods her head pointedly towards the jewelry." instead;

Before going from the jewelers shop when DS49 is fired and the player has dress jewelry:
	say "As you leave, Dame Sandler calls out, 'Good luck, Lady Jacqueline.'";
	[ continue the action; ]

Chapter 2 - Further Conversation

DS31 is a quip. The display text is "Dame Sandler looks you over and smiles[if DS1 is fired]. She does not seem at all surprised by your change in dress[end if].[paragraph break]'Lady Jacqueline,' she says, inclining her head, 'how nice to see you[if DS1 is fired] finally looking yourself, after all these years[end if]. Come, child, let me get a look at you.' She takes your chin gently between finger and thumb and tilts your face back and forth. 'Hmm. You've simply destroyed your hair, of course, but there's nothing to be done now. It will grow out eventually.'"

DS32 is a quip. The menu text is "'[if DS1 is fired]You knew I was a girl all along[otherwise]You know who I am[end if]?'". The display text is "Dame Sandler laughs, not unkindly. '[if DS1 is fired]Oh, child, did you really think I hadn't guessed? [end if]It's not that great a secret, you know; more people have had their eye on you than you realize. We've all been waiting for the Duke's daughter to finally make herself known, and now here you are.'"

DS33 is a transitional quip. The menu text is "'Who's had their eye on me?'". The display text is "'What do you mean, [']had their eye on me[']?' you say, all your old suspicions returning. 'Who else knew? Who's been watching me?'"
The following quip is DS38.

DS34 is a quip. The menu text is "'Widow Fiona sent me.'". The display text is "'Widow Fiona sent me,' you say. 'She said that you could help me.'[paragraph break]Dame Sandler nods. 'I know, child. I've been expecting her to send you to me any day now. I'm sorry I couldn't help you before, but we — Fiona and I — dared not risk Baron Fossville discovering that I knew who you were. I was only to step in as a last resort, when the Baron had tracked you down and chased you from every other hiding place.'"

DS35 is a quip. The menu text is "'You and Fiona planned all this?'". The display text is "'So you and Widow Fiona planned all this? You knew she was really working for the — for my father?' you ask. Your head is spinning with conspiracies; no one is who they seem to be.[paragraph break]'Fiona and I have known each other for a long time,' Dame Sandler says, 'since before you were born. She sought my help when your father died. We had our suspicions even then, and we made plans in case the worst should come to pass.'"

DS36 is a transitional quip. The menu text is "'Suspicions?'". The display text is "'Suspicions?' you ask. 'Suspicions of what?'[paragraph break]Dame Sandler looks down at her hands for a long time. When she looks up at you again, you are shocked to see tears in her eyes. 'Fossville may have murdered your father.'"
The following quip is DS41.

DS37 is a transitional quip. The menu text is "'Why is Fossville after me?'". The display text is "'But why does Fossville hate me so much?' you cry. 'I just don't understand any of this — I've never done anything to him!'"
The following quip is DS38.

DS38 is a quip. The display text is "Dame Sandler sighs and her expression turns serious. 'What you must realize, Jacqueline, is that you represent a threat to certain people with ambitions toward the throne. The laws of ascension in our land are quite clear: in the absence of a named successor, the ruler is determined by the will of the people.'[paragraph break]She gives you a long, significant look. 'The Duke — your father — was very popular with the common people. Baron Fossville... is not.'[paragraph break]Your mouth goes dry, and the floor seems to tilt beneath your feet for a moment. 'Are you saying...'[paragraph break]'Yes,' says Dame Sandler. 'I am saying that you have a credible claim to the throne.'"

DS39 is a quip. The menu text is "'That's ridiculous.'". The display text is "You try to catch your breath, but it's all you can do to keep from bursting out into hysterical giggles. 'But that's just... ridiculous,' you say. 'Completely ridiculous. I can't take the [i]throne[r], I'm just an orphan, a street girl, a [i]nobody[r]...'[paragraph break]'You are the daughter of one of the most loved and respected noblemen in living memory,' Dame Sandler says quietly. 'If the Duke were alive today there is no question that he would take the Ascension if he so desired it. As his heir — even as his illegitimate heir — much of that esteem would fall to you. Many would think you worthy.'"

DS40 is a transitional quip. The menu text is "'Is that the only reason Fossville hates me?'". The display text is "'So that's it? That's why Fossville hates me and wants to lock me away?' you ask. 'Because he thinks I'll get the throne instead of him?'[paragraph break]Dame Sandler looks down at her hands for a long time. 'There may be one other reason,' she says. 'Many suspect this, but few will speak of it.' When she looks up at you again, you are shocked to see tears in her eyes. 'Fossville may have murdered your father.'"
The following quip is DS41.

DS41 is a quip. The display text is "Your stomach feels as though you are falling down a deep hole, but on the outside, you are still and calm. You think you should be surprised, but somehow, you know you had been expecting this all along.[paragraph break]'How did he do it?' you whisper.[paragraph break]'Poison seems most obvious,' Dame Sandler says, regaining a measure of control. 'But no one really knows. There's never been any proof.'[paragraph break]You nod. You feel anger, but it's a smoldering, far-away anger. It is something you will have to deal with later. Right now, there are too many other things you need to know."

DS42 is a quip. The menu text is "'How would I take the throne?'". The display text is "'Even if I do have a... a credible claim to the throne,' you ask, 'what would I do with it? How am I supposed to take the Ascension? I can't just walk around Grubber's Market shouting, [i]I'm the Duke's daughter, vote for me![r]' You think about it for a moment, then add, 'Can I?'[paragraph break]Dame Sandler smiles. 'Well, the other thing you must realize is that there are also a number of people who are interested in seeing you succeed, and who can help you press your claim. Some of them may believe that you are the better candidate; more of them simply want to spite Fossville; all of them will expect you to be appropriately grateful for their aid should you actually succeed. It is a very valuable commodity, the gratitude of a queen.'"

DS43 is a quip. The menu text is "'I can't do this.'". The display text is "You shake your head. 'I can't do this. I can't rule a kingdom, it's... it's just insane.'[paragraph break]Dame Sandler puts a hand on your arm. 'I realize this is a bit sudden and frightening, my dear, but you must accept it and act quickly.' Her voice is soothing, but firm. 'This will not simply go away. Fossville does not care whether you want the Ascension. He will hunt you down regardless. So why not take him head-on? This is who you are, now. Enjoy it, play it to the hilt.'"

DS44 is a quip. The menu text is "'What do I have to do?'". The display text is "You take a long, deep breath. Part of you still can't believe this is even happening. But you hear yourself saying, 'All right. What do I have to do?'[paragraph break]'Before you can enlist anyone's aid,' says Dame Sandler, 'you must let it be known that you are in the game, and that you are a player worth backing. You must announce yourself. And to do that, you must attend Fossville's ball tonight.'[paragraph break]In spite of everything that's happened, you find yourself smiling at the idea of attending a royal ball.[paragraph break]'Of course, you cannot attend the ball looking like this,' Dame Sandler says, her tone becoming brisk and businesslike again. 'You will need three things: a dress, some jewelry... and a weapon.'"

DS45 is a quip. The menu text is "'What's wrong with the dress I have on?'". The display text is "You look down at the dress you're wearing, the one Shannon found in Red Gate Estate.[paragraph break]'You mean this dress isn't good enough?' you ask, a bit hurt. 'Where can I get a better one?'[paragraph break]Dame Sandler smiles sympathetically. 'It's very nice, Jacqueline, but I'm afraid it's just a little bit out of fashion. The clothier right next door should be able to fit you with something more appropriate.'"

DS46 is a quip. The menu text is "'Why do I need a weapon?'". The display text is "'A weapon?' you ask. 'Why do I need a weapon at a fancy ball?'[paragraph break]'I admit it's unlikely that Fossville would try to kill you in front of a hundred guests,' Dame Sandler says drily, 'but you can never be too careful. There's a weapons dealer on Commerce Street who should have what you need.'"

DS47 is a quip. The menu text is "'How am I going to pay for this?'". The display text is "'I appreciate your advice, Dame Sandler,' you say, 'but how am I going to pay for all this? I don't have any money.'[paragraph break]'Ah, but you have something even better than money, Jacqueline,' Dame Sandler says. 'You have credibility. Go to the Chorus Brothers here in Lord's Market. Show them proof of your heritage, and they should be more than willing to help you with money.'"

DS48 is a quip. The menu text is "'Where can I get some jewelry?'". The display text is "'Where am I going to get some jewelry?' you ask. It occurs to you that you had a very nice necklace, but then you traded it for a cloak...[paragraph break]'Let me take care of that,' Dame Sandler says. She unlocks the display case with a tiny silver key, lifts up the lid, and gestures to the sparkling jewels within. 'Take any one you like, child. I'm one of those who would see you succeed; this is my gift to you.'"

DS49 is a quip. The menu text is "'Thank you.'". The display text is "'I hardly know what to say, Dame Sandler,' you breathe. 'Thank you.'[paragraph break]She merely smiles and inclines her head. 'Now hurry — Fossville's ball is tonight, and there's not much time left. See the moneylenders first, then get a suitable dress and a discreet blade. Pieter will go with you—'[paragraph break]Pieter blinks. 'What, to the ball?' he asks.[paragraph break]'Yes, Pieter,' Sandler says pointedly, 'to the ball. Just do what you always do — keep your mouth shut and make sure no one harms the valuables.'[paragraph break]She turns back to you and smiles 'Go, child, and good luck.'"

DS49B is a quip. The display text is "'A fine choice,' says Dame Sandler, closing and locking the case once again."

Chapter 3 - Structure of Conversation

The response of DS31 is { DS32, DS34 }.

The response of DS32 is { DS33, DS34 }.

The response of DS34 is { DS32, DS35, DS37 }.

The response of DS35 is { DS36, DS37 }.

The response of DS38 is { DS39, DS40, DS42 }.

The response of DS39 is { DS40, DS42 }.

The response of DS41 is { DS42 }.

The response of DS42 is { DS43, DS44 }.

The response of DS43 is { DS44 }.

The response of DS44 is { DS45, DS46, DS48 }.

The response of DS45 is { DS47, DS45, DS46, DS48 }.

The response of DS46 is { DS47, DS45, DS46, DS48 }.

The response of DS47 is { DS47, DS45, DS46, DS48 }.

The response of DS48 is { DS47, DS45, DS46, DS48 }.

The response of DS49 is { }.

The response of DS49B is { DS45, DS46, DS47, DS48 }.

After populating DS32:
	if DS34 is fired,
		remove DS34 from the current conversation, if present;

After populating DS34:
	if DS32 is fired,
		remove DS32 from the current conversation, if present;

After populating DS39:
	if DS41 is fired,
		remove DS40 from the current conversation, if present;

DS45 is clustered with DS46, DS47, DS48, DS49B.

After populating a quip that is clustered with DS45:
	[if every quip clustered with DS45 is fired, change the current conversation to { DS49 } instead;]
	repeat with q running through quips clustered with DS45:
		if q is fired,
			remove q from the current conversation, if present;
	if every quip clustered with DS45 is fired:
		start conversation with Dame Sandler on DS49 instead;

[ Originally, once every other conversation option had been fired, we offered the opportunity to say "Thank you." But if the player tries to leave, Dame Sandler stops and says, "There's something you should know." But really all she wants is for you to say thank you. This comes off as weird and passive aggressive. So instead, we'll fire the Thank you quip automatically once all conversation options are fired, signalling that the conversation is now over and the player can leave.]

After firing DS48:
	remove the valuable jewelry from play;
	now all dress jewelry is in the display case;
	now the display case is open;
	now the display case is not locked;

After taking dress jewelry:
	now the noun is worn;
	start conversation with Dame Sandler on DS49B;

After firing DS49B:
	repeat with x running through dress jewelry in the display case begin;
		remove x from play;
	end repeat;
	now the display case is closed;
	now the display case is locked;
	now the display case contains the valuable jewelry;

Chapter 4 - Some Jewelry

Dress jewelry is a kind of thing. Dress jewelry is usually wearable.

Understand "jewelry", "jewelery", "jewel", "jewels", "gem", "gems", "gemstone", "gemstones", "diamonds", "diamond", "silver", "encrusted" as dress jewelry.

A silver ring is dress jewelry. The description is "The ring is silver and inlaid all over with tiny, expertly cut diamonds, set in an intricate pattern of looping swirls around the circumference of the ring, like curls of glittering thread." The printed name is "ring".

A silver pendant is dress jewelry. The description is "The pendant is a teardrop the size of your thumb, encrusted with diamonds and sapphires, hanging from a necklace of delicate, silver links." Understand "necklace", "sapphire", "sapphires", "teardrop", "link", "links" as the silver pendant. The printed name is "pendant".

A silver bracelet is dress jewelry. The description is "The bracelet is a band of silver, just big enough to squeeze over the bones of your hand. A circle of diamonds surrounds a larger sapphire set in the center." Understand "band" as the silver bracelet. The printed name is "bracelet".

A silver brooch is dress jewelry. The description is "The brooch is wrought of silver in the shape of a cup, symbol of the Goddess Magdalena. The stem is decorated with diamonds, and a line of sapphires adorns the cup's rim." Understand "pin", "cup", "stem", "rim", "sapphire", "sapphires" as the silver brooch. The printed name is "brooch".

Understand "rings" as the silver ring when the item described is in the display case.
Understand "pendants" as the silver pendant when the item described is in the display case.
Understand "bracelets", "bands" as the silver bracelet when the item described is in the display case.
Understand "brooches" as the silver brooch when the item described is in the display case.

Part 2 - Pieter

Chapter 1 - Pieter as bodyguard

The Bodyguard scene is a scene. The bodyguard scene begins when DS49B is fired. The bodyguard scene ends when the location is the ballroom.

When the bodyguard scene begins:
	now Pieter is not scenery.

When the bodyguard scene ends:
	now Pieter is scenery;

After assembling during the Bodyguard Scene:
	if Pieter is on-stage,
		add Pieter to the roster of available characters, if absent;

After going to somewhere during the bodyguard scene (this is the Pieter following rule):
	if the location is the ballroom:
		continue the action;
	if Pieter was in the location:
		if the current tension is at most unimportant:
			fire ATMOS_Pieter_following;
	move Pieter to the location;
	continue the action;

ATMOS_Pieter_following is a dramatic event. The display text is "Pieter [one of]follows[or]hurries after you[or]hurries to catch up[or]sticks close to you[at random], [one of]looking around warily[or]his hand on his sword hilt[or]doggedly keeping you in sight[at random]."

The Pieter following rule is listed first in the after rulebook. [It has to run before other rules which may stop the action -- rules such as the greeting when entering the clothier's shop.]

Every turn when a random chance of 1 in 3 succeeds during Journey to the Ball:
	if Pieter is nearby and the location is not Commerce Street:
		add ATMOS_Pieter to the dramatic possibilities, if absent;

ATMOS_Pieter is a dramatic event. The display text is "[one of]'Remember, keep that dagger hidden until you absolutely need it,' says Pieter. 'It’s no good in a sword fight, so your only advantage is surprise.'[or]'I don't know why I let Dame Sandler talk me into this,' Pieter mutters.[or]'I'm not sure what Dame Sandler expects you to do at this ball,' says Pieter. 'Introduce yourself to a lot of people, I guess?'[or]'When we get there, try to stay in a well-lit, crowded place, okay, Jack?' says Pieter. 'Don’t go wandering off.'[or]'Don’t worry,' says Pieter, 'I’ll have your back. But you’re going to have to do all the talking.'[or]'If the Baron tries anything, we are leaving. Immediately,' growls Pieter.[or]'Try not to get into any fights,' Pieter warns. 'I got you that dagger for protection, not so you could wreak your vengeance.'[or]'Try to do whatever it is you need to do quickly,' says Pieter. 'I don’t want to stay any longer than we have to.'[or]'If we manage to survive tonight, it’ll be a miracle,' mutters Pieter.[or]'I have a bad feeling about this,' says Pieter under his breath.[in random order]";

ATMOS_Pieter has a number called the firing count.

After firing ATMOS_Pieter:
	increment the firing count of ATMOS_Pieter by 1;

Rule for firing ATMOS_Pieter when the firing count of ATMOS_Pieter is at least 10:
	change the firing count of ATMOS_Pieter to 10; [prevent its infinite incrementation]
	do nothing;

Instead of giving the secret letter to Pieter:
	try showing the secret letter to Pieter;

Instead of showing the secret letter to Pieter:
	say "'Don't lose that,' Pieter says.[if the player does not have the purse] 'You'll need that to prove your credentials to the Chorus Brothers.'[end if]";

Chapter 2 - Constrained exits when with Pieter

Instead of going south from Lords Market during the Bodyguard Scene:
	say "'No,' says Pieter, pulling you back. 'You'll not be going back to Maiden House any time soon. That's exactly where Fossville will expect you to go.'"

Instead of going west from Lords Market when the player does not have a gown during the Bodyguard Scene:
	say "'Wait a minute,' Pieter says. 'Dame Sandler said you needed a gown, right? We shouldn't leave until you have one.'"

Instead of going to the jewelers shop during the Bodyguard scene:
	say "'There's no need to go back and bother Dame Sandler,' Pieter says. 'We've more important things to do.'";

Instead of going to the moneylenders shop when the player has the purse during the Bodyguard scene:
	say "'We've got what we need from those moneylenders,' Pieter said, 'I'd just as soon stay out of their clutches for now.'";

Instead of going to the clothiers shop when the player has the ball gown during the Bodyguard scene:
	say "'You already have your gown,' says Pieter. 'We need to move on.'";

Chapter 2 - Conversation 

Rule for initiating conversation with Pieter:
	change the chosen opening gambit to PI1;

PI1 is a quip. The display text is "'Hey Pieter,' you say, 'mind if I ask you something?'[paragraph break]'[one of]I don't know if we have much time to chat[or]We really should be getting to the ball[or]I don't know if I have any answers for you[at random], [Jack stutteringly],' says Pieter gruffly, 'but okay.'"

PI2 is a quip. The menu text is "'Did you know I was a girl, too?'". The display text is "'So did Dame Sandler keep you in the dark,' you ask, 'or did you know I was a girl all this time, too?'[paragraph break]Pieter looks so confused and uncomfortable, you have to suppress a smile. 'Oh, I was in the dark, all right. To tell you the truth, it's a bit of a shock, seeing you like this... er, Jacqueline.'"

PI3 is a quip. The menu text is "'Do you really think I could be Queen?'". The display text is "'All that stuff Dame Sandler was saying, about me having a credible claim... do you think I could really do that?' you ask. 'Take the Ascension, become Queen?'[paragraph break]Pieter sighs. 'To tell you the truth, Jacqueline — Goddesses, but it's weird calling you that — to tell you the truth, I don't know much about Royal politics at all. But if Dame Sandler says you have a chance at it, then I'd guess that you do. It wouldn't be the most far-fetched thing I ever heard that turned out to be true.'"

PI4 is a quip. The menu text is "'What do you think of Dame Sandler?'". The display text is "[first time]'What do you think of Dame Sandler?' you ask.[paragraph break]'She took me in when no one else would hire me,' says Pieter, 'on account of some bad business that happened back east, and don't bother asking about it by the way. She's the shrewdest, toughest, fairest woman I ever met. I'd lay down my life for that lady. And if she wants you protected, then by the Goddesses you'll have nothing to fear from anyone.'[subsequently][rp] 'she's as good an ally as you could ask for.'[only]"

PI5 is a quip. The menu text is "'What do you think of Baron Fossville?'". The display text is "[first time]'What do you think of Baron Fossville?' you ask.[paragraph break]'Don't you worry about Baron Fossville,' growls Pieter through clenched teeth. 'That bastard's going to get what's coming to him. For Bobby and for your father.'[subsequently][rp] 'he'll get his.'[only]"

PI6 is a quip. The menu text is "'What did you think of my father?'". The display text is "[first time]'I guess you remember when my father ruled the city,' you remark. 'What did you think of him, Pieter?'[paragraph break]'I thought he was a just ruler,' Pieter says. 'I'd have had him for king gladly, if he were still alive. You could do worse for a father, Jacqueline. And I suppose he could have done worse for a daughter,' he adds with a smile.[subsequently][rp] 'he was a good man, and I think he'd be proud of you.'[only]"

PI7 is a quip. The menu text is "'What do you know about fancy balls?'". The display text is "[first time]'Well, Pieter, you ever been to a fancy dress ball, before?' you ask. 'What can we expect?'[paragraph break]Pieter spreads his hands in a shrug. 'I'm afraid I won't be much help, Jacqueline — I'm not very comfortable at parties. Dame Sandler said you're just supposed to socialize, let people know who you are. [']Make yourself visible,['] she said. Whatever that means.'[subsequently][rp] 'I have no idea.'[only]"

PI8 is a quip. The menu text is "'What do you know about the Chorus Brothers?'". The display text is "[first time]'So what do you know about these moneylenders, the Chorus Brothers?' you ask.[paragraph break]'Those two hear nothing but the clink of coin,' grumbles Pieter. 'They'll lend you money quick enough as long as they think they'll get some return out of it. I'd rather not do business with them at all, to be honest, but Dame Sandler says you need their backing.'[subsequently][rp] 'as long as you show them your credentials, you'll be fine.'[only]"

PI9 is a quip. The menu text is "'What do you know about this clothier?'". The display text is "[first time]'What do you know about this clothier at the Royal Tunic?' you ask.[paragraph break]Pieter rolls his eyes shrugs. 'I'm a lifelong bachelor, Jacqueline. What I know about a lady's clothes you could fit into a thimble and have room left over.'[subsequently][rp] 'you're on your own in the dress-buying department.'[only]"

PI10 is a quip. The menu text is "'What do you know about this weapon seller?'". The display text is "[first time]'Have you ever done business with this weapon seller Dame Sandler mentioned?' you ask.[paragraph break]Pieter snorts. 'That'd be Olgan Minor. He's nothing more than a dim thug. He'll try to intimidate you because you're young and a girl, but there's nothing behind his threats. Let me do the talking and we'll get your weapon soon enough.'[subsequently][rp] 'don't let him intimidate you.'[only]"

PI11 is a quip. The menu text is "'Never mind.'". The display text is "'Never mind, it wasn't important,' you say.[paragraph break]'All right,' says Pieter, unperturbed."

PI12 is a quip. The menu text is "'Do you know your way through these woods?'". The display text is "[first time]'I don't suppose you've, uh... ever been in these woods before, huh, Pieter?' you ask. You really don't want to admit that you're lost.[paragraph break]Pieter just shakes his head. 'The old Duke used these for game hunting, but Fossville let them grow wild. I've never explored them, myself. Maybe if you search the underbrush, you'll see a landmark?'[subsequently][rp] 'I've never been here before.'[only]"

The response of PI1 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI12, PI11 }.

The response of PI2 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI12, PI11 }.

The response of PI3 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI12, PI11 }.

The response of PI4 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI12, PI11 }.

The response of PI5 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI12, PI11 }.

The response of PI6 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI12, PI11 }.

The response of PI7 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI12, PI11 }.

The response of PI8 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI11 }.

The response of PI9 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI11 }.

The response of PI10 is { PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, PI11 }.

The response of PI11 is { }.

The response of PI12 is { PI2, PI3, PI4, PI5, PI6, PI7, PI11 }.

PI1 is clustered with PI2, PI3, PI4, PI5, PI6, PI7, PI8, PI9, PI10, and PI12.

After populating a quip (called q) that is clustered with PI1:
	remove q from the current conversation, if present;
	if PI2 is fired,
		remove PI2 from the current conversation, if present;
	if PI3 is fired,
		remove PI3 from the current conversation, if present;
	if the player carries the purse,
		remove PI8 from the current conversation, if present;
	if the player carries the ball gown,
		remove PI9 from the current conversation, if present;
	if the player carries the plain dagger,
		remove PI10 from the current conversation, if present;
	if the location is not the Woods,
		remove PI12 from the current conversation, if present;

Chapter 3 - Commerce Street

Instead of going to the bakers shop during the Bodyguard scene:
	say "'We're not here for food,' Pieter says. 'We have more important things to worry about.'";

Instead of going to the butchers shop during the Bodyguard scene:
	say "'We're not here for food,' Pieter says. 'We have more important things to worry about.'";

Instead of going to the armory shop when the player has the plain dagger during the Bodyguard scene:
	say "'You've got your weapon already,' Pieter says. 'It's time to move on.'";

Instead of going east from Commerce Street during the Bodyguard scene:
	say "'We don't have time to go traipsing around the city,' says Pieter. 'We need to get to Fossville's ball.'";

Chapter 4 - In Olgan Minor's Shop

After going to the armory shop during the Bodyguard scene:
	now Olgan Minor is scenery;
	now Pieter is scenery;
	try looking;
	now Olgan Minor is not scenery;
	now Pieter is not scenery;
	say "[first time]The proprietor makes a face as you walk into his shop, but when he sees Pieter behind you he quickly finds something else to look at.[only]";
	update the character list;

Instead of talking to Olgan Minor during the Bodyguard scene:
	say "'Don't waste your breath making conversation, Jacqueline,' Pieter says. 'Just pick something and buy it and let's move on.' [first time]You get the impression that Pieter [i]really[r] doesn't like Olgan Minor, and that maybe there's a history between the two.[only]";

Instead of buying the big weapons during the Bodyguard scene:
	say "Pieter shakes his head. 'Those sorts of weapons are too big to wear discreetly,' he tells you, 'and most are too heavy for you to lift in any case.'"

Instead of buying the blades:
	try buying the dagger display instead;

Instead of buying the dagger display during the Bodyguard scene:
	now the player wears the plain dagger;
	say "Pieter leans over your shoulder as you inspect the daggers. 'Having the right weapon is as important as knowing what to do with it,' he tells you. 'You don't want big or fancy; you want something sharp, long enough to do damage to a man when you stick him with it, but small enough to hide under your, er...' he suddenly blushes and clears his throat. 'Anyway. That one,' he says, pointing to a plain-handled blade about six inches long. 'That's the one you want.'[paragraph break]Olgan Minor grumbles, but Pieter glares at him again and suggests that if your gold is no good here, then perhaps some other metal might suit the shopkeeper better. Grudgingly, Minor hands over the knife, and makes a show of biting your gold coin to make sure of its worth.[paragraph break]You slip the knife under your gown. Already you feel more confident."

Section 2 - Plain Dagger

The plain dagger is a wearable weapon. The description is "It's a plain-looking blade about six inches long, easily concealed beneath your gown." Understand "knife", "blade" as the plain dagger. The printed name is "dagger".

Rule for printing inventory information of the plain dagger when the player wears the plain dagger:
	say " (being worn under your ball gown)";

Understand "draw [plain dagger]" as taking off.

Instead of dropping the plain dagger:
	say "You may need that dagger — keep it close!";

Instead of showing or giving the plain dagger to someone:
	say "Best to keep it hidden for now.";

Instead of inserting the dagger into the ball gown when the player is wearing the dagger:
	[ > HIDE DAGGER IN DRESS ]
	say "The dagger is already well hidden under your dress.";

Instead of taking the plain dagger when the plain dagger is worn by the player:
	try taking off the plain dagger;

Instead of taking off the plain dagger while High Society has not happened:
	Say "Best to keep it hidden for now.";

Instead of taking off the plain dagger during High Society:
	if Brief Encounter is happening:
		say "'I would advise against that,' whispers the Prince as you reach for your dagger. 'Draw your blade now, and the mercenaries will surely overpower you and take it from you. Wait until the right moment before you strike.'";
	otherwise:
		say "Drawing a weapon in the middle of the ball would probably not be the most subtle way of 'announcing' yourself.";

Chapter 5 - Lord's Road

Instead of going south from Lords Road during the Bodyguard Scene:
	say "'We can't go back now,' says Pieter. 'The ball will be starting any minute — and Dame Sandler will have my head on a platter if I don't get you to it on time!'";

Part 3 - Announcement of the Ball

Journey to the Ball is a scene. Journey to the Ball begins when the player has the ball gown and the player has dress jewelry and the player has the plain dagger and the location is Commerce Street. Journey to the Ball ends when the location is the Ballroom.

When Journey to the Ball begins:
	ratchet the current tension to important;
	say "Evening is falling over the city, and the streets are clearing of people.[paragraph break]'The ball at Lord's Keep will be starting soon,' mutters Pieter. 'We'd better hurry!'";
	set the scene for night;
	remove the purse from play; [bag of coins]
	remove Bobby from play; [Pesky Bobby! -- setting the scene for night brings him back to the market place. Never mind.]

Part 4 - Southern Gate

The Southern Gate is north of Lords Road and west of the Crossing. "The Lord's Road ends here at the keep's southern gate. Tonight, for the ball, the portcullis is raised; however, the guards are on high alert tonight, checking invitations as each guest arrives." Understand "lords", "lord", "road", "keep", "end", "end of", "gatehouse", "portcullis" as the Southern Gate.

The distant description of the Southern Gate is "Far to the [quick best route from location to Southern Gate], you can see Lord's Keep sticking up from the horizon."

Instead of going to Southern Gate during Bobby's Adventure:
	say "That way would just take you to Lord's Keep, which is guarded day and night."

After assembling available exits of the Southern Gate:
	add north at entry 1 in the viable directions, if absent;

Chapter 1 - The Gatehouse

The Gatehouse is scenery, in the Southern Gate. The description is "The portcullis — a massive gate of thick, iron bars — is raised in its archway under the gatehouse." The printed name is "portcullis". Understand "portcullis", "gate", "archway", "house", "massive", "thick", "iron", "metal", "bar", "bars" as the gatehouse.

Instead of doing something other than examining to the Gatehouse:
	say "You can't get near the portcullis without attracting the attention of the guards.";

Before going north in the Southern Gate:
	say "The guards won't let you in unless you have an official invitation. The way things are going lately, they might well be looking out for you specifically." instead;

Instead of facing southwest in Southern Gate, say "There's nothing in that direction but rocks and the occasional thornbush."
Instead of facing west in Southern Gate, say "There's nothing in that direction but rocks and the occasional thornbush."
Instead of facing northwest in Southern Gate, say "There's no way around the keep in that direction."
Instead of facing northeast in Southern Gate, say "There's no way around the keep in that direction."

Instead of facing north in Southern Gate, try examining the Gatehouse.

Instead of going southwest in Southern Gate, try facing southwest.
Instead of going west in Southern Gate, try facing west.
Instead of going northwest in Southern Gate, try facing northwest.
Instead of going northeast in Southern Gate, try facing northeast.

Chapter 2 - The Guards Checking Invites

Some Lords Guards checking invites are people, male, scenery, in the Southern Gate. The description is "The guards are wearing polished mail underneath tabards decorated with the Toresal coat of arms, and carrying long spears. They look alert and ready for trouble." The printed name is "guards". Understand "guard", "man", "men" as the Lords Guards checking invites.

Some tabards are carried by the Lords Guards checking invites. The description of the tabards is "You can't get close enough to do that without attracting the guards['] attention." Understand "polished", "mail", "tabard", "armor", "long", "spear", "spears", "coat of arms", "arms", "weapon", "weapons" as the tabards. The printed name is "guards['] things".

Instead of doing something other than examining to the Lords Guards checking invites when Pieter is nearby:
	say "Pieter holds you back. 'Don't, Jack,' he whispers. 'You can't beat them, and you won't gain anything by making a scene.'";

Part 4 - Foyer

The Entrance Hall is north of Lower Bailey. "The entrance hall has been decorated for the occasion, its stark, stone walls hidden behind festive tapestries and garlands. A few guests stand around in groups, chatting, but the main event seems to be in the ballroom, north of here." The printed name is "Foyer". Understand "foyer", "inner", "keep", "castle" as the foyer.

Rule for printing the distant description of the Entrance Hall when in the Lower Bailey:
	say "You can't quite make out the details of the foyer from out here. ";

Instead of inspecting the Entrance Hall when in the Ballroom:
	try facing south;

Instead of listening to the Entrance Hall when in the Entrance Hall, say "You can hear the sounds of laughter and music coming from the ballroom to the north."
Instead of listening to the Ballroom in the Entrance Hall, try listening to the Entrance Hall.

Section 1 - Scenery (Tapestries)

Some tapestries are scenery, in the Entrance Hall. The description is "The tapestries depict important battles in the history of Miradania, noblemen leading their soldiers into war against the western clans. You see a number of coats-of-arms represented in the battle scenes. Interestingly, Fossville's is not among them." Understand "tapestry", "hangings", "hanging", "battle", "battles", "noblemen", "soldiers", "coats of arms", "coat of arms", "festive", "nobles", "noble", "nobleman", "soldiers", "soldier", "war", "western", "clans", "clan", "scene", "scenes" as the tapestries.

Some garlands are scenery, in the Entrance Hall. Understand "garland" as the garlands. The description is "The garlands add a touch of festive greenery to the room." Understand "festive", "greenery" as the garlands.

Section 2 - Event on exiting

After going north to the Entrance Hall for the first time:
	output chapter heading "Chapter 10 - The Ball";
	say "A servant in formal livery bows, then takes your and Pieter's cloaks and deftly spirits them away.[paragraph break]Pieter takes a deep breath. 'Well, we made it,' he whispers. 'Although frankly, I'd rather we were still sneaking through dark, dirty tunnels. I guess you'd better head into the ballroom and start mingling.'";
	continue the action;

Instead of going south from the Entrance Hall during Journey To The Ball:
	say "Pieter puts his hand on your back, gently but firmly. 'Don't lose your nerve now,' he says. 'You'll be fine; I'll be right behind you the whole time.'";

Part 5 - Ballroom

Chapter 1 - Description

Before going a direction to the ballroom for the first time:
	select theme "Arrival";
	now Pieter is scenery;
	move Pieter to the ballroom;
	say "You step into a world of light...";

The Ballroom is north of the Entrance Hall. "Hundreds of candles spread an unearthly golden glow throughout the hall. From massive chandeliers dripping with crystal, from golden candelabras ensconced in the walls, the multitude of flames flicker like stars. The ballroom is packed with people; all of Toresal's high society seems to be here tonight, and still the enormous hall does not seem full. Servants navigate through the crowds, carrying silver platters loaded with pastries and canapés. Somewhere, a string quartet is playing a lively concerto.[first time][paragraph break]It's breathtaking. It's every fantasy of beauty and riches that you ever dreamed of, brought to life.[only]". The dead-end description of the Ballroom is "The only exit from the ballroom seems to be south, but there are too many people in the way to get there."

Instead of exiting when in the ballroom:
	try going south instead;

Instead of going from the ballroom:
	say "The press of people is so close, you can't make your way back to the Foyer without rudely pushing your way through at least a dozen conversations. Besides, you can't leave without Pieter. [first time]Where did he get off to...? [only][paragraph break]";

Instead of facing a planar direction in the Ballroom, say "The guests are crowded so close together, you can't even see to the edges of the room."

Rule for printing the distant description of the Ballroom when in the Entrance Hall:
	say "You can see glittering lights and hear the sounds of laughter and music coming from the ballroom to the north. ";

Understand "hall", "ball", "party", "lights", "light", "glittering", "laughter", "music", "voices", "sounds" as the Ballroom.

Instead of listening to the Ballroom when in the Ballroom, say "The constant wave of voices, conversations, laughter, and music is overwhelming."

Chapter 2 - Scenery

Section 1 - Scenery (Candles)

Some decorative candles are scenery, in the Ballroom. The printed name is "candles". The description is "The candles are literally everywhere; their light suffuses the entire ballroom." Understand "candle", "light", "glow", "golden", "gold", "flames", "flame", "flickering", "stars", "star", "opulent", "multitude", "multitude of flames" as the candles.

Instead of doing something other than examining to the candles, say "Everything here is so opulent, you're kind of afraid to touch anything."

Section 2 - Scenery (Chandelier)

Some enormous chandeliers are scenery, in the Ballroom. The description is "The chandeliers are enormous sculptures of dangling crystal, glittering with candlelight." Understand "chandelier", "crystal", "enormous", "sculptures", "dangling", "crystals", "glittering", "opulence", "multitude", "massive" as the enormous chandeliers.

Instead of doing something other than examining to the enormous chandeliers, say "The chandeliers are hanging high above your reach."

Rule for printing the name of the enormous chandeliers:
	say "chandeliers";

Section 3 - Scenery (Candelabras)

Some candelabras are scenery, in the Ballroom. The description is "The candelabras are elgantly sculpted from solid gold." Understand "gold", "golden", "candelabra", "elegant", "scupted", "opulent", "opulence" as the candelabras.

Instead of doing something other than examining to the candelabras, say "Everything here is so opulent, you're kind of afraid to touch anything."

Section 4 - Scenery (Guests)

Some guests are a backdrop, in the Ballroom, the Entrance Hall and the Lower Bailey. The description is "The guests are all the brightest, richest, and most beautiful in Toresal. They laugh and chatter and compliment each other on their extravagant costumes and jewelry. Amidst all the gaeity and ostentatious wealth, you can't help but feel a little bit self-conscious and plain." Understand "guest", "crowd", "crowds", "people", "partygoer", "partygoers", "party goer", "party goers", "extravagant", "beautiful", "bright", "rich", "talk", "talking", "small talk" as the guests.

Instead of talking to the guests:
	say "Most of the people here are making small-talk on subjects you know nothing about[if JE1 is fired]. According to Jacobs, though, the people you really [i]should[r] be talking to are Duke Inhyron, Baron Amhyron, Earl Bresa, the Queen, and the Princess[end if].";

Instead of listening to the guests, say "Most of the people here are making small-talk on subjects you know nothing about."

Instead of doing something other than examining the guests:
	say "The guests are all far too absorbed in the party to pay attention to you.";

Section 5 - Scenery (String quartet)

A strings quartet is a person, scenery, in the Ballroom. The printed name is "string quartet." The description is "Somewhere over in the far corner of the ballroom, a string quartet is playing stately chamber music."
Understand "chamber", "group", "band", "orchestra", "musicians", "musician", "music", "string", "corner", "far corner" as the strings quartet.

Instead of listening in the presence of the Strings Quartet:
	try examining the Strings Quartet;

Instead of doing something other than examining or listening to the strings quartet:
	say "There are too many other guests crowded around the musicians for you to get close enough to them.";

Section 6 - Scenery (Pieter in absentia)

Before doing anything when (the noun is Pieter or the second noun is Pieter) and the location is the Ballroom:
	remove Pieter from play;
	update the character list;
	say "You can't see Pieter anywhere; he must have gotten separated from you in the press of guests. For the time being at least, you're on your own." instead;

Section 7 - Scenery (Party food)

Some waiters are a person, in the Ballroom. The waiters are scenery. The printed name of the waiters is "servants". The description of the waiters is "The servants weave deftly through the crowd, intent on making sure that no guest goes without a drink or a canape for more than ten seconds." Understand "servant", "servants", "waiter", "dozens of" as the waiters.

Instead of talking to the waiters, say "The servants seem to be doing their best to ignore you."
Instead of showing or giving something to the waiters, say "The servants seem to be doing their best to ignore you." 

Instead of kissing or attacking the guests, say "You should probably avoid making a scene."
Instead of throwing something at the guests, say "You should probably avoid making a scene."

Some platters are a supporter, carried by the waiters. The description of the platters is "The silver platters are polished to a mirror shine, piled high with food and wine." Understand "platter", "silver", "polished", "shine", "mirror", "shiny" as the platters.

Some party food is on the platters. The printed name of the party food is "food and drink". The description of the party food is "Tiny sandwiches, delicate pastries (still fresh from Germaise's oven, you imagine), and neatly ordered ranks of wine glasses are balanced on the platters being carried around by dozens of servants." Understand "drink", "drinks", "wine", "glasses", "glass", "tiny", "sandwich", "sandwiches", "delicate", "pastry", "pastries" as the party food.

Instead of smelling the party food, say "It all smells delicious."
Instead of doing something other than examining or smelling the party food, say "For some reason, you can't get any of the servants to stop long enough to sample anything on the platters. It's actually very annoying."

Rule for supplying a missing noun when drinking in the Ballroom:
	change the noun to the party food;

Instead of drinking the party food:
	say "There is no shortage of drink to be had here, but you need to keep your wits about you.";

Part 6 - Social airs

High Society is a scene. High Society begins when Journey To The Ball has ended and the time since Journey to the Ball ended is 4 minutes.

Brief respite is a scene. Brief respite begins when High Society begins. Brief respite ends when every ballgoer in the Ballroom is spoken to.

Definition: a ballgoer is spoken to if it has been spoken to.
	
To decide if (someone - a ballgoer) has been spoken to:
	if the someone is:
		-- Jacobs the Elder:
			decide on whether or not JE20 is fired;
		-- the Princess:
			decide on whether or not a quip clustered with PR15 is fired; 
		-- the Duchess of Inhyron:
			decide on whether or not the Duke of Inhyron has been spoken to;
		-- the Duke of Inhyron:
			decide on whether or not a quip clustered with IN13 is fired;
		-- the Baron of Amhyron:
			decide on whether or not AM18 is fired;
		-- the Earl of Bresa:
			decide on whether or not BR12 is fired;
		-- the Queen:
			decide yes;
		-- otherwise:
			decide no;

Instead of kissing a ballgoer, say "That doesn't seem like it would be appropriate behavior at a formal ball."

Dancing with is an action applying to one thing.

Understand "dance" as dancing with when the player can see a ballgoer.
Understand "dance with [ballgoer]" as dancing with.
Understand "dance" as dancing with.

Rule for supplying a missing noun when dancing with:
	change the noun to the player;

Check dancing with someone:
	say "You may be nimble on your feet in a tight spot, but you've never danced before and you're far too self-conscious to start now." instead;

Chapter 1 - Face-to-face conversations

A ballgoer is a kind of person. 
The indefinite article of a ballgoer is "the". [The ballgoers are titled nobles. So instead of "A Queen", "An Earl of Bresa", we have "The Queen", "The Earl of Bresa" etc. ]

Rule for deciding the concealed possessions of a ballgoer (called the individual):
	if the individual is the current conversationalist, no;
	yes;

Chapter 2 - Descriptions from afar

A ballgoer has a text called the distant description.

Before examining a ballgoer when the noun is not the current conversationalist:
	say distant description of the noun, paragraph break instead;

Chapter 3 - A sense of people dancing

Rule for writing a paragraph about The Queen: 
	now the Duke of Inhyron is plural-named;
	let x be the current conversationalist;
	say "[if x is not the player][The x] [is or are for x] standing nearby. [end if][The list of nearby dancing ballgoers] are elsewhere in the crowd.";
	now the Duke of Inhyron is not plural-named;

Rule for printing the name of the Duke of Inhyron when writing a paragraph about the Queen:
	say "Duke and Duchess of Inhyron";

Definition: a ballgoer is dancing:
	if they are the current conversationalist, no;
	if they are the Queen, no;
	if they are the Duchess of Inhyron, no; [printed with the Duke]
	yes.

Chapter 4 - Social butterfly

The making excuses rules is an object-based rulebook. The making excuses rules have default success.

First before talking to a ballgoer when the noun is not the current conversationalist and the current conversationalist is not the player:
	consider the making excuses rules for the noun;
	if the rule succeeded:
		carry out the terminating conversation with activity with the current conversationalist;
		[just momentarily, the player is not in conversation with anyone...]

[ ...and then the "talking to" action completes as normal. ]

[Rule for terminating conversation with someone while making excuses in favor:
	now the current conversationalist is the player;
	[but say nothing - e.g. suppress the usual blank topiclist output.] ]

Part 7 - Various Ballgoers

Chapter 1 - Jacobs the Elder

Section 1 - Description

Jacobs the Elder is a proper-named, male ballgoer.

The description of Jacobs the Elder is "Jacobs is a large man like his son, though age and indulgence have made him less imposing. He wears an ostentatious velvet cloak with a thick, furred collar, far too heavy for the season, and beads of sweat glitter on his balding scalp. His eyes are bloodshot, heavy-lidded, and cynical." Understand "father", "merchant", "man" as Jacobs the Elder.

The distant description of Jacobs the Elder is "Jacobs stands slightly apart, a goblet of spiced wine in one hand, watching the other guests with gruff contempt."

A velvet cloak is worn by Jacobs the Elder. The description is "It looks far too hot to be wearing in a crowded room like this." Understand "thick", "furred", "fur", "collar" as the velvet cloak.

A goblet of spiced wine is carried by Jacobs the Elder. The description is "Jacobs isn't the only person here drinking more than is good for him, but he's certainly putting in a good effort." The printed name is "wine". Understand "spice" as the goblet of spiced wine.

Section 2 - Jacobs's Conversation

When High Society begins:
	move Jacobs the Elder to the location;
	start conversation with Jacobs The Elder on JE1;

Rule for initiating conversation with Jacobs the Elder:
	change the chosen opening gambit to JE12;

JE1 is a quip. The display text is "'Well now, look who we have here,' says a gruff voice behind you. 'You must be the girl my son helped out of prison recently.' You turn and are startled to see a familiar face — it's Jacobs, your former cellmate from the jailhouse![paragraph break]Then you realize — no, it's not him. This man has the same heavy brow and hulking build, but he's older, more jowly, and his hair is gray at the temples. This must be Jacobs['] father, the wealthy and notorious merchant."

JE2 is a transitional quip. The menu text is "'It was the other way around, actually.'". The display text is "[i]What would Dame Sandler do in this situation?[r] you wonder. You decide to play it cool and tricky. 'Funny,' you say. 'I remember it was me who helped your son out of prison.'"
The following quip is JE5.

JE3 is a quip. The menu text is "'And how is your son lately?'". The display text is "'And how is your son lately, Master Jacobs?' you ask. 'Staying out of prison?' [i]And improving his lock picking skills, one hopes,[r] you resist the urge to add.[paragraph break]Jacobs shrugs. 'I make no excuses for my progeny,' he says. 'He's his own man, and answers to no one. Just like his father.'"

JE4 is a transitional quip. The menu text is "'How do you know who I am?'". The display text is "This man's apparent familiarity with your situation is making you decidedly nervous. [if JE3 is unfired]'How do you know about that?' you ask, in a low, tight voice. [end if]'How do you know who I am?'"
The following quip is JE5.

JE5 is a quip. The display text is "He holds up a pudgy, ring-encrusted hand. 'Don't bother denying it, I have informants everywhere. I know exactly who you are... and [I]why[R] you were in a prison cell.' Jacobs laughs. 'Don't worry, I've no plans to reveal your presence to Fossville.'"

JE6 is a quip. The menu text is "'I thought you were a crook.'". The display text is "'I've heard you're a crook and a pirate,' you say, suspiciously. 'Why should I trust you?'[paragraph break]Jacob the Elder's face hardens. 'I'm a businessman,' he says loftily, 'with many diversified interests. Whatever else anyone may tell you is a pernicious lie. And I wouldn't turn you over to Fossville in any case because there's nothing in it for me. He will almost certainly find out about it soon enough without my help.'"

JE7 is a quip. The menu text is "'Then what do you want?'". The display text is "'If you're not here to blow the whistle on me, then what do you want?' you ask.[paragraph break]Jacobs the Elder smiles and leans close to you, close enough that you can smell the spiced wine on his breath. 'The way I see it, you owe my son quite a favor,' he murmurs in your ear. 'The way I see it, you'll be owing many people favors before you're done. But if you're to start off well, you'll need to know who those people are.[paragraph break]'Over there for example, is the Queen,' Jacobs says, pointing across the room to a woman you can barely see for all the servants and courtiers pressed around her. 'She knows who you are, too. Best to not cross her path tonight, though I doubt you'll get close enough even if you try.'[paragraph break]He points out a beautiful young woman who seems to be deliberately ignoring the fact that you're staring at her. 'And there's her daughter, working the room,' he says. 'She's Fossville's quarry tonight, but every warm-blooded male with an ounce of ambition will be trying to woo her anyway.'[paragraph break]Slowly, Jacobs turns you full circle, pointing out notable guests as they come into view. 'The Duke and Duchess of Inhyron, in the Seven Lakes region. An old family, very influential. Insufferably snobbish, as well, but at least they liked your father. And there's the Baron of Amhyron. You'll like him; he's a bit less formal, and less likely to hold your lack of upbringing against you. On the other hand, he is utterly beholden to the Inhyron Duchy. Then there's the Earl of Bresa,' Jacobs sighs. 'A merchant lord from the Kozar Delta. A foolish man with a purchased title. He has all the money in the world, but no power, because he is utterly indiscriminate in how he spends it.'[paragraph break]He turns back to you. 'Those are the players of the game in this room, and you will have to spar with all of them if you want to win.'"

JE9 is a quip. The menu text is "'Why are you telling me this?'". The display text is "'I still don't understand,' you say. 'Why are you telling me this? Why are you helping me?'[paragraph break]Jacobs the Elder shrugs. 'I'll be frank,' he says, 'you're a long shot. I don't blame you for trying to capitalize on your father's legacy, but you must know that your chances of doing so successfully are next to zero. Still, I didn't get to be where I am today without laying a few coins down on the occasional dark horse. A bit of free advice is no skin off my nose, and maybe you'll remember it if, by some outside whim of the Goddesses, you actually get somewhere.'"

JE10 is a quip. The menu text is "'But what do I have to do?'". The display text is "'But how do I [']win?[']' you ask, feeling a bit lost. 'What am I supposed to say to these people?'[paragraph break]Jacobs puts his hand on your arm. 'The bottom line, my dear girl, is this: unless you have an army at your beck and call, which you clearly do not, you cannot take the throne by yourself. It's all about how many allies you have, and there is only one way to bring people over to your side. It doesn't matter if they like you; it doesn't even matter if they think you'd make a good Queen. All that matters is whether they think you'll repay them well if they help you.'"

JE11 is a quip. The display text is "Suddenly he claps you on the back in a most uncourtly manner. 'Well, that should be enough to get you started. Good luck, my girl.' And he wanders off, chuckling, to mingle with the other guests."

JE12 is a quip. The display text is "Jacobs raises an eyebrow and purses his thick lips as you approach. 'Back for more, hmm?'"

JE13 is a quip. The menu text is "'What else can you tell me about Fossville?'". The display text is "'Since you're not going to turn me in, why don't you tell me more about Baron Fossville?' you ask.[paragraph break]Jacobs chuckles. 'I have to admit, Fossville has guts. I don't think there's a person here that doesn't at least suspect that he poisoned your father. Except for Bresa, perhaps,' he adds, rolling his eyes. 'No, tonight Fossville is sending a message. He's letting the others know that he knows that they know, and it makes no difference to his plans. He's hoping this show of confidence will sway them over to his side.'[paragraph break]He takes a long swig of wine. 'On the other hand,' he muses, 'maybe he planned this whole thing just to trap you.'"
The repeat text is "[rp] 'this ball is to show his rivals that he has what it takes to be King.'"

JE14 is a quip. The menu text is "'Can you tell me anything about my father?'". The display text is "'Can you tell me anything about my father?' you ask.[paragraph break]Jacobs smirks. 'The old Duke spent too much time worrying about his subjects, and not enough time worrying about who else might want to rule them. He left himself open. You'd do well to remember that.'"
The repeat text is "[rp] 'your father was a good man, but too trusting.'"

JE15 is a quip. The menu text is "'Tell me about the Queen.'". The display text is "'Tell me about the Queen,' you say.[paragraph break]'Fossville thinks the Queen is here to endorse his courtship of the Princess,' Jacobs explains. 'But in fact, what she's doing is watching everyone else to see how they react to his schemes. She has no qualms about marrying her daughter to a murderer, but not if there's a chance it might not work out for her politically. Of course, the one thing the Queen hates more than anything else is an unexpected hitch in her plans.' He smiles and points his chubby finger. 'And that would be you, my dear girl.'"
The repeat text is "[rp] 'the Queen will not make any moves yet. Tonight, she merely watches the field.'"

JE16 is a quip. The menu text is "'Tell me about the Princess.'". The display text is "'Tell me about the Princess,' you say.[paragraph break]'The Princess is an opportunist,' says Jacobs, 'and she sees Fossville as her best chance. There's even been rumors that she's been trying to help the Baron raise money. Anyway, I can't imagine she is very happy to see you here,' he adds. 'You and she are competing for the same position, now. And you know how jealous girls can get.'"
The repeat text is "[rp] 'she will do anything to win the Ascension. Watch her carefully.'"

JE17 is a quip. The menu text is "'Tell me about the Duke of Inhyron.'". The display text is "'Tell me about the Duke of Inhyron,' you say.[paragraph break]'Duke Inhyron is a stuffed shirt and a hypocrite,' Jacobs says with a sour face. 'He'd like you to think that he's above all this petty politicking, that he's only here to respect the old Duke's memory, but he's looking for an angle just like everyone else. He has a son of his own, and he'd like nothing better than to marry the little brat into whichever family seizes the throne.'"
The repeat text is "[rp] 'the Duke is here to further his own interests, just like everyone else.'"

JE18 is a quip. The menu text is "'Tell me about the Baron of Amhyron.'". The display text is "'Tell me about the Baron of Amhyron,' you say.[paragraph break]'The Baron of Amhyron is that rarest of breeds,' says Jacobs, 'honest, good-hearted, and an effective ruler. Most folks only manage, at most, two out of three.' Jacobs thinks for a moment. 'The Baron might actually be a good ally for you to cultivate, but you'd need to tread carefully. The Inhyrons gifted his family their lands two generations ago, and they've never let him forget it. He is bound to support their interests whether he likes it or not.'"
The repeat text is "[rp] 'Baron Amhyron would be a good ally, if only he were free to act on his own.'"

JE19 is a quip. The menu text is "'Tell me about the Earl of Bresa.'". The display text is "'Tell me about the Earl of Bresa,' you say.[paragraph break]Jacobs snorts, spraying a mouthful of wine on a passing partygoer. 'Bresa is a fat idiot,' he says, flatly. 'He thinks that by throwing coins about like bird feed, he will create many allies. Instead he creates spongers who know they can get anything from him without providing anything in return. Ignore him; he's useless.'"
The repeat text is "[rp] 'the Earl is useless. Ignore him.'"

JE20 is a quip. The menu text is "'Good-bye.'". The display text is "'Thank you for your advice, Master Jacobs,' you say. 'I think I'll wander around and play the game with one of the other guests.'[paragraph break]Jacobs nods his head as you leave. 'Always a pleasure, my dear girl.'"

Rule for making excuses for someone when the current conversationalist is Jacobs the Elder:
	carry out the firing activity with JE20;

The response of JE1 is { JE2, JE3, JE4 }.

The response of JE3 is { JE4, JE6 }.

The response of JE5 is { JE6, JE7 }.

The response of JE6 is { JE7 }.

The response of JE7 is { JE9, JE10 }.

The response of JE9 is { JE10 }.

The response of JE10 is { JE9 }.

The response of JE11 is { }.

The response of JE12 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE13 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE14 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE15 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE16 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE17 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE18 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE19 is { JE13, JE14, JE15, JE16, JE17, JE18, JE19, JE20 }.

The response of JE20 is { }.

JE9 is clustered with JE10.

JE12 is clustered with JE13, JE14, JE15, JE16, JE17, JE18, and JE19.

After firing JE1:
	now every ballgoer is in the Ballroom;
	[except:] remove the Prince of Gravesal from play;
	update the character list;

After populating a quip that is clustered with JE9:
	if every quip clustered with JE9 is fired begin;
		[transition to JE11]
		carry out the firing activity with JE11;
		carry out the populating activity with JE11;
	end if;

After populating a quip (called q) that is clustered with JE12:
	remove q from the current conversation, if present;

Chapter 2 - The Queen

The Queen is a female ballgoer. The indefinite article of the Queen is "the".

The distant description of the Queen is "Her Royal Majesty is not a tall lady, and most of the time all you can see of her is a flash of golden crown or silver hair glimpsed between the shoulders of her entourage. But every now and then the shoulders part, and you get a good look at her: a pale, angry-looking woman with sharp, shrewish features. Her tiny face is almost lost within the explosion of jewel-encrusted brocade and lace that geysers up from the collar of her gown[first time].[paragraph break]Then, for just a moment, her gaze meets yours. Her stern expression never changes, but her eyes seem to flash a warning. [i]I'm watching you,[no line break][r] she seems to say. [i]Do not cross me[r][only]." Understand "royal", "majesty", "woman", "lady" as the Queen.

First before doing something other than examining with the Queen:
	say "With so many people crowding around trying to get the Queen's attention, you can't even get near her." instead;

Chapter 3 - The Princess

Section 1 - Description

The Princess is a female ballgoer. The indefinite article of the Princess is "the". The description is "The Princess is tall, shapely, and wisp-thin, wearing a long gown of wine-colored silk embroidered with amethysts. She's beautiful, but she's also haughty, and the way she looks down her nose at you with those cold, blue eyes irritates you to no end. After all, she's only a few years older than you are — what (besides her title and her piles of riches) makes [i]her[r] so special?"

The distant description of the Princess is "Even from across the room, the Princess commands attention; wherever she mingles, conversations stop, and men's eyes follow her. Strangely, she still seems to be avoiding your gaze, although why she would be discomfited by one person when everyone else is openly staring at her is beyond you."

A wine-colored silk gown is worn by the Princess. The description is "You hate to admit it, but her dress is quite simply stunning." Understand "dress", "amythest", "amythests", "wine", "coloured", "colored" as the wine-colored silk gown. 

Section 2 - Princess's Conversation

Rule for initiating conversation with the Princess:
	change the chosen opening gambit to PR1;

PR1 is a quip. The display text is "The Princess pointedly ignores you for a few moments, then, when it's clear that you're not going to go away, she crosses her arms and snaps, 'Oh, you must be that ambitious little street urchin Mother told me about. How charming.'"

PR2 is a transitional quip. The menu text is "'I'm not a street urchin.'". The display text is "'I am not a street urchin,' you say coldly. Sure, technically you were one just yesterday, but you don't have to take that from [i]her[r]. 'I am the daughter of a Duke, and I'd ask that you address me with some respect.'" The following quip is PR5.

PR3 is a transitional quip. The menu text is "'You should be afraid of me.'". The display text is "'You should be afraid of me, you know,' you say, trying to show some bravado. 'Even if you are the Princess, that doesn't mean you have any greater claim to the throne than I do. And you have fewer supporters than you think, too. Fossville isn't well loved around here.'" The following quip is PR5.

PR4 is a transitional quip. The menu text is "'So, do you like parties?'". The display text is "'So...' You flounder, struggling to think of something to say. 'This is a pretty nice place. Do you, uh, like, uh... balls?'" The following quip is PR5.

PR5 is a quip. The display text is "The Princess looks at you incredulously and says. 'Are you trying to have a [i]conversation[r] with me?' she asks."

PR6 is a transitional quip. The menu text is "'Yes, I am.'". The display text is "'Yes, your Highness, I am,' you say, determined not to be put off. 'It seems to me that getting to know to each other might be to our mutual benefit, even if we do find ourselves at odds.'" The following quip is PR9.

PR7 is a transitional quip. The menu text is "'Did you know that Fossville is a murderer?'". The display text is "You press your advantage. 'So how does it feel to be courting a murderer?' you ask. 'You do know that the Baron is a murderer, right? That he poisoned my father?'" The following quip is PR9.

PR8 is a transitional quip. The menu text is "'It must be nice, being a Princess.'". The display text is "Once again, you find yourself holding the conversational ball with no idea of where you should throw it. 'So... what's it like, being a Princess?' you try. 'Must be nice.'" The following quip is PR9.

PR9 is a quip. The display text is "The Princess looks around the room to see who's watching. 'Oh, Goddesses,' she moans, 'this is so embarrassing.'"

PR10 is a transitional quip. The menu text is "'I know, I know!'". The display text is "You find yourself nodding eagerly. 'I know, isn't it?' you say. 'I've been feeling terribly awkward almost from the moment I started talking to you.'" The following quip is PR15.

PR11 is a transitional quip. The menu text is "'I'm sorry I've made you so uncomfortable.'". The display text is "'I'm sorry I've made your Highness so uncomfortable,' you say, letting just the slightest hint of sarcasm into your voice. 'Hopefully, after I've won the Ascension, our social engagements won't be quite as awkward.'" The following quip is PR15.

PR12 is a transitional quip. The menu text is "'What can you tell me about the Duke of Inhyron?'". The display text is "'What can you tell me about—'". The following quip is PR15.

PR13 is a transitional quip. The menu text is "'What can you tell me about the Baron of Amhyron?'". The display text is "'What can you tell me about—'". The following quip is PR15.

PR14 is a transitional quip. The menu text is "'What can you tell me about the Earl of Bresa?'". The display text is "'What can you tell me about—'". The following quip is PR15.

PR15 is a quip. The display text is "'Enough,' interrupts the Princess in a strong whisper, 'I understand all too well what you represent to Miradania. You're father was [bold type]loved[roman type]. Is loved even in death. As long you remain alive and have access to royalty, you're going to be a thorn in [italic type]my[roman type] plans. I don't think you'll be much competition, but I have no interest in giving you legitimacy either way. So if you'll excuse me...' And then she shoulders past you and disappears into the crowd."

PR16 is a quip. The display text is "'If you'll excuse me, your Highness,' you say, dropping an awkward curtsey, 'I would take your leave-'[paragraph break]'My pleasure,' she whispers, shouldering past you and disappearing into the crowd."

Rule for making excuses for someone when the current conversationalist is the Princess:
	carry out the firing activity with PR16;

The response of PR1 is { PR2, PR3, PR4 }. 

The response of PR5 is { PR6, PR7, PR8 }. 

The response of PR9 is { PR10, PR11, PR12, PR13, PR14 }. 

PR15 is clustered with PR16.

After firing a quip that is clustered with PR15:
	remove the Princess from play;
	update the character list;

Chapter 4 - The Duke and Duchess of Inhyron

The Duke of Inhyron is a ballgoer. The description is "The Duke is a handsome, elderly man, with snow-white hair swept back from a widow's peak and a neatly trimmed beard. His tunic is of an old-fashioned, aristocratic cut, deep purple with golden buttons at the shoulder and braided epaulets to hold his formal cloak in place." Understand "man", "husband", "Duchesss husband", "Duke and Duchess" as the Duke of Inhyron.

The distant description of the Duke of Inhyron is "The Duke of Inhyron seems content to stand quietly at one end of the ballroom, occasionally nodding to guests as they pass by. His eyes are everywhere, however, flickering constantly around the room. The Duchess never moves from his side."

Rule for clarifying the parser's choice of the Duke of Inhyron:
	do nothing;

The Duchess of Inhyron is a female ballgoer, scenery. The description is "The Duke's wife is thin and nervous and never speaks. She does, however, occasionally peek out from behind her husband's arm, and stare at you with wide eyes and quivering nostrils." Understand "wife", "woman", "Dukes wife" as the Duchess of Inhyron.

The distant description of the Duchess of Inhyron is "The Duchess of Inhyron hovers, frail and mouselike, at her husband's side."

An aristocratic tunic is worn by The Duke of Inhyron. The description is "The Duke's uniform makes him look every inch the genteel statesman." The printed name is "Duke's clothes". Understand "button", "buttons", "epaulet", "epaulets", "cloak", "uniform", "Dukes" as the aristocratic tunic.

Does the player mean doing something to the Duchess of Inhyron:
	it is unlikely;

Section 2 - Conversation

Definition: a thing is the Duchy of Inhyron:
	if it is the Duke of Inhyron, yes;
	if it is the Duchess of Inhyron, yes;
	no;

Rule for initiating conversation with the Duke of Inhyron when IN1 is unfired:
	change the chosen opening gambit to IN1.

Rule for initiating conversation with the Duke of Inhyron when IN1 is fired:
	change the chosen opening gambit to IN15.

Instead of talking to the Duke of Inhyron when IN14 is fired:
	say "The Duke coldly and deliberately turns his back to you. Not much chance he'll talk to you now.";

Instead of talking to the Duchess of Inhyron when IN14 is fired:
	say "The Duke takes his wife firmly by the hand and leads her away from you without so much as a glance in your direction.";

Instead of talking to the Duchess of Inhyron when the current conversationalist is the Duke of Inhyron:
	unless Brief Encounter is happening:
		say "The Duchess shrinks back, as though startled that you would deign to speak to her at all.";
	otherwise:
		continue the action;

Before talking to the Duchess of Inhyron when the current conversationalist is not the Duke of Inhyron and IN14 is not fired:
	say "The Duchess shrinks back as you approach, and the Duke quickly moves to stand between you and her, greeting you smoothly as though you had intended to talk to him all along.";
	try talking to the Duke of Inhyron instead;

IN1 is a quip. The display text is "Duke Inhyron inclines his head as you approach. 'No need for introductions,' he says sternly. 'I am aware of who you are, and I see no reason to incriminate either of us by announcing it.' He looks you up and down with only a slight squint of disapproval. 'Your father was a good ruler and a good man,' he says, 'and though I do not think you have what it takes to fill his shoes, he was a friend, and you are his kin. So there we are. What would you ask of me, girl?'"

IN2 is a quip. The menu text is "'You don't like me very much, do you?'". The display text is "'I get the impression that you don't like me very much,' you say, making an effort to stay polite and unruffled. 'Would you care to tell me why?'[paragraph break]The Duke gives a short, exasperated sigh. 'Very well. I do not feel it is appropriate for you to pursue the Ascension given the... [i]circumstances[r] surrounding your birth. I am not partisan; I have no personal stake in who is chosen to take the throne. However, there are proper, traditional methods for going about it. I would see your father's memory honored properly. If you can convince me that coming out publicly as his illegitimate heir for political gain would somehow not besmirch his family name, then I might be persuaded to look more favorably on your endeavors.'"

IN3 is a quip. The menu text is "'You were a friend of my father?'". The display text is "'You were a friend of my father?' you ask. You find it hard to believe that your father would have been friendly with this stuck-up chauvinist.[paragraph break]'I counted your father as a friend and an equal,' says the Duke. 'It pained me to learn of his moral lapse, but none of us are perfect, I suppose. What's done is done, and I do not judge him.'"

IN4 is a quip. The menu text is "'What do you know about Jacobs the Elder?'". The display text is "'What do you know about Jacobs the Elder?' you ask.[paragraph break]The Duke gives you a look of withering contempt. 'Jacobs is a common gangster. I'm having difficulty imagining how a conversation about him could possibly be fruitful.'"
The repeat text is "[rp] 'I have nothing to say about Jacobs. Let us talk about something else.'"

IN5 is a quip. The menu text is "'What do you know about Baron Fossville?'". The display text is "'What do you know about Baron Fossville?' you ask.[paragraph break]'Fossville can be a bit uncouth, I admit, but he has good breeding,' the Duke says. 'I've no specific reason to think that he wouldn't be a good candidate for the Ascension, although I am open to the possibility of alternatives.'"
The repeat text is "[rp] 'Fossville comes from a respectable family and I see no reason not to endorse his claim to the throne.'"

IN6 is a quip. The menu text is "'Don't you know Fossville murdered my father?'". The display text is "Unable to keep the acid tone out of your voice, you ask, 'It doesn’t bother you that Fossville murdered my father?'[paragraph break]The Duke looks a bit uncomfortable. 'There is no evidence that he did any such thing,' he says finally. 'Only malicious rumors spread by the Baron's political enemies. Among which, I feel compelled to point out, you now number yourself. You must admit, it would be very convenient for you if it became widely believed that Baron Fossville is a murderer, whether or not such a thing were actually true.' He smiles infuriatingly and spreads his hands. 'I am merely trying to remain objective. It would be irresponsible of me to not take these things into consideration.'"

IN7 is a quip. The menu text is "'What do you know about the Queen?'". The display text is "'What do you know about the Queen?' you ask.[paragraph break]'I have the utmost regard for Her Majesty,' says the Duke, glancing over at the woman, 'and I respect her judgment. I expect that her opinion and my own are in accordance on many things, particularly with regard to the Ascension. Which is not to say that she and I have never disagreed, but it would take a strong argument to convince me that she was wrong.'"
The repeat text is "[rp] 'I respect the Queen's judgment.'"

IN8 is a quip. The menu text is "'What do you think about the Princess?'". The display text is "'What do you think about the Princess?' you ask.[paragraph break]The Duke smiles. 'She is a fine young lady, with the most respectable of bloodlines, obviously. My own son has only just turned nine; otherwise I myself would be petitioning on his behalf for the Princess's hand.'"
The repeat text is "[rp] 'she is a most respectable young lady.'"

IN9 is a quip. The menu text is "'What can you tell me about Baron Amhyron?'". The display text is "'What can you tell me about Baron Amhyron?' you ask.[paragraph break]'My grandfather granted the Baron's grandfather his title and lands more than half a century ago,' the Duke informs you. 'Since then, the Amhyrons have fulfilled their vassal obligations more than adequately. I sometimes fear that the Baron is too lenient with his subjects, especially in the area of moral guidance, but that is his own concern. So long as the King's Law is enforced and taxes are levied, I am content to merely set an example and allow others to emulate it as they see fit.'"
The repeat text is "'[rp] 'Baron Amhyron is an adequate ruler, no more.'"

IN10 is a quip. The menu text is "'Why don't you let Baron Amhyron follow his own interests?'". The display text is "'According to Jacobs, you won't let the Baron sneeze without your say-so,' you remark. 'Is it really necessary to keep him so beholden to you? Why don't you let him follow his own interests?'[paragraph break]The Duke stiffens, clearly furious at the idea of someone like Jacobs (or even, you realize, someone like you) gossiping about him. 'The traditions of vassalage are ancient and intricately bound to concepts of honor and moral duty, so it does not surprise me that Jacobs has such a poor understanding of them,' he snaps. 'But fundamental to this tradition is the idea of loyalty. Without loyalty, we are all weakened. If you cannot grasp that, then I have little optimism for your chances for the throne.'"

IN11 is a quip. The menu text is "'What do you know about the Earl of Bresa?'". The display text is "'What do you know about the Earl of Bresa?' you ask.[paragraph break]'The Earl of Bresa is a sterling example of what happens when material wealth is divorced from proper family lineage,' sniffs the Duke. 'People of his class were never meant to wield power, and have no idea how to properly do so.'"
The repeat text is "[rp] 'the Earl of Bresa is a buffoon and unworthy of his title.'"

IN12 is a quip. The menu text is "'You say you have a son?'". The display text is "'Do I understand correctly Your Grace, that you have a young son?' you ask.[paragraph break]The Duke puffs himself up. 'That's right. He'll be ten years old come this autumn, and already he is a strapping lad, a credit to his bloodline.'"

IN13 is a quip. The menu text is "'I might be willing to marry him.'". The display text is "Choosing your words carefully, you say, 'Since bloodlines are so important to you, I'm sure you're aware of the advantages of marrying into a Royal one. If I win the Ascension, I could make that happen for you.'[paragraph break]The Duke, for once, looks speechless.[paragraph break]You clear your throat and hurry on. 'Think about it, Your Grace. The Princess strikes me as someone who would be very particular about whom she married, but I won't have the luxury of being picky. Of course, I am illegitimate... but I'd still be the Queen. And that's what really matters, right?'[paragraph break]The Duke stares at you for a long time, then gives a short, curt bow. 'I will remember that,' he says, then turns stiffly and strides away. His wife scurries after him.[paragraph break]You take a deep breath and try to swallow the nasty taste in your mouth. You've just promised to marry the nine-year-old son of a conceited, condescending bigot. But that might just make the difference when it's time for the Duke to decide whom he's going to support for the Ascension.[paragraph break]You have a feeling that Dame Sandler would be pleased. You're not exactly sure that makes you happy."

IN14 is a quip. The menu text is "'Well, keep him away from me.'". The display text is "You nod. 'Well, if I do become Queen — and I just might, you know — do me a favor and keep him far away from me. In fact, if I have anything to say about it, I'll make sure there's not a respectable family in the kingdom who'll have anything to do with him. It would be worth it to see the look on your face when your son is forced to marry some illegitimate street urchin just to keep your family name going.'[paragraph break]The Duke's face goes purple. 'I will remember this,' he chokes out, then turns on his heel and strides away. His wife scurries after him.[paragraph break]'Except that I'd probably feel too sorry for the street urchin!' you shout after him. Your blood is up and you're breathing hard. You've completely ruined your chances of getting the Duke's support now, of course, but you don’t care — it felt so good to put that conceited, condescending bigot in his place."

IN15 is a quip. The display text is "Duke Inhyron nods as you approach. 'Yes, girl? What is it now?'"

IN16 is a quip. The menu text is "'Good-bye.'". The display text is "'If you'll excuse me, Your Grace,' you say, trying a small curtsey, 'I think I'll speak with some of the other guests.'[paragraph break]The Duke nods politely as you leave."

The response of IN1 is { IN2, IN3, IN4, IN5, IN7, IN8, IN9, IN11, IN12 }.

The response of IN2 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN3 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN4 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN5 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN6 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN7 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN8 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN9 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN10 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN11 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12, IN16 }.

The response of IN12 is { IN13, IN14 }.

The response of IN13 is { }.

The response of IN14 is { }.

The response of IN15 is { IN2, IN3, IN6, IN4, IN5, IN7, IN8, IN9, IN10, IN11, IN12 }.

The response of IN16 is { }.

IN1 is clustered with IN2, IN3, IN4, IN5, IN6, IN7, IN8, IN9, IN10, IN11 and IN15.

IN16 is clustered with IN13 and IN14.

Rule for making excuses for someone (called the target) when the current conversationalist is the Duke of Inhyron:
	unless the target is the Duchess of Inhyron:
		carry out the firing activity with IN16;
	otherwise:
		rule fails;

After populating a quip (called q) that is clustered with IN1:
	remove q from the current conversation, if present;

After populating a quip (called q) that is clustered with IN1:
	if IN2 is fired begin;
		remove IN2 from the current conversation, if present;
	end if;
	if IN3 is fired begin;
		remove IN3 from the current conversation, if present;
	end if;

After populating a quip (called q) that is clustered with IN1:
	if IN6 is unfired and IN3 is unfired and IN5 is unfired begin;
		remove IN6 from the current conversation, if present;
	otherwise if IN6 is fired;
		remove IN6 from the current conversation, if present;
	end if;

After populating a quip (called q) that is clustered with IN1:
	if IN10 is unfired and IN9 is unfired begin;
		remove IN10 from the current conversation, if present;
	otherwise if IN10 is fired;
		remove IN10 from the current conversation, if present;
	end if;

After populating a quip (called q) that is clustered with IN1:
	if IN12 is unfired and IN8 is unfired and JE17 is unfired begin;
		remove IN12 from the current conversation, if present;
	otherwise if IN12 is fired;
		remove IN12 from the current conversation, if present;
	end if;

Chapter 5 - The Baron of Amhyron

Rule for initiating conversation with the Baron of Amhyron:
	if AM1 is unfired:
		change the chosen opening gambit to AM1;
	otherwise:
		change the chosen opening gambit to AM17.

The Baron of Amhyron is a male ballgoer. The description of the Baron of Amhyron is "Baron Amhyron is middle-aged, somewhat stout, with a red face and kind crinkles around his deep, brown eyes. His clothes are not quite as rich or as formal as those of some of the other guests, but he still cuts a respectable figure." Understand "lord", "falton", "man" as the Baron of Amhyron.

The distant description of the Baron of Amhyron is "The Baron of Amhyron is mingling near the banquet table. He looks a little overwhelmed, although he appears to be enjoying himself."

AM1 is a quip. The display text is "Amhyron smiles as you approach. 'Good evening, milady,' he says, bowing and taking your hand. 'I don't believe I've had the pleasure of an introduction. I am Lord Falton, Baron of Amhyron. And you are?'[paragraph break]'Lady Jacqueline,' you manage, 'of, er... of Toresal.'[paragraph break]The Baron's eyebrows lift slightly. 'Do tell. How... intriguing. How may I be of service to you this evening, Lady Jacqueline?'"

AM2 is a quip. The menu text is "'I'm surprised you don't know who I am.'". The display text is "'I'm surprised you don't know who I am already,' you say with a smile. 'It sometimes seems like everyone else here does.'[paragraph break]'I didn't recognize you at first, no,' says the Baron, 'But I can't say I've never heard of you. There were rumors that the old Duke Toresal had a child hidden away somewhere, but I never gave them much thought. As to why you're here, I think I can guess.' He smiles. 'I suppose you're something of a stick in Fossville's spokes. Aren't you worried he'll come after you?'"

AM3 is a quip. The menu text is "'He's already tried.'". The display text is "'He's already tried, more than once,' you say, proudly. 'He hasn't caught me yet.'[paragraph break]The Baron's smile broadens. 'You certainly seem like a capable young woman,' he says. 'I suppose it's him I should be worried about.'"

AM4 is a quip. The menu text is "'He doesn't know I'm here.'". The display text is "'I doubt it,' you say. 'I don't think he even knows I'm here. I kind of snuck in through the back door,' you confide.[paragraph break]The Baron frowns slightly. 'But he'll find out soon enough, especially with everyone at the ball talking about you. And they are definitely talking about you, by the way,' he adds, with a slight smile. 'You should probably be more careful.'"

AM5 is a quip. The menu text is "'You look like you'd rather be somewhere else.'". The display text is "'You look like you'd rather be somewhere else,' you say. 'In fact, if you don't mind my saying so, you're the only person I've met so far who looks as uncomfortable as I feel. I thought a nobleman like yourself would be right at home at a fancy party like this.'[paragraph break]The Baron spreads his hands and shrugs. 'I'm something of an outsider here, I'm afraid,' he says. 'Just like you. I'm not very good at idle conversation, and I'm not important enough for anyone to want to chat me up.'"

AM6 is a quip. The menu text is "'Would you support me in the Ascension?'". The display text is "Something about the Baron's manner puts you at ease, and you decide to take the direct approach. 'Listen,' you say, 'uh, milord. The only reason I'm here is because I need allies. I don't have very many, and the truth is, I'm not very good at getting more, and I'm getting kind of desperate. So I'm going to come right out and say it. You know who I am... would you be willing to support me in the Ascension?'[paragraph break]The Baron, who has listened to you carefully throughout your whole speech, looks down at the floor and sighs.[paragraph break]'Thank you for your candor, Lady Jacqueline,' he says finally, looking you in the eye. 'I will try to be equally honest. I'd support just about anyone against Fossville. And I would be overjoyed to support the heir of Duke Toresal most especially. Unfortunately, that may not mean very much. I couldn't garner much more support beyond my own single vote, first of all. I haven't the influence.'[paragraph break]The Baron pauses, then looks away. 'And secondly, I can't oppose Duke Inhyron's interests without putting myself in jeopardy,' he says quietly. 'He's the one you need to convince, not me.'"

AM7 is a quip. The menu text is "'Why can't you act against Inhyron?'". The display text is "'What makes you so beholden to Inhyron?' you ask. 'Can't you speak your own mind?'[paragraph break]'Oh, I speak my mind often,' the Baron says quickly. 'Everyone pretty much ignores me. Or else they smile condescendingly and say how nice it is that the common folk have someone to represent their interests. And so it goes.'[paragraph break]He heaves another sigh. 'As for why I am so beholden, my family's land and titles were gifted by Inhyron's ancestor. That means he can remove them whenever he likes.'"

AM8 is a quip. The menu text is "'Would that be so bad?'". The display text is "'Would it be so bad if he did?' you press. 'You can't be happy serving under him like that.'[paragraph break]'You're right, I'm not very happy,' concedes the Baron. 'I'd give it all away, lands and title, except... except if Inhyron removed me, he'd replace me with someone worse — probably himself. I've managed to do some good in my little corner of Miradania, and I don't want to leave my subjects to someone who's just going to treat them like... well, like peasants. Better to stay where I am and do what I can, then to make waves and lose the ability to do even that much.'"

AM9 is a quip. The menu text is "'If I become Queen, I could help you.'". The display text is "'If I became Queen, I could help you,' you point out. 'I could grant you Royal lands, or something. You could be independent of Inhyron.'[paragraph break]The Baron grins wrily. 'That would be wonderful,' he says. 'But you can't make me independent of the Duke until you're Queen. And to become Queen, you'll need support. And I can't support you until I'm independent of the Duke. And so it goes.' He chuckles. 'I'm afraid my hands are tied, Lady Jacqueline.'"

AM10 is a quip. The menu text is "'What do you know about Fossville?'". The display text is "'What do you know about Fossville?' you ask.[paragraph break]'Hmm. Fossville, well.' The Baron pauses, carefully choosing his words. 'I don't like him. He is motivated purely by personal ambition; I don't think he cares a whit about the welfare of his subjects or about actually running the Kingdom, and that will spell disaster for Miradania if he takes the throne. That would be bad enough, but then there are the allegations about how the old Duke Toresal died... I would oppose his candidacy if I could, of course, but even then I'm afraid I wouldn't have enough influence for that to mean very much.'"
The repeat text is "[rp] 'I don't like him, but I can't openly oppose him if Inhyron supports him.'"

AM11 is a quip. The menu text is "'Did you know my father?'". The display text is "'Did you know my father?' you ask.[paragraph break]'Not personally, milady,' the Baron says, 'but from what I understand he was beloved by his subjects and respected by his fellow noblemen. Many people were sorry when he died.'"
The repeat text is "[rp] 'he was a good man and I was sorry when he passed on.'"

AM12 is a quip. The menu text is "'What do you think of Jacobs?'". The display text is "'What do you think of Jacobs the Elder?' you ask.[paragraph break]'Jacobs is an... interesting character,' says the Baron. 'I'm not quite sure what to make of him, except that he's probably not one to trifle with. And certainly not one to make casual deals with, unless you enjoy losing the shirt off your back without ever realizing that you'd wagered it.'"
The repeat text is "[rp] 'he's a tricky fellow. I hope you didn't promise him anything.'"

AM13 is a quip. The menu text is "'What do you think of the Queen?'". The display text is "'What do you think of the Queen?' you ask.[paragraph break]'The Queen is being short-sighted,' the Baron says. 'She's backing Fossville because she wants to maintain her family's dynasty, but she's not considering what that would mean for the Kingdom as a whole. I have no doubt that she'd as easily switch to some other candidate, but whomever she backs, she'll back for the wrong reasons.'"
The repeat text is "[rp] 'the Queen will back whomever helps her most politically, without regard for the consequences.'"

AM14 is a quip. The menu text is "'What do you think of the Princess?'". The display text is "'What do you think of the Princess?' you ask. [paragraph break]The Baron smiles. 'Well, the Princess is young,' he says. 'You can't really blame her for wanting the same thing her mother wants, now can you?'"
The repeat text is "[rp] 'the Princess is young. I tend to give her the benefit of the doubt for that.'"

AM15 is a quip. The menu text is "'What do you think of the Duke of Inhyron?'". The display text is "'What do you think of the Duke of Inhyron?' you ask.[paragraph break]The Baron smiles ruefully. 'What do I think of him? I think he's an arrogant, self-righteous bigot who's obsessed with his family lineage to an unhealthy degree. And my family owes his family everything. So I keep my mouth shut.'"
The repeat text is "[rp] 'it's not a good idea for me to speak ill of the Duke.'"

AM16 is a quip. The menu text is "'What do you think of the Earl of Bresa?'". The display text is "'What do you think of the Earl of Bresa?' you ask.[paragraph break]The Baron looks over to where the Earl is telling crude jokes and stuffing his face at the refreshment table. 'Oh, Bresa's a good fellow,' he says with a smile. 'He doesn't care for the social niceties, and that infuriates some peope. But he's smarter than most give him credit for.'"
The repeat text is "[rp] 'the Earl has his eccentricities, but he's as good a man as anyone here."

AM17 is a quip. The display text is "'Hello again, milady,' says the Baron with a slight bow. 'You are finding the festivities enjoyable, I hope?'"

AM18 is a quip. The menu text is "'Good-bye'". The display text is "'I've enjoyed talking to you, milord Baron,' you say, and are surprised to find that you actually mean it. 'But if you'll excuse me, I should talk to some of the other guests for a while.'[paragraph break]'Of course,' the Baron says amiably. 'The pleasure has been all mine, Lady Jacqueline.'"

Rule for making excuses for someone when the current conversationalist is the Baron of Amhyron:
	carry out the firing activity with AM18;

The response of AM1 is { AM2, AM5, AM10, AM11, AM12, AM13, AM14, AM15, AM16 }.

The response of AM2 is { AM3, AM4 }.

The response of AM3 is { AM5, AM6, AM10, AM11, AM12, AM13, AM14, AM15, AM16 }.

The response of AM4 is { AM5, AM6, AM10, AM11, AM12, AM13, AM14, AM15, AM16 }.

The response of AM5 is { AM2, AM6, AM10, AM11, AM12, AM13, AM14, AM15, AM16 }.

The response of AM6 is { AM2, AM5, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16 }.

The response of AM7 is { AM8, AM9 }.

The response of AM8 is { AM9 }.

The response of AM9 is { AM2, AM5, AM6, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM10 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM11 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM12 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM13 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM14 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM15 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM16 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16, AM18 }.

The response of AM17 is { AM2, AM5, AM6, AM7, AM10, AM11, AM12, AM13, AM14, AM15, AM16 }.

The response of AM18 is { }.

AM3 is clustered with AM4.

After populating AM6:
	if AM2 is fired,
		remove AM2 from the current conversation, if present;
	if AM7 is fired,
		remove AM7 from the current conversation, if present;

AM9 is clustered with AM10, AM11, AM12, AM13, AM14, AM15, and AM16.

After populating a quip (called q) that is clustered with AM9:
	remove q from the current conversation, if present;

After populating a quip (called q) that is clustered with AM9:
	if AM7 is unfired and AM6 is unfired and AM7 is unfired begin;
		remove AM7 from the current conversation, if present;
	otherwise if AM7 is fired;
		remove AM7 from the current conversation, if present;
	end if;

After populating when the current conversationalist is the Baron of Amhyron:
	if AM2 is fired:
		remove AM2 from the current conversation, if present;
	if AM5 is fired:
		remove AM5 from the current conversation, if present;
	if AM6 is fired:
		remove AM6 from the current conversation, if present;
	if AM7 is fired:
		remove AM7 from the current conversation, if present;

Chapter 6 - Earl of Bresa

The Earl of Bresa is a male ballgoer. The description of the Earl of Bresa is "The Earl of Bresa has a bald head and a huge, curly, red beard. His lavish, colorful clothes are stained with wine and food. He laughs too loudly and has a habit of putting his greasy fingers on other guests — usually a friendly slap on the back for the men, elsewhere for the women. Despite Jacobs['] warning, you find yourself liking him immediately." Understand "man" as the Earl of Bresa.

The distant description of the Earl of Bresa is "The Earl is easy to spot from anywhere in the room: he's the one roaring with laughter and throwing fistfuls of money at the chamber orchestra, much to the guests['] amusement and the musicians['] embarassment."

Section 2 - Conversation 

Rule for initiating conversation with the Earl of Bresa:
	if BR1 is unfired:
		change the chosen opening gambit to BR1;
	otherwise:
		change the chosen opening gambit to BR11;

BR1 is a quip. The display text is "'Ah-HA!' Earl Bresa roars when he spots you. 'The young up-and-comer! The stick in the spokes! The crumb in everyone's eye!' He seems utterly unconcerned that anyone else might hear him — although it seems like most of the other guests are making an effort to ignore him anyway. 'Listen,' he says, putting his hand on your arm. 'I like you already. You need anything, backing, whatever, you just let me know. Now, what was it you wanted to talk about?'"

BR2 is a quip. The menu text is "'You're willing to support me?'". The display text is "'You mean you're willing to support me?' you ask, taken aback. 'Just like that? Without knowing anything about me?'[paragraph break]'Young lady,' the Earl slurs, 'I know one thing about you and one thing only, and it is the only thing I need to know: that you are an insufferable aggravation to nearly every influential person at this Goddess-forsaken party. And that makes you all right by me.'"

BR3 is a quip. The menu text is "'Don't you want anything in return?'". The display text is "'I don't want to seem ungrateful,' you say, 'but that seems like a strange reason to support anyone for the Ascension. Don't you want anything in return?'[paragraph break]The Earl flaps his hand dismissively. 'Bah! What I want is good food and strong wine, and that I can buy. Why get involved in politics? Why break my back trying to curry favor with a pack of throne-sniffing hyenas who all hate me anyway? But you can stick in their craws in a way that I cannot, and for that, I will gladly back you.'"

BR4 is a quip. The menu text is "'What can you tell me about Baron Fossville?'". The display text is "'Can you tell me anything about Baron Fossville?' you ask.[paragraph break]'Fossville? A mere scarecrow, a wooden soldier, a coward.' rants the Earl. 'You know, they say he poisoned the old Duke. Er...' he wipes his mouth with his sleeve to hide his embarassment. 'Sorry to bring that up. My condolences and all that. But it wouldn't surprise me a bit if it were true. It would serve the Queen right to have that conniving weasel for a son-in-law.'"
The repeat text is "[rp] 'the man is a conniving weasel.'"

BR5 is a quip. The menu text is "'What can you tell me about my father?'". The display text is "'Did you know my father well?' you ask.[paragraph break]The Earl becomes sober for a rare moment. 'There was no finer man in the kingdom than your father, my girl' he says, putting his greasy hand on your arm. 'He was the one man I respected out of all of these stuffed peacocks. No one was his equal. Certainly not Fossville. Certainly not any of the backstabbing dandies parading around this room tonight.'"
The repeat text is "[rp] 'your father was a great man. Always remember that.'"

BR6 is a quip. The menu text is "'What do you think about Jacobs the Elder?'". The display text is "'What do you think about Jacobs the Elder?' you ask. 'He doesn't seem to have a very high opinion of you.'[paragraph break]'Why should I care what that self-inflated stallkeeper thinks of me?' yells the Earl, deliberately pitching his voice so that Jacobs, standing in a nearby corner of the room, can hear. 'He's just jealous of my title, is all.'"
The repeat text is "[rp] 'I [i]don't[r] think about Jacobs the Elder, if I can help it.'"

BR7 is a quip. The menu text is "'What can you tell me about the Queen?'". The display text is "'What can you tell me about the Queen?' you ask.[paragraph break]'Bah, the Queen's been all but useless ever since the King died,' the Earl says loudly, ignoring all the horrified stares. 'Now she's just desperate to remain relevant.'"
The repeat text is "[rp] 'the Queen's on her way out. That's good enough news for me.'"

BR8 is a quip. The menu text is "'What do you think about the Princess?'". The display text is "'What do you think about the Princess?' you ask.[paragraph break]The Earl gets a foolish grin on his face. 'Oh, she's a pretty thing, all right. I wouldn't mind marrying her myself, if it didn't mean moving in with her hungry old hag of a mother.' He opens his mouth and lets loose a long, ripping belch, then gives you a knowing, satisfied nod."
The repeat text is "[rp] 'she's very pretty. What else do you want to know?'"

BR9 is a quip. The menu text is "'What do you think of the Duke of Inhyron?'". The display text is "'What do you think of the Duke of Inhyron?' you ask.[paragraph break]'Oh, what a noble and proper gentleman is the Duke of Inhyron,' intones the Earl in grave tones. Then he throws back his head and roars, '[i]Balderdash![r] What a noble and proper heap of cow pies, more like! Always strutting about his precious lineages. He hates me, you know, because I'm [']low-born['] — which just means I earned my money the hard way instead of inheriting it from Mummy and Daddy — and even so I'm richer than he is. I flaunt it just to rub his pointy little nose in it.'[paragraph break]He leans close and 'whispers' loudly enough for guests over by the chamber orchestra to hear, 'Between you and me, I think the great Duke protests too much. Anyone that obsessed with bloodlines probably has something in his own that he'd rather people not know about.'"
The repeat text is "[rp] 'the Duke is a pompous fart who probably has a few dirty secrets of his own.'"

BR10 is a quip. The menu text is "'What do you think of the Baron of Amhyron?'". The display text is "'What do you think of the Baron of Amhyron?' you ask.[paragraph break]'Amhyron's a decent bloke,' grunts the Earl, 'but he's got no backbone. He lets Duke Inhyron pull his strings like a puppet, and he never says one word against it. He says he puts up with it for the good of his subjects, but really he's just afraid to make waves. He ought to stand up to the Duke for once in his life; it'd do him good.'"
The repeat text is "[rp] 'the Earl would be a better man if he could crawl out from under Inhyron's thumb.'"

BR11 is a quip. The display text is "'Hello again!' yells the Earl, swaying slightly as he waves you over."

BR12 is a quip. The menu text is "'Good-bye.'". The display text is "'It's been a pleasure,' you tell the Earl, 'but I should probably go annoy some of the other guests now.'[paragraph break]The Earl laughs with his mouth full of hors d'oeuvres. 'Spread the love around, my girl!' he calls after you."

Rule for making excuses for someone when the current conversationalist is the Earl of Bresa:
	carry out the firing activity with BR12;

The response of BR1 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10 }.

The response of BR2 is { BR3, BR4, BR5, BR6, BR7, BR8, BR9, BR10 }.

The response of BR3 is { BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR4 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR5 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR6 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR7 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR8 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR9 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR10 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10, BR12 }.

The response of BR11 is { BR2, BR4, BR5, BR6, BR7, BR8, BR9, BR10 }.

The response of BR12 is { }.

BR4 is clustered with BR5, BR6, BR7, BR8, BR9, BR10 and BR11.

After populating a quip (called q) that is clustered with BR4:
	if BR2 is fired,
		remove BR2 from the current conversation, if present;

Chapter 7 - Prince of Gravesal

Section 1 - Scene opening

Brief Encounter is a scene. Brief Encounter begins when Brief Respite ends.

After talking to a ballgoer when every ballgoer in the Ballroom is spoken to:
	stop the action;

When Brief Encounter begins:
	move the Prince of Gravesal to the ballroom;
	move the several mercenaries to the ballroom;
	now the current conversationalist is the Prince of Gravesal;
	update the character list;
	start conversation with the Prince of Gravesal on GR1;

Section 2 - Description

The Prince of Gravesal is a male ballgoer. The description is "The Prince cuts such a striking figure, you can't imagine how you failed to notice him until now. Tall and slim, with smooth skin the color of strong tea and long, ink-black hair tied back at the nape of his neck. His clothes, though finely cut, are black as well, devoid of decoration except for a single garnet brooch pinning his cloak at the shoulder. He looks young, perhaps only a few years older than you. And something about his deep, brown eyes makes it difficult for you to concentrate..."

Instead of talking to someone during Brief Encounter:
	if the noun is not the Prince of Gravesal:
		say "'Please stay,' the Prince says, his hand still gently holding your elbow. 'There's not much time left; grant me the pleasure of your company for just a few moments longer.'";

A hawk brooch is worn by the Prince of Gravesal. The description is "The brooch is shaped like a hawk, and decorated with bright red garnets." The printed name is "brooch". Understand "garnet", "garnets", "red", "decoration" as the hawk brooch. 

A black cloak is worn by the Prince of Gravesal. The description is "The Prince's cloak and clothes are plain but handsome, and black as coal." The printed name is "cloak". Understand "clothes", "coal" as the black cloak.

Section 3 - Conversation

GR1 is a quip. The display text is "'Pardon me,' someone says in a low, strangely accented voice, and a hand touches your elbow. You turn to see a tall, dark-skinned man leaning close to you. 'Please forgive my forwardness,' he murmurs. 'I have been waiting for the right moment to speak to you for some time now, but I fear if I wait much longer, I will never get the chance.'[paragraph break]He nods towards the ballroom entrance — where several of Fossville's mercenaries are standing![paragraph break]'Allow me to introduce myself,' the stranger says, as though nothing at all were amiss. 'I am Prince Gravesal.'"

GR2 is a quip. The menu text is "'Who are you?'". The display text is "Jacobs never mentioned this guy, and you don't remember seeing him in the ballroom until now. 'Who are you?' you demand.[paragraph break]'Merely an interested observer,' says the Prince with a hint of a smile. 'I have been watching you from afar, and watching the reactions of others to you. I am intrigued.'"

GR3 is a quip. The menu text is "'You've been watching me?'". The display text is "'You've been watching me?' you ask. You want to feel offended, but the way he's looking at you makes that difficult. 'Do you mean watching me as a potential companion or are you one of Fossville's spies?'[paragraph break]The Prince chuckles. 'Neither, actually. Of course I know who you are and why you're here, but that's not what intrigues me. There's something... special about you. I could see it from the moment I laid eyes on you.'"

GR4 is a quip. The menu text is "'I'm special?'". The display text is "You raise one eyebrow. 'So I'm [']special['], huh? How so?'[paragraph break]The Prince looks away for a moment. 'It is... difficult to explain. People in my family have always been able to see these things. It's like an aura.' He looks directly at you again. 'You have... a highly [i]visible[r] fate.'"

GR5 is a quip. The menu text is "'Is this about the Ascension?'". The display text is "'And this has nothing to do with the Ascension?' you ask, trying to keep a note of healthy skepticism in your voice. 'You don't have some ulterior political motive? You're not looking for some sort of favor down the line?'[paragraph break]The Prince shrugs. 'My homeland is far from here,on the outermost edge of the kingdom. I doubt very much the outcome of the Ascension will make much of a difference to me one way or the other.' He looks right into your eyes in a way that makes a lump rise up in your throat. 'No, mainly I am interested in [i]you[r].'"

GR6 is a quip. The menu text is "'I have to get out of here.'". The display text is "'Look,' you say, glancing over at the mercenaries working their way through the crowd, 'this has been very interesting, but I have to get out of here before those mercenaries spot me.'[paragraph break]The Prince seems unconcerned. 'It seems they have all the exits blocked, and they're combing the crowd very thoroughly. They [i]will[r] catch you. Why not spend the moments before they do in pleasant conversation, instead of scampering futilely about?'[paragraph break]Even though every instinct you have is urging you to run, you do have to admit there's a certain insane logic to what the Prince is saying."

GR7 is a quip. The menu text is "'What is my fate?'". The display text is "'Okay then, I'll bite,' you say. 'What do you see in my highly [i]visible[r] fate?'[paragraph break]The Prince looks deeply into your eyes, and goosebumps ripple up your arms as his fingertips brush across your skin. 'I see a long journey ahead of you. And many trials.'[paragraph break]For just a moment, all you can see are his deep, brown eyes.[paragraph break]'And I see death,' he whispers.[paragraph break]The Prince suddenly glances over your shoulder and smiles. 'It would seem that your company is desired by someone with greater powers of persuasion than mine,' he says. He looks back at you. 'Thank you. I hope I will see you again.'[paragraph break]He bows deeply; his lips are cool against the back of your hand. And then he is lost in the crowd, as suddenly as he appeared.[paragraph break]A rough hand falls on your shoulder. 'Come with us, girl,' growls a familiar voice. 'The Baron asked us not to drag you out kicking and screaming, so let's not disappoint him, eh?'"

The response of GR1 is { GR2, GR3, GR6 }.

The response of GR2 is { GR3, GR5, GR6 }.

The response of GR3 is { GR2, GR4, GR5, GR6 }.

The response of GR4 is { GR7 }.

The response of GR5 is { GR2, GR3, GR4, GR6 }.

The response of GR6 is { GR2, GR3, GR4, GR5 }.

GR2 is clustered with GR3, GR5 and GR6.

After populating a quip that is clustered with GR2:
	if GR2 is fired,
		remove GR2 from the current conversation, if present;
	if GR3 is fired,
		remove GR3 from the current conversation, if present;
	if GR5 is fired,
		remove GR5 from the current conversation, if present;
	if GR6 is fired,
		remove GR6 from the current conversation, if present;
	unless GR3 is fired:
		remove GR4 from the current conversation, if present;

After populating GR7:
	pause the game;
	move the player to the War Room, without printing a room description;

Chapter 8 - Several Mercenaries

Some several mercenaries are people, male. The initial appearance is "The mercenaries are working their way around the edge of the ballroom, scanning the crowd." The printed name is "mercenaries". The description is "Rough and ugly, with dirty clothes and mean, scarred faces. They carry serious-looking swords on their hips, and each has a long knife buckled at the small of his back." Understand "mercenary", "soldiers", "soldier", "rough", "ugly", "dirty", "clothes", "mean", "scarred", "scar", "scars", "face", "faces", "merc", "mercs" as the several mercenaries.

Rule for writing a paragraph about the several mercenaries when the location is the War Room:
	say "The mercenaries stand nearby, watching your every move.";

The mercenary-stuff is part of the several mercenaries. Understand "swords", "sword", "knives", "knife", "long", "serious", "serious looking", "buckle", "buckles", "weapon", "weapons", "arms" as the mercenary-stuff. The description is "The weapons look grossly out of place at this fancy ball, but no one else seems to notice[first time]. Maybe they're not noticing on purpose[only]."

Instead of doing something other than examining to the mercenary-stuff, say "You'd have to get close to the mercenaries to do that, and that doesn't seem like a good idea."

Instead of doing something other than examining to the several mercenaries:
	say "It would probably be best if you didn't attract their attention." instead;

Chapter 9 - Scene Ending

Brief Encounter ends when the location is the War Room;

High Society ends when Brief Encounter ends.

Book 13 - The Baron

Confronting the Baron is a scene. Confronting the Baron begins when High Society ends. Confronting the Baron ends when the skirmish begins.

When Confronting the Baron begins:
	select theme "Confrontation";
	output chapter heading "Chapter 11 - Baron Fossville";
	say "The mercenaries march you out of the ballroom, through the foyer, and into the inner reaches of the keep. At every doorway you pass a pair of Lord's Guards standing at attention; some of them glare at the ill-kempt and ill-disciplined mercenaries, but no one makes a move to stop them or help you. You are keenly aware of the dagger strapped to your leg under your dress, but there seems to be no opportunity to use it. Not that you'd last very long against a half-dozen armed men anyway.[paragraph break]The last door is not guarded. Beyond it is a room filled with dim, flickering light. The mercenaries push you into a chair, and then the door clicks shut behind you.";
	move Pieter to the War Room;
	now Pieter is caught;
	now Pieter is not scenery;
	now the knotted ropes are part of Pieter;
	[move the several mercenaries to the War Room; [here so that they appear first after the room description--] ]
	move the player to the blackwood chair, without printing a room description;
	try looking;
	move Baron Fossville to the War Room;
	change the current script to {FOSS0, FOSS1, FOSS2, FOSS5, FOSS6, FOSS7, FOSS8,  FOSS8a, FOSS9, FOSS10, FOSS11, FOSS12};
	ratchet the current tension to important;

Chapter 2 - War Room

Section 1 - Description

The War Room is a room. "[if the skirmish is happening]The room has become a chaos of shouting voices and ringing steel.[otherwise]A fireplace lights this grim chamber with a hellish, red glow. A huge, round table sits in the center of the room. Hanging opposite the fireplace is a vast map of Miradania, covering nearly the entire wall. An iron-reinforced door leads east.[end if]"

Understand "grim", "chamber" as the War Room.

Instead of facing east in the War Room, try examining the heavy iron reinforced door.

Instead of facing a planar direction in the War Room, say "There seems to be no way out of here besides the door."

Rule for assembling available exits of the War Room:
	change the viable directions to { east };

Section 2 - Scenery (Fireplace)

The fireplace is scenery, in the War Room. The description is "The fire is banked high, flames roaring and popping." Understand "fire", "flames", "hellish", "red", "glow", "high", "banked", "flame", "roaring", "popping", "hearth", "knothole", "knot", "spark", "sparks", "tornado", "light", "firelight" as the fireplace.

Instead of entering, taking or touching the fireplace, say "You'd be burned quite badly."

Every turn when the fireplace is nearby and a random chance of 1 in 10 succeeds:
	add ATMOS_fireplace to the dramatic possibilities, if absent;

ATMOS_fireplace is a dramatic event. The display text is "A knothole explodes in the fire, sending a tornado of sparks up the flue.";

Section 3 - Scenery (Table)

The blackwood table is a supporter, scenery, in the War Room. The description is "The table is polished blackwood. The reflection of the firelight in its ink-dark surface looks like molten lead." Understand "round", "polished", "black", "wood", "wooden", "reflection", "surface", "inky", "dark", "ink dark", "molten" as the blackwood table.

The map of Miradania is scenery, in the War Room. The description of the map is "The map depicts the entire kingdom, from the northern reaches to the Seven Lakes. The cities are marked with semi-precious stones embedded in the map's surface. Aubrimere, the capital, is represented by a ruby the size of your thumb." Understand "Kingdom", "hanging", "entire" as the map. The printed name is "map".

Some map-regions are part of the map of Miradania. The description is "All of the regions of Miradania, including many you've never heard of are represented on the map." Understand "map", "regions", "region", "reaches", "reach", "lakes", "lake", "cities", "city", "aubrimere", "northern", "seven" as the map-regions. The printed name is "regions".

Some precious stones are part of the map of Miradania. The description is "The semi-precious stones are cunningly woven into the hanging." Understand "semi", "precious", "stone", "ruby", "cunning" as the precious stones.

Section 3A - Scenery (Chair)

The blackwood chair is a supporter, scenery, in the War Room. The description is "It's made from the same blackwood as the table, and it's very sturdy." Understand "furniture", "black", "wood", "blackwood", "wooden", "sturdy" as the blackwood chair.

[
Some rough ropes are [off-stage now] scenery[, in the War Room]. The description is "Those ropes are tight enough to keep anyone from moving very much." Understand "rope", "bonds", "bondage", "binding", "bond", "tight", "your", "my" as the rough ropes.]

[
Rule for printing the room description heading details of the blackwood chair when the player is tied up:
	say "(tied to a chair)";
]
[Rule for printing the name of the rough ropes while clarifying the parser's choice
	say "ropes binding you to the chair";]

[Does the player mean doing something with the rough ropes:
	it is likely;]

[Rule for clarifying the parser's choice of the rough ropes:
	do nothing;

Definition: a person (called p) is tied up: no.[ if p is the player and the location is the War Room and the rough ropes are on-stage.

Instead of freeing the player when the player is tied up:
	try cutting the rough ropes with the plain dagger instead;

Instead of cutting the rough ropes:
	try cutting the rough ropes with the plain dagger instead;

Instead of pulling, pushing, or untying the knotted ropes when Pieter is caught: [this previously applied to the player]
	say "The ropes are too tight for you to just pull them off of him, although you might be able to cut them.";
]]
Section 4 - Scenery (Door)

The heavy iron reinforced door is a door. It is east of the War Room. It is closed, locked, openable, and scenery. The description is "The door is [if closed]locked tight. You'll never get out that way[otherwise]open, but blocked by fighting mercenaries and soldiers[end if]."

Understand "lock" as the heavy iron reinforced door.

Instead of opening the heavy iron reinforced door when the noun is closed, try examining the noun.

Section 5 - Scenery (Pieter)

[Again, we use the real live Pieter for this. See his chapter for descriptions etc.]

Every turn when Pieter is nearby and Pieter is caught and a random chance of 1 in 10 succeeds during Confronting the Baron:
	add ATMOS_Pieter_tied to the dramatic possibilities, if absent;

ATMOS_Pieter_tied is a dramatic event. The display text is "Pieter struggles weakly against his bonds." The priority of ATMOS_Pieter_tied is interesting.

Every turn when Pieter is nearby and Pieter is caught during the Skirmish:
	add ATMOS_Pieter_struggle to the dramatic possibilities, if absent;

ATMOS_Pieter_struggle is a dramatic event. The display text is "Pieter [one of]struggles desperately against his bonds[or]strains against the ropes holding him[or]shouts unintelligibly through his gag[at random]." The priority of ATMOS_Pieter_struggle is important.

Section 6 - Scenery (Still more Mercenaries)

[ Rather than create yet another mercenary object here with essentially the same properties as that in the Ballroom, we'll just move the one in the Ballroom here (see When Confronting the Baron Begins, above) and add some location-specific rules. ]

Instead of communicating when the noun is the several mercenaries and the location is the War Room:
	say "The mercenaries ignore you.";

Instead of doing something other than examining to the several mercenaries when the location is the War Room:
	say "[if the Skirmish is not happening]The mercenaries are watching you too closely; you can't make a move against them[otherwise]The fighting is so chaotic, so vicious, that you can't get near enough to anyone to do anything.[end if].";

Section 7 - Baron Fossville

Instead of talking to someone during Confronting the Baron:
	say "[first time]Fossville's black-gloved hand whips out and cracks you across the face. Stars explode in your vision.[paragraph break]'Please be quiet while I am talking,' says the Baron, as calmly as though he were asking you to pass the salt at the dinner table[subsequently]Not feeling too keen on getting hit again, you decide to keep your mouth shut[only].";

Instead of doing something when the dagger is physically involved and the Skirmish has not happened during Confronting the Baron:
	say "Fossville's eyes are on you constantly. He'd see you if you made a move for your dagger.";

Instead of taking off the plain dagger when the Skirmish has not happened during Confronting the Baron:
	say "You might [i]just[r] be able to reach your dagger if you stretched your fingers... but there's no way to do it without the Baron seeing you.";

Instead of doing something when the Skirmish has not happened and when the action is physical during Confronting the Baron : say "[one of]Before you can move, the Baron makes a gesture, and you hear the creak of leather as the mercenaries loosen their blades in their sheaths. 'Ah, ah... no funny moves, now,' warns the Baron.[or]You hear the hiss of steel sliding out of its sheath, and suddenly someone is pushing the pointy end of a sword into your back. 'One more false move, and I'll have my men run you through,' says the Baron flatly.[or]Not wishing to be stabbed, you decide to remain still for the moment.[stopping]" 


Section 8 - Scripted events

FOSS0 is a scripted event. The display text is "Someone steps out of the shadows and walks over to stand between you and Pieter. The fire throws his shadow across the length of the room, huge and quivering.[paragraph break]'You know, it's funny,' says Baron Fossville, 'when I first began planning this venture, I always assumed that you would be the [i]easiest[r] obstacle to take care of.'"

FOSS1 is a scripted event. The display text is "'Poisoning your father, now [i]that[r] was easy.' The Baron laughs. 'Took a lot of patience. Bit by bit, for weeks and months. So many servants to bribe. But the old fool never suspected a thing. Thought he was only sick until the very end.'[paragraph break]He glares at you, eyes glowing in the fire light. 'Would that I'd been as patient with you.'"

FOSS2 is a scripted event. The display text is "Fossville smiles. 'But all that's done with now. Once I realized that you were being manipulated into making your [i]own[r] play for the throne—' He stops and glances at you. 'You [i]do[r] realize you're being manipulated, don't you? No one [i]really[r] thinks gutter-trash like you has a legitimate claim to the throne, no matter who your father was. Your [']backers['] are just using you as a pawn to further their own political ends. Where was I?' He pauses, scratching his chin. 'Ah, yes. Once I realized what you were up to, I knew you would show up here. And now this tiresome little game is nearly over, but for a few loose ends. I hope you can forgive my forwardness...' Fossville searches you with rough, undignified efficiency, and quickly finds your father's letter in a fold of your gown. He yanks the parchment free with a short, triumphant laugh.[paragraph break][i]He didn't find the dagger[r], you think. [i]I still have that[r]."

FOSS5 is a scripted event. The display text is "Fossville holds the letter up to the light, shaking his head slightly as he reads the old words. 'Such a nuisance,' he mutters. 'Very clever of you to find it, by the way. Never hide what you should have just destroyed in the first place. Well, let it be a lesson.' And without another word, he walks over to the fireplace and tosses the letter in.[paragraph break]The dry parchment catches instantly, flares up — and it's gone. Your history, your heritage, your one shot at a world beyond the crumbling orphanage and the back alleys of the city — your [i]life[r] — gone in a flash and a swirl of ashes up the chimney. The shock of it hits you like a punch in the gut.[paragraph break]Fossville draws his sword and admires its blade, glowing in the firelight. He smiles as he approaches you, raising his weapon. 'And now there's just one last — eh?' He stops, looks to the door.[paragraph break]You can hear shouting outside, the sound of metal ringing on metal."

FOSS6 is a scripted event. The display text is "Suddenly the door bursts open — a mercenary staggers backwards into the room, crashes into the table — men rush in, shouting, swords flashing —[paragraph break]Your breath catches in your throat.[paragraph break][i]It's Bobby![paragraph break][r]'Sit tight, Jack! Everything's going to be all right!' he shouts, slamming his fist into a mercenary's face. He flashes you that devil-may-care grin of his before slashing his way into the room, making a beeline for Baron Fossville."

FOSS7 is a scripted event. The display text is "'Give up, Fossville!' yells Bobby. 'You're guilty of murdering Duke Toresal! Ten of my men heard you confess from outside the door. Your bid for the throne is over!'[paragraph break]Fossville brings his own sword up and parries Bobby's cut with a resounding [i]clang[r]. 'I knew it, you [i]are[r] a spy,' growls the Baron. 'But how did you escape the noose?'[paragraph break]'That's my own secret for now,' laughs Bobby."

FOSS8 is a scripted event. The display text is "Fossville counterattacks, slashing brutally at Bobby's face. Bobby falls back, parrying each blow, until his back hits the edge of the table. 'This time you'll stay dead, spy,' snarls the Baron. 'But first I want to know who you're working for. Is it Inhyron? The old King? Some ally of the Duke's?'[paragraph break]Bobby, pushing back against Fossville's blade, grins. 'I don't answer to any royalty, Baron. I answer to the people of Miradania. And so will you!'"

FOSS8a is a scripted event. The display text is "The Baron roars and brings his sword up for a killing blow, but Bobby jumps up onto the table, rolls over it, and lands on the other side. He's breathing hard — perhaps the Baron is a better swordsman than Bobby had anticipated."

FOSS9 is a scripted event. The display text is "The two swords clash and clash again. Fossville pushes Bobby back, back across the room, this time backing him towards the roaring fireplace. Bobby is a skilled fighter, but he's losing ground before the Baron's fury. Flames lick the edge of his cloak as he staggers back against the mantelpiece.[if the player is not tied up][paragraph break]You have to do something![end if]"

FOSS10 is a scripted event. The display text is "Bobby edges Fossville towards Pieter laying on the ground and Fossville stumbles. Bobby presses Fossville backwards and regains the momentum, but is clearly tiring."

FOSS11 is a scripted event. The display text is "Fossville slows the fight, now toying with the young spy. 'You're clearly a well-trained swordsman. I would be very curious to know who paid for such skill. You're not royal born. The kingdom is clearly deteriorating if the likes of you gain power,' and the Baron presses forward once again."

FOSS12 is a scripted event. The display text is "The Baron feints, then strikes Bobby across the face with his pommel. Bobby's sword clatters to the floor and he falls to his knees. Pieter slumps helplessly against his bonds, groaning with dismay.[paragraph break]'It's over, spy,' snarls Fossville as he steps forward, sword raised."

After firing FOSS2:
	now Baron Fossville carries the secret letter;

After firing FOSS5:
	remove the secret letter from play;

After firing FOSS6:
	remove the several mercenaries from play;
	now Baron Fossville is scenery;
	move Bobby to the War Room;
	now the heavy iron reinforced door is unlocked;
	now the heavy iron reinforced door is open;
	move the fighting to the War Room;
	update the character list;

After firing FOSS12:
	ratchet the current tension to show-stopping;


Chapter 3 - Bobby in shining armour / The Skirmish

Section 1 - Bobby's entrance

The Skirmish is a scene. The skirmish begins when FOSS6 is fired. The skirmish ends when the number of entries in the current script is 0.

The fighting is scenery. The description is "The battle rages everywhere, in the room and out in the hallway. Men are shouting and screaming in pain, steel is clashing on steel." Understand "fight", "battle", "clash", "mercenaries", "mercenary", "men", "hallway", "hall", "steel", "clashing", "shouting", "screaming", "shouts", "shout", "chaotic", "chaos", "vicious", "deafening", "weapon", "weapons", "sword", "swords", "knives" as the fighting. Instead of doing something other than examining or listening to the fighting, say "The fighting is so chaotic, so vicious, that you can't get near enough to anyone to do anything." Instead of listening to the fighting, say "The screams and clashing weapons are deafening."

Before going a direction in the War Room during the Skirmish:
	say "The hallway is filled with men fighting, Bobby's reinforcements against the Baron's mercenaries. Both sides are so intent on hacking each other to pieces, you'd never get through. [if Pieter is caught]Besides, you can't leave Pieter tied up![end if]" instead;

Instead of attacking Baron Fossville when the Skirmish has not happened during Confronting the Baron:
	say "Seeing the Baron in this light brings up all of the emotions from the last few days. The chase in Grubber's, Bobby's kidnapping and hanging, finding out about your parents. Gripped with unbridled rage, you throw yourself at the Baron.[line break][line break]The Baron easily pushes you back into the chair and laughs." instead;
	
Instead of throwing the plain dagger at Baron Fossville, try attacking Baron Fossville with the plain dagger.
Instead of throwing the plain dagger at someone (called the target), say "[The target] [don't or doesn't for the target] need it!"

Instead of attacking Baron Fossville with the plain dagger during the Skirmish:
	change the current script to { }; [ thus ending the scene ]
	remove Bobby from play;
	remove Baron Fossville from play;
	say "You dart into the fray, lashing out with the dagger. You've never been much of a fighter, but you put all your rage and fear and confusion behind the blow, and the blade is sharp enough to do the rest. Fossville howls as the dagger sinks deep into his arm.[paragraph break]'Well done, Jack! We've got him on the ropes now!' cries Bobby, renewing his attack. The Baron staggers back, blood dripping from his sword arm. He stumbles... and then turns and runs out the door, calling for his mercenaries to defend him.[paragraph break]Bobby turns to you, grinning. 'Great work, Jack,' he says. 'You stay and help Pieter. I'll catch that villain quick enough, and then I'll explain everything.' And then with a bound, he is out the door.";
	update the character list;

Instead of doing something other than talking to or examining to Baron Fossville during the Skirmish:
	say "The Baron is fighting so ferociously, unless you're planning to attack him, he's likely to take [one of]out your eye[or]off your ear[or]off your hand[or]off your head[at random] with a random [one of]backswing[or]slash[or]thrust[or]jab[at random].";

Instead of talking to Baron Fossville during the Skirmish:
	say "The Baron is so focused on attacking Bobby, he doesn't even hear you."

Instead of communicating when the noun is Bobby during the Skirmish:
	say "So many questions fill your throat that you can't get them out: [i]What are you doing here? Where have you been? How did you survive?[no line break][r]But there's no time to talk, now — Bobby is fighting for his life!"

Section 2 - Dagger interactions

[A person can be knocked over. The player is not knocked over.

Instead of taking or taking off the plain dagger when the player is tied up and the player wears the plain dagger during the Skirmish: 
	move the plain dagger to the location;
	say "With all the commotion, no one notices you inching your hand around, stretching your fingers... you just manage to nudge the dagger out of your sheath... you [i]almost have it... [r][paragraph break][i]Aagh! [r]You [i]dropped[r] it! It's lying on the floor, right under your chair!";

Instead of kicking the plain dagger when the plain dagger is in the location and the player is tied up during the Skirmish:
	say "You would if you could, but your feet are tied, too!";

Instead of pushing the blackwood chair when the dagger is in the location and the player is tied up during the Skirmish:
	try taking the plain dagger;

Instead of pulling the rough ropes when the player is tied up during the Skirmish:
	try taking the plain dagger;

Instead of taking the plain dagger when the player is tied up and the player is not knocked over and the plain dagger is in the location during the Skirmish: 
	now the player is knocked over;
	say "Grunting and swearing, you strain against the ropes, scrunching down, rocking the chair back and forth, trying to reach the dagger. Suddenly someone — Bobby or Fossville, you're not sure who — bumps into you. The chair tilts over, taking you with it, and you slam into the floor, knocking your breath out.[paragraph break]The dagger is even further away than before!";
	Pieter kicks the dagger over in one turn from now;

Instead of taking the plain dagger when the player is tied up and the player is knocked over and the plain dagger is in the location during the Skirmish:
	say "It's no use. Tied to the chair and lying on your side, there's no way you can reach the dagger, no matter how hard you struggle. [paragraph break]";
	fire TRIG_PIETER_KICKS_DAGGER;

At the time when Pieter kicks the dagger over:
	fire TRIG_PIETER_KICKS_DAGGER;

TRIG_PIETER_KICKS_DAGGER is a trigger. 

Rule for firing unfired TRIG_PIETER_KICKS_DAGGER:
	say "Suddenly, over the noise of the battle, you hear someone frantically grunting. It's Pieter! Though bound even more tightly than you, he manages to scoot across the floor and kick the dagger in your direction. You feel the cold steel of the blade thump against your fingers. The dagger is in your hand!";
	now the player carries the plain dagger;

Instead of cutting the rough ropes with the plain dagger when the player does not wear the plain dagger during the Skirmish:
	say "You flip the dagger around and start working it back and forth across the ropes binding your wrist to the arm of the chair. It seems to take forever, and your fingers are almost numb from the constant tightness of the ropes... then, with a snap, the rope falls away. Feeling rushes back into your hand, and you make quick work of the rest of your bonds. You're free!";
	remove the rough ropes from play;
	move the player to the War Room, without printing a room description;]

Instead of entering the blackwood chair when the player is not on the blackwood chair during the Skirmish:
	say "This is no time to sit down!";

Section 2 - Freeing actions

Freeing is an action applying to one thing.

Check freeing the player (this is the block freeing oneself rule):
	if the player is free and the wandering mercenaries are not grabbing begin;
		say "You [i]are[r] free — for the moment at least." instead;
	otherwise;
		say "Good idea. The question is, [i]how?[r]" instead;
	end if;

Check freeing (this is the block freeing rule):
	if the noun is a caught person:
		say "Good idea. The question is, [i]how?[r]";
	otherwise:
		say capitalised "[he or she for the noun]", " [don't or doesn't for the noun] seem to be caught." instead;

Understand "free [something]" as freeing.
Understand "release [something]" as freeing.

Section 3 - Untying action

Untying is an action applying to one thing.

Check untying (this is the block untying rule):
	if the noun is the player:
		say "You aren't tied up.";
	otherwise:
		say capitalised "[he or she for the noun]", " [don't or doesn't for the noun] appear to be tied up." instead;

Understand "untie [something]" as untying.

Section 4 - Freeing Pieter

Understand "remove [knotted ropes]" as cutting.
Understand "take off [knotted ropes]" as cutting.
Understand "take [something] off [someone]" as removing it from.

Instead of untying or cutting or taking off the knotted ropes when the player is not tied up:
	try freeing Pieter;

Instead of removing the knotted ropes from Pieter when the player is not tied up:
	try freeing Pieter;
	
Instead of untying a caught Pieter when the player is not tied up:
	try freeing Pieter;

Instead of freeing a caught Pieter when the player is not tied up during the Skirmish:
	try cutting the knotted ropes with the plain dagger;

Instead of cutting the knotted ropes with the plain dagger when the player is not tied up:
	change the current script to { }; [ thus ending the scene ]
	now Pieter is free;
	remove the knotted ropes from play;
	remove Pieter from play;
	remove Bobby from play;
	remove Baron Fossville from play;
	say "Working quickly, you saw through the ropes holding Pieter. 'Thank you,' he gasps, rubbing feeling back into his wrists. Then he grabs up the sword of a fallen mercenary and rushes to Bobby's aid.[paragraph break]Bobby and Pieter fight side by side, attacking one after the other so that the Baron has no chance to counter. He falls back before their onslaught, stumbles... and then he turns and runs out the door, calling for his mercenaries to defend him.[paragraph break]With a shout, Pieter gives chase. Bobby turns to you, grinning. 'Great work, Jack,' he says. 'Now wait for us here. We'll catch that villain quick enough, and then I'll explain everything.' And then with a bound, he is gone.";
	update the character list;

Some knotted ropes are scenery in the War Room. The printed name is "ropes". The description is "[if Pieter is caught]The ropes are knotted tight, cutting deeply into Pieter's skin.[otherwise]The ropes are frayed where you cut through them.[end if]".

Understand "Pieters", "bonds", "rope", "tight", "cords", "cord", "bond", "binding", "bindings", "bondage", "gag" as the knotted ropes. 

Understand "frayed", "fraying" as the knotted ropes when Pieter is not caught.

When the skirmish ends:
	select theme "Kidnapped";
	say "At that moment, there is a sudden [i]crack[r] and a rumbling, and a cold draft rushes into the room, tugging at the flames in the fireplace and throwing the room into wildly pitching shadow.[paragraph break]Behind you, the map wall has opened to reveal a dark, cavernous passageway, and [i]more[r] men are rushing out — men dressed all in black, with dark hoods and scarves hiding their faces.[paragraph break]'That's her! Get her!' cries a voice. A young woman's voice. A voice you've heard before.[paragraph break]It happens so fast. Hands are on you, [if the player is tied up]cutting you loose from the chair, [end if]lifting you off your feet, [if the player is not tied up and the player has the dagger]wresting the dagger from your grasp, [end if]pulling you back into that dark, dark tunnel. The firelight falls away, shrinks down to a flickering, orange rectangle. The rumbling starts again, and the rectangle grows narrower, narrower...[paragraph break]'Don't worry,' says a familiar voice. 'We're not going to [i]hurt[r] you. It's simply ocurred to me that you could be useful after all. You clearly have more friends than I had thought possible.'[paragraph break]You catch a flash of the princess's face and then everything goes black.";
	end the game in victory;

Rule for printing the player's obituary when the game ended in death:
	say "    [b]*** Your adventure is over ***[r][paragraph break]";

Rule for printing the player's obituary when the game ended in victory:
	say "    [b]*** To be continued ***[r][paragraph break]";
	say "Stay tricky! Jacqueline Toresal's adventures will continue in [i]The Miradania Chronicles, Part 2: Jacqueline Toresal and the Trials of the Vedd![r]";

Book W - Bits and pieces

[Things end up here when they cause too much conflict otherwise by being defined early. This makes me sad. ]

Part 0 - Bobby's Route

Bobby's route is {Lords Road, Pasture, Crossing, Woods, Clearing, Underneath the Fountain, the Secret Passage, Chapel, Lower Bailey, Upper Bailey, Guardhouse}.

Part 1 - Global backdrops

Chapter 1 - Floors

Section 1 - Groundedness

Definition: a room is grounded:
	if it is Top of Center Post, no;
	yes;

Section 2 - Floor

The backdrop-floor is a backdrop. The backdrop-floor is everywhere.

Check entering the backdrop-floor:
	if the player is in the location, [as opposed to, say, on a chair -- ]
		say "You're standing on it already." instead;

Check entering the backdrop-floor:
	try exiting instead;

Check lying on the backdrop-floor:
	try sleeping instead;

Check getting off the backdrop-floor:
	if the player is in the location, [again as opposed to on a chair --]
		try jumping instead;

Before putting something on the backdrop-floor:
	try dropping the noun instead;

Understand "take [things] from [backdrop-floor]" as removing it from.

First check removing something from the backdrop-floor:
	if the noun is in the location,
		try taking the noun instead;
	otherwise
		say "[The noun] isn't on [the second noun]." instead;

Check putting something on the backdrop-floor:
	try dropping the noun instead;

Understand "floor", "ground" as the backdrop-floor when the location is grounded. [so unreferable-to otherwise.]

The printed name of the backdrop-floor is "[if the location is interior]floor[otherwise]ground[end if]".

Chapter 2 - Ceilings

Section 1 - Interiors and Exteriors

Definition: a room is interior:
	if it is in the Open-Air Market, no;
	if it is Grubbers Market Teishas Tent, yes;
	if it is Outer Market Roof, no;
	if it is Top of Center Post, no;
	if it is Upper Bailey, no;
	if it is Lower Bailey, no;
	if it is in the rooftops region, no;
	[if it is the Third Floor Balcony, no;]
	if it is a shop, yes;
	if it is a store, yes;
	if it is in Maiden House, yes;
	if it is in the Prison Region, yes; 
	if it is in the Sewers Region, yes;
	if it is in The Tunnels, yes;
	if it is in the Red Gate Estate Region, yes;
	if it is in the Black Gate Estate Region, yes; [excluding the rooftops]
	if it is in the Greater Lord's Keep, yes;
	no;

Definition: a room is exterior:
	if it is interior, no;
	yes;

Section 2 - Ceiling

The backdrop-ceiling is a backdrop. The backdrop-ceiling is everywhere.
Understand "ceiling" as the backdrop-ceiling when the location is interior.

The printed name of the backdrop-ceiling is "ceiling".

Chapter 3 - The Sky, Sun and Stars

Definition: a thing is heavenly:
	if it is the backdrop-sky, yes;
	if it is part of the backdrop-sky, yes;
	no;

Section 1 - Sky

The backdrop-sky is a backdrop. The backdrop-sky is everywhere.
Understand "sky" as the backdrop-sky when the location is exterior.
Understand "clear", "sky", "day", "blue" as the backdrop-sky when it is day and the location is exterior.
Understand "dark", "night", "night time", "black", "sky" as the backdrop-sky when it is night and the location is exterior.

The description of the backdrop-sky is "[if it is day]The sky is blue and clear, not a cloud in sight[otherwise]The night sky is thick with stars[end if]."

Before printing the name of the backdrop-sky when it is night:
	say "night-time ";

The printed name of the backdrop-sky is "sky".

Before doing something when the backdrop-sky is physically involved:
	say "The sky is far, far above you." instead;

Section 2 - Sun

The backdrop-sun is part of the backdrop-sky. The printed name of the backdrop-sun is "sun".

Understand "sun", "daylight", "sunlight", "light" as the backdrop-sun when it is day and the location is exterior.

The description of the backdrop-sun is "[if Trouble in Grubbers is happening]It's late morning, and the sun is already hot and high in the sky.[otherwise if Trouble in Grubbers has ended and the Hallway is unvisited]It's afternoon, getting on towards evening, and the sun is dropping down behind the buildings.[otherwise if New Morning is happening and Bedside Consolation has not happened]It's morning, and the sun is still low behind the buildings.[otherwise if Leaving Maiden House has ended and the Bodyguard Scene has not happened]It's early afternoon, and the sun bright and shining.[otherwise if Journey to the Ball has not happened]Evening is coming fast, and the sun is already low.[otherwise]The sun is hot and high in the sky.[end if]".

Before doing something when the backdrop-sun is physically involved:
	say "The sun is far, far above you." instead;

Section 3 - Stars

Some backdrop-stars are part of the backdrop-sky. The printed name of the backdrop-stars is "stars".

Understand "stars", "star", "constellation", "constellations", "pinprick", "pinpricks", "light", "twinkling" as the backdrop-stars when it is night and the location is exterior.

The description of the backdrop-stars is "The stars twinkle overhead, thousands of pinpricks of light. Widow Fiona taught you some of the constellations, but you can never find them on your own.".

Before doing something when the backdrop-stars are physically involved:
	say "The stars are far, far above you." instead;

Section 4 - Moon

The backdrop-moon is part of the backdrop-sky. The printed name of the backdrop-moon is "moon".

Understand "full", "pale", "moon", "moonlight", "night", "light" as the backdrop-moon when it is night and the location is exterior.

The description of the backdrop-moon is "The moon is full and pale tonight."

Before doing something when the backdrop-moon is physically involved:
	say "The moon is far, far above you." instead;

Chapter 4 - Looking high, looking low

Check facing up when the room up from the location is nothing:
	if the location is interior:
		try examining the backdrop-ceiling instead;
	otherwise:
		try examining the backdrop-sky instead;

Check facing down when the room up from the location is nothing:
	if the location is grounded:
		try examining the backdrop-floor instead;

Part 2 - Rooms generally

Understand "area", "surroundings", "place", "vicinity", "location" as a room.
Understand "room" as a room when the location is interior.

Does the player mean doing something with a room:
	it is very unlikely;

Check examining a doorway:
	let x be the other side of the noun;
	if x is a room:
		try inspecting x instead;

Book X - Walkthrough - Not for release

test scenetest with "scenes / test walkthrough";

test walkthrough with "test walk1 / test walk2 / test walk3"

test walkthrough2 with "test walk1 / test walk2 / test walk25"

test toball with "test walk1 / test redgate/test sandler/test moneylender/test clothier/test armory/test ballgoing"

Test walk1 with "test intro/test market/test meeting/test adventure/test jail/test sewers"

Test walk2 with "test rooftops/test blackgate/test hanging/test maidens"

Test brick with "test walk1/test walk2/test redgate/test sandler/test moneylender/test clothier/test armory"

test walk25 with "test redgate/test sandler/test moneylender/test clothier/test armory/test ballgoing"

Test walk3 with "test redgate/test sandler/test moneylender/test clothier/test armory/test ballgoing/test socializing/test warroom" 

Test Intro with "u/z/z/z/z/d/se/s/s/ne/ne"

Test Market with "xyzzy/e/steal banana/unxyzzy/sw/se/n/n/u/give banana to monkey/d/s/give necklace to teisha/n/u/slide down northeast cable with gray cloak/get cloak/wear silk cloak/s/e"

Test meeting with "e/e/s/1/1/2/sw/s/nw/sleep/se/sw/climb through window/ne/ne/n/w/w/w"

Test meeting2 with "e/e/e/2/1/1/4/2/w/ne/talk to clothier/sw/se/talk to brothers/nw/e/talk to sandler/1/w/s/1/1/2/sw/s/nw/sleep/se/sw/climb through window/ne/ne/n/w/w/w"

Test adventure with "n/e/n/xyzzy/n/unxyzzy/n/d/n/n/sw/u/n/listen at slit/listen to slit/s/d/ne/s/s/u"

Test jail with "z/z/1/1/1/z/3/z/z/search straw/unlock door with wire/n/get wire/unlock north door with wire/sw/lift grate"

Test sewers with "xyzzy/down/s/s/u/unxyzzy"

Test rooftops with "n / 1 / 1 / 1 / 2 / climb gutter / e / e / se / n"

Test blackgate with " d / s / s / s / turn winch / n / n / n / d / s / open doors / s / s / x chandelier / get letter / n / z / z / z"

Test hanging with "e / x structure / z / z / z"

Test maidens with "3 / se / se / n / z / z / z / z / z / z / nw / 4 / sw / enter window"

Test redgate with "ne / climb wall / unlock red gate estate with key / n / n /  break furniture / get wood / d / open boiler / put wood in boiler / u / u / u / s / s / take bath / wear dress / n / n / d / d / s / s / s "

Test sandler with "e / e / 1 / 1 / 1 / 1 / 1 / 2 / 3 / 3 / 2 / 1 / 1 / take brooch / w"

Test moneylender with "se / show letter to brothers / 2 / 1 / 1 / nw"

Test clothier with "ne / buy ballgown / sw"

Test armory with "w / w / ne / buy dagger / sw"

Test ballgoing with "w / n / e / n / n / search underbrush / n /press bolt/d/n/search wall/pull brick/pull lever/n/sw/n/n"

Test socializing with "z/z/z/1/1/1/1/1/talk to princess/1/2/2/talk to baron/2/4/2/2/1/1/10/talk to duchess/2/2/1/1/3/6/1/talk to earl/1/1/8/talk to jacobs/1/1/talk to baron/1/1/2/2/1/1"

Test warroom with "z/z/z/z/z/z/z/attack fossville/cut ropes"

Section Y - Fixing the sewers

Xyzzying is an action out of world.
Unxyzzying is an action out of world.
Understand "xyzzy" as xyzzying.
Understand "unxyzzy" as unxyzzying.

Carry out xyzzying:
	seed the random-number generator with 1234;

Report xyzzying:
	say "A hollow voice whispers 'groo.'";

Carry out unxyzzying:
	seed the random-number generator with 0;

Report unxyzzying:
	say "A hollow voice whispers 'oorg.'";


Book JAL - Jacqueline's Section - Not for release

[JACQNOTE: SLIDE DOWN THE WIRE PUZZLE: 
	Please also implement 'wrap' and consider 'cover' in addition to 'loop' for 'hang.' Also, when the interactor has both a silk and a wool cloak and tries to merely 'put cloak on northwest wire,' consider having the game autodisambiguate for the sturdier wool cloak.]

[***** GRUBBERS TESTING *****]

Test Grubbers with "test me/test alley/u/test outermarketroof/put hat and cloak in satchel/d/se/test northwestjunction/ne/test grocerystall/se/test fruit stall/w/test candlestall/sw/test ropestall/w/test herbstall/s/test gemstall/se/test weaponstall/e/test leatherstall/ne/test hatstall/n/test easternjunction/w/test potterystall/sw/test outsidetent/n/test insidetent/n/test behindtent/u/test topofthepost"

Test Me with "smell me"

Test Alley with "x narrow alley/x little piles of garbage/x people/x buyers/x shoppers/x crowd/x crowds/"

Test OuterMarketRoof with "x roof/x top of roof/x vista/x everything/x northwest junction/x lord's road/x leader/x mercenaries' armor/x mercenaries' arms/x tall wooden post/x se/x ground/take wires/take awnings/take people/listen to men/listen to voices/"

Test NorthwestJunction with "x corner/x edge of market/x knots of people/x customers/x wide paved road/x road to the north/x alley"

Test GroceryStall with "x kinds of food/x outer ring/x center of market"

Test FruitStall with "x fruit vendor's stall/x fruit seller/x apple/x brightly-colored fruit/x berry/x exotic fare/x kello fruits/x aisles/x aisle/smell cloying reek/"

Test CandleStall with "x candlemaker/x heat/x candlemaker's stall/x stalls/x other stalls/take fumes/"

Test RopeStall with "x dangling ropes/x rope seller/x post/x rope dealer's stall/x other stalls"

Test HerbStall with "x herb seller/x herb seller's stall/smell mosses/"

Test GemStall with "x cut/x cuts/x gem seller/x gem seller's stall/x other stalls"

Test WeaponStall with "x weapon seller/x armorer/x smith/x other stalls"

Test LeatherStall with "x leather seller/x other stalls"

Test HatStall with "x sizes of hats/x forest of headgear/x hat vendor/x milliner"

Test EasternJunction with "x commerce street/x outer ring/x central hub/take commerce street"

Test PotteryStall with "x dish/x dishes/x decoration/x decorations/x pottery merchant/x potter"

Test OutsideTent with "x dusty tent"

Test InsideTent with "x silk tent/x teisha's wares/x teisha's hair/x main market/x silk seller/x clothier/x teisha's tent/x other stalls/smell/listen"

Test BehindTent with "x cramped little square/x central wooden support post/x wire/x wires/x u/take other stalls"

Test TopofthePost with "x central wooden support post/x lord's road/x lords road/x lord's keep/x lords keep/x miradan/x miradania/x rolling meadows/x meadow/x pasture/x pastures/x anchor cables/x northwest anchor cable/x northeast anchor cable/x southwest anchor cable/x southeast anchor cable/x nw anchor cable/x ne anchor cable/x sw anchor cable/x se anchor cable/x monkey's hand/x monkey's chest/smell monkey"

[***** COMMERCE STREET TESTING *****]

[NB: The following 'Test' is merely a short-cut for me to get to the Banana']
Test GetBanana with "u/put hat and cloak in satchel/d/se/s/s/e/ne/ne/get banana"

[NB: The following 'Test' is merely a short-cut for me to get to Commerce Street from Outside Silk Tent']
Test GoCommerce with "n/n/u/give banana to monkey/d/s/trade necklace for cloak/n/u/wear silk/take wool cloak/loop wool over northeast cable/s/e"


Test CommerceStreet with "smell/listen/x market/x grubber's/x lord's/x sidewalk/x raised sidewalk/x sidewalks/x raised sidewalks/x pedestrians/x people/x mud/x dung/x horse dung/x cart/x carts/x shop/x shops/x building/x buildings/x avenue/x street/x road/x storefronts/x bakery/x loaf of bread/x butcher's shop/x armory/x north side of street/x sign/x signs/x baker's sign/x baker sign/x bakery sign/x bakery/x butcher's sign/x butcher sign/x butcher/x armory's sign/x armorer's sign/x armory sign/x armorer sign/x armory/x armorer/x armor/x brick/x blackened brick/x bricks/x blackened bricks/x axe/x shield/wear hat"

[JACQNOTE re: COMMERCE STREET
	Unimplemented as of 6/17/08:
		>X MARKET
		>X GRUBBER'S MARKET
		>X LORD'S MARKET
		>X SIDEWALKS (Plural)
			>RAISED SIDEWALKS
		>X RAISED SIDEWALK (Singular)
		>X PEDESTRIAN(S)
			X PEOPLE
			X SHOPPER(S)
			X BUYER(S)
			X CROWD(S)
		>X HORSE DUNG
		>X CART(S)
		>X SHOPS
		>X AVENUE
			>X ROAD
		>X SIGNS (Plural)
			>X LOAF OF BREAD
		>X BRICK(S)
		>X BLACKENED BRICK(S)
		>X NORTH SIDE OF STREET
		>X SMELL
		>X LISTEN
]


Test Bakery with "smell/listen/x bread/x baked bread/x fresh bread/x freshly baked bread/x counter/x counters/x loaf/x loaves/x gold loaf/x gold loaves/x golden loaf/x golden loaves/x roll/x rolls/x baguette/x baguettes/x man/x germaise/x baker/x tall man/x thin man/x proprietor/x owner/x shopkeeper/x merchant/x x dough/x ceiling/x sky"

[JACQNOTE re: COMMERCE STREET
	Unimplemented as of 6/17/08:
		>SMELL
		>X BAKED BREAD
			>X FRESH BREAD
			>X FRESHLY BAKED BREAD
			>X GOLD LOAF/LOAVES
			>X GOLDEN LOAF/LOAVES
		>X COUNTERS
		>X MERCHANT
			>X OWNER
			>X TALL THIN MAN
		>X CEILING
	ALSO NOTE:
		Sky is in scope inside the Bakery!
]


Test ArmorShop with "smell/x armorer/x man/x proprietor/x owner/x shopkeeper/x merchant/x olgan minor/x muscular man/x axe/x axes/x flail/x flails/x spiked gauntlet/x spiked gauntlets/x sharp knives/x weapons/x rack/x racks/x display/x displays/x knife/x knives/x gleaming blades/x blade/x blades/x beautiful blades/x polished blades/x row/x rows/x wicked stilettos/x stilettos/x stiletto/x demure stickers/x stickers/x sticker/x gigantic cleavers/x cleavers/x cleaver/x daggers/x dagger/x assorted knives/x ceiling/x sky"

[JACQNOTE re: COMMERCE STREET
	Unimplemented as of 6/17/08:
		>X ARMORER
			>X MAN
			>X PROPRIETOR
			>X OWNER
			>X SHOPKEEPER
			>X MUSCULAR MAN
		>X SHARP KNIVES
		>X GLEAMING BLADES
			>X BEAUTIFUL BLADES
			>X POLISHED BLADES
		>X ROW(S)
		>X WICKED STILETTOS
		>X DEMURE LITTLE STICKERS
		>X GIGANTIC CLEAVERS
		>X ASSORTED KNIVES
		>X ASSORTMENT OF KNIVES
		>X WEAPONS (Singular)
		>X DISPLAYS (Plural - Yes, I know there's only 1)
		>X CEILING
	ALSO NOTE:
		Sky is in scope inside the Armory!
]


Test Butcher with "smell/listen/x chain/x chains/x link/x links/x links of sausage/x links of sausages/x sausage link/x sausage links/x thick red cuts/x thick cut/x meat/x dangling meat/x hook/x hooks/x ceiling hooks/x hooks in the ceiling/x unidentifiable bits/x less identifiable bits/x less pleasant-looking bits/x bit/x bits/x counter/x counters/x man/x holstenoffer/x squat man/x squat brick of a man/x scar/x scars/x scarred face/x tattoo/x tattoos/x sailor tattoo/x sailor tattoos/x sailor's tattoo/x sailor's tattoos/x ceiling/x sky"

[JACQNOTE re: COMMERCE STREET
	Unimplemented as of 6/17/08:
		>SMELL
		>X CHAIN(S)
			>X LINK(S)
			>X LINKS OF SAUSAGE
			>X SAUSAGE LINK
		>X THICK RED CUT(S) OF MEAT
		>X DANGLING MEAT
		>X HOOK (Singular)
			>X CEILING HOOK(S)
		>X CEILING
		>X UNIDENTIFIABLE BITS
			>X LESS PLEASANT-LOOKING BITS
			>X BIT (Singular)
		>X COUNTER(S)
		>X SQUAT MAN
			>X SQUAT BRICK OF A MAN
		>X SCAR(S)
			>X SCARRED FACE
		>X SAILOR('S) TATTOO(S)
	ALSO NOTE:
		Sky is in scope inside the Butcher's Shop!
]

